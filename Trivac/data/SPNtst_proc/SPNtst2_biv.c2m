*----
*  TEST CASE SPNtst2_biv
*  CARTESIAN 2D BENCHMARK
*  SPN THEORY IN BIVAC (ANISOTROPIC SCATTERING)
*  MACROLIB-DEFINED CROSS SECTIONS
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM MACRO TRACK SYSTEM FLUX EDIT ;
MODULE GEO: MAC: BIVACT: BIVACA: FLUD: DELETE: END: ;
PROCEDURE assertS ;
*
GEOM := GEO: :: CAR2D  10 10
           X- SYME X+ VOID
           Y- SYME Y+ VOID
           MIX  1 1 1 1 1 2 2 2 2 2
                1 1 1 1 1 2 2 2 2 2
                1 1 1 1 1 2 2 2 2 2
                1 1 1 1 1 2 2 2 2 2
                1 1 1 1 1 2 2 2 2 2
                2 2 2 2 2 2 2 2 2 2
                2 2 2 2 2 2 2 2 2 2
                2 2 2 2 2 2 2 2 2 2
                2 2 2 2 2 2 2 2 2 2
                2 2 2 2 2 2 2 2 2 2
           MESHX -0.4 0.4 0.8 1.2 1.6 2.0 2.4 2.8 3.2 3.6 4.0
           MESHY -0.4 0.4 0.8 1.2 1.6 2.0 2.4 2.8 3.2 3.6 4.0
           ;
MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 2 NIFI 1 ANIS 2
 READ INPUT
 MIX     1
     TOTAL  0.8
      SCAT  1 1 0.7
            1 1 0.0
    NUSIGF  0.27
       CHI  1.0
 MIX     2
     TOTAL  1.0
      SCAT  1 1 0.99
            1 1 0.495
 ;
TRACK := BIVACT: GEOM ::
      TITLE 'BENCHMARK TOMASEVIC 1 GROUP'
      EDIT 1 MAXR 1000 DUAL 1 1 SPN 1 ;
SYSTEM := BIVACA: MACRO TRACK ;
FLUX := FLUD: SYSTEM TRACK :: EDIT 2 ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.9304934 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
TRACK := BIVACT: GEOM ::
      TITLE 'BENCHMARK TOMASEVIC 1 GROUP'
      EDIT 1 MAXR 1000 DUAL 2 1 SPN 1 ;
SYSTEM := BIVACA: MACRO TRACK ;
FLUX := FLUD: SYSTEM TRACK :: EDIT 2 ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.9305934 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
TRACK := BIVACT: GEOM ::
      TITLE 'BENCHMARK TOMASEVIC 1 GROUP'
      EDIT 1 MAXR 1000 DUAL 1 1 SPN 5 ;
SYSTEM := BIVACA: MACRO TRACK ;
FLUX := FLUD: SYSTEM TRACK :: EDIT 2 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.012244 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
TRACK := BIVACT: GEOM ::
      TITLE 'BENCHMARK TOMASEVIC 1 GROUP'
      EDIT 1 MAXR 1000 DUAL 2 1 SPN 5 ;
SYSTEM := BIVACA: MACRO TRACK ;
FLUX := FLUD: SYSTEM TRACK :: EDIT 2 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.011836 ;
ECHO "test SPNtst2_biv completed" ;
END: ;
