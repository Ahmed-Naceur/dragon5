*----
*  TEST CASE SPNtst2d_biv
*  CARTESIAN 2D BENCHMARK -- DIFFUSION THEORY IN BIVAC
*  MACROLIB-DEFINED CROSS SECTIONS
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM MACRO TRACK SYSTEM FLUX EDIT ;
MODULE GEO: MAC: BIVACT: BIVACA: FLUD: DELETE: END: ;
SEQ_ASCII LMFBR_ref OBJ10 ;
PROCEDURE assertS ;
*
GEOM := GEO: :: CAR2D  10 10
           X- SYME X+ VOID
           Y- SYME Y+ VOID
           MIX  1 1 1 1 1 2 2 2 2 2
                1 1 1 1 1 2 2 2 2 2
                1 1 1 1 1 2 2 2 2 2
                1 1 1 1 1 2 2 2 2 2
                1 1 1 1 1 2 2 2 2 2
                2 2 2 2 2 2 2 2 2 2
                2 2 2 2 2 2 2 2 2 2
                2 2 2 2 2 2 2 2 2 2
                2 2 2 2 2 2 2 2 2 2
                2 2 2 2 2 2 2 2 2 2
           MESHX -0.4 0.4 0.8 1.2 1.6 2.0 2.4 2.8 3.2 3.6 4.0
           MESHY -0.4 0.4 0.8 1.2 1.6 2.0 2.4 2.8 3.2 3.6 4.0
           ;
MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 2 NIFI 1
 READ INPUT
 MIX     1
     TOTAL  0.8
      DIFF  0.4166666667
      SCAT  1 1 0.7
    NUSIGF  0.27
       CHI  1.0
 MIX     2
     TOTAL  1.0
      DIFF  0.3333333333
      SCAT  1 1 0.99
 ;
TRACK := BIVACT: GEOM ::
      TITLE 'BENCHMARK TOMASEVIC 1 GROUP'
      EDIT 5 MAXR 1000 DUAL 1 1 ;
SYSTEM := BIVACA: MACRO TRACK ;
FLUX := FLUD: SYSTEM TRACK :: EDIT 2 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.148997 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
TRACK := BIVACT: GEOM ::
      TITLE 'BENCHMARK TOMASEVIC 1 GROUP'
      EDIT 5 MAXR 1000 DUAL 3 1 ;
SYSTEM := BIVACA: MACRO TRACK ;
FLUX := FLUD: SYSTEM TRACK :: EDIT 2 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.148576 ;
ECHO "test SPNtst2d_biv completed" ;
END: ;
