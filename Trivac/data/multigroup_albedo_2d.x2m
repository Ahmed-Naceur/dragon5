*----
*  TEST CASE multigroup_albedo_2d.x2m
*  MACROLIB-DEFINED CROSS SECTIONS AND ALBEDOS
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOMETRY MACRO TRACK SYSTEM FLUX ;
MODULE GEO: MAC: BIVACT: BIVACA: TRIVAT: TRIVAA: FLUD: DELETE:
       END: ;
PROCEDURE assertS ;
*
GEOMETRY := GEO: :: CAR2D 9 9
           EDIT 2
           X- DIAG X+ ALBE 1
           Y- SYME Y+ DIAG
           MIX  3 2 2 2 3 2 2 1 4
                  2 2 2 2 2 2 1 4
                    2 2 2 2 1 1 4
                      2 2 2 1 4 4
                        3 1 1 4 0
                          1 4 4 0
                            4 0 0
                              0 0
                                0
           MESHX 0.0 20.0 40.0 60.0 80.0 100.0 120.0 140.0 160.0 180.0
           ;
MACRO := MAC: ::
 EDIT 2 NGRO 2 NMIX 4 NIFI 1
 ALBP 1 0.3 0.7
 READ INPUT
 MIX     1
      DIFF  1.500E+00  4.0000E-01
     TOTAL  3.012E-02  8.0032E-02
    NUSIGF  0.000E+00  1.3500E-01
  H-FACTOR  0.000E+00  1.3500E-01
      SCAT  1 1 0.0 2 2 0.0   0.2E-01
 MIX     2
      DIFF  1.500E+00  4.0000E-01
     TOTAL  3.012E-02  8.5032E-02
    NUSIGF  0.000E+00  1.3500E-01
  H-FACTOR  0.000E+00  1.3500E-01
      SCAT  1 1 0.0 2 2 0.0   0.2E-01
 MIX     3
      DIFF  1.500E+00  4.00000E-01
     TOTAL  3.012E-02  1.30032E-01
    NUSIGF  0.000E+00  1.35000E-01
  H-FACTOR  0.000E+00  1.35000E-01
      SCAT  1 1 0.0 2 2 0.0   0.2E-01
 MIX     4
      DIFF  2.000E+00  3.0000E-01
     TOTAL  4.016E-02  1.0024E-02
    NUSIGF  0.000E+00  1.35000E-02
  H-FACTOR  0.000E+00  1.35000E-02
      SCAT  1 1 0.0 2 2 0.0   0.4E-01
 ;
*----
* BIVAC
*----
TRACK := BIVACT: GEOMETRY ::
      TITLE '2D GEOMETRY WITH MULTIGROUP ALBEDOS'
      EDIT 2 MAXR 81 DUAL 2 1 ;
SYSTEM := BIVACA: MACRO TRACK ;
FLUX := FLUD: SYSTEM TRACK ::
      EDIT 2 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.103389 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
*
TRACK := BIVACT: GEOMETRY ::
      TITLE '2D GEOMETRY WITH MULTIGROUP ALBEDOS'
      EDIT 2 MAXR 81 PRIM 2 2 ;
SYSTEM := BIVACA: MACRO TRACK ;
FLUX := FLUD: SYSTEM TRACK ::
      EDIT 2 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.127118 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
*----
* TRIVAC
*----
TRACK := TRIVAT: GEOMETRY ::
      TITLE '2D GEOMETRY WITH MULTIGROUP ALBEDOS'
      EDIT 2 MAXR 81 DUAL 2 1 ;
SYSTEM := TRIVAA: MACRO TRACK ;
FLUX := FLUD: SYSTEM TRACK ::
      EDIT 2 ADJ ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.103389 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
*
TRACK := TRIVAT: GEOMETRY ::
      TITLE '2D GEOMETRY WITH MULTIGROUP ALBEDOS'
      EDIT 2 MAXR 81 DUAL 2 2 ;
SYSTEM := TRIVAA: MACRO TRACK ;
FLUX := FLUD: SYSTEM TRACK ::
      EDIT 2 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.098498 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
*
TRACK := TRIVAT: GEOMETRY ::
      TITLE '2D GEOMETRY WITH MULTIGROUP ALBEDOS'
      EDIT 2 MAXR 81 PRIM 2 ;
SYSTEM := TRIVAA: MACRO TRACK ;
FLUX := FLUD: SYSTEM TRACK ::
      EDIT 2 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.127115 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
*
TRACK := TRIVAT: GEOMETRY ::
      TITLE '2D GEOMETRY WITH MULTIGROUP ALBEDOS'
      EDIT 2 MAXR 81 MCFD 2 ;
SYSTEM := TRIVAA: MACRO TRACK ;
FLUX := FLUD: SYSTEM TRACK ::
      EDIT 2 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.098498 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
ECHO "test multigroup_albedo_2d completed" ;
END: ;
