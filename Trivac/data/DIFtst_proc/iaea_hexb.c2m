LINKED_LIST HEX MACRO TRACK SYSTEM FLUX EDIT ;
MODULE GEO: MAC: TRIVAT: TRIVAA: FLUD: OUT: DELETE: END: ;
PROCEDURE assertS ;
*
HEX := GEO: :: HEX  20
       EDIT 2
       HBC   S30  ALBE  0.0
       SIDE 11.5470054
       MIX
       3
       2
       2  3
       2  2
       3  2  2
       2  2  2
       1  1  1  1
       4  4  4  4
       ;
MACRO := MAC: ::
 EDIT 2 NGRO 2 NMIX 4 NIFI 1
 READ INPUT
 MIX     1
      DIFF  1.5E+00  4.0E-01
     TOTAL  3.0E-02  8.0E-02
    NUSIGF  0.0E+00  1.35E-01
  H-FACTOR  0.0E+00  5.6E-02
      SCAT  1 1 0.0 2 2 0.0  0.2E-01
 MIX     2
      DIFF  1.5E+00  4.0E-01
     TOTAL  3.0E-02  8.5E-02
    NUSIGF  0.0E+00  1.35E-01
  H-FACTOR  0.0E+00  5.6E-02
      SCAT  1 1 0.0 2 2 0.0  0.2E-01
 MIX     3
      DIFF  1.5E+00  4.0E-01
     TOTAL  3.0E-02  1.3E-01
    NUSIGF  0.0E+00  1.35E-01
  H-FACTOR  0.0E+00  5.6E-02
      SCAT  1 1 0.0 2 2 0.0  0.2E-01
 MIX     4
      DIFF  1.5E+00  4.0E-01
     TOTAL  4.0E-02  1.0E-02
      SCAT  1 1 0.0 2 2 0.0  0.4E-01
 ;
HEX := GEO: HEX ::
      SPLITH 0 SPLITL 0 ;
TRACK := TRIVAT: HEX ::
      TITLE 'TEST HEXAGONAL IAEA BENCHMARK CASE B (WITH REFLECTOR).'
      EDIT 5 MAXR 500 MCFD (*IELEM=*) 1 ;
SYSTEM := TRIVAA: MACRO TRACK ::
      EDIT 2 ;
FLUX := FLUD: SYSTEM TRACK ::
      EDIT 2 ;
EDIT := OUT: FLUX HEX MACRO TRACK ::
      EDIT 2 INTG IN ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.009611 ;
TRACK SYSTEM FLUX EDIT := DELETE: TRACK SYSTEM FLUX EDIT ;
*
HEX := GEO: HEX ::
      SPLITH 0 SPLITL 2 ;
TRACK := TRIVAT: HEX ::
      TITLE 'TEST HEXAGONAL IAEA BENCHMARK CASE B (WITH REFLECTOR).'
      EDIT 5 MAXR 5000 DUAL (*IELEM=*) 2 (*ICOL=*) 3 ;
SYSTEM := TRIVAA: MACRO TRACK ::
      EDIT 2 ;
FLUX := FLUD: SYSTEM TRACK ::
      ADI 2 EDIT 2 ;
EDIT := OUT: FLUX HEX MACRO TRACK ::
      EDIT 2 INTG IN ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.005512 ;
ECHO "test iaea_hexb completed" ;
END: ;
