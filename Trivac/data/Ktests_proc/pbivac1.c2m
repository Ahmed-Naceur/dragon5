*********************************************
* Procedure :  pbivac1.c2m                  *
* Author    :  D. Sekki (04/2008)           *
*********************************************
PARAMETER MACRO TRACK SYSTEM FLUX ::
      ::: LINKED_LIST MACRO TRACK SYSTEM FLUX ; ;
MODULE GEO: MAC: BIVACT: BIVACA: FLUD: DELETE: END: ;
LINKED_LIST GEOM ;
INTEGER MaxReg degre quadr ;
STRING  method ;
REAL    precf ;
 :: >>MaxReg<< >>method<< >>degre<<
        >>quadr<< >>precf<< ;
GEOM := GEO: :: CAR1D 10  EDIT 0
        X- ZERO  X+ ZERO
        MIX 2 1 1 1 1 3 3 3 3 2
        MESHX   0.0 40.0 117.5 195.0  272.5
        350.0 427.5 505.0 582.5 660.0 700.0 ;
MACRO := MAC: ::
  EDIT 0 NGRO 2 NMIX 3 NIFI 1 READ INPUT
  MIX  1
       DIFF  1.264E+00  9.328E-01
      TOTAL  8.154E-03  4.100E-03
     NUSIGF  0.000E+00  4.562E-03
       SCAT  1 1 0.0 2 2 0.0 7.368E-03
      OVERV  1.000E-07  5.000E-06
        CHI  1.000E+00  0.000E+00
  MIX  2
       DIFF  1.310E+00  8.695E-01
      TOTAL  1.018E-02  2.117E-04
       SCAT  1 1 0.0 2 2 0.0 1.018E-02
      OVERV  1.000E-07  5.000E-06
        CHI  0.000E+00  0.000E+00
  MIX  3
       DIFF  1.264E+00  9.328E-01
      TOTAL  8.154E-03  4.100E-03
     NUSIGF  0.000E+00  4.562E-03
       SCAT  1 1 0.0 2 2 0.0 7.368E-03
      OVERV  1.000E-07  5.000E-06
        CHI  1.000E+00  0.000E+00 ;
IF method "PRIM" = THEN
   TRACK := BIVACT: GEOM :: EDIT 0
   MAXR <<MaxReg>> PRIM <<degre>> <<quadr>> ;
ELSEIF method "DUAL" = THEN
   TRACK := BIVACT: GEOM :: EDIT 0
   MAXR <<MaxReg>> DUAL <<degre>> <<quadr>> ;
ENDIF ;
GEOM := DELETE: GEOM ;
SYSTEM := BIVACA: MACRO TRACK :: EDIT 0 UNIT ;
FLUX := FLUD: SYSTEM TRACK :: EDIT 1
        EXTE <<precf>> ;
END: ;
QUIT .
