*----
*  TEST CASE TCWU10
*  MOSTELLER BENCHMARK FOR 1-D ANNULAR CELL
*  WLUP JEFF3.1 XMAS Library
*  FIXED SOURCE PROBLEM IN MULTIPLICATIVE MEDIA
*
*  REF: None
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  MOSTELA VOLMAT LIBRARY PIJ FLUX OUT ;
MODULE
  LIB: MAC: GEO: SYBILT: SHI: ASM: FLU: EDI: DELETE: END: ;
PROCEDURE assertS assertV ;
*----
*  Microscopic cross section from file WLUP172 format WIMSD4
*  Fixed source of 1.0E5 in group 6
*----
LIBRARY := LIB: ::
  EDIT 0 NMIX 3  CTRA WIMS
  MIXS LIB: WIMSD4 FIL: WLUP172
  MIX 1 600.0                        O16      = '6016'   4.61309E-2
    U235     = '2235'   1.66078E-4 1 
    U238     = '8238'   2.28994E-2 1 
  MIX 2 600.0
    Zr91     = '91'     3.83243E-2
  MIX 3 600.0
    H1H2O    = '3001'   4.42326E-2   O16H2O   = '6016'   2.21163E-2
    BNat     = '1011'   1.02133E-5
  ;
LIBRARY  := MAC: LIBRARY  ::
  EDIT 0
  READ INPUT
  MIX 3 FIXE
  0.0 0.0 0.0 0.0 0.0 1.0E+5 0.0 0.0 0.0 0.0
  0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
  0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
  0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
  0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
  0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
  0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
  0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
  0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
  0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
  0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
  0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
  0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
  0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
  0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
  0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
  0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
  0.0 0.0
  ;
*----
*  Geometry MOSTELA : Annular cell with reflective BC
*----
MOSTELA := GEO: :: TUBE 3
  RADIUS 0.0 0.39306 0.45802 0.71206
  SPLITR     2       1       1
  MIX        1       2       3
  R+ REFL ;
*----
*  Self-Shielding calculation SYBIL
*  Transport calculation      SYBIL
*  Flux calculation for keff
*----
VOLMAT := SYBILT: MOSTELA ::
  TITLE 'TCWU10: SYBIL TRACK MOSTELLER BENCHMARK'
  MAXR 4 QUA1 5 ;
LIBRARY := SHI: LIBRARY VOLMAT :: EDIT 0 NOLJ ;
PIJ := ASM: LIBRARY VOLMAT ;
FLUX := FLU: PIJ LIBRARY VOLMAT  ::
  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.8227208 ;
OUT := EDI: FLUX LIBRARY VOLMAT  ::
  EDIT 4 MERG MIX 1 2 3 COND 4.0 ;
FLUX := DELETE: FLUX ;
FLUX := FLU: PIJ LIBRARY VOLMAT  :: EDIT 99
  TYPE S EXTE 30 UNKT 1.0E-3 ;
assertV FLUX :: 'FLUX' (*GROUP*) 10 (*REGION*) 4 2.757832E+05 ;
OUT := EDI: OUT FLUX LIBRARY VOLMAT  ::
  EDIT 4 MERG MIX 1 2 3 COND 4.0 ;
OUT FLUX PIJ LIBRARY VOLMAT := DELETE:
  OUT FLUX PIJ LIBRARY VOLMAT  ;
ECHO "test TCWU10 completed" ;
END: ; 
QUIT "LIST" .
