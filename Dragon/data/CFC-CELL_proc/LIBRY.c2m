* DRAGON CROSS-SECTION LIBRARY PROCEDURE DECK
***********************************************************************
* Purpose:
* define properties values of the mixtures.
*
* author:
* MAJID FASSI FEHRI 09/08/31
* CNSC
*
* Modified:
* Richard CHAMBON 11/02/24
* Ecole Polytechnique
*
***********************************************************************
PARAMETER LIBR ::
 ::: LINKED_LIST  LIBR ;  ;
MODULE END: INFO: LIB: ;
INTEGER CALCU ;
STRING LIBNAM := "E6MLIB" ;
PROCEDURE PERTRB ;
REAL TFuel TCool TMode DCool DMode
     PCool PMode Bor  Xe   Sm  Np  Gad XeRef SmRef NpRef ;
REAL FWGTU5 FWGTU8 FWGTO16 CWGTH1 CWGTD2 CWGTO16 
     MWGTH1 MWGTD2 MWGTO16 ;
REAL PTtemp PTdens Gaptemp Gapdens CTtemp CTdens 
     Shtemp Shdens Fdens ;
REAL BWGT0 BWGT1 GWGT5 GWGT7 ;
REAL XWGT5 SWGT9 NWGT9 ;

ECHO "LIBRY PROCEDURE" ;

* Get current calculation value
:: >>CALCU<< ;

* Get current properties values
PERTRB ::  <<CALCU>>
   >>TFuel<< >>TCool<< >>TMode<< >>DCool<< >>DMode<<
   >>PCool<< >>PMode<< >>Bor<< >>Xe<< >>Sm<< >>Np<< 
   >>Gad<< ;

* Set non-changing properties values
EVALUATE Fdens   := 10.39475 ;
EVALUATE Shdens  := 6.3918   ;
EVALUATE PTdens  := 6.57   ;
EVALUATE CTdens  := 6.44   ;
EVALUATE Gapdens := 0.00118 ;
EVALUATE Shtemp  := TCool 5.0 +   ;
EVALUATE PTtemp  := TCool 2.0 -   ;
EVALUATE CTtemp  := TMode 2.0 +   ;
EVALUATE Gaptemp := PTtemp CTtemp + 2.0 / ;

* Change units fron fraction to percent
EVALUATE BWGT0   := 0.199 ;
! EVALUATE BWGT0   := BWGT0 0.000109 * Bor * ;
EVALUATE BWGT0   := BWGT0  Bor * 100.0 * ;
EVALUATE BWGT1   := 0.801 ;
! EVALUATE BWGT1   := BWGT1 0.000109 * Bor * ;
EVALUATE BWGT1   := BWGT1  Bor * 100.0 * ;
EVALUATE GWGT5   := 0.482837 ;
! EVALUATE GWGT5   := GWGT5 0.000033 * Gad * ;
EVALUATE GWGT5   := GWGT5 Gad * 100.0 * ;
EVALUATE GWGT7   := 0.517163 ;
! EVALUATE GWGT7   := GWGT7 0.000033 * Gad * ;
EVALUATE GWGT7   := GWGT7 Gad * 100.0 * ;
* Arbitrary coefficients
EVALUATE XWGT5   :=  Xe 0.00001 * ;
EVALUATE SWGT9   :=  Sm 0.00001 * ;
EVALUATE NWGT9   :=  Np 0.00001 * ;

*----
*  FUEL WGT%
*----
INFO: ::  EDIT 0
  TMP: <<TFuel>> K 
  ENR: 0.711 WGT%
  LIB: WIMSAECL FIL: <<LIBNAM>>
  ISO: 3        U235   U238   O16 
  CALC WGT% UO2 >>FWGTU5<<  >>FWGTU8<<  >>FWGTO16<<  ;
 

*----
*  Coolant WGT%
*----
INFO: ::  EDIT 0
  TMP: <<TCool>> K 
  PUR: <<PCool>> WGT% 
  LIB: WIMSAECL FIL: <<LIBNAM>>
  ISO: 3        H1H2O   DD2O   O16 
  CALC WGT% D2O >>CWGTH1<<  >>CWGTD2<<  >>CWGTO16<<  ;


*----
*  Moderator WGT%
*----
INFO: ::  EDIT 0
  TMP: <<TMode>> K 
  PUR: <<PMode>> WGT% 
  LIB: WIMSAECL FIL: <<LIBNAM>>
  ISO: 3        H1H2O   DD2O   O16
  CALC WGT% D2O >>MWGTH1<<  >>MWGTD2<<  >>MWGTO16<<  ;

ECHO "PARAMETERS USED" ;
ECHO FWGTU5  FWGTU8  FWGTO16 ;
ECHO CWGTH1  CWGTD2  CWGTO16 ;
ECHO MWGTH1  MWGTD2  MWGTO16 ;
ECHO XWGT5 SWGT9  NWGT9 ;
ECHO BWGT0 BWGT1 GWGT5 GWGT7 ;
ECHO "PARAMETERS USED SUITE" ;
ECHO  PCool PMode Bor  Xe   Sm  Np  Gad ;
ECHO  DCool DMode Fdens Shdens PTdens CTdens Gapdens ;
ECHO  TCool TMode TFuel Shtemp PTtemp CTtemp Gaptemp ;

ECHO "LIBRARY CREATION STARTED" ;

*----
*  Create library
*----
LIBR   := LIB:  ::
  EDIT 0
  NMIX 10  
  CTRA WIMS
  DEPL LIB: WIMSAECL FIL: <<LIBNAM>>
  MIXS LIB: WIMSAECL FIL: <<LIBNAM>>

*  Coolant 
  MIX 1 <<TCool>>  <<DCool>> 
          CH1  = H1H2O      <<CWGTH1>>
          CD2  = DD2O       <<CWGTD2>> 
          CO16 = O16        <<CWGTO16>>

*  Pressure tube 
  MIX  2  <<PTtemp>>    <<PTdens>>
          ZR90PT        49.3443000
          ZR91PT        10.8805220
          ZR92PT        16.8136710
          ZR94PT        17.4101970
          ZR96PT         2.8645910
          NB93           2.5800000
          FE54           0.0026659
          FE56           0.0429768
          FE57           0.0010016
          FE58           0.0001359
          CR50           0.0003376
          CR52           0.0067696
          CR53           0.0007823
          CR54           0.0001984
          NI58           0.0024819
          NI60           0.0009815
          NI64           0.0000365
          B10            0.0000243

*  Gap
  MIX  3  <<Gaptemp>>  <<Gapdens>>
          C             27.11
          O16           72.89 

*  Calendria tube
  MIX  4 <<CTtemp>>     <<CTdens>>    
          ZR90CT        49.7984827
          ZR91CT        10.9806700
          ZR92CT        16.9684300
          ZR94CT        17.5704469
          ZR96CT         2.8909577
          FE54           0.0076934
          FE56           0.1240245
          FE57           0.0028905
          FE58           0.0003921
          NI58           0.0390021
          NI60           0.0154242 
          NI64           0.0005736
          CR50           0.0041737
          CR52           0.0836990
          CR53           0.0096726
          CR54           0.0024534
          B10            0.0000596

*  Moderator
  MIX  5 <<TMode>>   <<DMode>> 
          MH1  = H1H2O      <<MWGTH1>>
          MD2  = DD2O       <<MWGTD2>> 
          MO16 = O16        <<MWGTO16>>  
          MB10 = B10        <<BWGT0>> 
          MB11 = B11        <<BWGT1>>
          MG5  = GD155      <<GWGT5>>
          MG7  = GD157      <<GWGT7>>

*   Central Ring Fuel
  MIX 6 <<TFuel>>  <<Fdens>> 
        FU3  = U233    0.0             1
        FU4  = U234    0.0             1
        FU5  = U235   <<FWGTU5>>       1
        FU6  = U236    0.0             1
        FU8  = U238   <<FWGTU8>>       1 
        FO16 = O16    <<FWGTO16>> 
        FP9  = PU239   0.0             1
        FP0  = PU240   0.0             1
        FP1  = PU241   0.0             1
        FP2  = PU242   0.0             1
               AM241   0.0             1  
               AM242M  0.0             1
               AM243   0.0             1
        NP9 =  NP239   <<NWGT9>>             
        XE5 =  XE135   <<XWGT5>>               
        SM9 =  SM149   <<SWGT9>>             

*   Inner Ring Fuel
  MIX   7  COMB  6 1.0

*   Intermediate Ring Fuel
  MIX   8  COMB  6 1.0

*   Outer Ring Fuel
  MIX   9  COMB  6 1.0

*  Sheath 
  MIX   10   <<Shtemp>>  <<Shdens>>   
          ZR90CL        49.7847918
          ZR91CL        10.9776511
          ZR92CL        16.9637650
          ZR94CL        17.5656164
          ZR96CL         2.8901629
          FE54           0.0119675
          FE56           0.1929270
          FE57           0.0044963
          FE58           0.0006100
          NI58           0.0049639
          NI60           0.0019631
          NI64           0.0000730
          CR50           0.0041737
          CR52           0.0836990
          CR53           0.0096726
          CR54           0.0024534
          B10            0.0000596
;

ECHO "LIBRARY CREATION FINISHED" ;

END: ;
