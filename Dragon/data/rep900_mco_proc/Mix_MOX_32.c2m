*DECK Mix_Assb_DRA
*----
*  Name          : Mix_Assb_DRA.c2m
*  Type          : DRAGON procedure
*  Use           : Microlib generation with Draglibs for
*                  MOX calculation with 32 fuel regions
*  Author        : R. Vallerent
*
*  Procedure called as: 
*
*  LIBRARY := Mix_Assb_DRA ::
*   <<dens_mod>> <<pbore>> <<N_U5_MOX>> <<N_Pu8>> <<N_Pu9>> <<N_Pu0>>
*   <<N_Pu1>> <<N_Pu2>> <<N_Am1>> <<densU_MOX>> <<densPu_MOX>>
*   <<teneur1_Pu>> <<teneur2_Pu>> <<teneur3_Pu>> <<temp_comb>>
*   <<temp_mode>> ;
*  ;
*
*  Input data   :
*    dens_mod   : Theoretical moderator density
*    pbore      : Natural boron concentration (ppm)
*    N_U5_MOX   : U235 enrichment in MOX-Fuel
*    N_Pu8      : Pu238 percentage in Pu-vector
*    N_Pu9      : Pu239 percentage in Pu-vector
*    N_Pu0      : Pu240 percentage in Pu-vector
*    N_Pu1      : Pu241 percentage in Pu-vector
*    N_Pu2      : Pu242 percentage in Pu-vector
*    N_Am1      : Am241 percentage in Pu-vector
*    densU_MOX  : Theoretical density of Uranium in MOX-Fuel
*    densPu_MOX : Theoretical density of Plutonium in MOX-Fuel
*    teneur1_Pu : Plutonium content in MOX-high enrichment region
*    teneur2_Pu : Plutonium content in MOX-medium enrichment region
*    teneur3_Pu : Plutonium content in MOX-low enrichment region
*    temp_comb  : Theoretical Fuel temperature (K)
*    temp_mode  : Theoretical moderator temperature (K)
*
*  Output data  :
*    LIBRARY    : Microlib with table of probabilities


PARAMETER  LIBRARY  ::  
       EDIT 0 
           ::: LINKED_LIST LIBRARY  ; 
   ;

*----
*  Input data recovery
*----
STRING Library ;
:: >>Library<< ;

REAL dens_mod     pbore     N_U5_MOX     N_Pu8     N_Pu9     N_Pu0  
     N_Pu1     N_Pu2     N_Am1     densU_MOX     densPu_MOX  
     teneur1_Pu     teneur2_Pu     teneur3_Pu     temp_comb  
     temp_mode ;
:: >>dens_mod<< >>pbore<< >>N_U5_MOX<< >>N_Pu8<< >>N_Pu9<< >>N_Pu0<<
   >>N_Pu1<< >>N_Pu2<< >>N_Am1<< >>densU_MOX<< >>densPu_MOX<<
   >>teneur1_Pu<< >>teneur2_Pu<< >>teneur3_Pu<< >>temp_comb<<
   >>temp_mode<< ;

*----
*  Modules used in this procedure
*----
MODULE  LIB: UTL: DELETE: END: ;

REAL TfuelK := temp_comb ;
REAL TmodeK := temp_mode ;

REAL TgainK := TmodeK ;

* Calculation of moderator data
* NOMBRE D AVOGADRO
REAL A := 6.022094E-1 ; ! Normalisation des concentrations par 10E-24

REAL M_H2O := 15.9994 2.0 1.00794 * + ;
REAL N_H2O := dens_mod 0.6022 * M_H2O / ;
REAL b10 b11 := 0.199 0.801 ;
REAL coeff := 0.199 A 1.0E-06 * * 10.8110164 / ;
REAL N_B10 := coeff 0.659 pbore * * ; ! densite nominale
REAL N_2H2O N_B11 := 2.0 N_H2O * N_B10 b11 * b10 / ;

* Data for all enrichment MOX
REAL den_uo2t den_puo2t := densU_MOX densPu_MOX ;

REAL N_U5 := N_U5_MOX ;
REAL N_U4 := N_U5 54.0E-04 * 0.72 / ;
REAL N_U6 := 1.00E-14 ;
REAL N_U8 := 1. N_U5 - N_U4 - N_U6 - ;

REAL N_Np7 := 1.00E-14 ;

REAL M_U4          M_U5          M_U8          M_U6          := 
     234.040952088 235.043929918 238.050788247 236.045568006 ;
REAL M_Pu8         M_Pu9         M_Pu0         M_Pu1         :=
     238.049559894 239.052163381 240.053813545 241.056851456 ;
REAL M_Pu2         M_Am1         M_Np7         M_O16          :=
     242.058742611 241.056829144 237.048173444 15.99491461956 ;
*taken from : http://amdc.in2p3.fr/masstables/Ame2003/mass.mas03
*                                                                      ;
*----
* MOX-high enrichment region
*----
REAL teneur1_U  := 1. teneur1_Pu - ;
REAL xu1 xp1 := teneur1_U den_uo2t / teneur1_Pu den_puo2t / ;
REAL den_mox1 := 1. xu1 xp1 + / ;

REAL X1_U4  X1_U5  := N_U4  teneur1_U *  N_U5  teneur1_U *  ;
REAL X1_U6  X1_U8  := N_U6  teneur1_U *  N_U8  teneur1_U *  ;
REAL X1_Pu8 X1_Pu9 := N_Pu8 teneur1_Pu * N_Pu9 teneur1_Pu * ;
REAL X1_Pu0 X1_Pu1 := N_Pu0 teneur1_Pu * N_Pu1 teneur1_Pu * ;
REAL X1_Pu2 X1_Am1 := N_Pu2 teneur1_Pu * N_Am1 teneur1_Pu * ;
REAL X1_Np7        := N_Np7 teneur1_Pu * ;

REAL     X1_O16 :=        X1_U4  M_U4  /   X1_U5  M_U5  / + ;
EVALUATE X1_O16 := X1_O16 X1_U8  M_U8  / + X1_U6  M_U6  / + ;
EVALUATE X1_O16 := X1_O16 X1_Pu8 M_Pu8 / + X1_Pu9 M_Pu9 / + ;
EVALUATE X1_O16 := X1_O16 X1_Pu0 M_Pu0 / + X1_Pu1 M_Pu1 / + ;
EVALUATE X1_O16 := X1_O16 X1_Pu2 M_Pu2 / + X1_Am1 M_Am1 / + ;
EVALUATE X1_O16 := X1_O16 X1_Np7 M_Np7 / + 2.0 * M_O16 * ;

REAL X1_TOT := X1_U4 X1_U5 + X1_U6 + X1_U8 + ;
EVALUATE X1_TOT := X1_TOT X1_Pu8 + X1_Pu9 + X1_Pu0 + ;
EVALUATE X1_TOT := X1_TOT X1_Pu1 + X1_Pu2 + X1_Am1 + X1_Np7 + X1_O16 + ;

EVALUATE X1_U4  X1_U5  := X1_U4  X1_TOT / X1_U5  X1_TOT / ;
EVALUATE X1_U6  X1_U8  := X1_U6  X1_TOT / X1_U8  X1_TOT / ;
EVALUATE X1_Pu8 X1_Pu9 := X1_Pu8 X1_TOT / X1_Pu9 X1_TOT / ;
EVALUATE X1_Pu0 X1_Pu1 := X1_Pu0 X1_TOT / X1_Pu1 X1_TOT / ;
EVALUATE X1_Pu2 X1_Am1 := X1_Pu2 X1_TOT / X1_Am1 X1_TOT / ;
EVALUATE X1_Np7 X1_O16 := X1_Np7 X1_TOT / X1_O16 X1_TOT / ;

*----
* MOX-medium enrichment region
*----
REAL teneur2_U  := 1. teneur2_Pu - ;
REAL xu2 xp2 := teneur2_U den_uo2t / teneur2_Pu den_puo2t / ;
REAL den_mox2 := 1. xu2 xp2 + / ;

REAL X2_U4  X2_U5  := N_U4  teneur2_U *  N_U5  teneur2_U *  ;
REAL X2_U6  X2_U8  := N_U6  teneur2_U *  N_U8  teneur2_U *  ;
REAL X2_Pu8 X2_Pu9 := N_Pu8 teneur2_Pu * N_Pu9 teneur2_Pu * ;
REAL X2_Pu0 X2_Pu1 := N_Pu0 teneur2_Pu * N_Pu1 teneur2_Pu * ;
REAL X2_Pu2 X2_Am1 := N_Pu2 teneur2_Pu * N_Am1 teneur2_Pu * ;
REAL X2_Np7        := N_Np7 teneur2_Pu * ;

REAL     X2_O16 :=        X2_U4  M_U4  /   X2_U5  M_U5  / + ;
EVALUATE X2_O16 := X2_O16 X2_U8  M_U8  / + X2_U6  M_U6  / + ;
EVALUATE X2_O16 := X2_O16 X2_Pu8 M_Pu8 / + X2_Pu9 M_Pu9 / + ;
EVALUATE X2_O16 := X2_O16 X2_Pu0 M_Pu0 / + X2_Pu1 M_Pu1 / + ;
EVALUATE X2_O16 := X2_O16 X2_Pu2 M_Pu2 / + X2_Am1 M_Am1 / + ;
EVALUATE X2_O16 := X2_O16 X2_Np7 M_Np7 / + 2.0 * M_O16 * ;

REAL X2_TOT := X2_U4 X2_U5 + X2_U6 + X2_U8 + ;
EVALUATE X2_TOT := X2_TOT X2_Pu8 + X2_Pu9 + X2_Pu0 + ;
EVALUATE X2_TOT := X2_TOT X2_Pu1 + X2_Pu2 + X2_Am1 + X2_Np7 + X2_O16 + ;

EVALUATE X2_U4  X2_U5  := X2_U4  X2_TOT / X2_U5  X2_TOT / ;
EVALUATE X2_U6  X2_U8  := X2_U6  X2_TOT / X2_U8  X2_TOT / ;
EVALUATE X2_Pu8 X2_Pu9 := X2_Pu8 X2_TOT / X2_Pu9 X2_TOT / ;
EVALUATE X2_Pu0 X2_Pu1 := X2_Pu0 X2_TOT / X2_Pu1 X2_TOT / ;
EVALUATE X2_Pu2 X2_Am1 := X2_Pu2 X2_TOT / X2_Am1 X2_TOT / ;
EVALUATE X2_Np7 X2_O16 := X2_Np7 X2_TOT / X2_O16 X2_TOT / ;


*----
* MOX-low enrichment region
*----
REAL teneur3_U  := 1. teneur3_Pu - ;
REAL xu3 xp3 := teneur3_U den_uo2t / teneur3_Pu den_puo2t / ;
REAL den_mox3 := 1. xu3 xp3 + / ;

REAL X3_U4  X3_U5  := N_U4  teneur3_U *  N_U5  teneur3_U *  ;
REAL X3_U6  X3_U8  := N_U6  teneur3_U *  N_U8  teneur3_U *  ;
REAL X3_Pu8 X3_Pu9 := N_Pu8 teneur3_Pu * N_Pu9 teneur3_Pu * ;
REAL X3_Pu0 X3_Pu1 := N_Pu0 teneur3_Pu * N_Pu1 teneur3_Pu * ;
REAL X3_Pu2 X3_Am1 := N_Pu2 teneur3_Pu * N_Am1 teneur3_Pu * ;
REAL X3_Np7        := N_Np7 teneur3_Pu * ;

REAL     X3_O16 :=        X3_U4  M_U4  /   X3_U5  M_U5  / + ;
EVALUATE X3_O16 := X3_O16 X3_U8  M_U8  / + X3_U6  M_U6  / + ;
EVALUATE X3_O16 := X3_O16 X3_Pu8 M_Pu8 / + X3_Pu9 M_Pu9 / + ;
EVALUATE X3_O16 := X3_O16 X3_Pu0 M_Pu0 / + X3_Pu1 M_Pu1 / + ;
EVALUATE X3_O16 := X3_O16 X3_Pu2 M_Pu2 / + X3_Am1 M_Am1 / + ;
EVALUATE X3_O16 := X3_O16 X3_Np7 M_Np7 / + 2.0 * M_O16 * ;

REAL X3_TOT := X3_U4 X3_U5 + X3_U6 + X3_U8 + ;
EVALUATE X3_TOT := X3_TOT X3_Pu8 + X3_Pu9 + X3_Pu0 + ;
EVALUATE X3_TOT := X3_TOT X3_Pu1 + X3_Pu2 + X3_Am1 + X3_Np7 + X3_O16 + ;

EVALUATE X3_U4  X3_U5  := X3_U4  X3_TOT / X3_U5  X3_TOT / ;
EVALUATE X3_U6  X3_U8  := X3_U6  X3_TOT / X3_U8  X3_TOT / ;
EVALUATE X3_Pu8 X3_Pu9 := X3_Pu8 X3_TOT / X3_Pu9 X3_TOT / ;
EVALUATE X3_Pu0 X3_Pu1 := X3_Pu0 X3_TOT / X3_Pu1 X3_TOT / ;
EVALUATE X3_Pu2 X3_Am1 := X3_Pu2 X3_TOT / X3_Am1 X3_TOT / ;
EVALUATE X3_Np7 X3_O16 := X3_Np7 X3_TOT / X3_O16 X3_TOT / ;

* --------------------------------
* Cladding calculation
* --------------------------------
REAL P_Zr       P_Fe       P_Cr      P_O16     P_Sn      := 
     98.115E-02 0.210E-02  0.100E-02 0.125E-02 1.450E-02 ;

REAL P_Zr90     P_Zr91     P_Zr92    P_Zr94    P_Zr96    :=
     51.45E-02  11.22E-02  17.15E-02 17.38E-02 2.80E-02  ;
REAL P_Fe54     P_Fe56     P_Fe57    P_Fe58    :=
     5.602E-02  91.865E-02 2.243E-02 0.290E-02 ;
*isot 5.8 %     91.72%     2.20%     0.28%
REAL P_Cr50     P_Cr52     P_Cr53    P_Cr54    :=
     4.178E-02  83.701E-02 9.673E-02 2.448E-02 ;
*isot 4.35%     83.79%     9.50%     2.36%
REAL P_Sn112    P_Sn114    P_Sn115   P_Sn116   P_Sn117
     P_Sn118    P_Sn119    P_Sn120   P_Sn122   P_Sn124   :=
     0.92E-02   0.62E-02   0.33E-02  14.21E-02 7.56E-02
     24.06E-02  8.59E-02   32.92E-02 4.75E-02  6.04E-02  ;

REAL M_Zr := P_Zr90 89.904711 * P_Zr91 90.905643 * P_Zr92 91.905039 *
             P_Zr94 93.906320 * P_Zr96 95.90829 * + + + + ;
REAL M_Fe := P_Fe54 53.9396148 * P_Fe56 55.9349421 * 
             P_Fe57 56.9353987 * P_Fe58 57.9332805 * + + + ;
REAL M_Cr := P_Cr50 49.9460496 * P_Cr52 51.9405119 * 
             P_Cr53 52.9406538 * P_Cr54 53.9388849 * + + + ;
REAL M_Sn := P_Sn112 111.90483 * P_Sn114 113.90278 * P_Sn115 114.90335 *
             P_Sn116 115.901748 * P_Sn117 116.902961 *
             P_Sn118 117.901613 * P_Sn119 118.903316 *
             P_Sn120 119.902207 * P_Sn122 121.903451 *
             P_Sn124 123.905283 * + + + + + + + + + ;

REAL rho_clad := 6.55 ;
REAL N_Zr_clad := P_Zr rho_clad A * M_Zr / * ;
REAL N_Zr90       N_Zr91       N_Zr92       N_Zr94       N_Zr96    :=
     N_Zr_clad P_Zr90 *  N_Zr_clad P_Zr91 *  N_Zr_clad P_Zr92 *
     N_Zr_clad P_Zr94 *  N_Zr_clad P_Zr96 *  ;

REAL N_Fe_clad := P_Fe rho_clad A * M_Fe / * ;
REAL N_Fe54         N_Fe56         N_Fe57         N_Fe58        :=
     N_Fe_clad P_Fe54 *  N_Fe_clad P_Fe56 *  N_Fe_clad P_Fe57 *
     N_Fe_clad P_Fe58 * ;

REAL N_Cr_clad := P_Cr rho_clad A * M_Cr / * ;
REAL N_Cr50         N_Cr52         N_Cr53         N_Cr54        :=
     N_Cr_clad P_Cr50 *  N_Cr_clad P_Cr52 *  N_Cr_clad P_Cr53 *
     N_Cr_clad P_Cr54 * ;

REAL N_O16G := P_O16 rho_clad A * 15.9949150 / * ;

REAL N_Sn_clad := P_Sn rho_clad A * M_Sn / * ;
REAL N_Sn112        N_Sn114        N_Sn115        N_Sn116
     N_Sn117        N_Sn118        N_Sn119        N_Sn120
     N_Sn122        N_Sn124        :=
     N_Sn_clad P_Sn112 * N_Sn_clad P_Sn114 * N_Sn_clad P_Sn115 *
     N_Sn_clad P_Sn116 * N_Sn_clad P_Sn117 * N_Sn_clad P_Sn118 *
     N_Sn_clad P_Sn119 * N_Sn_clad P_Sn120 * N_Sn_clad P_Sn122 *
     N_Sn_clad P_Sn124 * ;
 
*                                                                      ;
LIBRARY := LIB: ::
  EDIT 0
  DEPL LIB: DRAGON FIL: <<Library>>
  NMIX 40 CTRA APOL
  SUBG
  MIXS LIB: DRAGON FIL: <<Library>>

*----
*    Cladding TG et TI
*----
  MIX 1 <<TgainK>> NOEV
    Zr90g    = Zr90   <<N_Zr90>> 2
    Zr91g    = Zr91   <<N_Zr91>> 2
    Zr92g    = Zr92   <<N_Zr92>> 2
    Zr94g    = Zr94   <<N_Zr94>> 2
    Zr96g    = Zr96   <<N_Zr96>> 2
    Fe54g    = Fe54   <<N_Fe54>>
    Fe56g    = Fe56   <<N_Fe56>>
    Fe57g    = Fe57   <<N_Fe57>>
    Fe58g    = Fe58   <<N_Fe58>>
    Cr50g    = Cr50   <<N_Cr50>>
    Cr52g    = Cr52   <<N_Cr52>>
    Cr53g    = Cr53   <<N_Cr53>>
    Cr54g    = Cr54   <<N_Cr54>>
    O16Gg    = O16    <<N_O16G>>
    Sn112g   = Sn112  <<N_Sn112>>
    Sn114g   = Sn114  <<N_Sn114>>
    Sn115g   = Sn115  <<N_Sn115>>
    Sn116g   = Sn116  <<N_Sn116>>
    Sn117g   = Sn117  <<N_Sn117>>
    Sn118g   = Sn118  <<N_Sn118>>
    Sn119g   = Sn119  <<N_Sn119>>
    Sn120g   = Sn120  <<N_Sn120>>
    Sn122g   = Sn122  <<N_Sn122>>
    Sn124g   = Sn124  <<N_Sn124>>

*----
*    Cladding fuel
*----
  MIX 2 <<TgainK>> NOEV
    Zr90f    = Zr90   <<N_Zr90>> 3
    Zr91f    = Zr91   <<N_Zr91>> 3
    Zr92f    = Zr92   <<N_Zr92>> 3
    Zr94f    = Zr94   <<N_Zr94>> 3
    Zr96f    = Zr96   <<N_Zr96>> 3
    Fe54f    = Fe54   <<N_Fe54>>
    Fe56f    = Fe56   <<N_Fe56>>
    Fe57f    = Fe57   <<N_Fe57>>
    Fe58f    = Fe58   <<N_Fe58>>
    Cr50f    = Cr50   <<N_Cr50>>
    Cr52f    = Cr52   <<N_Cr52>>
    Cr53f    = Cr53   <<N_Cr53>>
    Cr54f    = Cr54   <<N_Cr54>>
    O16Gf    = O16    <<N_O16G>>
    Sn112f   = Sn112  <<N_Sn112>>
    Sn114f   = Sn114  <<N_Sn114>>
    Sn115f   = Sn115  <<N_Sn115>>
    Sn116f   = Sn116  <<N_Sn116>>
    Sn117f   = Sn117  <<N_Sn117>>
    Sn118f   = Sn118  <<N_Sn118>>
    Sn119f   = Sn119  <<N_Sn119>>
    Sn120f   = Sn120  <<N_Sn120>>
    Sn122f   = Sn122  <<N_Sn122>>
    Sn124f   = Sn124  <<N_Sn124>>

*----
*    Gap
*----
  MIX 3 <<TgainK>> NOEV
    Al27     = Al27    1.00E-08

*----
*    Moderator
*----
  MIX 4 <<TmodeK>> NOEV ! pastille d'U
    H1_H2O   = H1_H2O <<N_2H2O>>
    O16_H2O  = O16    <<N_H2O>> 
    B10      = B10    <<N_B10>> 
    B11      = B11    <<N_B11>> 
  MIX 5 
    COMB 4 1.0 ! pastille d'U (2)
  MIX 6 
    COMB 4 1.0 ! gaines T2
  MIX 7 
    COMB 4 1.0 ! gaines T1
  MIX 8
    COMB 4 1.0 ! gaine TI

*----
*    Fuel -> 8 * 4 evolving mixes
*----
  MIX 9 <<TfuelK>> <<den_mox1>>
    U234    = U234    <<X1_U4>>    
    U235    = U235    <<X1_U5>>   1
    U236    = U236    <<X1_U6>>   1
    U238    = U238    <<X1_U8>>   1
    Pu238   = Pu238   <<X1_Pu8>>   
    Pu239   = Pu239   <<X1_Pu9>>  1
    Pu240   = Pu240   <<X1_Pu0>>  1
    Pu241   = Pu241   <<X1_Pu1>>  1
    Pu242   = Pu242   <<X1_Pu2>>  1
    Am241   = Am241   <<X1_Am1>>   
    Np237   = Np237   <<X1_Np7>>
    O16     = O16     <<X1_O16>>
  MIX 10 
    COMB 9 1.0
  MIX 11 
    COMB 9 1.0
  MIX 12 
    COMB 9 1.0

  MIX 13 
    COMB 9 1.0
  MIX 14 
    COMB 9 1.0
  MIX 15 
    COMB 9 1.0
  MIX 16 
    COMB 9 1.0

  MIX 17 
    COMB 9 1.0
  MIX 18 
    COMB 9 1.0
  MIX 19 
    COMB 9 1.0
  MIX 20 
    COMB 9 1.0

  MIX 21 <<TfuelK>> <<den_mox2>>
    U234    = U234    <<X2_U4>>    
    U235    = U235    <<X2_U5>>   1
    U236    = U236    <<X2_U6>>   1
    U238    = U238    <<X2_U8>>   1
    Pu238   = Pu238   <<X2_Pu8>>   
    Pu239   = Pu239   <<X2_Pu9>>  1
    Pu240   = Pu240   <<X2_Pu0>>  1
    Pu241   = Pu241   <<X2_Pu1>>  1
    Pu242   = Pu242   <<X2_Pu2>>  1
    Am241   = Am241   <<X2_Am1>>   
    Np237   = Np237   <<X2_Np7>>
    O16     = O16     <<X2_O16>>
  MIX 22 
    COMB 21 1.0
  MIX 23 
    COMB 21 1.0
  MIX 24 
    COMB 21 1.0

  MIX 25 
    COMB 21 1.0
  MIX 26 
    COMB 21 1.0
  MIX 27 
    COMB 21 1.0
  MIX 28 
    COMB 21 1.0

  MIX 29 
    COMB 21 1.0
  MIX 30 
    COMB 21 1.0
  MIX 31 
    COMB 21 1.0
  MIX 32 
    COMB 21 1.0

  MIX 33 <<TfuelK>> <<den_mox3>>
    U234    = U234    <<X3_U4>>    
    U235    = U235    <<X3_U5>>   1
    U236    = U236    <<X3_U6>>   1
    U238    = U238    <<X3_U8>>   1
    Pu238   = Pu238   <<X3_Pu8>>   
    Pu239   = Pu239   <<X3_Pu9>>  1
    Pu240   = Pu240   <<X3_Pu0>>  1
    Pu241   = Pu241   <<X3_Pu1>>  1
    Pu242   = Pu242   <<X3_Pu2>>  1
    Am241   = Am241   <<X3_Am1>>   
    Np237   = Np237   <<X3_Np7>>
    O16     = O16     <<X3_O16>>
  MIX 34 
    COMB 33 1.0
  MIX 35 
    COMB 33 1.0
  MIX 36 
    COMB 33 1.0

  MIX 37 
    COMB 33 1.0
  MIX 38 
    COMB 33 1.0
  MIX 39 
    COMB 33 1.0
  MIX 40 
    COMB 33 1.0
;

END: ;
QUIT .
