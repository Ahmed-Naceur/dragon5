* Regression test for SENS: module based on a pin cell calculation.
* jef2.2 library used
* Running time = ~ 30sec
* C. Laville
*

LINKED_LIST
    GEOMETRY ASSEMBLY TRACKING FLUX MACLIB MICLIB EDIT
ADJ_ASSEMBLY ADJ_FLUX ADJ_MACLIB ADJ_EDIT ;
MODULE
    LIB: GEO: EDI: SYBILT: USS: ASM: FLU: END: T: DELETE: GREP:
    SENS: ;
PROCEDURE
  assertS ;    
REAL kiso b2 ;
SEQ_ASCII SENS.sdf :: FILE './SENS.sdf' ;
*
*--------------------------------------------------------
*                Calcul flux direct
*--------------------------------------------------------
*
MICLIB := LIB: ::
    NMIX 8
    CTRA APOL
    ANIS 6
    ADED 4 NELAS N4N N2N N3N
    SUBG
    MIXS LIB: DRAGON FIL: DLIB_J2
    MIX 1 293.0
        Pu238 7.38570E-06  1
        Pu239 4.20190E-03  1
        Pu240 5.60600E-04  1
        Pu241 8.75230E-05  1
        Pu242 1.69720E-05  1
        U235 1.22230E-04  1
        U238 1.68760E-02  1
        Am241 3.42580E-05  1
        O16 4.37130E-02
   MIX 2 COMB 1 1.0
   MIX 3 COMB 1 1.0
   MIX 4 COMB 1 1.0
   MIX 5 293.0
        O16 1.00000E-06
   MIX 6 293.0
        Fe54 3.20914E-03
        Fe56 5.07487E-02
        Fe57 1.21726E-03
        Fe58 1.54924E-04
        Cr50 6.20381E-04
        Cr52 1.20564E-02
        Cr53 1.37463E-03
        Cr54 3.42577E-04
        Ni58 6.89729E-03
        Ni60 2.66523E-03
        Ni61 1.20916E-04
        Ni62 3.71893E-04
        Ni64 1.09739E-04
        Mo0  2.11680E-03
        Mn55 1.69350E-03
        C0 6.77390E-05
        Si0 8.46730E-04
   MIX 7 293.0
        O16 3.33680E-02
        H1_H2O 6.67330E-02
   MIX 8 COMB 7 1.0
;
*
GEOMETRY := GEO: :: TUBE 8
  R+ REFL 
  RADIUS
  0.00000
  0.174666
  0.220937
  0.24076 
  0.247015
  0.254
  0.2921
  0.418214
  0.5374
  MIX  1 2 3 4 5 6 7 8 ;
*
TRACKING := SYBILT: GEOMETRY :: ;

MACLIB := USS: MICLIB TRACKING :: TRAN PASS 2 ;
*
ASSEMBLY := ASM: MACLIB TRACKING :: PIJ  ;
*
FLUX := FLU: ASSEMBLY MACLIB TRACKING :: TYPE K ;
GREP: FLUX :: GETVAL 'K-INFINITY' 1 1 1 >>kiso<< ;
IF kiso 1.0 > THEN
FLUX := DELETE: FLUX ;
FLUX := FLU: ASSEMBLY MACLIB TRACKING :: TYPE B B0TR ;
ENDIF ;
GREP: FLUX :: GETVAL 'B2  B1HOM' 1 1 1 >>b2<< ;
*
*--------------------------------------------------------
*                Calcul flux adjoint
*--------------------------------------------------------
*
ADJ_MACLIB := T: MACLIB ;
ADJ_ASSEMBLY := ASM: ADJ_MACLIB TRACKING :: PIJ  ;
*
ADJ_FLUX := FLU: ADJ_ASSEMBLY ADJ_MACLIB TRACKING ::
TYPE K B0TR PNL BUCK <<b2>> ;
*
*--------------------------------------------------------
*                Calcul de sensibilité
*--------------------------------------------------------
*
SENS.sdf := SENS: MACLIB TRACKING FLUX ADJ_FLUX ::
EDIT 10 ANIS 1 ;
*
*
assertS FLUX :: 'K-EFFECTIVE' 1 1.0 ;
END: ;
