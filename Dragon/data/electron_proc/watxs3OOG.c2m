*--------------------------------------------------------------------- 
*  Electron Source on 1D Slab Geometry (Water)
*  CEPXS-BFP library
*  Author: Ahmed Naceur.
*---------------------------------------------------------------------

* ---- Define STRUCTURES and MODULES used

MODULE       GEO: FMAC: MAC: SNT: ASM: FLU: OUT: HEAT: DELETE: PSOUR: 
             END: ;

PROCEDURE    assertS ;

LINKED_LIST  MACRO OLDMACRO GEOM TRACK SYSTEM FLUX DEPOS EEDIT GWT0 
             GEDIT ESOUR EEDITc ;

SEQ_ASCII    FMAC_M :: FILE 
'./FMAC_e_water_lobatto_10.00MeV_0g_300e_P9_NC.txt' ;
*'./FMAC_e_water_lobatto_10.00MeV_300g_300e_P9_FC.txt' ;

SEQ_ASCII    edep_xs.txt     :: FILE 'edep_xs.txt' ;
SEQ_ASCII    edit_xs.txt     :: FILE 'edit_xs.txt' ;
SEQ_ASCII    macro_xs.txt    :: FILE 'macro_xs.txt' ;
SEQ_ASCII    flux_xs.txt     :: FILE 'flux_xs.txt' ;
SEQ_ASCII    syst_xs.txt     :: FILE 'syst_xs.txt' ;
SEQ_ASCII    geom_xs.txt     :: FILE 'geom_xs.txt' ;
SEQ_ASCII    track_xs.txt    :: FILE 'track_xs.txt' ;

* ---- Geometry 

GEOM := GEO: :: CAR1D 1
           X- VOID  X+ VOID
           MIX  1
           MESHX 0.0 8.0
           SPLITX 100 ;

* ---- Tracking    

TRACK := SNT: GEOM ::
      TITLE 'Water-Slab: CEPXS-BFP-DRAGON'
      EDIT 0 MAXR 1000000 SCHM 1 DIAM 0 NLIVO SN 64 SCAT 9 EPSI 1.E-5
      MAXI 300 BFPG ;


* ---- Library access and structuring

MACRO    := FMAC: FMAC_M :: EDIT 2 PARTICLE B ;

macro_xs.txt  := MACRO ;

OLDMACRO := MACRO ;
MACRO    := MAC: MACRO OLDMACRO :: MIX 1 1 OLDL ;
OLDMACRO := DELETE: OLDMACRO ;


* ---- Electron beam definition

ESOUR := PSOUR: MACRO TRACK GEOM :: EDIT 2 MONO 1 X- INTG 1 1.0 
                                    DIR 1.0 0.0 0.0 ; 

* ---- Electron flux

SYSTEM := ASM: MACRO TRACK :: EDIT 1 ARM ;
FLUX   := FLU: MACRO TRACK SYSTEM ESOUR :: EDIT 1 TYPE S EXTE 100 ;

* --- Recover flux and reaction rates

EEDIT := OUT: FLUX TRACK MACRO GEOM :: EDIT 2 COND INTG NONE ;

 
* --- Energy deposition from the e-source

EEDITc := EEDIT ;
DEPOS  := HEAT: EEDIT EEDITc :: EDIT 2 ; 

* --- Save data structure for post-treatement 

edep_xs.txt   := DEPOS ; 
edit_xs.txt   := EEDIT ;  
track_xs.txt  := TRACK ;
geom_xs.txt   := GEOM ; 
syst_xs.txt   := SYSTEM ;
flux_xs.txt   := FLUX  ;

* --- Delete data structure 

EEDIT FLUX SYSTEM MACRO TRACK GEOM := DELETE: 
  EEDIT FLUX SYSTEM MACRO TRACK GEOM ; 

END: ; QUIT "LIST" .


