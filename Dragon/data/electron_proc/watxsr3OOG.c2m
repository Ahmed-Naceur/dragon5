*--------------------------------------------------------------------- 
*  Electron Source on 1D Slab Geometry
*  NJOY-[CEPXSR] library
*  Author: Ahmed Naceur
*---------------------------------------------------------------------

* --- Define STRUCTURES and MODULES used

MODULE       GEO: LIB: MAC: SNT: ASM: FLU: HEAT: OUT: DELETE: 
             END: EDI: PSOUR: ;
PROCEDURE    assertS ;
LINKED_LIST   MACRO DEPOS MICRO OLDMACRO GEOM TRACK SYSTEM FLUX 
              ESOUR EEDIT EDITION EEDITc ;

          
SEQ_ASCII    edep_xsr.txt   :: FILE 'edep_xsr.txt' ;
SEQ_ASCII    edit_xsr.txt   :: FILE 'edit_xsr.txt' ;
SEQ_ASCII    micr_xsr.txt   :: FILE 'micr_xsr.txt' ;
SEQ_ASCII    macr_xsr.txt   :: FILE 'macr_xsr.txt' ;
SEQ_ASCII    flux_xsr.txt   :: FILE 'flux_xsr.txt' ;
SEQ_ASCII    syst_xsr.txt   :: FILE 'syst_xsr.txt' ;
SEQ_ASCII    geom_xsr.txt   :: FILE 'geom_xsr.txt' ;
SEQ_ASCII    trac_xsr.txt   :: FILE 'trac_xsr.txt' ;

* --- Geometry 

GEOM := GEO: :: CAR1D 1
           X- VOID  X+ VOID
           MIX  1
           MESHX 0.0 8.0
           SPLITX 100 ;

* --- Tracking       

TRACK := SNT: GEOM ::
      TITLE 'Water-Slab: NJOY[CEPXSR]-DRAGON'
      EDIT 0 MAXR 1000000 SCHM 1 DIAM 0 NLIVO SN 64 SCAT 9 EPSI 1.E-5
      MAXI 300 BFPG ;

* --- Library access

MICRO  := LIB: :: 
          EDIT 99
          NMIX 1 
          ANIS 10
          MIXS LIB: MATXS2 FIL: _wat300g_10MeV 
          MIX  1  300.0  1.0 NOGAS
          Hnat = h 0.111894 
          Onat = o 0.888106 ;


* --- Library structuring 


MACRO := MICRO :: STEP UP MACROLIB ;

micr_xsr.txt   := MICRO ;
macr_xsr.txt   := MACRO ;


OLDMACRO := MACRO ;
MACRO := MAC: MACRO OLDMACRO :: MIX 1 1 OLDL ;
OLDMACRO := DELETE: OLDMACRO ;


* ---- Electron beam definition

ESOUR := PSOUR: MACRO TRACK GEOM :: EDIT 2 MONO 1 X- INTG 1 1.0 
                                    DIR 1.0 0.0 0.0 ; 

* ---- Electron flux

SYSTEM := ASM: MACRO TRACK :: EDIT 1 ARM ;
FLUX   := FLU: MACRO TRACK SYSTEM ESOUR :: EDIT 1 TYPE S EXTE 100 ;

* --- Recover flux and reaction rates

EEDIT := OUT: FLUX TRACK MACRO GEOM :: EDIT 2 COND INTG NONE ;


* --- Energy deposition from the e-source

EEDITc := EEDIT ;
DEPOS  := HEAT: EEDIT EEDITc :: EDIT 2 ;

* --- Save data structure for post-treatement 

edep_xsr.txt   := DEPOS ; 
edit_xsr.txt   := EEDIT ;  
trac_xsr.txt   := TRACK ;
geom_xsr.txt   := GEOM ; 
syst_xsr.txt   := SYSTEM ;
flux_xsr.txt   := FLUX  ;

* --- Delete data structure 

EEDIT FLUX SYSTEM MACRO MICRO TRACK GEOM := DELETE: 
  EEDIT FLUX SYSTEM MACRO MICRO TRACK GEOM ; 

END: ; QUIT "LIST" .


