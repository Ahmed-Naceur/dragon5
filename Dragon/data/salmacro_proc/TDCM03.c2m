*----
*  TEST CASE TDCM03
*  MACROSCOPIC CROSS SECTIONS
*  2-D CARTESIAN CELL 
*  REFLECTIVE (WHITE) BOUNDARY CONDITIONS 
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  MACRO GEOM TRACK SYS FLUX ;
SEQ_BINARY
  SEQTRK ;
MODULE
  GEO: G2S: SALT: MAC: ASM: FLU: TLM: END: ;
SEQ_ASCII Fil_SAL.tdt :: FILE './Fil_SAL_TDCM03.tdt' ;
SEQ_ASCII Fil_SAL.ps :: FILE './Fil_SAL_TDCM03.ps' ;
INTEGER an2d := 8 ;
REAL densur := 20.0 ;
PROCEDURE assertS ;
SEQ_ASCII Lines3.m :: FILE './Lines3.m' ;
*----
* Macroscopic XS
*----
MACRO :=  MAC: ::
  NGRO 1 NMIX 2 NIFI 1
  READ INPUT
  MIX 1 TOTAL  1.250  SCAT 1 1  1.242  
        NUSIGF 0.37  CHI 1.0
  MIX 2 TOTAL  0.625  SCAT 1 1  0.355
  ;
*----
GEOM := GEO: :: CAR2D 5 5 (*5 X 5 ASSEMBLY*)
          X- ALBE 1.0 X+ ALBE 1.0
          Y- ALBE 1.0 Y+ ALBE 1.0
          CELL C1 C1 C1 C1 C1
               C1 C1 C1 C1 C1
               C1 C1 C1 C1 C1
               C1 C1 C1 C1 C1
               C1 C1 C1 C1 C1
          ::: C1 := GEO: CARCEL 1
              RADIUS 0.000 0.450
              MIX 1  2
              MESHX -0.625  0.625
              MESHY -0.625  0.625 ;
          ;

Fil_SAL.tdt Fil_SAL.ps := G2S: GEOM ;
TRACK SEQTRK := SALT: Fil_SAL.tdt ::
  EDIT 1
  TISO <<an2d>> <<densur>> REND LONG ;

Lines3.m := TLM: TRACK SEQTRK ::
  EDIT 1
  NTPO 1
  DIRECTIONS NoPause DIR 0 ;

SYS := ASM: MACRO TRACK SEQTRK ;
*---- 
*  Solution : K-EFFECTIVE
*----
FLUX := FLU: SYS MACRO TRACK ::
  TYPE K
  ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.088356 ;
ECHO "test TDCM03 completed" ;
END: ;
QUIT "LIST" .
