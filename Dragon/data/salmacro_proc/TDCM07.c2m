*----
*  TEST CASE TDCM07
*  MACROSCOPIC CROSS SECTIONS
*  FISSION SOURCE PROBLEM
*  2-D CARTESIAN CELL 
*  REFLECTIVE AND VOID BOUNDARY CONDITIONS 
*
*  REF: none 
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST 
  MACRO LATGEOR LATREGR2 SYSR FLUXR EDITR 
        LATGEOV LATREGV2 SYSV FLUXV EDITV ;
SEQ_BINARY
  TRKR2 TRKV2 ;
SEQ_ASCII
  Fil_SAL Fil_SAL.ps ;
MODULE
  MAC: GEO: G2S: SALT: ASM: FLU: EDI: DELETE: END: ;
PROCEDURE assertS ;
*----
* Macroscopic XS
*----
MACRO := MAC: ::
  NGRO 1 NMIX 2 NIFI 1
  READ INPUT 
  MIX  1 TOTAL 0.75 SCAT 1 1 0.50 NUSIGF 1.00  CHI 1.0                
  MIX  2 TOTAL 0.75 SCAT 1 1 0.50                
  ;
*----
*  Geometry : LATGEOR - Cartesian 2D with reflection BC
*             LATGEOR - Cartesian 2D with void BC
*  Tracking : EXCELT 
*----
LATGEOR  := GEO: :: CAR2D 2 2
  X- REFL X+ REFL MESHX 0.00 1.0 2.00 SPLITX  3 3  
  Y- REFL Y+ REFL MESHY 0.00 1.0 2.00 SPLITY  3 3
  MIX  1  2  2  2  ;
LATGEOV  := GEO: LATGEOR  ::
  X- VOID X+ VOID Y- VOID Y+ VOID  ;
Fil_SAL Fil_SAL.ps := G2S: LATGEOR ;
LATREGR2 TRKR2 := SALT: Fil_SAL ::
     EDIT 1
     TSPC 12 20.0
;
Fil_SAL Fil_SAL.ps := DELETE: Fil_SAL Fil_SAL.ps ;
Fil_SAL Fil_SAL.ps := G2S: LATGEOV ;
LATREGV2 TRKV2 := SALT: Fil_SAL ::
     EDIT 1
     TISO 49 20.0
;
*---- 
*  Solution : TYPE K
*----
SYSR := ASM: MACRO LATREGR2 TRKR2 ;
FLUXR := FLU: SYSR MACRO LATREGR2 :: 
  TYPE K ACCE 3 0 ;
assertS FLUXR :: 'K-EFFECTIVE' 1 1.635383 ;
EDITR := EDI: FLUXR MACRO LATREGR2 ::
  EDIT 1 ;
SYSV := ASM: MACRO LATREGV2 TRKV2 ;
FLUXV := FLU: SYSV MACRO LATREGV2 :: 
  TYPE K ACCE 3 0 ;
assertS FLUXV :: 'K-EFFECTIVE' 1 0.6699510 ;
EDITV := EDI: FLUXV MACRO LATREGV2 ::
  EDIT 1 ;
LATGEOR LATREGR2 SYSR FLUXR EDITR TRKR2
   LATGEOV LATREGV2 SYSV FLUXV EDITV TRKV2 := DELETE: 
LATGEOR LATREGR2 SYSR FLUXR EDITR TRKR2
   LATGEOV LATREGV2 SYSV FLUXV EDITV TRKV2 ;
MACRO := DELETE: MACRO ;
ECHO "test TDCM07 completed" ;
END: ;
QUIT "LIST" .
