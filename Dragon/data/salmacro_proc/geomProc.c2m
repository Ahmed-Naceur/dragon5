****************************************************************
*                                                              *
* Procedure :  geomProc.c2m                                    *
* Purpose   :  flux calculation using a SALOME geometry        *
* Author    :  A. Hebert                                       *
*                                                              *
* CALL      :                                                  *
*  Flux := geomProc GeoSurfacic Macro :: (data) ;              *
*                                                              *
*  Output object:                                              *
*    Flux       : Flux                                         *
*                                                              *
*  Input object:                                               *
*    GeoSurfacic: surfacic file generated by SALOME            *
*    Macro      : macrolib                                     *
*                                                              *
*  Input data:                                                 *
*    delr       : distance between tracks                      *
*    an2d       : number of angles in radial plane             *
*                                                              *
****************************************************************
PARAMETER Flux GeoSurfacic Macro ::
  ::: LINKED_LIST Flux Macro ;
  ::: SEQ_ASCII GeoSurfacic ; ;
MODULE SALT: MCCGT: ASM: FLU: END: ;
LINKED_LIST TRACK ABC FLUX ;
SEQ_BINARY TF_EXC ;
INTEGER an2d ; :: >>an2d<< ;
REAL delr ; :: >>delr<< ;
REAL densur := 1.0 delr / ;

TRACK TF_EXC := SALT: GeoSurfacic ::
     EDIT 5
     TSPC EQW2 <<an2d>> <<densur>> REND
;
TRACK := MCCGT: TRACK TF_EXC ::
  CACB 4 AAC 80 TMT EPSI 1E-5 MCU 2500 ;
ABC := ASM: Macro TRACK TF_EXC :: ARM EDIT 1 ;    
Flux := FLU: Macro ABC TRACK TF_EXC :: 
     EDIT 1 TYPE K ;
END: ;
