*----
*  TEST CASE TDCM02
*  MACROSCOPIC CROSS SECTIONS
*  FIXED SOURCE PROBLEM WITH SAL
*  FOR 1/8 7X7 PWR ASSEMBLY
*
*  REF: Z. Stankovski, Nucl. Sci. Eng. 92, 255 (1986)
*       R. Roy et al. Advances in Mathematics, Computation
*       and Reactor Physics, April 28 - May 2 1991, Pittsburgh
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST 
  PWR T_EXC MACRO SYS FLUX EDITION ;
SEQ_BINARY 
  TF_EXC ;
SEQ_ASCII
  Fil_SAL Fil_SAL.ps  ;
MODULE 
  GEO: G2S: SALT: MAC: ASM: FLU: EDI: END: DELETE: ;
PROCEDURE assertV ;
*----
* Macroscopic XS
*----
MACRO := MAC: ::
  NGRO 1 NMIX 3
  READ INPUT
  MIX 1 TOTAL  1.250  SCAT 1 1  1.242  FIXE 1.000
  MIX 2 TOTAL  0.625  SCAT 1 1  0.355  FIXE 0.000
  MIX 3 TOTAL 14.000  SCAT 1 1  0.000  FIXE 0.000
  ;
*----
*  Geometry : PWR - Cartesian 4X4
*  Tracking : SAL
*----
PWR := GEO: :: CAR2D 4 4
  X- DIAG  X+ ALBE 1.0 Y- SYME  Y+ DIAG
  CELL   P F F F
           F F F
             F F
               F 
  ::: F := GEO: CARCEL 1
    RADIUS 0.000 0.450 MIX 2  1
    MESHX -0.625  0.625 SPLITX 2
    MESHY -0.625  0.625 SPLITY 2               
    ;
  ::: P := GEO: F
    MIX  3  1  SPLITR 3 
    ;
  ;
Fil_SAL Fil_SAL.ps := G2S: PWR ;
T_EXC TF_EXC := SALT: Fil_SAL ::
     EDIT 3
     TISO 2 10.0
     LONG
;
*---- 
*  Solution : FIXED SOURCE PROBLEM
*  Editing  : Absorption rates taken from STANKOVSKI
*    cell 1 = 0.83799 0.00689 cell 2 = 0.73979 0.03571
*    cell 3 = 0.82218 0.03991 cell 4 = 0.85166 0.04104
*    cell 5 = 0.78722 0.03824 cell 6 = 1.67049 0.08092
*    cell 7 = 1.71199 0.08252 cell 8 = 0.85350 0.04120
*    cell 9 = 1.72122 0.08328 cell 10= 0.86023 0.04174
*    NOTE: There is a factor 4.0 with the EDI: results of DRAGON
*----
*----
*  USE EXCELP DOOR
*----
SYS := ASM: MACRO T_EXC TF_EXC :: PIJ ;
FLUX := FLU: MACRO SYS T_EXC TF_EXC :: 
     EDIT 1 TYPE S ;
assertV FLUX :: 'FLUX' (*GROUP*) 1 (*REGION*) 10 4.803257 ;
EDITION := EDI: FLUX MACRO T_EXC Fil_SAL ::
  EDIT 2 SAVE
  MERGE G2S 3 RECT 1.875 4.375 0.0 1.875
              TRIA 0.0 0.0 1.875 0.0 1.875 1.875
              TRIA 1.875 1.875 4.375 1.875 4.375 4.375
  ;
ECHO "test TDCM02 completed" ;
END: ;
QUIT "LIST" .
