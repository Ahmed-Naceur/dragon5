*DECK GeoNG.c2m
*----------
*  
*  Description de la procedure : 
*
*    GEOMC GEOMV := GeoNG :: <<TubeCal>> <<PasReseau>>
*     ;
*
*  GEOMF         : Structure geometrie pour 
*                  flux avec caloporteur
*  GEOMS         : Structure geometrie pour 
*                  auto-protection avec caloporteur      
*  TubeCal       : Rayon externe du tube de calandre en cm 
*  PasReseau     : Pas de reseau
* 
*----------
* Definition des parametres de la procedure
* 
PARAMETER
  GEOMF GEOMS
  ::
    EDIT 1
    ::: LINKED_LIST GEOMF GEOMS ;
  ;
*
*  Lire pas de reseau
*
REAL  
  TubeCal PasReseau 
  ;
::
  >>TubeCal<< >>PasReseau<<
  ;  
* 
*  Structures de donnees locales et
*  modules et parametres
*
MODULE
  GEO:
  ;
*
*  Rayon Final =  PasReseau * sqrt(1/Pi)
*
ECHO "Tube de calandre  ="  TubeCal    "cm" ;
ECHO "Pas de reseau     ="  PasReseau  "cm" ;  
REAL  
  Rtpi    Rtpo     Rgapo    Rtco    := 
  5.16890 5.7689   7.5      7.8
  ;
REAL  Delt  := Rtco Rgapo - ;
EVALUATE Rtco Rgapo :=
  TubeCal TubeCal Delt -     ;
REAL  
  RayonFinal 
  ; 
EVALUATE
  RayonFinal := PasReseau 0.67 *
  ;
REAL 
  Rci   Rgi   Rce   Rge   
  sub1
  sub2
  sub3

  subis1
  subis2
  subis3
  subis4
  subis5

  subisc1
  subisc2
  subisc3
  subisc4
  subisc5
  subisc6
  subisc7
  subisc8
  subisc9
  :=
  0.627 0.675 0.533 0.575
  0.70710681186550
  0.89442719099992
  0.97467943448090

  0.63245553203368
  0.83666002653408
  0.89442719099992
  0.94868329805051
  0.97467943448090

  0.5
  0.670820393
  0.774596669
  0.836660027
  0.866025404
  0.894427191
  0.921954446
  0.94868329805051
  0.97467943448090
  ;
REAL
  Rci1 Rci2 Rci3 Rce1 Rce2 Rce3 
  Rcib1 Rcib2 Rcib3 Rcib4 Rcib5 
  Rcic1 Rcic2 Rcic3 Rcic4 Rcic5 Rcic6 Rcic7 Rcic8 Rcic9 :=
  Rci sub1 *
  Rci sub2 *
  Rci sub3 *
  Rce sub1 *
  Rce sub2 *
  Rce sub3 *
  Rci subis1 *
  Rci subis2 *
  Rci subis3 *
  Rci subis4 *
  Rci subis5 *
  Rci subisc1 *
  Rci subisc2 *
  Rci subisc3 *
  Rci subisc4 *
  Rci subisc5 *
  Rci subisc6 *
  Rci subisc7 *
  Rci subisc8 *
  Rci subisc9 *
  ;
INTEGER NbAzAng := 7 ;
REAL AzAng := 20.0 ;
*
*  Geometrie avec caloporteur fine
*
GEOMF := GEO: :: CAR2D 1 1
     X- REFL  X+ REFL
     Y- REFL  Y+ REFL
     MIX  C1
     ::: C1 := GEO: CARCEL 8
       MESHX  0.0 <<PasReseau>>
       MESHY  0.0 <<PasReseau>>
       RADIUS  0.0   0.8670   2.4545   3.7295   <<Rtpi>> 
               <<Rtpo>> <<Rgapo>> <<Rtco>> <<RayonFinal>>
       SPLITR         1        2         2        2 
                      1        1         1        10
       MIX           18       18        18       18
                     17       20        17       19    19
       CLUSTER ROD1 ROD2 ROD3 ROD4
       ::: ROD1 := GEO: TUBE 5  MIX  1 2 3 4 17  
            RADIUS 0.00000 <<Rci1>> <<Rci2>> <<Rci3>>
                                     <<Rci>> <<Rgi>>
            NPIN  1 RPIN 0.0000 APIN 0.0000    ;
       ::: ROD2 := GEO: TUBE 5  MIX  5 6 7 8 17
            RADIUS 0.00000 <<Rci1>> <<Rci2>> <<Rci3>>
                                    <<Rci>> <<Rgi>>
            NPIN  7 RPIN 1.7340 APIN 0.0000    ;
       ::: ROD3 := GEO: TUBE 5  MIX  9 10 11 12 17
            RADIUS 0.00000  <<Rce1>> <<Rce2>> <<Rce3>>
                                    <<Rce>> <<Rge>>
            NPIN 14 RPIN 3.0750 APIN 0.224399475  ;
       ::: ROD4 := GEO: TUBE 5  MIX  13 14 15 16 17
            RADIUS 0.00000  <<Rce1>> <<Rce2>> <<Rce3>>
                                    <<Rce>> <<Rge>>
            NPIN 21 RPIN 4.3840 APIN 0.0       ;
  ;
  ;
*
*  Geometrie avec caloporteur fine
*
GEOMS := GEO: :: CAR2D 1 1
     X- REFL  X+ REFL
     Y- REFL  Y+ REFL
     MIX  C1
     ::: C1 := GEO: CARCEL 8
       MESHX  0.0 <<PasReseau>>
       MESHY  0.0 <<PasReseau>>
       RADIUS  0.0   0.8670   2.4545   3.7295   <<Rtpi>> 
               <<Rtpo>> <<Rgapo>> <<Rtco>> <<RayonFinal>>
       MIX           18       18        18       18
                     17       20        17       19    19
       CLUSTER ROD1 ROD2 ROD3 ROD4
       ::: ROD1 := GEO: TUBE 5  MIX  1 2 3 4 17  
            RADIUS 0.00000 <<Rci1>> <<Rci2>> <<Rci3>>
                                     <<Rci>> <<Rgi>>
            NPIN  1 RPIN 0.0000 APIN 0.0000    ;
       ::: ROD2 := GEO: TUBE 5  MIX  5 6 7 8 17
            RADIUS 0.00000 <<Rci1>> <<Rci2>> <<Rci3>>
                                    <<Rci>> <<Rgi>>
            NPIN  7 RPIN 1.7340 APIN 0.0000    ;
       ::: ROD3 := GEO: TUBE 5  MIX  9 10 11 12 17
            RADIUS 0.00000  <<Rce1>> <<Rce2>> <<Rce3>>
                                    <<Rce>> <<Rge>>
            NPIN 14 RPIN 3.0750 APIN 0.224399475  ;
       ::: ROD4 := GEO: TUBE 5  MIX  13 14 15 16 17
            RADIUS 0.00000  <<Rce1>> <<Rce2>> <<Rce3>>
                                    <<Rce>> <<Rge>>
            NPIN 21 RPIN 4.3840 APIN 0.0       ;
  ;
  ;
*  
*  terminer execution
*
QUIT "LIST" .
