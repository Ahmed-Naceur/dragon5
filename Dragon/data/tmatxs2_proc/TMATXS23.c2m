*----
*  TEST CASE TMATXS23
*  electr generated microlib
*  1D slab sn electron testcase from ROZ6
*----
*----
*  Define STRUCTURES and MODULES used
*----
MODULE GEO: LIB: MAC: SNT: ASM: FLU: OUT: DELETE: END: ;
MODULE ABORT: ;
PROCEDURE assertS ;
LINKED_LIST MICRO MACRO MACRO2 GEOM TRACK SYSTEM FLUX EEDITS GWT0 ;
*----
*  Set and track the geometry
*----
GEOM := GEO: :: CAR1D 2
           X- VOID  X+ VOID
           MIX  1 2
           MESHX 0.0 0.3 4.0
           SPLITX 30 70
           ;
TRACK := SNT: GEOM ::
      TITLE 'HOMOGENIZED W_11Mev_12e_CSD BENCHMARK WITH MATXS DATA'
      EDIT 2 MAXR 1000 NLIVO SN 16 SCAT 8 EPSI 1.E-4 MAXI 300 BFPL ;
*----
*  Compute the electron flux
*----
MICRO := LIB: ::
  EDIT 99
  NMIX 1
  ANIS 8
  MIXS LIB: MATXS2 FIL: _matxsW
  MIX 1 300.0 19.3 NOGAS
    W0      = w      1.0
;

MACRO := MICRO :: STEP UP MACROLIB ;
MACRO2 := MACRO ;
MACRO := MAC: MACRO MACRO2 ::
     MIX 2 1 OLDL
     ;
MACRO2 := DELETE: MACRO2 ;
REAL qsour := 1.E10 2.0 / $Pi_R / ;
ECHO "qsour=" qsour ;
MACRO := MAC: MACRO ::
     READ INPUT
     MIX 1 FIXE
     <<qsour>> 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     ;
SYSTEM := ASM: MACRO TRACK :: EDIT 2 ARM ;
FLUX := FLU: MACRO TRACK SYSTEM :: EDIT 2 TYPE S EXTE 100 ;
EEDITS := OUT: FLUX TRACK MACRO GEOM ::
    EDIT 2 COND NONE SOUR 0.0518134 INTG NONE ! 1/(\ro_{W} g/cm^{3})
    ;
EEDITS := DELETE: EEDITS ;
EEDITS := OUT: FLUX TRACK MACRO GEOM ::
    EDIT 2 COND SOUR 0.0518134 INTG NONE ! 1/(\ro_{W} g/cm^{3})
    ;
*----
*  Asserts
*----
GWT0 := EEDITS :: STEP UP GROUP STEP AT 1 ;
assertS GWT0 :: 'FLUX-INTG' 1 3.056295E-4 ;
assertS GWT0 :: 'FLUX-INTG' 5 5.433087E-4 ;
assertS GWT0 :: 'FLUX-INTG' 10 6.579186E-4 ;
assertS GWT0 :: 'FLUX-INTG' 15 6.961873E-4 ;
assertS GWT0 :: 'FLUX-INTG' 20 6.769511E-4 ;
assertS GWT0 :: 'FLUX-INTG' 25 6.001669E-4 ;
assertS GWT0 :: 'FLUX-INTG' 30 4.418837E-4 ;
*
ECHO "test TMATXS23 completed" ;
END: ;
