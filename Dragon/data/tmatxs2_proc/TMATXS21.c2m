*
* 1 Fuel Cell geometry
* Author: Dusan Calic
* Institute Jozef Stefan
* June 2011
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  LIBRARY LIBRARY2 GEOM TRACK SYS FLUX ;
MODULE
  GEO: NXT: LIB: USS: ASM: FLU: END: ;
INTEGER COMB0101 COMB0102 COMB0103 COMB0104 GAIN1
        MODE1 MODE2 := 
        1 2 3 4 5 6 7  ; (* DISTRIBUTED SELF-SHIELDING *)
REAL RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 ;
REAL P_Zr90     P_Zr91     P_Zr92    P_Zr94    P_Zr96    :=
     51.45E-02  11.22E-02  17.15E-02 17.38E-02 2.80E-02  ;
REAL N_Zr0 := 4.3241E-2 ;
REAL N_Zr90       N_Zr91       N_Zr92       N_Zr94       N_Zr96    :=
     N_Zr0 P_Zr90 *  N_Zr0 P_Zr91 *  N_Zr0 P_Zr92 *  N_Zr0 P_Zr94 *
     N_Zr0 P_Zr96 *  ;
SEQ_BINARY TRKFL ;
PROCEDURE assertS ;
*----
* Microscopic cross sections from file iaea
* 172-group WIMSD-formatted library based on the ENDF/B-VII.0
*----
LIBRARY := LIB: ::
  EDIT 2 
  NMIX 7
  CTRA NONE
  SUBG      (*HELIOS TYPE PROBABILITY TABLES*)
  MIXS LIB: MATXS2 FIL: _matxsH2O
   MIX 1 300.0
     H1_H2O  = H1_H2O   4.51290E-02 THER 80 HH2O
  MIXS LIB: MATXS2 FIL: _matxsU238
   MIX 1 300.0
     U238  = U238   2.2604E-2 1 THER 80 FREE
  MIXS LIB: DRAGON FIL: DLIB_J2
   MIX <<COMB0101>> 300.0
     U235   = U235     1.09380E-03 1 IRSET 0.0 81
 MIX <<COMB0102>> COMB <<COMB0101>> 1.0 (*COMB0102*)
 MIX <<COMB0103>> COMB <<COMB0101>> 1.0 (*COMB0103*)
 MIX <<COMB0104>> COMB <<COMB0101>> 1.0 (*COMB0104*)
 MIX <<GAIN1>> 293.0 (*GAIN1*)
    Zr90    = Zr90    <<N_Zr90>>
    Zr91    = Zr91    <<N_Zr91>>
    Zr92    = Zr92    <<N_Zr92>>
    Zr94    = Zr94    <<N_Zr94>>
    Zr96    = Zr96    <<N_Zr96>>
 MIX <<MODE1>> 293.0 (*MODE1*)
    H1      = H1_H2O 4.6892E-2
    O16     = O16    2.3446E-2
 MIX <<MODE2>> 293.0 (*MODE2*)
    H1      = H1_H2O 4.6892E-2
    O16     = O16    2.3446E-2
 ;
*----
*  Geometry GEOM
*----
GEOM := GEO: :: CARCEL 6
  X- REFL X+ REFL    Y- REFL Y+ REFL
  MESHX 0.0 1.2  MESHY 0.0 1.2
  RADIUS 0.0 0.288712 0.365195 0.397962
         0.4083 0.45 0.5748331
  MIX 1 2 3 4 5 6 7 ;
TRACK TRKFL := NXT: GEOM ::
  TISO 20 15.0 ;
*----
*  Transport and flux calculations
*----
LIBRARY2 := USS: LIBRARY TRACK TRKFL :: EDIT 1 TRAN PASS 2 PIJ ;
SYS := ASM: LIBRARY2 TRACK TRKFL :: PIJ ;
FLUX := FLU: LIBRARY2 TRACK SYS :: TYPE K ;
*----
* End
*----
assertS FLUX :: 'K-EFFECTIVE' 1 1.459458 ;
ECHO "test TMATXS21 completed" ;
END: ;
QUIT "LIST" .
