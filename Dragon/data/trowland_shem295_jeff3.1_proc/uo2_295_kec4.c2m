*----
*
*  TEST CASE uo2_295_kec4
*  UO2 ROWLAND'S BENCHMARK
*  DISTRIBUTED SELF-SHIELDING
*  295-GROUP JEF3.1.1 DRAGLIB
*
*  Author: A. Hebert
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM TRACK_SS TRACK LIBRARY LIBRARY2 CP CALC ;
SEQ_ASCII UOX_TBH ;
SEQ_BINARY TF_EXC ;
MODULE LIB: GEO: SYBILT: G2S: SALT: MAC: USS: TONE: ASM: FLU: DELETE: ;
INTEGER COMB0101 COMB0102 COMB0103 COMB0104 COMB0105 COMB0106 GAIN1
        MODE1 := 
        1 2 3 4 5 6 7 8 ; (* DISTRIBUTED SELF-SHIELDING *)
REAL RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 ;
PROCEDURE assertS ;
REAL P_Zr90     P_Zr91     P_Zr92    P_Zr94    P_Zr96    :=
     51.45E-02  11.22E-02  17.15E-02 17.38E-02 2.80E-02  ;
REAL N_Zr0 := 4.3241E-2 ;
REAL N_Zr90       N_Zr91       N_Zr92       N_Zr94       N_Zr96    :=
     N_Zr0 P_Zr90 *  N_Zr0 P_Zr91 *  N_Zr0 P_Zr92 *  N_Zr0 P_Zr94 *
     N_Zr0 P_Zr96 *  ;
INTEGER an2d := 12 ;
REAL densur := 20.0 ;
*
EVALUATE RCOMB6 := 0.4 ;
EVALUATE RCOMB1 := 0.4 SQRT RCOMB6 * ;
EVALUATE RCOMB2 := 0.7 SQRT RCOMB6 * ;
EVALUATE RCOMB3 := 0.8 SQRT RCOMB6 * ;
EVALUATE RCOMB4 := 0.9 SQRT RCOMB6 * ;
EVALUATE RCOMB5 := 0.95 SQRT RCOMB6 * ;
GEOM := GEO: :: CARCEL 8
      X- REFL X+ REFL Y- REFL Y+ REFL
      RADIUS 0.0 <<RCOMB1>> <<RCOMB2>> <<RCOMB3>> <<RCOMB4>>
                 <<RCOMB5>> <<RCOMB6>> 0.45 0.5748331
      MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
          <<COMB0105>> <<COMB0106>> <<GAIN1>> <<MODE1>> <<MODE1>>
      MESHX 0.0 1.2 MESHY 0.0 1.2
          ;
TRACK_SS := SYBILT: GEOM ::
     TITLE 'UO2 ROWLAND S BENCHMARK'
     MAXR 20 QUA2 20 3 DP01 MAXZ 200000 ;
UOX_TBH := G2S: GEOM ;
TRACK TF_EXC := SALT: UOX_TBH ::
  EDIT 3
  ALLG
  TSPC <<an2d>> <<densur>> REND
;
*
* Subgroup projection method
LIBRARY := LIB: ::
 EDIT 1
 NMIX 8    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 ADED 4 NELAS N4N N2N N3N
 CALENDF 3 PT      (*CALENDF TYPE PROBABILITY TABLES*)
*
 MIXS LIB: DRAGON FIL: DLIB_295
 MIX <<COMB0101>> 600.0 (*COMB0101*)
    O16     = O16    4.6624E-2
    U235    = U235   7.0803E-4 1 IRSET PT 1
    U238    = U238   2.2604E-2 1 IRSET PT 1
 MIX <<COMB0102>> COMB <<COMB0101>> 1.0 (*COMB0102*)
 MIX <<COMB0103>> COMB <<COMB0101>> 1.0 (*COMB0103*)
 MIX <<COMB0104>> COMB <<COMB0101>> 1.0 (*COMB0104*)
 MIX <<COMB0105>> COMB <<COMB0101>> 1.0 (*COMB0105*)
 MIX <<COMB0106>> COMB <<COMB0101>> 1.0 (*COMB0106*)
 MIX <<GAIN1>> 600.0 (*GAIN1*)
    Zr90    = Zr90   <<N_Zr90>> 2 IRSET PT 1
    Zr91    = Zr91   <<N_Zr91>> 2 IRSET PT 1
    Zr92    = Zr92   <<N_Zr92>> 2 IRSET PT 1
    Zr94    = Zr94   <<N_Zr94>>
    Zr96    = Zr96   <<N_Zr96>>
 MIX <<MODE1>> 600.0 (*MODE1*)
    H1      = H1_H2O 4.6892E-2
    O16     = O16    2.3446E-2
 ;
LIBRARY2 := USS: LIBRARY TRACK_SS :: EDIT 1 TRAN PASS 3 GRMIN 52 ;
CP := ASM: LIBRARY2 TRACK TF_EXC :: EDIT 1 PIJ ;
CALC := FLU: CP LIBRARY2 TRACK :: EDIT 1 TYPE K ;
assertS CALC :: K-EFFECTIVE 1 1.314346 ;
*
* Tone's method
LIBRARY CP CALC := DELETE: LIBRARY CP CALC ;
LIBRARY := LIB: ::
 EDIT 1
 NMIX 8    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 ADED 4 NELAS N4N N2N N3N
*
 MIXS LIB: DRAGON FIL: DLIB_295
 MIX <<COMB0101>> 600.0 (*COMB0101*)
    O16     = O16    4.6624E-2
    U235    = U235   7.0803E-4 1
    U238    = U238   2.2604E-2 1
 MIX <<COMB0102>> COMB <<COMB0101>> 1.0 (*COMB0102*)
 MIX <<COMB0103>> COMB <<COMB0101>> 1.0 (*COMB0103*)
 MIX <<COMB0104>> COMB <<COMB0101>> 1.0 (*COMB0104*)
 MIX <<COMB0105>> COMB <<COMB0101>> 1.0 (*COMB0105*)
 MIX <<COMB0106>> COMB <<COMB0101>> 1.0 (*COMB0106*)
 MIX <<GAIN1>> 600.0 (*GAIN1*)
    Zr90    = Zr90   <<N_Zr90>> 2
    Zr91    = Zr91   <<N_Zr91>> 2
    Zr92    = Zr92   <<N_Zr92>> 2
    Zr94    = Zr94   <<N_Zr94>>
    Zr96    = Zr96   <<N_Zr96>>
 MIX <<MODE1>> 600.0 (*MODE1*)
    H1      = H1_H2O 4.6892E-2
    O16     = O16    2.3446E-2
 ;
LIBRARY := TONE: LIBRARY TRACK_SS :: EDIT 1 TRAN MXIT 3 SPH GRMIN 52 ;
CP := ASM: LIBRARY TRACK TF_EXC :: EDIT 1 PIJ ;
CALC := FLU: CP LIBRARY TRACK :: EDIT 1 TYPE K ;
assertS CALC :: K-EFFECTIVE 1 1.313378 ;
ECHO "test uo2_295_kec4 completed" ;
QUIT "LIST" .
