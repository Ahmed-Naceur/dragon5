*----
*  TEST CASE TCWU12
*  iaea WLUP Library
*  GENERATE A LIBRARY USING INFO AND OTHER OPTIONS
*
*  REF: None
*
*----
*  Define variables and initialize
*    Coolant properties
*    a) Input
*       TempCool    = Coolant temperature (K)
*       Purity      = D2/(D2+H1) Weight % ratio in Coolant
*    b) Output
*       DensCool    = Coolant Density (g/cm**3)
*       WH1C        = H1 Weight % in Coolant
*       WD2C        = D2 Weight % in Coolant
*       WO16C       = O16 Weight % in Coolant
*    Fuel properties
*    a) Input
*       TempFuel    = Fuel temperature  (K)
*       Enrichment  = U235/(U235+U238) Weight % ratio in Fuel
*       DensFuel    = Fuel Density (g/cm**3)
*    b) Output
*       WU235F      = U235 Weight % in Fuel
*       WU238F      = U238 Weight % in Fuel
*       WO16F       = O16 Weight % in Fuel
*----
REAL
  TempCool Purity TempFuel Enrichment DensFuel   :=
  560.66   99.95  941.29   0.72       10.437501 ;
REAL
 WH1C   WD2C   WO16C  DensCool Pres
 WU235F WU238F WO16F ;
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  LIBRARY ISOT ;
MODULE
  LIB: INFO: END: ;
PROCEDURE assertS ;
*----
*  Get Coolant properties
*----
ECHO
  "Case 1. Coolant density vs temperature " ;
ECHO
  "Input - Coolant temperature (K)             " TempCool ;
ECHO
  "Input - D2/(D2+H1) Weight % ratio in Coolant" Purity ;
INFO: ::
  TMP: <<TempCool>> K
  PUR: <<Purity>>   WGT%
  CALC DENS WATER >>DensCool<<
  LIB: WIMSD4 FIL: iaea
  ISO: 3 '3001'   '3002'   '6016'
  CALC WGT% D2O >>WH1C<< >>WD2C<< >>WO16C<<
  ;
ECHO
  "Output - Coolant Density (g/cm**3)" DensCool ;
ECHO
  "Output - H1 Weight % in Coolant   " WH1C ;
ECHO
  "Output - D2 Weight % in Coolant   " WD2C ;
ECHO
  "Output - O16 Weight % in Coolant  " WO16C ;
ECHO
  "Case 2. Coolant density vs temperature and pressure " ;
EVALUATE
  TempCool Purity Pres :=
  366.72   100.0   20.8  ;
ECHO
  "Input - Coolant temperature (K)             " TempCool ;
ECHO
  "Input - Coolant pressure (MPa)              " Pres ;
ECHO
  "Input - D2/(D2+H1) Weight % ratio in Coolant" Purity ;
INFO: ::
  TMP: <<TempCool>> C
  PRES: <<Pres>> MPa
  PUR: <<Purity>>   WGT%
  CALC DENS PWATER >>DensCool<<
  LIB: WIMSD4 FIL: iaea
  ISO: 3 '3001'   '3002'   '6016'
  CALC WGT% D2O >>WH1C<< >>WD2C<< >>WO16C<<
  ;
ECHO
  "Output - Coolant Density (g/cm**3)" DensCool ;
ECHO
  "Output - H1 Weight % in Coolant   " WH1C ;
ECHO
  "Output - D2 Weight % in Coolant   " WD2C ;
ECHO
  "Output - O16 Weight % in Coolant  " WO16C ;
ECHO
  "Case 3. Fuel enrichment " ;
*----
*  Get Fuel properties
*----
ECHO
  "Input - Fuel temperature (K)                   " TempFuel ;
ECHO
  "Input - U235/(U235+U238) Weight % ratio in Fuel" Enrichment ;
ECHO
  "Input - Fuel Density (g/cm**3)                 " DensFuel ;
INFO: ::
  ENR: <<Enrichment>> WGT%
  LIB: WIMSD4 FIL: iaea
  ISO: 3 '2235'    '8238'    '6016'
  CALC WGT% UO2 >>WU235F<< >>WU238F<< >>WO16F<< 
  ;
ECHO
  "Output - U235 Weight % in Fuel" WU235F ;
ECHO
  "Output - U238 Weight % in Fuel" WU238F ;
ECHO
  "Output - O16 Weight % in Fuel " WO16F ;
*----
*  Microscopic cross sections from file iaea format WIMSD4
*----
LIBRARY := LIB: ::
  NMIX 5 CTRA WIMS
  MIXS LIB: WIMSD4 FIL: iaea
  MIX  1  <<TempCool>> <<DensCool>>  O16      = '6016'   <<WO16C>>
    D2D2O    = '3002'   <<WD2C>>     H1H2O    = '3001'   <<WH1C>>
  MIX  2  <<TempFuel>> <<DensCool>>  O16      = '6016'   <<WO16C>>
    D2D2O    = '3002'   <<WD2C>>     H1H2O    = '3001'   <<WH1C>>
  MIX  3  <<TempFuel>> <<DensFuel>>  O16      = '6016'   <<WO16F>>
    U235     = '2235'   <<WU235F>> 1 
    U238     = '8238'   <<WU238F>> 1 
  MIX  4 COMB 2 0.5 0 0.5
  MIX  5 COMB 3 0.1 3 0.9
  ;
ISOT := LIBRARY :: STEP UP ISOTOPESLIST STEP AT 15 ; (*U238    0005*)
assertS ISOT :: NG 35 5.000504E-01 ;
ECHO "test TCWU12 completed" ;
END: ;
QUIT "LIST" .
