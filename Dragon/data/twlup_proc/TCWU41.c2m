*---
* Regression test 6 for MCCG:
*---
*  TEST CASE TCWU41
*  CANDU-6 CARTESIAN CELL
*  WIMSD4 69 GROUPS LIBRARY FILE WNEALIB
*  STAINLESS STELL RODS IN 3D SUPERCELL
*----

*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  LIBRARY LIBRARY2 CANDU6F CANDU6S TRACK SYS FLUX EDITION0 EDITION
  BCO BCI TEMP ;
SEQ_BINARY
  INTLIN ;
MODULE
  GEO: EXCELT: MCCGT: LIB: SHI: ASM: FLU: EDI: DELETE: END: ;
PROCEDURE assertS ;
INTEGER split1 splitz := 1 2 ;
INTEGER split2 := split1 2 * ;
INTEGER nang := 4 ;
REAL dens := 2.5 ;
INTEGER stis := 1 ;
*----
*  Microscopic cross sections from file WNEALIB format WIMSD4
*----
PROCEDURE TCWU05Lib ;
INTEGER   iedit := 1 ;
LIBRARY := TCWU05Lib :: <<iedit>> ;
*----
*  CELL CALCULATION
*  Geometry CANDU6S : 14 regions Cartesian cluster for self-shielding
*           CANDU6F : 32 regions Cartesian cluster for transport
*           BCO     : 48 regions 3D Cartesian geometry
*           BCI     : 48 regions 3D Cartesian geometry
*----
CANDU6S := GEO: ::  CARCEL  5
  X+ REFL X- REFL MESHX -14.2875 14.2875
  Y+ REFL Y- REFL MESHY -14.2875 14.2875
  RADIUS  0.00000 5.16890 5.60320  6.44780 6.58750 14.00
  MIX  1 2 3 4 5 5
  CLUSTER ROD1 ROD2 ROD3 ROD4
  ::: ROD1 := GEO: TUBE 2 MIX 6 10 NPIN  1 RPIN 0.0000 APIN 0.0000
    RADIUS 0.00000 0.6122 0.6540 ;
  ::: ROD2 := GEO: ROD1   MIX 7 10 NPIN  6 RPIN 1.4885 APIN 0.0000 ;
  ::: ROD3 := GEO: ROD1   MIX 8 10 NPIN 12 RPIN 2.8755 APIN 0.261799 ;
  ::: ROD4 := GEO: ROD1   MIX 9 10 NPIN 18 RPIN 4.3305 APIN 0.0 ;
  ;
CANDU6F := GEO: CANDU6S :: SPLITR  6 1 1 1 10
  ::: ROD1 := GEO: ROD1 SPLITR 2 1 ;
  ::: ROD2 := GEO: ROD2 SPLITR 2 1 ;
  ::: ROD3 := GEO: ROD3 SPLITR 2 1 ;
  ::: ROD4 := GEO: ROD4 SPLITR 2 1 ;
  ;
*----
*  Self-Shielding calculation
*  Transport calculation
*  Flux calculation
*----
TRACK INTLIN := EXCELT: CANDU6S ::
  TITLE 'TCWU41: CANDU-6 CARTESIAN FUEL TEMP= 941.29'
  EDIT 0 MAXR 14 TRAK TISO 29 20.0 SYMM 4 ;
LIBRARY := SHI: LIBRARY TRACK INTLIN :: NOLJ
  EDIT 0 ;
TRACK INTLIN := DELETE: TRACK INTLIN ;
TRACK INTLIN := EXCELT: CANDU6F ::
  TITLE 'TCWU41: CANDU-6 CARTESIAN FUEL TEMP= 941.29'
  EDIT 0 MAXR 32 TRAK TISO 29 20.0 SYMM 4 ;
TRACK := MCCGT: TRACK INTLIN :: 
     EDIT 1 LCMD 2 
     AAC  50 TMT EPSI 1E-5
     MAXI 1 KRYL 0 SCR 0 HDD 0.0 ;
SYS := ASM: LIBRARY TRACK INTLIN ::
  EDIT 0 ARM ;
FLUX := FLU: SYS LIBRARY TRACK INTLIN ::
  TYPE K  THER 5 5E-5 EXTE 100 1E-5 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.120511 ;
*----
*  Microscopic cross sections from WIMSLIB for stainless steel
*  MIX 1,2,3 from EDI: : fuel, structure material and moderator
*  MIX 4 stainless steel rods
*----
EDITION0 := EDI: LIBRARY TRACK FLUX ::
  EDIT 0 MERGE MIX 1 2 2 2 3 1 1 1 1 1 SAVE ON SSRODS ;
EDITION := EDITION0 :: STEP UP SSRODS ;
LIBRARY2 := LIB: EDITION ::
  EDIT 2 NMIX 4 CTRA WIMS
  MIXS LIB: WIMSD4 FIL: iaea
  MIX 4    345.66                    Fe56     = '2056'   6.19027E-2
    Cr52     = '52'     1.56659E-3   Ni58     = '58'     6.83337E-3
    Si29     = '29'     7.79072E-4   C12      = '2012'   1.46552E-4
    Mn55     = '55'     1.25431E-3
  ;
EDITION0 EDITION TRACK INTLIN SYS FLUX := DELETE:
  EDITION0 EDITION TRACK INTLIN SYS FLUX ;
*----
*  SUPERCELL CALCULATION
*  Geometry BCO     : 27 regions 3D Cartesian geometry with rods out
*           BCI     : 27 regions 3D Cartesian geometry with rods in
*----
BCO := GEO: :: CAR3D 3 2 2
  X- REFL X+ SYME Y- REFL Y+ SYME Z- REFL Z+ SYME
  CELL  M   MX  MX MX  FXY MXY  M   MX  BX MX  FXY BXY
  TURN  A   A   A  F   A   A    A   A   A  F   A   A
  ::: M := GEO: CAR3D 1 1 1 MIX 3
    MESHX  0.0 7.14375 SPLITX <<split1>>
    MESHY  0.0 7.14375 SPLITY <<split1>>
    MESHZ -8.25500 8.25500 SPLITZ <<splitz>>  ;
  ::: MX := GEO: M  MESHX -7.14375 +7.14375 SPLITX <<split2>> ;
  ::: MXY := GEO: MX MESHY -7.14375 +7.14375 SPLITY <<split2>> ;
  ::: BX := GEO: CARCELY 2 1 MIX 3 3 3
    MESHX -7.14375 7.14375 SPLITX <<split2>>
    MESHY 0.0 7.14375 SPLITY <<split1>>
    MESHZ -8.25500 8.25500 SPLITZ <<splitz>>
    RADIUS 0.0 3.5100 3.8100 ;
  ::: BXY := GEO: BX MESHY -7.14375 +7.14375 SPLITY <<split2>> ;
  ::: FXY := GEO: CARCELZ 2 1 MIX 1  2  3
    MESHX -7.14375 7.14375 SPLITX <<split2>>
    MESHY -7.14375 7.14375 SPLITY <<split2>>
    MESHZ -8.25500 8.25500 SPLITZ <<splitz>>
    RADIUS 0.0 5.16890 6.58750 ;
  ;
BCI := GEO: BCO ::
  ::: BX := GEO: BX  MIX 3 4 3 ;
  ::: BXY := GEO: BXY MIX 3 4 3 ;
  ;
*----
*  Flux calculation
*  Homogenized properties for rod out
*----
TRACK INTLIN := EXCELT: BCO ::
  EDIT 1 MAXR 2000 TRAK TISO <<nang>> <<dens>> ;
TEMP := TRACK ;
TRACK := MCCGT: TRACK INTLIN ::
EDIT 3 EPSI 1E-8 MAXI 100
DIFC TMT STIS <<stis>> ;
SYS := ASM: LIBRARY2 TRACK INTLIN ::
  EDIT 2 ARM ;
FLUX := FLU: SYS LIBRARY2 TRACK INTLIN ::
 EDIT 1 TYPE K THER 5 5E-5 EXTE 10 1E-5 ;
!assertS FLUX :: 'K-EFFECTIVE' 1 1.118734 ;
TRACK := DELETE: TRACK ;
TRACK := TEMP ;
TRACK := MCCGT: TRACK INTLIN ::
EDIT 3 EPSI 1E-7
AAC 50 TMT
SCR 0
KRYL 0 MAXI 1 STIS <<stis>> ;
FLUX := FLU: FLUX
 SYS LIBRARY2 TRACK INTLIN ::
  EDIT 1  TYPE K
  THER 5 5E-5 EXTE 100 1E-5 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.127860 ;
EDITION := EDI: LIBRARY2 TRACK FLUX ::
  EDIT 2 MERG COMP COND 4.0 SAVE ON  'NOBC' ;
SYS TRACK INTLIN := DELETE: SYS TRACK INTLIN ;
*----
*  Flux calculation
*  Homogenized properties for rod in
*----
TRACK INTLIN := EXCELT: BCI ::
  EDIT 0 MAXR 2000 TRAK TISO <<nang>> <<dens>> ;
TRACK := MCCGT: TRACK INTLIN ::
EDIT 2 EPSI 1E-7
AAC 50 TMT
SCR 0
KRYL 0 MAXI 1 STIS <<stis>> ;
SYS := ASM: LIBRARY2 TRACK INTLIN ::
  EDIT 2 ARM ;
FLUX := FLU: FLUX
SYS LIBRARY2 TRACK INTLIN ::
  EDIT 1  TYPE K
  THER 5 5E-5 EXTE 100 1E-5 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.042392 ;
EDITION := EDI: EDITION LIBRARY2 TRACK FLUX ::
  EDIT 2 MERG COMP COND 4.0 STAT DELS REFE 'NOBC' ;
*---
* Memory Release
*---
TRACK INTLIN SYS := DELETE: TRACK INTLIN SYS ;
ECHO "test TCWU41 completed" ;
END: ; 
QUIT "LIST" .
