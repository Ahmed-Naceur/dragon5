*----
*  TEST CASE TCWU09
*  MOSTELLER BENCHMARK FOR 1-D ANNULAR CELL
*  iaea WLUP Library
*  REFLECTIVE AND VOID BC
*
*  REF: None
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  MOSTELA MOSTELV VOLMAT LIBRARY PIJ FLUX OUT ;
MODULE
  LIB: GEO: SYBILT: SHI: ASM: FLU: EDI: DELETE: END: ;
PROCEDURE assertS ;
*----
*  Microscopic cross section from file iaea format WIMSD4
*----
LIBRARY := LIB: ::
  NMIX 3 CTRA WIMS
  MIXS LIB: WIMSD4 FIL: iaea
  MIX 1 600.0                        O16      = '6016'   4.61309E-2
    U235     = '2235'   1.66078E-4 1 
    U238     = '8238'   2.28994E-2 1 
  MIX 2 600.0
    Zr91     = '91'     3.83243E-2
  MIX 3 600.0
    H1H2O    = '3001'   4.42326E-2   O16H2O   = '6016'   2.21163E-2
    BNat     = '1011'   1.02133E-5
  ;
*----
*  Geometry MOSTELA : Annular cell with reflective BC
*           MOSTELV : Annular cell with void BC
*----
MOSTELA := GEO: :: TUBE 3
  RADIUS 0.0 0.39306 0.45802 0.71206
  SPLITR     2       1       1
  MIX        1       2       3
  R+ REFL ;
MOSTELV := GEO: MOSTELA ::
  R+ VOID ;
*----
*  Self-Shielding calculation SYBIL
*  Transport calculation      SYBIL
*  Flux calculation for keff
*----
VOLMAT := SYBILT: MOSTELA ::
  TITLE 'TCWU09: SYBIL TRACK MOSTELLER BENCHMARK REFLECTIVE BC '
  MAXR 4 QUA1 5 ;
LIBRARY := SHI: LIBRARY VOLMAT :: EDIT 0 NOLJ ;
PIJ := ASM: LIBRARY VOLMAT ;
FLUX := FLU: PIJ LIBRARY VOLMAT ::
  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.8276187 ;
OUT := EDI: LIBRARY VOLMAT FLUX ::
  EDIT 4 MERG MIX 1 2 3 COND 4.0 SAVE ;
PIJ VOLMAT := DELETE: PIJ VOLMAT ;
VOLMAT := SYBILT: MOSTELV ::
  TITLE 'TCWU09: SYBIL TRACK MOSTELLER BENCHMARK VOID BC '
  MAXR 4 QUA1 5 ;
PIJ := ASM: LIBRARY VOLMAT ;
FLUX := FLU: FLUX PIJ LIBRARY VOLMAT ::
  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.023486E-02 ;
OUT := EDI: OUT LIBRARY VOLMAT FLUX ::
  EDIT 4 MERG MIX 1 2 3 COND 4.0 SAVE ;
OUT FLUX PIJ LIBRARY VOLMAT := DELETE:
  OUT FLUX PIJ LIBRARY VOLMAT  ;
ECHO "test TCWU09 completed" ;
END: ;  
QUIT "LIST" .
