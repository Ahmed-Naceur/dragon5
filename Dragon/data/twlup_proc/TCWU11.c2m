*----
*  TEST CASE TCWU11
*  CANDU-6 ANNULAR CELL
*  iaea WLUP Library
*  TWO GROUP BURNUP
*  POWER (KW)         =  615.00000
*  BURN POWER (KW/KG) =   31.97130
*  URANIUM MASS       =   19.23600
*  UO2 REAL DENSITY   =   10.59300
*  UO2 EFF DENSITY    =   10.43750
*  UO2 TEMPERATURE    =  941.28998
*  ENRICHMENT         =    0.71140
*  COOLANT D2 AT %    =   99.222
*  MODERATOR D2 AT %  =   99.911
*  NUMBER OF DAYS     =   50
*
*----
*  Define variables
*     Burnup paremeters
*     a) Power
*        =  31.9713 kw/kg for    0.0 to 300.0 days
*     b) 69 Groups Burnup time interval Delt
*        =   300 day  for    0 to  300 day
*     c) 2 Groups Burnup time interval Delt
*        =     1 day  for    0 to    1 day
*        =     4 days for    1 to    5 days
*        =     5 days for    5 to   10 days
*        =    10 days for   10 to   50 days
*        =    20 days for   50 to  150 days
*        =    50 days for  150 to  300 days
*     c) Days with burnup interval changes
*        = 1.0, 5.0, 10.0, 50.0, 150.0 and 300.0 days
*     d) Burnup control time variables Timei, Timef
*        Timei = initial time
*        Timef = final time
*----
REAL
  Power    Delt   Timec   Timei Timef :=
  31.9713  1.0    300.0   0.0   0.0 ;
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST LIBRARY ;
LINKED_LIST
   CANDU6S CANDU6F VOLMATS VOLMATF PIJ FLUX BURNUP EDITION ;
SEQ_BINARY
  INTLINS INTLINF ;
SEQ_ASCII
  res ;
MODULE
  GEO: EXCELT: LIB: SHI: ASM: FLU: EVO: EDI: SPH: DELETE: END: ;
PROCEDURE assertS ;
*----
*  Depletion data from file iaea format WIMSD4
*  Microscopic cross sections from file iaea format WIMSD4
*----
PROCEDURE TCWU05Lib ;
INTEGER   iedit := 1 ;
LIBRARY := TCWU05Lib :: <<iedit>> ;
*----
*  Geometry CANDU6S : 13 regions annular cluster for self-shielding
*           CANDU6F : 31 regions annular cluster for transport
*----
CANDU6S := GEO: ::  TUBE 5
  R+ REFL RADIUS  0.00000 5.16890 5.60320  6.44780 6.58750 16.12171
  MIX  1 2 3 4 5
  CLUSTER ROD1 ROD2 ROD3 ROD4
  ::: ROD1 := GEO: TUBE 2 MIX 6 10 NPIN  1 RPIN 0.0000 APIN 0.0000
    RADIUS 0.00000 0.6122 0.6540 ;
  ::: ROD2 := GEO: ROD1   MIX 7 10 NPIN  6 RPIN 1.4885 APIN 0.0000 ;
  ::: ROD3 := GEO: ROD1   MIX 8 10 NPIN 12 RPIN 2.8755 APIN 0.261799 ;
  ::: ROD4 := GEO: ROD1   MIX 9 10 NPIN 18 RPIN 4.3305 APIN 0.0 ;
  ;
CANDU6F := GEO: CANDU6S :: SPLITR  6 1 1 1 10
  ::: ROD1 := GEO: ROD1 SPLITR 2 1 ;
  ::: ROD2 := GEO: ROD2 SPLITR 2 1 ;
  ::: ROD3 := GEO: ROD3 SPLITR 2 1 ;
  ::: ROD4 := GEO: ROD4 SPLITR 2 1 ;
  ;
*----
*  Self-Shielding calculation EXCEL
*  Transport calculation      EXCEL
*  Flux calculation for keff
*----
VOLMATS INTLINS := EXCELT: CANDU6S ::
  TITLE 'TCWU11: FEW GROUP BURNUP / SELF-SHIELDING TRACKING'
  EDIT 0 MAXR 13 TRAK TISO 5 10.0 SYMM 12 ;
LIBRARY := SHI: LIBRARY VOLMATS INTLINS :: EDIT 0 NOLJ ;
VOLMATF INTLINF := EXCELT: CANDU6F ::
  TITLE 'TCWU11: FEW GROUP BURNUP / TRANSPORT TRACKING'
  EDIT 0 MAXR 31 TRAK TISO 5 10.0 SYMM 12 ;
PIJ := ASM: LIBRARY VOLMATF INTLINF ;
FLUX := FLU: PIJ LIBRARY VOLMATF ::
  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.121035 ;
EDITION := EDI: LIBRARY VOLMATF FLUX CANDU6F ::
  MERG REGI  6  6 10  7  7 10  1  1  8  8 10  1
             1  9  9 10  1  1  2  3  4  5  5  5
             5  5  5  5  5  5  5
  COND 4.0 MICR ALL SAVE
  MGEO CANDU6F
  ;
EDITION := SPH: EDITION VOLMATF INTLINF ;
*----
*  69 group Burnup
*----
BURNUP LIBRARY := EVO: LIBRARY FLUX VOLMATF ::
  EDIT 3 EXPM 200.0 DEPL <<Timei>> <<Timec>> DAY POWR <<Power>> ;
LIBRARY := SHI: LIBRARY VOLMATS INTLINS :: EDIT 0 NOLJ ;
PIJ := DELETE: PIJ ;
PIJ := ASM: LIBRARY VOLMATF INTLINF ;
FLUX := FLU: FLUX PIJ LIBRARY VOLMATF ::
  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.9414081 ;
EDITION := EDI: EDITION LIBRARY VOLMATF FLUX CANDU6F ::
  MGEO CANDU6F
  ;
EDITION := SPH: EDITION VOLMATF INTLINF ;
BURNUP FLUX PIJ LIBRARY INTLINS VOLMATS CANDU6S := DELETE:
  BURNUP FLUX PIJ LIBRARY INTLINS VOLMATS CANDU6S ;
*----
*  2 group Burnup
*----
LIBRARY :=  EDITION :: STEP UP 'REF-CASE0001' ;
EDITION := DELETE: EDITION ;
PIJ := ASM: LIBRARY VOLMATF INTLINF ;
FLUX := FLU: PIJ LIBRARY VOLMATF ::
  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.121052 ;
EDITION := EDI: LIBRARY VOLMATF FLUX ::
  EDIT 1 MERGE COMP MICR ALL SAVE ;
EVALUATE Timec := 1.0 ;
WHILE Timei Timec < DO
  EVALUATE Timef := Timei Delt + ;
  IF Timei 0.0 = THEN
    BURNUP LIBRARY := EVO: LIBRARY FLUX VOLMATF ::
      EDIT 3 DEPL <<Timei>> <<Timef>> DAY POWR <<Power>> ;
  ELSE
    BURNUP LIBRARY := EVO: BURNUP LIBRARY FLUX VOLMATF ::
      EDIT 3 NOEX DEPL <<Timei>> <<Timef>> DAY POWR <<Power>> ;
  ENDIF ;
  PIJ := DELETE: PIJ ;
  PIJ := ASM: LIBRARY VOLMATF INTLINF ;
  FLUX := FLU: FLUX PIJ LIBRARY VOLMATF ::
    TYPE K ;
  EDITION := EDI: EDITION LIBRARY VOLMATF FLUX ::
    SAVE ;
*----
*  change delta t for burnup and final time if required
*----
  IF Timef Timec = THEN
    IF Timec 5.0 = THEN
      EVALUATE Delt Timec := 5.0 10.0 ;
    ENDIF ;
    IF Timec 1.0 = THEN
      EVALUATE Delt Timec := 4.0 5.0 ;
    ENDIF ;
  ENDIF ;
  EVALUATE Timei := Timef ;
ENDWHILE ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.075327 ;
res := EDITION ;
EDITION BURNUP FLUX PIJ LIBRARY INTLINF VOLMATF CANDU6F := DELETE:
  EDITION BURNUP FLUX PIJ LIBRARY INTLINF VOLMATF CANDU6F ;
ECHO "test TCWU11 completed" ;
END: ;   
QUIT "LIST" .
