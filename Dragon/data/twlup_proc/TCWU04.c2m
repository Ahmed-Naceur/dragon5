*----
*  TEST CASE TCWU04
*  iaea WLUP Library
*  ANNULAR MOSTELLER BENCHMARK WITH BURNUP
*
*  REF: R. Mosteller et al. Nucl. Sci. Eng. 107, 265 (1991)
*
*----
*  Define variables
*----
INTEGER
  istep := 1 ;
REAL
  evobeg evoend ;
REAL
  step2 step3  step4  step5  step6  step7 :=
  1.0   27.1739 47.5544 67.9348 101.9022 135.8696 ;
*----
*  Define STRUCTURES and MODULES used
*----
PARAMETER  res :: ::: SEQ_ASCII res ; ;
LINKED_LIST
  LIBRARY MOSTELAS MOSTELA TRACKS TRACK SYS FLUX BURNUP EDITION
  DATABASE ISOT ;
MODULE
  GEO: SYBILT: LIB: SHI: ASM: FLU: EVO: EDI: COMPO: DELETE: END: ;
PROCEDURE assertS ;
*----
*  Depletion data from file iaea format WIMSD4
*  Microscopic cross sections from file iaea format WIMSD4
*----
LIBRARY := LIB: ::
  NMIX 3 CTRA WIMS
  DEPL LIB: WIMSD4 FIL: iaea
  MIXS LIB: WIMSD4 FIL: iaea
  MIX 1 600.0                        O16      = '6016'   4.61309E-2
    U235     = '2235'   1.66078E-4 1 
    U238     = '8238'   2.28994E-2 1 
    U236     = '236'    0.0        1
    Pu239    = '6239'   0.0        1 
  MIX 2 600.0
    Zr91     = '91'     3.83243E-2
  MIX 3 600.0
    H1H2O    = '3001'   4.42326E-2   O16H2O   = '6016'   2.21163E-2
    BNat     = '1011'   1.02133E-5
  ;
*----
*  Geometry MOSTELAS : 3 regions annular cell for self-shielding
*           MOSTELA  : 4 regions annular cell for transport
*----
MOSTELAS := GEO: ::  TUBE 3
  R+ REFL RADIUS 0.0 0.39306 0.45802 0.71206
  MIX 1  2  3 ;
MOSTELA  := GEO:  MOSTELAS ::
  SPLITR 2  1  1 ;
*----
*  Create the reactor database
*----
DATABASE := COMPO: ::
  EDIT 5
  COMM  'Multi-parameter reactor database' ENDC
  PARA  'BURN' IRRA
  PARA  'FLUB' FLUB
  INIT
  ;
*----
*  Self-Shielding calculation SYBIL
*  Transport calculation      SYBIL
*  Flux calculation for keff with imposed buckling
*  using B1 homogeneous leakage model
*----
TRACKS := SYBILT: MOSTELAS ::
  TITLE 'TCWU04: MOSTELLER BENCHMARK WITH BURNUP'
  EDIT 1 MAXR 3 ;
LIBRARY := SHI: LIBRARY TRACKS :: EDIT 0 NOLJ ;
TRACK := SYBILT: MOSTELA ::
  TITLE 'TCWU04: MOSTELLER BENCHMARK WITH BURNUP'
  EDIT 1 MAXR 4 ;
SYS := ASM: LIBRARY TRACK ;
FLUX := FLU: SYS LIBRARY TRACK ::
  TYPE K B1 PNL BUCK 0.2948E-2 ;
EDITION := EDI: LIBRARY TRACK FLUX ::
  EDIT 3 MICR RES MERG COMP COND 4.0 SAVE  ;
*----
*  Burnup loop: for first step BURNUP is created
*  while for other steps it is modified
*  two burnup per step:
*  1) get a first approximation of final composition followed
*     by a transport calculation
*  2) use approximation for final flux distribution to get a
*     better approximation for final composition
*----
EVALUATE evoend := 0.0 ;
WHILE evoend step2 < DO
  EVALUATE evobeg := evoend ;
  EVALUATE evoend := step2 ;
  IF istep 1 = THEN
    BURNUP LIBRARY := EVO: LIBRARY FLUX TRACK ::
      SAVE <<evobeg>> DAY POWR 36.8
      DEPL <<evobeg>> <<evoend>> DAY POWR 36.8
      SET <<evoend>> DAY ;
  ELSE
    BURNUP LIBRARY := EVO: BURNUP LIBRARY FLUX TRACK ::
      SAVE <<evobeg>> DAY POWR 36.8
      NOEX DEPL <<evobeg>> <<evoend>> DAY POWR 36.8
      SET <<evoend>> DAY ;
  ENDIF ;
  LIBRARY := SHI: LIBRARY TRACKS :: EDIT 0 NOLJ ;
  SYS := DELETE: SYS ;
  SYS := ASM: LIBRARY TRACK ;
  FLUX := FLU: FLUX SYS LIBRARY TRACK ::
    TYPE K B1 PNL BUCK 0.2948E-2 ;

  BURNUP LIBRARY := EVO: BURNUP LIBRARY FLUX TRACK ::
    SAVE <<evoend>> DAY POWR 36.8
    DEPL <<evobeg>> <<evoend>> DAY POWR 36.8
    SET <<evoend>> DAY ;
  LIBRARY := SHI: LIBRARY TRACKS :: EDIT 0 NOLJ ;
  SYS := DELETE: SYS ;
  SYS := ASM: LIBRARY TRACK ;
  FLUX := FLU: FLUX SYS LIBRARY TRACK ::
    TYPE K B1 PNL BUCK 0.2948E-2 ;

  EDITION := EDI: EDITION LIBRARY TRACK FLUX ::
    EDIT 3 SAVE ;
  BURNUP LIBRARY := EVO: BURNUP LIBRARY FLUX TRACK ::
    SAVE <<evoend>> DAY POWR 36.8 ;
  DATABASE := COMPO: DATABASE EDITION BURNUP ::
    EDIT 3
    SET <<evoend>> DAY
    ;
  EVALUATE step2 step3 step4 step5 step6 step7 :=
           step3 step4 step5 step6 step7 step2 ;
  EVALUATE istep := istep 1 + ;
ENDWHILE ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.7332095 ;

res := DATABASE ;

ISOT := DATABASE :: STEP UP default
                    STEP UP MIXTURES STEP AT 1
                    STEP UP CALCULATIONS STEP AT 2
                    STEP UP ISOTOPESLIST STEP AT 1 ;
assertS ISOT :: 'NWT0' 1 3.838717E+01 ;
assertS ISOT :: 'NWT0' 2 1.695042E+01 ;

ECHO "test TCWU04 completed" ;
END: ;   
QUIT "LIST" .
