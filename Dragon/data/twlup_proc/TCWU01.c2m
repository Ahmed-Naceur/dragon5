*----
*  TEST CASE TCWU01
*  MOSTELLER BENCHMARK: 1-D ANNULAR CELL AND 2-D CARTESIAN CELL
*  WIMSD4 69 GROUPS LIBRARY FILE iaea FROM WLUP
*
*  REF: R. Mosteller et al. Nucl. Sci. Eng. 107, 265 (1991)
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  MOSTELA MOSTELC DISCR LIBRARY CP CALC OUT ;
SEQ_BINARY
  TRKSPC ;
MODULE
  LIB: GEO: SYBILT: EXCELT: SHI: ASM: FLU: EDI: DELETE: END: ;
PROCEDURE assertS ;
*----
*  Microscopic cross sections from file iaea format WIMSD4
*----
LIBRARY := LIB: ::
  NMIX 3 CTRA WIMS
  MIXS LIB: WIMSD4 FIL: iaea
  MIX 1 600.0                        O16      = '6016'     4.61309E-2
    U235     = '2235'   1.66078E-4 1 
    U238     = '8238'   2.28994E-2 1 
  MIX 2 600.0
    Zr91     = '91'     3.83243E-2
  MIX 3 600.0
    H1H2O    = '3001'   4.42326E-2   O16H2O   = '6016'     2.21163E-2
    BNat     = '1011'   1.02133E-5
  ;
*----
*  Geometry MOSTELA : annular 3 region geometry
*           MOSTELC : Cartesian 3 region geometry
*----
MOSTELA := GEO: :: TUBE 3
  R+ REFL RADIUS 0.0 0.39306 0.45802 0.71206 SPLITR 2 1 1
  MIX 1 2 3 ;
MOSTELC := GEO: :: CARCEL 2
  X- REFL X+ REFL MESHX 0.0 1.26209
  Y- REFL Y+ REFL MESHY 0.0 1.26209
  RADIUS 0.0 0.39306 0.45802 SPLITR 2 1
  MIX  1 2 3 ;
*----
*  Case 1 -- annular
*  Self-Shielding calculation SYBIL
*  Transport calculation      SYBIL
*  Flux calculation for K no leakage
*----
DISCR := SYBILT: MOSTELA ::
  TITLE 'TCWU01: MOSTELLER BENCHMARK (SYBIL / SYBIL)'
  MAXR 4 QUA1 5 ;
LIBRARY := SHI: LIBRARY DISCR :: EDIT 0 NOLJ ;
CP := ASM: LIBRARY DISCR ;
CALC := FLU: CP LIBRARY DISCR ::
  TYPE K ;
assertS CALC :: 'K-EFFECTIVE' 1 0.8276153 ;
OUT := EDI: LIBRARY DISCR CALC ::
  EDIT 4 MERG MIX 1 2 3 COND 4.0 SAVE ;
DISCR CP := DELETE: DISCR CP ;
*----
*  Case 2 -- Cartesian
*  Self-Shielding calculation SYBIL
*  Transport calculation      SYBIL
*  Flux calculation for K no leakage
*----
DISCR := SYBILT: MOSTELC ::
  TITLE 'TCWU01: MOSTELLER BENCHMARK (SYBIL / SYBIL)'
  MAXR 4 QUA1 5 QUA2 6 5 ;
LIBRARY := SHI: LIBRARY DISCR :: EDIT 0 NOLJ ;
CP := ASM: LIBRARY DISCR ;
CALC := FLU: CALC CP LIBRARY DISCR ::
  TYPE K ;
assertS CALC :: 'K-EFFECTIVE' 1 0.8277465 ;
OUT := EDI: OUT LIBRARY DISCR CALC ::
  EDIT 1 MERG MIX 1 2 3 COND 4.0 STAT ALL REFE 1 ;
DISCR CP := DELETE: DISCR CP ;
*----
*  Case 3 -- annular
*  Self-Shielding calculation EXCEL-ISO
*  Transport calculation      EXCEL-ISO
*  Flux calculation for K no leakage
*----
DISCR TRKSPC := EXCELT: MOSTELC ::
  TITLE 'TCWU01: MOSTELLER BENCHMARK (EXCELL)'
  MAXR 4 TRAK TISO 12 20.0 ;
LIBRARY := SHI: LIBRARY DISCR TRKSPC :: EDIT 0 NOLJ ;
CP := ASM: LIBRARY DISCR TRKSPC ;
CALC := FLU: CALC CP LIBRARY DISCR ::
  TYPE K ;
assertS CALC :: 'K-EFFECTIVE' 1 0.8287268 ;
OUT := EDI: OUT LIBRARY DISCR CALC ::
  EDIT 1 MERG MIX 1 2 3 COND 4.0 STAT ALL REFE 1 ;
DISCR TRKSPC CP := DELETE: DISCR TRKSPC CP ;
*----
*  Case 4 -- Cartesian
*  Self-Shielding calculation EXCEL-SPC
*  Transport calculation      EXCEL-SPC
*  Flux calculation for K no leakage
*----
DISCR TRKSPC := EXCELT: MOSTELC ::
  TITLE 'TCWU01: MOSTELLER BENCHMARK (EXCELL)'
  MAXR 4 TRAK TSPC 12 20.0 ;
LIBRARY := SHI: LIBRARY DISCR TRKSPC :: EDIT 0 NOLJ ;
CP := ASM: LIBRARY DISCR TRKSPC ;
CALC := FLU: CALC CP LIBRARY DISCR ::
  TYPE K ;
assertS CALC :: 'K-EFFECTIVE' 1 0.8307597 ;
OUT := EDI: OUT LIBRARY DISCR CALC ::
  EDIT 1 MERG MIX 1 2 3 COND 4.0 STAT ALL REFE 1 ;
DISCR TRKSPC CP := DELETE: DISCR TRKSPC CP ;
ECHO "test TCWU01 completed" ;
END: ;
QUIT "LIST" .
