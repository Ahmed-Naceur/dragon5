*----
*  TEST CASE TCM67
*  FMAC-M generated homogeneous macrolib
*  1D slab sn electron-photon coupled testcase from ROZ6
*----
*----
*  Define STRUCTURES and MODULES used
*----
MODULE GEO: FMAC: MAC: SNT: ASM: FLU: PSOUR: OUT: DELETE: END: UTL:
       HEAT: ABORT: ;
LINKED_LIST  EMACRO EMACRO2 GMACRO GMACRO2 GEOM ETRACK GTRACK SYSTEM
       EFLUX GFLUX ESOUR GSOUR EEDITS GEDITS DEPOS EEDITS2 GEDITS2 ;
REAL esum DELTA ;
SEQ_ASCII FMAC_M :: FILE './water.txt' ;
*----
*  Set and track the geometry
*----
GEOM := GEO: :: CAR1D 2
           X- VOID  X+ VOID
           MIX 1 2
           MESHX 0.0 0.3 30.0
           SPLITX 3 297
           ;
GTRACK := SNT: GEOM ::
      TITLE 'HOMOGENIZED W_6Mev_e_ph_CSD BENCHMARK WITH FMAC-M'
      EDIT 2 MAXR 1000000 DIAM 0 NLIVO SN 32 SCAT 16 EPSI 1.E-4
      MAXI 300 BTE ;
ETRACK := GTRACK ;
ETRACK := SNT: ETRACK GEOM :: EDIT 2 BFPG ;
*----
*  Compute the photon flux
*----
GMACRO := FMAC: FMAC_M ::
  EDIT 2
  PARTICLE GAMA
;
GMACRO2 := GMACRO ;
GMACRO := MAC: GMACRO GMACRO2 ::
     MIX 2 1 OLDL
     ;
GMACRO2 := DELETE: GMACRO2 ;
REAL qsour := 0.00001 ;
*1E10 2.0 / $Pi_R / ;
ECHO "qsour=" qsour ;
GMACRO := MAC: GMACRO ::
     READ INPUT
     MIX 1 FIXE
     <<qsour>> 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
*     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
*     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
*     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
*     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
*     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
*     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
*     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
*     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
*     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
*     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
*     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
*     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     ;
SYSTEM := ASM: GMACRO GTRACK :: EDIT 2 ARM ;
GFLUX := FLU: GMACRO GTRACK SYSTEM ::
    EDIT 1 TYPE S EXTE 100
    ;
GEDITS := OUT: GFLUX GTRACK GMACRO GEOM ::
    EDIT 2 COND INTG NONE
    ;
SYSTEM := DELETE: SYSTEM ;
*----
*  Set the electron source
*----
EMACRO := FMAC: FMAC_M ::
  EDIT 2
  PARTICLE BETA
;
UTL: EMACRO :: STEP UP GROUP STEP AT 1 DIR IMPR ESTOPW * ;
EMACRO2 := EMACRO ;
EMACRO := MAC: EMACRO EMACRO2 ::
     MIX 2 1 OLDL
     ;
ESOUR := PSOUR: EMACRO ETRACK GFLUX ::
   EDIT 1
   PARTICLE GAMA
   ;
*----
*  Compute the electron flux
*----
SYSTEM := ASM: EMACRO ETRACK :: EDIT 1 ARM ;
EFLUX := FLU: EMACRO ETRACK SYSTEM ESOUR ::
    EDIT 2 TYPE S EXTE 100
    ;
EEDITS := OUT: EFLUX ETRACK EMACRO GEOM ::
    EDIT 2 COND INTG NONE
    ;
SYSTEM := DELETE: SYSTEM ;
*----
*  Compute the primary energy deposition
*----
DEPOS := HEAT: GEDITS EEDITS ::
    EDIT 1 ! SOUR 1.0 ! 0.0518134 ! 1/(\ro_{W} g/cm^{3})
    ;
GFLUX := DELETE: GFLUX ;
*----
*  Compute the secondary photon source
*----
GSOUR := PSOUR: GMACRO GTRACK EFLUX ::
   EDIT 1
   PARTICLE BETA
   ;
*----
*  Compute the secondary photon flux
*----
SYSTEM := ASM: GMACRO GTRACK :: EDIT 1 ARM ;
GFLUX := FLU: GMACRO GTRACK SYSTEM GSOUR ::
    EDIT 2 TYPE S EXTE 100
    ;
GEDITS2 := OUT: GFLUX GTRACK GMACRO GEOM ::
    EDIT 2 COND INTG NONE
    ;
SYSTEM ESOUR EFLUX := DELETE: SYSTEM ESOUR EFLUX ;
*----
*  Compute the secondary electron source
*----
ESOUR := PSOUR: EMACRO ETRACK GFLUX ::
   EDIT 1
   PARTICLE GAMA
   ;
*----
*  Compute the secondary electron flux
*----
SYSTEM := ASM: EMACRO ETRACK :: EDIT 1 ARM ;
EFLUX := FLU: EMACRO ETRACK SYSTEM ESOUR ::
    EDIT 2 TYPE S EXTE 100
    ;
EEDITS2 := OUT: EFLUX ETRACK EMACRO GEOM ::
    EDIT 2 COND INTG NONE
    ;
SYSTEM GSOUR := DELETE: SYSTEM GSOUR ;
*----
*  Compute the secondary total energy deposition
*----
DEPOS := HEAT: DEPOS GEDITS2 EEDITS2 ::
    EDIT 1 PICKE >>esum<<
    ;
ECHO "total energy deposition=" esum "MeV/cc" ;
*----
*  Assess result
*----
REAL REFVALUE := 5.884968E-2 ;
EVALUATE DELTA := esum REFVALUE - REFVALUE / ABS ;
IF DELTA 1.0E-4 < THEN
  ECHO "TEST SUCCESSFUL; DELTA=" DELTA ;
ELSE
  ECHO "------------" ;
  ECHO "TEST FAILURE" ;
  ECHO "------------" ;
  ECHO "REFERENCE=" REFVALUE " CALCULATED=" esum ;
  ABORT: ;
ENDIF ;
*
ECHO "test TCM67 completed" ;
END: ;
