*----
*  TEST CASE TCWW07
*  CANDU-6 CARTESIAN CELL
*  WIMS-AECL 69 GROUPS LIBRARY FILE WIMSLIB
*  TEST VARIOUS LEAKAGE OPTIONS
*
*  REF: R. Roy et al.  Ann. Nucl. Energy 21, 115 (1994)
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  LIBRARY CANDU6S CANDU6T CANDU6SV CANDU6TV TRACK
  SYS FLUX EDITION ;
MODULE
  GEO: EXCELT: LIB: SHI: ASM: FLU: EDI: DELETE: END: ;
SEQ_BINARY
  INTLIN ;
PROCEDURE assertS ;
*----
*  Microscopic cross sections from file WIMSLIB format WIMS-AECL
*----
LIBRARY := LIB: ::
  EDIT 0 NMIX 10 CTRA WIMS
  MIXS LIB: WIMSAECL FIL: WIMSLIB
  MIX  1  560.66   0.81212           O16      = O16      7.99449E-1
    D2D2O    = D2D2O    1.99768E-1   H1H2O    = H1H2O    7.83774E-4
  MIX  2  560.66   6.57              Nb93     = NB93     2.50000
    BNat     = B-NAT    2.10000E-4
    Zr91     = ZR91     9.75000E+1
  MIX  3  345.66   0.0014            He4      = HE4      1.00000E+2
  MIX  4  345.66   6.44              Fe56     = FE56A    1.60000E-1
    Ni58     = NI58     6.00000E-2   Cr52     = CR52     1.10000E-1
    BNat     = B-NAT    3.10000E-4
    Zr91     = ZR91     9.97100E+1
  MIX  5  345.66   1.082885          O16      = O16      7.98895E-1
    D2D2O    = D2D2O    2.01016E-1   H1H2O    = H1H2O    8.96000E-5
  MIX  6  941.29  10.4375010         O16      = O16      1.18473E+1
    U235     = U235     6.27118E-1 1
    U238     = U238     8.75256E+1 1
  MIX  7  COMB 6 1.0
  MIX  8  COMB 6 1.0
  MIX  9  COMB 6 1.0
  MIX 10   560.66   6.44             Fe56     = FE56A    1.60000E-1
    Ni58     = NI58     6.00000E-2   Cr52     = CR52     1.10000E-1
    BNat     = B-NAT    3.10000E-4
    Zr91     = ZR91     9.97100E+1
  ;
*----
*  Geometry CANDU6S : GEOMETRY FOR SELF-SHIELDING  (NO VOID)
*           CANDU6F : GEOMETRY FOR TRANSPORT       (NO VOID)
*           CANDU6FV: GEOMETRY FOR TRANSPORT       (COOLANT VOID)
*           CANDU6FV: GEOMETRY FOR TRANSPORT       (COOLANT VOID)
*----
CANDU6S := GEO: :: CARCEL  5
  X+ REFL X- REFL MESHX -14.2875 14.2875
  Y+ REFL Y- REFL MESHY -14.2875 14.2875
  RADIUS  0.00000 5.16890 5.60320 6.44780 6.58750 14.00
  MIX     1       2       3       4       5       5
  CLUSTER ROD1 ROD2 ROD3 ROD4
  ::: ROD1 := GEO: TUBE 2 MIX  6 10 NPIN  1 RPIN 0.0000 APIN 0.0000
    RADIUS 0.00000 0.6122 0.6540 ;
  ::: ROD2 := GEO: ROD1   MIX  7 10 NPIN  6 RPIN 1.4885 APIN 0.0000   ;
  ::: ROD3 := GEO: ROD1   MIX  8 10 NPIN 12 RPIN 2.8755 APIN 0.261799 ;
  ::: ROD4 := GEO: ROD1   MIX  9 10 NPIN 18 RPIN 4.3305 APIN 0.0      ;
  ;
CANDU6T := GEO: CANDU6S :: SPLITR  6 1 1  1 10
  ::: ROD1 := GEO: ROD1 SPLITR 2 1 ;
  ::: ROD2 := GEO: ROD2 SPLITR 2 1 ;
  ::: ROD3 := GEO: ROD3 SPLITR 2 1 ;
  ::: ROD4 := GEO: ROD4 SPLITR 2 1 ;
  ;
CANDU6SV := GEO: CANDU6S  :: MIX 0 2 3 4 5 5 ;
CANDU6TV := GEO: CANDU6SV :: SPLITR  6 1 1  1 10
  ::: ROD1 := GEO: ROD1 SPLITR 2 1 ;
  ::: ROD2 := GEO: ROD2 SPLITR 2 1 ;
  ::: ROD3 := GEO: ROD3 SPLITR 2 1 ;
  ::: ROD4 := GEO: ROD4 SPLITR 2 1 ;
  ;
*----
*  CASE WITH NO VOID
*  Self-Shielding calculation EXCEL
*  Transport calculation      EXCEL
*  Flux TYPE K AND B WITH VARIOUS LEAKAGE OPTIONS
*----
TRACK INTLIN := EXCELT: CANDU6S ::
  TITLE 'TCWW07: CANDU-6 CARTESIAN FUEL TEMP= 941.29'
  EDIT 0 MAXR 14 TRAK TISO 7 20.0 SYMM 4 ;
LIBRARY := SHI: LIBRARY TRACK INTLIN :: EDIT 0 NOLJ ;
TRACK INTLIN := DELETE: TRACK INTLIN ;
TRACK INTLIN := EXCELT: CANDU6T ::
  TITLE 'TCWW07: CANDU-6 CARTESIAN FUEL TEMP= 941.29'
  EDIT 0 MAXR 32 ANIS 2 TRAK TISO 7 20.0 SYMM 4 ;
SYS := ASM: LIBRARY TRACK INTLIN ::
  EDIT 0 PIJK ;
FLUX := FLU: SYS LIBRARY TRACK ::
  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.117214 ;
EDITION := EDI: FLUX LIBRARY TRACK ::
  EDIT 3 SAVE COND 4.0 TAKE REGI 1 4 7 10 16 24 ;
FLUX := FLU: FLUX SYS LIBRARY TRACK ::
  TYPE B B1 PNL ;
assertS FLUX :: 'K-INFINITY' 1 1.109208 ;
EDITION := EDI: EDITION FLUX LIBRARY TRACK ::
  EDIT 3 SAVE COND 4.0  TAKE REGI 1 4 7 10 16 24 ;
FLUX := FLU: FLUX SYS LIBRARY TRACK ::
  TYPE B B1 HETE ;
assertS FLUX :: 'K-INFINITY' 1 1.108972 ;
EDITION := EDI: EDITION FLUX LIBRARY TRACK ::
  EDIT 3 SAVE COND 4.0 TAKE REGI 1 4 7 10 16 24 ;
FLUX SYS := DELETE: FLUX SYS ;
SYS := ASM: LIBRARY TRACK INTLIN ::
  EDIT 0 PIJ ECCO ;
FLUX := FLU: SYS LIBRARY TRACK ::
  TYPE B B1 ECCO ;
assertS FLUX :: 'K-INFINITY' 1 1.108980 ;
EDITION := EDI: EDITION FLUX LIBRARY TRACK ::
  EDIT 3 SAVE COND 4.0  TAKE REGI 1 4 7 10 16 24 ;
FLUX SYS TRACK INTLIN := DELETE: FLUX SYS TRACK INTLIN ;
*----
*  CASE WITH COOLANT VOIDED
*  Self-Shielding calculation EXCEL
*  Transport calculation      EXCEL
*  Flux TYPE K AND B WITH VARIOUS LEAKAGE OPTIONS
*----
TRACK INTLIN := EXCELT: CANDU6SV ::
  TITLE 'TCWW07: CANDU-6 CARTESIAN FUEL TEMP= 941.29'
  EDIT 0 MAXR 14 TRAK TISO 7 20.0 SYMM 4 ;
LIBRARY := SHI: LIBRARY TRACK INTLIN :: EDIT 0 NOLJ ;
TRACK INTLIN := DELETE: TRACK INTLIN ;
TRACK INTLIN := EXCELT: CANDU6TV ::
  TITLE 'TCWW07: CANDU-6 CARTESIAN FUEL TEMP= 941.29'
  EDIT 0 MAXR 32 ANIS 2 TRAK TISO 7 20.0 SYMM 4 ;
SYS := ASM: LIBRARY TRACK INTLIN ::
  EDIT 0 PIJK ;
FLUX := FLU: SYS LIBRARY TRACK ::
  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.137481 ;
EDITION := EDI: EDITION FLUX LIBRARY TRACK ::
  EDIT 3 SAVE COND 4.0 TAKE REGI 1 4 7 10 16 24 ;
FLUX := FLU: FLUX SYS LIBRARY TRACK ::
  TYPE B B1 PNL ;
assertS FLUX :: 'K-INFINITY' 1 1.129739 ;
EDITION := EDI: EDITION FLUX LIBRARY TRACK ::
  EDIT 3 SAVE COND 4.0  TAKE REGI 1 4 7 10 16 24 ;
FLUX := FLU: FLUX SYS LIBRARY TRACK ::
  TYPE B B1 HETE ;
assertS FLUX :: 'K-INFINITY' 1 1.129756 ;
EDITION := EDI: EDITION FLUX LIBRARY TRACK ::
  EDIT 3 SAVE COND 4.0 TAKE REGI 1 4 7 10 16 24 ;
FLUX SYS := DELETE: FLUX SYS ;
SYS := ASM: LIBRARY TRACK INTLIN ::
  EDIT 0 PIJ ECCO ;
FLUX := FLU: SYS LIBRARY TRACK ::
  TYPE B B1 ECCO ;
assertS FLUX :: 'K-INFINITY' 1 1.129767 ;
EDITION := EDI: EDITION FLUX LIBRARY TRACK ::
  EDIT 3 SAVE COND 4.0  TAKE REGI 1 4 7 10 16 24 ;
FLUX SYS TRACK INTLIN := DELETE: FLUX SYS TRACK INTLIN ;
ECHO "test TCWW07 completed" ;
END: ; 
QUIT "LIST" .
