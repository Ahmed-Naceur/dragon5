*---
* Regression test 1 for MCCG: and MOCC:
*---
*  TEST CASE TCWW41 (based on TCWW02)
*  17 X 17 PWR ASSEMBLY WITHOUT POISON
*  WIMS-AECL 69 GROUPS LIBRARY FILE WIMSLIB
*---

*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  ASSMB ASSMB2 TRACK TRACK2 LIBRARY SYS FLX EDITION NEWLIB TEMP ;
SEQ_BINARY 
  TRACK_FIL TRACK_FIL2 ;
MODULE
  LIB: GEO: EXCELT: SHI: ASM: FLU: MCCGT: EDI: DELETE: END: ;
PROCEDURE assertS ;
*----
*  Microscopic cross sections
*----
LIBRARY := LIB: ::
  NMIX 8  CTRA  WIMS
  MIXS LIB: WIMSAECL FIL: WIMSLIB
  MIX 1 579.9
    H1H2O    = H1H2O    4.76690E-2   O16H2O   = O16H2O   2.38345E-2
    BNat     = B-NAT    2.38103E-5
  MIX 2 579.9                        O16      = O16      3.06711E-4
    Cr52     = CR52     7.54987E-5   Fe56     = FE56A    1.47624E-4
    Zr91     = ZR91     4.18621E-2
  MIX 3 579.9
    H1H2O    = H1H2O    4.65292E-2   O16H2O   = O16H2O   2.32646E-2
    Nb93     = NB93     7.07082E-6   Cr52     = CR52     4.79927E-5
    Fe56     = FE56A    4.45845E-5   Ni58     = NI58     1.13521E-4
    Co59     = CO59     1.07714E-7   Mo95     = MO95     4.03755E-6
    Ti48     = TI48     2.33305E-6   Al27     = AL27     2.35231E-6
    Mn55     = MN55     4.15901E-7
    BNat     = B-NAT    2.32761E-5
    Zr91     = ZR91     8.92427E-4
  MIX 4 579.9                        O16      = O16      2.87335E-4
    Cr52     = CR52     7.07291E-5   Fe56     = FE56A    1.38298E-4
    Zr91     = ZR91     3.92175E-2
  MIX 5 579.9
    H1H2O    = H1H2O    4.71346E-2   O16H2O   = O16H2O   2.35673E-2
    Nb93     = NB93     3.31482E-6   Cr52     = CR52     2.24991E-5
    Fe56     = FE56A    2.09013E-5   Ni58     = NI58     5.32188E-5
    Co59     = CO59     5.04968E-8   Mo95     = MO95     1.89281E-6
    Ti48     = TI48     1.09374E-6   Al27     = AL27     1.10277E-6
    Mn55     = MN55     1.94976E-7
    BNat     = B-NAT    2.35598E-5
    Zr91     = ZR91     4.18372E-4
  MIX 6 579.9
    H1H2O    = H1H2O    4.71676E-2   O16H2O   = O16H2O   2.35838E-2
    Nb93     = NB93     3.11049E-6   Cr52     = CR52     2.11122E-5
    Fe56     = FE56A    1.96130E-5   Ni58     = NI58     4.99383E-5
    Co59     = CO59     4.73842E-8   Mo95     = MO95     1.77614E-6
    Ti48     = TI48     1.02632E-6   Al27     = AL27     1.03479E-6
    Mn55     = MN55     1.82957E-7
    BNat     = B-NAT    2.35753E-5
    Zr91     = ZR91     3.92583E-4
  MIX 7 579.9
    H1H2O    = H1H2O    4.72020E-2   O16H2O   = O16H2O   2.36010E-2
    Nb93     = NB93     2.89640E-6   Cr52     = CR52     1.96591E-5
    Fe56     = FE56A    1.82630E-5   Ni58     = NI58     4.65011E-5
    Co59     = CO59     4.41227E-8   Mo95     = MO95     1.65389E-6
    Ti48     = TI48     9.55682E-7   Al27     = AL27     9.63569E-7
    Mn55     = MN55     1.70365E-7
    BNat     = B-NAT    2.35914E-5
    Zr91     = ZR91     3.65562E-4
  MIX 8 933.6                        O16      = O16      4.49355E-2
    U235     = U235     7.39237E-4 1
    U238     = U238     2.17285E-2 1 ;
*----
*  Geometry ASSMB : a 17 X 17 normal PWR assembly
*  contains  C1 : cell without fuel
*            C2 : normal fuel cell
*            C3 : peripheral cell
*            C4 : corner cell
*----
ASSMB := GEO: :: CAR2D 9 9
  X- DIAG X+ REFL Y- SYME Y+ DIAG
  CELL C1 C2 C2 C1 C2 C2 C1 C2 C3
          C2 C2 C2 C2 C2 C2 C2 C3
             C2 C2 C2 C2 C2 C2 C3
                C1 C2 C2 C1 C2 C3
                   C2 C2 C2 C2 C3
                      C1 C2 C2 C3
                         C2 C2 C3
                            C2 C3
                               C4
  MERGE 1  3 12 11 12 12 11 12 15
           4  6  5  6  6  5  6  8
             13  5  6  6  5  6  8
                 2  5  5 10  5  8
                   13  5  5  6  8
                       2  5  7  8
                         13  7  8
                            14  8
                                9
  TURN  H  H  B  H  H  B  H  H  A
           H  G  G  H  G  G  H  A
              A  E  E  F  E  E  A
                 H  H  F  H  H  A
                    H  E  G  H  A
                       H  H  A  A
                          H  A  A
                             A  A
                                A
  ::: C1 := GEO: CARCEL 2
    MESHX 0.0 1.26472 MESHY 0.0 1.26472
    RADIUS 0.0 0.572435 0.613142  MIX 1 2 3 ;
  ::: C2 := GEO: C1  RADIUS 0.0 0.412660 0.474364  MIX 8 4 5 ;
  ::: C3 := GEO: C2  MESHX 0.0 1.31472 MIX 8 4 6 ;
  ::: C4 := GEO: C3  MESHY 0.0 1.31472  MIX 8 4 7 ;
  ;
ASSMB2 := GEO: ASSMB ::
::: C2 := GEO: C2 SPLITR 4 1 ;
::: C3 := GEO: C3 SPLITR 4 1 ;
::: C4 := GEO: C4 SPLITR 4 1 ;
  ;
*----
*  Tracking calculation for Self-Shielding
*  Self-Shielding calculation
*  Tracking calculation for flux
*----
TRACK TRACK_FIL := EXCELT: ASSMB ::
     EDIT 0
     MAXR 200
     TITLE 'TCWW41 : SELF-SHIELDING'
     TRAK TISO 12 12.0 ;
LIBRARY := SHI: LIBRARY TRACK TRACK_FIL :: NOLJ ;
*----
*  MCCG (isotropic BC)
*----
TRACK2 TRACK_FIL2 := EXCELT: ASSMB2 ::
     EDIT 0
     MAXR 200
     TITLE 'TCWW41 : INIT AND FLUX'
     TRAK TISO 12 20.0 ;
*---
*  Initialisation of the mutigroup flux calculation by a CDD calculation
*---
TEMP := TRACK2 ;
TRACK2 := MCCGT: TRACK2 TRACK_FIL2 ASSMB2 ::
     EDIT 4 LCMD 2
     DIFC TMT EPSI 1E-8
     MAXI 100 KRYL 0 SCR 0 HDD 0.0 ;
SYS := ASM:  LIBRARY TRACK2 TRACK_FIL2 :: 
    EDIT 2 ARM ;
FLX := FLU: LIBRARY SYS TRACK2 TRACK_FIL2  ::
     EDIT 1 TYPE K THER 5 5E-5 EXTE 10 1E-5  ;
assertS FLX :: 'K-EFFECTIVE' 1 1.259025 ;
TRACK2 := DELETE: TRACK2 ;
TRACK2 := TEMP ;
*---
*  Multigroup Flux Calculation
*  Edition
*---
TRACK2 := MCCGT: TRACK2 TRACK_FIL2 ASSMB2 ::
     EDIT 4 LCMD 2 
     AAC  10 TMT EPSI 1E-5 STIS 1
     MAXI 1 KRYL 0 SCR 0 HDD 0.0 ;
FLX := FLU: FLX
 LIBRARY SYS TRACK2 TRACK_FIL2  ::
     EDIT 1 TYPE K THER 5 5E-5 EXTE 100 1E-5 ;
assertS FLX :: 'K-EFFECTIVE' 1 1.258883 ;
EDITION := EDI: FLX LIBRARY TRACK2 ASSMB2 :: 
EDIT 2
COND 4.0
MERGE MIX 1 2 3 4 5 6 7 8 ;
*---
* Memory Release
*---
TRACK TRACK_FIL TRACK2 TRACK_FIL2 ASSMB SYS FLX EDITION TEMP
 := DELETE: 
TRACK TRACK_FIL TRACK2 TRACK_FIL2 ASSMB SYS FLX EDITION TEMP ;
*----
*  MOCC (cyclic tracking)
*----
TRACK2 TRACK_FIL2 := EXCELT: ASSMB2 ::
     EDIT 0
     MAXR 200
     TITLE 'TCWW02 : INIT AND FLUX'
     TRAK TSPC 7 20.0 ;
*---
*  Initialisation of the mutigroup flux calculation by a CDD calculation
*---
TEMP := TRACK2 ;
TRACK2 := MCCGT: TRACK2 TRACK_FIL2 ASSMB2 ::
     EDIT 1 LCMD 2
     DIFC TMT EPSI 1E-8 STIS 1
     MAXI 100 KRYL 0 SCR 0 HDD 100000.0 ;
SYS := ASM:  LIBRARY TRACK2 TRACK_FIL2 :: 
     EDIT 2 ARM ;
FLX := FLU: LIBRARY SYS TRACK2 TRACK_FIL2  ::
     EDIT 1 TYPE K THER 5 5E-5 EXTE 10 1E-5  ;
assertS FLX :: 'K-EFFECTIVE' 1 1.258963 ; 
TRACK2 := DELETE: TRACK2 ;
TRACK2 := TEMP ;
*---
*  Multigroup Flux Calculation
*  Edition
*---
TRACK2 := MCCGT: TRACK2 TRACK_FIL2 ASSMB2 ::
     EDIT 1 LCMD 2 
     AAC  80 EPSI 1E-5 STIS 1
     MAXI 1 KRYL 0 SCR 0 HDD 100000.0 ;
FLX := FLU: FLX LIBRARY SYS TRACK2 TRACK_FIL2  ::
     EDIT 1 TYPE K THER 5 5E-5 EXTE 100 1E-5 ;
assertS FLX :: 'K-EFFECTIVE' 1 1.261892 ;
EDITION := EDI: FLX LIBRARY TRACK2 ASSMB2 :: 
EDIT 2
COND 4.0
MERGE MIX 1 2 3 4 5 6 7 8 ;
*---
* Memory Release
*---
TRACK2 TRACK_FIL2 LIBRARY ASSMB2 SYS FLX EDITION
 := DELETE: 
TRACK2 TRACK_FIL2 LIBRARY ASSMB2 SYS FLX EDITION ;
ECHO "test TCWW41 completed" ;
END: ;
QUIT "LIST" .
