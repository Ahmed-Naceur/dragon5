*----
*  TEST CASE TCWW06
*  CANDU-6 CARTESIAN CELL
*  WIMS-AECL 69 GROUPS LIBRARY FILE WIMSLIB
*  STAINLESS STELL RODS IN 3D SUPERCELL
*
*  REF: R. Roy et al.  Ann. Nucl. Energy 21, 115 (1994)
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  LIBRARY LIBRARY2 CANDU6F CANDU6S TRACK SYS FLUX EDITION BCO BCI
  SSRODS ;
SEQ_BINARY
  INTLIN ;
MODULE
  GEO: EXCELT: LIB: SHI: ASM: FLU: EDI: DELETE: END: ;
PROCEDURE assertS ;
*----
*  Microscopic cross sections from file WIMSLIB format WIMS-AECL
*----
LIBRARY := LIB: ::
  EDIT 0 NMIX 10 CTRA WIMS
  MIXS LIB: WIMSAECL FIL: WIMSLIB
  MIX  1  560.66   0.81212           O16      = O16      7.99449E-1
    D2D2O    = D2D2O    1.99768E-1   H1H2O    = H1H2O    7.83774E-4
  MIX  2  560.66   6.57              Nb93     = NB93     2.50000
    BNat     = B-NAT    2.10000E-4
    Zr91     = ZR91     9.75000E+1
  MIX  3  345.66   0.0014            He4      = HE4      1.00000E+2
  MIX  4  345.66   6.44              Fe56     = FE56A    1.60000E-1
    Ni58     = NI58     6.00000E-2   Cr52     = CR52     1.10000E-1
    BNat     = B-NAT    3.10000E-4
    Zr91     = ZR91     9.97100E+1
  MIX  5  345.66   1.082885          O16      = O16      7.98895E-1
    D2D2O    = D2D2O    2.01016E-1   H1H2O    = H1H2O    8.96000E-5
  MIX  6  941.29  10.4375010         O16      = O16      1.18473E+1
    U235     = U235     6.27118E-1 1
    U238     = U238     8.75256E+1 1
  MIX  7  COMB 6 1.0
  MIX  8  COMB 6 1.0
  MIX  9  COMB 6 1.0
  MIX 10   560.66   6.44             Fe56     = FE56A    1.60000E-1
    Ni58     = NI58     6.00000E-2   Cr52     = CR52     1.10000E-1
    BNat     = B-NAT    3.10000E-4
    Zr91     = ZR91     9.97100E+1
 ;
*----
*  CELL CALCULATION
*  Geometry CANDU6S : 14 regions Cartesian cluster for self-shielding
*           CANDU6F : 32 regions Cartesian cluster for transport
*           BCO     : 48 regions 3D Cartesian geometry
*           BCI     : 48 regions 3D Cartesian geometry
*----
CANDU6S := GEO: ::  CARCEL  5
  X+ REFL X- REFL MESHX -14.2875 14.2875
  Y+ REFL Y- REFL MESHY -14.2875 14.2875
  RADIUS  0.00000 5.16890 5.60320  6.44780 6.58750 14.00
  MIX  1 2 3 4 5 5
  CLUSTER ROD1 ROD2 ROD3 ROD4
  ::: ROD1 := GEO: TUBE 2 MIX 6 10 NPIN  1 RPIN 0.0000 APIN 0.0000
    RADIUS 0.00000 0.6122 0.6540 ;
  ::: ROD2 := GEO: ROD1   MIX 7 10 NPIN  6 RPIN 1.4885 APIN 0.0000 ;
  ::: ROD3 := GEO: ROD1   MIX 8 10 NPIN 12 RPIN 2.8755 APIN 0.261799 ;
  ::: ROD4 := GEO: ROD1   MIX 9 10 NPIN 18 RPIN 4.3305 APIN 0.0 ;
  ;
CANDU6F := GEO: CANDU6S :: SPLITR  6 1 1 1 10
  ::: ROD1 := GEO: ROD1 SPLITR 2 1 ;
  ::: ROD2 := GEO: ROD2 SPLITR 2 1 ;
  ::: ROD3 := GEO: ROD3 SPLITR 2 1 ;
  ::: ROD4 := GEO: ROD4 SPLITR 2 1 ;
  ;
*----
*  Self-Shielding calculation EXCEL
*  Transport calculation      EXCEL
*  Flux calculation for keff
*----
TRACK INTLIN := EXCELT: CANDU6S ::
  TITLE 'TCWW06: CANDU-6 CARTESIAN FUEL TEMP= 941.29'
  EDIT 0 MAXR 14 TRAK TISO 29 20.0 SYMM 4 ;
LIBRARY := SHI: LIBRARY TRACK INTLIN :: EDIT 0 NOLJ ;
TRACK INTLIN := DELETE: TRACK INTLIN ;
TRACK INTLIN := EXCELT: CANDU6F ::
  TITLE 'TCWW06: CANDU-6 CARTESIAN FUEL TEMP= 941.29'
  EDIT 0 MAXR 32 TRAK TISO 29 20.0 SYMM 4 ;
SYS := ASM: LIBRARY TRACK INTLIN ::
  EDIT 0 ;
FLUX := FLU: SYS LIBRARY TRACK ::
  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.117250 ;
*----
*  Microscopic cross sections from WIMSLIB for stainless steel
*  MIX 1,2,3 from EDI: : fuel, structure material and moderator
*  MIX 4 stainless steel rods
*----
EDITION := EDI: FLUX LIBRARY TRACK ::
  EDIT 0 MERGE MIX 1 2 2 2 3 1 1 1 1 1 SAVE ON SSRODS ;
SSRODS := EDITION :: STEP UP SSRODS ;
LIBRARY2 := LIB: SSRODS ::
  EDIT 0 NMIX 4 CTRA WIMS
  MIXS LIB: WIMSAECL FIL: WIMSLIB
  MIX 4    345.66                    Fe56     = FE56A    6.19027E-2
    Cr52     = CR52     1.56659E-3   Ni58     = NI58     6.83337E-3
    Si29     = SI29     7.79072E-4   C12      = C12      1.46552E-4
    Mn55     = MN55     1.25431E-3
  ;
EDITION TRACK INTLIN SYS FLUX SSRODS := DELETE:
  EDITION TRACK INTLIN SYS FLUX SSRODS ;
*----
*  SUPERCELL CALCULATION
*  Geometry BCO     : 27 regions 3D Cartesian geometry with rods out
*           BCI     : 27 regions 3D Cartesian geometry with rods in
*----
BCO := GEO: :: CAR3D 3 2 2
  X- REFL X+ SYME Y- REFL Y+ SYME Z- REFL Z+ SYME
  CELL  M   MX  MX MX  FXY MXY  M   MX  BX MX  FXY BXY
  TURN  A   A   A  F   A   A    A   A   A  F   A   A
  ::: M := GEO: CAR3D 1 1 1 MIX 3
    MESHX  0.0 7.14375
    MESHY  0.0 7.14375
    MESHZ -8.25500 8.25500 SPLITZ 2  ;
  ::: MX := GEO: M  MESHX -7.14375 +7.14375 SPLITX 2 ;
  ::: MXY := GEO: MX MESHY -7.14375 +7.14375 SPLITY 2 ;
  ::: BX := GEO: CARCELY 2 1 MIX 3 3 3
    MESHX -7.14375 7.14375 SPLITX  2
    MESHY 0.0 7.14375
    MESHZ -8.25500 8.25500 SPLITZ 2
    RADIUS 0.0 3.5100 3.8100 ;
  ::: BXY := GEO: BX MESHY -7.14375 +7.14375 SPLITY 2 ;
  ::: FXY := GEO: CARCELZ 2 1 MIX 1  2  3
    MESHX -7.14375 7.14375 SPLITX 2
    MESHY -7.14375 7.14375 SPLITY 2
    MESHZ -8.25500 8.25500 SPLITZ 2
    RADIUS 0.0 5.16890 6.58750 ;
  ;
BCI := GEO: BCO ::
  ::: BX := GEO: BX  MIX 3 4 3 ;
  ::: BXY := GEO: BXY MIX 3 4 3 ;
  ;
*----
*  Transport calculation      EXCEL
*  Flux calculation for keff
*  Homogenized properties for rod out
*----
TRACK INTLIN := EXCELT: BCO ::
  EDIT 0 MAXR 40 TRAK TISO 2 1.0 ;
SYS := ASM: LIBRARY2 TRACK INTLIN ::
  EDIT 0 ;
FLUX := FLU: SYS LIBRARY2 TRACK ::
  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.124153 ;
EDITION := EDI: FLUX LIBRARY2 TRACK ::
  EDIT 2 MERG COMP COND 4.0 SAVE ON  'NOBC' ;
SYS TRACK INTLIN := DELETE: SYS TRACK INTLIN ;
*----
*  Transport calculation      EXCEL
*  Flux calculation for keff
*  Homogenized properties for rod in
*----
TRACK INTLIN := EXCELT: BCI ::
  EDIT 0 MAXR 40 TRAK TISO 2 1.0 ;
SYS := ASM: LIBRARY2 TRACK INTLIN ::
  EDIT 0 ;
FLUX := FLU: FLUX SYS LIBRARY2 TRACK ::
  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.040033 ;
EDITION := EDI: EDITION FLUX LIBRARY2 TRACK ::
  EDIT 2 MERG COMP COND 4.0 STAT DELS REFE 'NOBC' ;
TRACK INTLIN SYS := DELETE: TRACK INTLIN SYS ;
ECHO "test TCWW06 completed" ;
END: ;
QUIT "LIST" .
