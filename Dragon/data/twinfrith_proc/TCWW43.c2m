*---
* Regression test for MCCG:
*---
*  TEST CASE TCWW43 (based on TCWW02)
*  17 X 17 PWR ASSEMBLY WITHOUT POISON
*  WIMS-AECL 69 GROUPS LIBRARY FILE WIMSLIB
*---

*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  ASSMB ASSMB2 TRACK LIBRARY SYS FLX EDITION NEWLIB TEMP ;
SEQ_BINARY 
  TRACK_FIL ;
MODULE
  LIB: GEO: EXCELT: SHI: ASM: FLU: MCCGT: EDI: GREP: DELETE: END: ;
PROCEDURE assertS ;
REAL k2D kpriz ;
*----
*  Microscopic cross sections
*----
LIBRARY := LIB: ::
  NMIX 8  CTRA  WIMS
  MIXS LIB: WIMSAECL FIL: WIMSLIB
  MIX 1 579.9
    H1H2O    = H1H2O    4.76690E-2   O16H2O   = O16H2O   2.38345E-2
    BNat     = B-NAT    2.38103E-5
  MIX 2 579.9                        O16      = O16      3.06711E-4
    Cr52     = CR52     7.54987E-5   Fe56     = FE56A    1.47624E-4
    Zr91     = ZR91     4.18621E-2
  MIX 3 579.9
    H1H2O    = H1H2O    4.65292E-2   O16H2O   = O16H2O   2.32646E-2
    Nb93     = NB93     7.07082E-6   Cr52     = CR52     4.79927E-5
    Fe56     = FE56A    4.45845E-5   Ni58     = NI58     1.13521E-4
    Co59     = CO59     1.07714E-7   Mo95     = MO95     4.03755E-6
    Ti48     = TI48     2.33305E-6   Al27     = AL27     2.35231E-6
    Mn55     = MN55     4.15901E-7
    BNat     = B-NAT    2.32761E-5
    Zr91     = ZR91     8.92427E-4
  MIX 4 579.9                        O16      = O16      2.87335E-4
    Cr52     = CR52     7.07291E-5   Fe56     = FE56A    1.38298E-4
    Zr91     = ZR91     3.92175E-2
  MIX 5 579.9
    H1H2O    = H1H2O    4.71346E-2   O16H2O   = O16H2O   2.35673E-2
    Nb93     = NB93     3.31482E-6   Cr52     = CR52     2.24991E-5
    Fe56     = FE56A    2.09013E-5   Ni58     = NI58     5.32188E-5
    Co59     = CO59     5.04968E-8   Mo95     = MO95     1.89281E-6
    Ti48     = TI48     1.09374E-6   Al27     = AL27     1.10277E-6
    Mn55     = MN55     1.94976E-7
    BNat     = B-NAT    2.35598E-5
    Zr91     = ZR91     4.18372E-4
  MIX 6 579.9
    H1H2O    = H1H2O    4.71676E-2   O16H2O   = O16H2O   2.35838E-2
    Nb93     = NB93     3.11049E-6   Cr52     = CR52     2.11122E-5
    Fe56     = FE56A    1.96130E-5   Ni58     = NI58     4.99383E-5
    Co59     = CO59     4.73842E-8   Mo95     = MO95     1.77614E-6
    Ti48     = TI48     1.02632E-6   Al27     = AL27     1.03479E-6
    Mn55     = MN55     1.82957E-7
    BNat     = B-NAT    2.35753E-5
    Zr91     = ZR91     3.92583E-4
  MIX 7 579.9
    H1H2O    = H1H2O    4.72020E-2   O16H2O   = O16H2O   2.36010E-2
    Nb93     = NB93     2.89640E-6   Cr52     = CR52     1.96591E-5
    Fe56     = FE56A    1.82630E-5   Ni58     = NI58     4.65011E-5
    Co59     = CO59     4.41227E-8   Mo95     = MO95     1.65389E-6
    Ti48     = TI48     9.55682E-7   Al27     = AL27     9.63569E-7
    Mn55     = MN55     1.70365E-7
    BNat     = B-NAT    2.35914E-5
    Zr91     = ZR91     3.65562E-4
  MIX 8 933.6                        O16      = O16      4.49355E-2
    U235     = U235     7.39237E-4 1
    U238     = U238     2.17285E-2 1 ;
*----
*  Geometry ASSMB2 : a 17 X 17 normal PWR assembly
*  contains  C1 : cell without fuel
*            C2 : normal fuel cell
*            C3 : peripheral cell
*            C4 : corner cell
*----
ASSMB2 := GEO: :: CAR2D 9 9
  X- DIAG X+ REFL Y- SYME Y+ DIAG
  CELL C1 C2 C2 C1 C2 C2 C1 C2 C3
          C2 C2 C2 C2 C2 C2 C2 C3
             C2 C2 C2 C2 C2 C2 C3
                C1 C2 C2 C1 C2 C3
                   C2 C2 C2 C2 C3
                      C1 C2 C2 C3
                         C2 C2 C3
                            C2 C3
                               C4
  MERGE 1  3 12 11 12 12 11 12 15
           4  6  5  6  6  5  6  8
             13  5  6  6  5  6  8
                 2  5  5 10  5  8
                   13  5  5  6  8
                       2  5  7  8
                         13  7  8
                            14  8
                                9
  TURN  H  H  B  H  H  B  H  H  A
           H  G  G  H  G  G  H  A
              A  E  E  F  E  E  A
                 H  H  F  H  H  A
                    H  E  G  H  A
                       H  H  A  A
                          H  A  A
                             A  A
                                A
  ::: C1 := GEO: CARCEL 2
    MESHX 0.0 1.26472 MESHY 0.0 1.26472
    RADIUS 0.0 0.572435 0.613142  MIX 1 2 3 ;
  ::: C2 := GEO: C1  RADIUS 0.0 0.412660 0.474364  MIX 8 4 5 ;
  ::: C3 := GEO: C2  MESHX 0.0 1.31472 MIX 8 4 6 ;
  ::: C4 := GEO: C3  MESHY 0.0 1.31472  MIX 8 4 7 ;
  ;
*----
*  Geometry ASSMB : 3D prismatic extension
*----
ASSMB := GEO: :: CAR3D 9 9 1
  X- DIAG X+ REFL Y- SYME Y+ DIAG
  Z- REFL Z+ REFL
  CELL C1 C2 C2 C1 C2 C2 C1 C2 C3
          C2 C2 C2 C2 C2 C2 C2 C3
             C2 C2 C2 C2 C2 C2 C3
                C1 C2 C2 C1 C2 C3
                   C2 C2 C2 C2 C3
                      C1 C2 C2 C3
                         C2 C2 C3
                            C2 C3
                               C4
  MERGE 1  3 12 11 12 12 11 12 15
           4  6  5  6  6  5  6  8
             13  5  6  6  5  6  8
                 2  5  5 10  5  8
                   13  5  5  6  8
                       2  5  7  8
                         13  7  8
                            14  8
                                9
  TURN  H  H  B  H  H  B  H  H  A
           H  G  G  H  G  G  H  A
              A  E  E  F  E  E  A
                 H  H  F  H  H  A
                    H  E  G  H  A
                       H  H  A  A
                          H  A  A
                             A  A
                                A
  ::: C1 := GEO: CARCELZ 2 1 1 1
    MESHX 0.0 1.26472 MESHY 0.0 1.26472 MESHZ 0.0 10.0
    RADIUS 0.0 0.572435 0.613142  MIX 1 2 3 ;
  ::: C2 := GEO: C1  RADIUS 0.0 0.412660 0.474364  MIX 8 4 5 ;
  ::: C3 := GEO: C2  MESHX 0.0 1.31472 MIX 8 4 6 ;
  ::: C4 := GEO: C3  MESHY 0.0 1.31472  MIX 8 4 7 ;
  ;
*----
*  Tracking calculation for Self-Shielding
*  Self-Shielding calculation
*----
TRACK TRACK_FIL := EXCELT: ASSMB2 ::
  EDIT 0 MAXR 200 TITLE 'TCWW43 : SELF-SHIELDING'
  TRAK TISO 6 10.0 ;
LIBRARY := SHI: LIBRARY TRACK TRACK_FIL :: NOLJ ;
*----
*  MCCG 2D calculation
*----
* Initialisation of the mutigroup flux calculation by a CDD calculation
TEMP := TRACK ;
TRACK := MCCGT: TRACK TRACK_FIL ASSMB2 ::
     EDIT 1 DGAU 2
     DIFC TMT EPSI 1E-8
     MAXI 100 KRYL 0 SCR 0 HDD 0.0 ;
SYS := ASM:  LIBRARY TRACK TRACK_FIL :: 
    EDIT 2 ARM ;
FLX := FLU: LIBRARY SYS TRACK TRACK_FIL  ::
     EDIT 2 TYPE K THER 5 5E-5 EXTE 10 1E-5  ;
assertS FLX :: 'K-EFFECTIVE' 1 1.257893 ;
TRACK := DELETE: TRACK ;
TRACK := TEMP ;
* Multigroup Flux Calculation
* Edition
TRACK := MCCGT: TRACK TRACK_FIL ASSMB2 ::
     EDIT 1 DGAU 2
     AAC  10 TMT EPSI 1E-5 STIS 1
     MAXI 1 KRYL 0 SCR 0 HDD 0.0 ;
FLX := FLU: FLX LIBRARY SYS TRACK TRACK_FIL  ::
     EDIT 2 TYPE K THER 5 5E-5 EXTE 100 1E-5 ;
assertS FLX :: 'K-EFFECTIVE' 1 1.258035 ;
GREP: FLX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>k2D<< ; 
EDITION := EDI: FLX LIBRARY TRACK ASSMB2 :: 
EDIT 2 COND 4.0
MERGE MIX 1 2 3 4 5 6 7 8 ;
TRACK TRACK_FIL ASSMB2 SYS FLX EDITION TEMP
 := DELETE: 
TRACK TRACK_FIL ASSMB2 SYS FLX EDITION TEMP ;
*----
*  MCCG 3D PRIZ calculation
*----
TRACK TRACK_FIL := EXCELT: ASSMB ::
  EDIT 0 MAXR 200 TITLE 'TCWW43 : PRIZ'
  PRIZ 5.0 GAUS 2 TRAK TISO 6 10.0 ;
* Initialisation of the mutigroup flux calculation by a CDD calculation
TEMP := TRACK ;
TRACK := MCCGT: TRACK TRACK_FIL ASSMB ::
     EDIT 1
     DIFC TMT EPSI 1E-8
     MAXI 100 KRYL 0 SCR 0 HDD 0.0 ;
SYS := ASM:  LIBRARY TRACK TRACK_FIL :: 
    EDIT 2 ARM ;
FLX := FLU: LIBRARY SYS TRACK TRACK_FIL  ::
     EDIT 2 TYPE K THER 5 5E-5 EXTE 10 1E-5  ;
assertS FLX :: 'K-EFFECTIVE' 1 1.258423 ;
TRACK := DELETE: TRACK ;
TRACK := TEMP ;
* Multigroup Flux Calculation
* Edition
TRACK := MCCGT: TRACK TRACK_FIL ASSMB ::
     EDIT 1
     AAC  10 TMT EPSI 1E-5 STIS 1
     MAXI 1 KRYL 0 SCR 0 HDD 0.0 ;
FLX := FLU: FLX
 LIBRARY SYS TRACK TRACK_FIL  ::
     EDIT 2 TYPE K THER 5 5E-5 EXTE 100 1E-5 ;
assertS FLX :: 'K-EFFECTIVE' 1 1.257924 ;
GREP: FLX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>kpriz<< ; 
EDITION := EDI: FLX LIBRARY TRACK ASSMB :: 
EDIT 2 COND 4.0
MERGE MIX 1 2 3 4 5 6 7 8 ;
ECHO "**********************" ;
ECHO "2D     " k2D ;
ECHO "3D PRIZ" kpriz ;
ECHO "**********************" ;
*---
* Memory Release
*---
LIBRARY TRACK TRACK_FIL ASSMB SYS FLX EDITION TEMP
 := DELETE: 
LIBRARY TRACK TRACK_FIL ASSMB SYS FLX EDITION TEMP ;
ECHO "test TCWW43 completed" ;
END: ;
QUIT "LIST" .
