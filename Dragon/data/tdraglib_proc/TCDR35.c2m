*----
*
*  TEST CASE TCDR35 WITH SN SELF-SHIELDING AND FLUX SOLUTIONS
*  FIRST UO2 ROWLAND'S BENCHMARK
*  DISTRIBUTED SELF-SHIELDING
*  172-GROUP JEF2.2 DRAGLIB
*
*  Author: A. Hebert
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST LIBRARY LIBRARY2 GEOM1 GEOM2 TRACK SYS FLUX ;
MODULE GEO: SNT: USS: MAC: LIB: ASM: FLU: DELETE: END: ;
INTEGER COMB0101 COMB0102 COMB0103 COMB0104 COMB0105 COMB0106 GAIN1
        MODE1 MODE2 := 
        1 2 3 4 5 6 7 8 9 ; (* DISTRIBUTED SELF-SHIELDING *)
REAL RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 ;
PROCEDURE assertS ;
*
LIBRARY := LIB: ::
 EDIT 3
 NMIX 9    (*MAXIMUM OF MATERIAL MIXTURES*)
 ANIS 2
 CTRA APOL
 PTSL      (*COMPUTE SUBGROUPS*)
*
 MIXS LIB: DRAGON FIL: DLIB_J2
 MIX <<COMB0101>> 293.0 (*COMB0101*)
    O16     = O16    4.6624E-2
    U235    = U235   7.0803E-4 CORR 1 IRSET PTSL 1
    U238    = U238   2.2604E-2 CORR 1 IRSET PTSL 1
 MIX <<COMB0102>> COMB <<COMB0101>> 1.0 (*COMB0102*)
 MIX <<COMB0103>> COMB <<COMB0101>> 1.0 (*COMB0103*)
 MIX <<COMB0104>> COMB <<COMB0101>> 1.0 (*COMB0104*)
 MIX <<COMB0105>> COMB <<COMB0101>> 1.0 (*COMB0105*)
 MIX <<COMB0106>> COMB <<COMB0101>> 1.0 (*COMB0106*)
 MIX <<GAIN1>> 293.0 (*GAIN1*)
    Zr0     = Zr0    4.3241E-2 2 IRSET PTSL 1
 MIX <<MODE1>> 293.0 (*MODE1*)
    H1      = H1_H2O 6.6988E-2
    O16     = O16    3.3494E-2
 MIX <<MODE2>> 293.0 (*MODE2*)
    H1      = H1_H2O 6.6988E-2
    O16     = O16    3.3494E-2
 ;
EVALUATE RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 := 0.2529822 
0.334664 0.3577709 0.3794733 0.3898718 0.40 ;
GEOM1 := GEO: :: TUBE 9
      R+ REFL
      RADIUS 0.0 <<RCOMB1>> <<RCOMB2>> <<RCOMB3>> <<RCOMB4>>
                 <<RCOMB5>> <<RCOMB6>> 0.45 0.5748331 0.6770275
      MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
          <<COMB0105>> <<COMB0106>> <<GAIN1>> <<MODE1>> <<MODE2>>
      SPLITR 1 1 1 1 1 1 1 -2 -2
      ;
GEOM2 := GEO: :: TUBE 9
      R+ REFL
      RADIUS 0.0 <<RCOMB1>> <<RCOMB2>> <<RCOMB3>> <<RCOMB4>>
                 <<RCOMB5>> <<RCOMB6>> 0.45 0.5748331 0.6770275
      MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
          <<COMB0105>> <<COMB0106>> <<GAIN1>> <<MODE1>> <<MODE2>>
      SPLITR 1 1 1 1 1 1 1 -8 -8
      ;
TRACK := SNT: GEOM1 ::
     EDIT 1
     MAXR 50 (*MAXIMUM OF 50 REGIONS*)
     SN 16 SCAT 1 QUAD 10 ;
LIBRARY2 := USS: LIBRARY TRACK :: EDIT 0 ARM PASS 4 GRMIN 45 ;
TRACK := DELETE: TRACK ;
TRACK := SNT: GEOM2 ::
     EDIT 1
     MAXR 50 (*MAXIMUM OF 50 REGIONS*)
     SN 16 SCAT 1 QUAD 10 ;
SYS := ASM: LIBRARY2 TRACK :: ARM ;
FLUX := FLU: SYS LIBRARY2 TRACK :: EDIT 1 TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.387591 ;
ECHO "test TCDR35 completed" ;
END: ;
