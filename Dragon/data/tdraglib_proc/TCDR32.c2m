*----
*
*  TEST CASE TCDR32
*  FIRST MOX ROWLAND'S BENCHMARK
*  DISTRIBUTED SELF-SHIELDING
*  172-GROUP JEF2.2 DRAGLIB
*
*  Author: A. Hebert
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM TRACK LIBRARY LIBRARY2 CP FLUX ;
MODULE LIB: GEO: SYBILT: SHI: USS: ASM: FLU: DELETE: END: ;
INTEGER COMB0101 COMB0102 COMB0103 COMB0104 COMB0105 COMB0106 GAIN1
        MODE1 MODE2 := 
        1 2 3 4 5 6 7 8 9 ; (* DISTRIBUTED SELF-SHIELDING *)
REAL RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 ;
PROCEDURE assertS ;
*
EVALUATE RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 := 0.259307 
0.343031 0.366715 0.388960 0.3996186 0.41 ;
GEOM := GEO: :: TUBE 9
      R+ REFL
      RADIUS 0.0 <<RCOMB1>> <<RCOMB2>> <<RCOMB3>> <<RCOMB4>>
                 <<RCOMB5>> <<RCOMB6>> 0.475 0.6045552 0.710879
      MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
          <<COMB0105>> <<COMB0106>> <<GAIN1>> <<MODE1>> <<MODE2>>
          ;
TRACK := SYBILT: GEOM ::
     TITLE 'FIRST MOX ROWLAND S BENCHMARK'
     MAXR 20 QUA1 5 ;
*----
* SHI LEVEL 1
*----
LIBRARY := LIB: ::
 EDIT 1
 NMIX 9    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 ADED 4 NELAS N4N N2N N3N
*
 MIXS LIB: DRAGON FIL: DLIB_J2
 MIX <<COMB0101>> 300.0 (*COMB0101*)
    U235     = U235  5.105E-5 1 IRSET 0.0 81
    U238     = U238  2.037E-2 1 IRSET 0.0 81
    PU238    = Pu238 4.669E-5 1 IRSET 0.0 81
    PU239    = Pu239 1.465E-3 1 IRSET 0.0 81
    PU240    = Pu240 5.691E-4 1 IRSET 0.0 81
    PU241    = Pu241 2.713E-4 1 IRSET 0.0 81
    PU242    = Pu242 1.413E-4 1 IRSET 0.0 81
    AM241    = Am241 3.028E-5 1 IRSET 0.0 81
    O16      = O16   4.588E-2
 MIX <<COMB0102>> COMB <<COMB0101>> 1.0 (*COMB0102*)
 MIX <<COMB0103>> COMB <<COMB0101>> 1.0 (*COMB0103*)
 MIX <<COMB0104>> COMB <<COMB0101>> 1.0 (*COMB0104*)
 MIX <<COMB0105>> COMB <<COMB0101>> 1.0 (*COMB0105*)
 MIX <<COMB0106>> COMB <<COMB0101>> 1.0 (*COMB0106*)
 MIX <<GAIN1>> 300.0 (*GAIN1*)
    ZR      = Zr0   3.88E-2 2 IRSET 0.0 81
 MIX <<MODE1>> 293.0 (*MODE1*)
    H1      = H1_H2O 4.744E-2
    O16     = O16 2.372E-2
 MIX <<MODE2>> 293.0 (*MODE2*)
    H1      = H1_H2O 4.744E-2
    O16     = O16 2.372E-2
 ;
LIBRARY2 := SHI: LIBRARY TRACK :: EDIT 1 LJ GRMIN 45 LEVEL 1 MXIT 3 ;
CP := ASM: LIBRARY2 TRACK :: PIJ ;
FLUX := FLU: CP LIBRARY2 TRACK :: TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.212449 ;
LIBRARY2 CP FLUX := DELETE: LIBRARY2 CP FLUX ;
*----
* SHI LEVEL 2
*----
LIBRARY2 := SHI: LIBRARY TRACK :: EDIT 1 LJ GRMIN 45 LEVEL 2 MXIT 3 ;
CP := ASM: LIBRARY2 TRACK :: PIJ ;
FLUX := FLU: CP LIBRARY2 TRACK :: TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.217413 ;
LIBRARY LIBRARY2 CP FLUX := DELETE: LIBRARY LIBRARY2 CP FLUX ;
*----
* USS
*----
LIBRARY := LIB: ::
 EDIT 1
 NMIX 9    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 ADED 4 NELAS N4N N2N N3N
 SUBG      (*HELIOS TYPE PROBABILITY TABLES*)
*
 MIXS LIB: DRAGON FIL: DLIB_J2
 MIX <<COMB0101>> 300.0 (*COMB0101*)
    U235     = U235  5.105E-5 1 IRSET 0.0 81
    U238     = U238  2.037E-2 1 IRSET 0.0 81
    PU238    = Pu238 4.669E-5 1 IRSET 0.0 81
    PU239    = Pu239 1.465E-3 1 IRSET 0.0 81
    PU240    = Pu240 5.691E-4 1 IRSET 0.0 81
    PU241    = Pu241 2.713E-4 1 IRSET 0.0 81
    PU242    = Pu242 1.413E-4 1 IRSET 0.0 81
    AM241    = Am241 3.028E-5 1 IRSET 0.0 81
    O16      = O16   4.588E-2
 MIX <<COMB0102>> COMB <<COMB0101>> 1.0 (*COMB0102*)
 MIX <<COMB0103>> COMB <<COMB0101>> 1.0 (*COMB0103*)
 MIX <<COMB0104>> COMB <<COMB0101>> 1.0 (*COMB0104*)
 MIX <<COMB0105>> COMB <<COMB0101>> 1.0 (*COMB0105*)
 MIX <<COMB0106>> COMB <<COMB0101>> 1.0 (*COMB0106*)
 MIX <<GAIN1>> 300.0 (*GAIN1*)
    ZR      = Zr0   3.88E-2 2 IRSET 0.0 81
 MIX <<MODE1>> 293.0 (*MODE1*)
    H1      = H1_H2O 4.744E-2
    O16     = O16 2.372E-2
 MIX <<MODE2>> 293.0 (*MODE2*)
    H1      = H1_H2O 4.744E-2
    O16     = O16 2.372E-2
 ;
LIBRARY2 := USS: LIBRARY TRACK :: EDIT 1 TRAN PASS 3 GRMIN 45 ;
CP := ASM: LIBRARY2 TRACK :: PIJ ;
FLUX := FLU: CP LIBRARY2 TRACK :: TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.216461 ;
LIBRARY LIBRARY2 CP FLUX := DELETE: LIBRARY LIBRARY2 CP FLUX ;
*----
* Ribon extended
*----
LIBRARY := LIB: ::
 EDIT 1
 NMIX 9    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 ADED 4 NELAS N4N N2N N3N
 PTSL      (*CALENDF TYPE PROBABILITY TABLES*)
*
 MIXS LIB: DRAGON FIL: DLIB_J2
 MIX <<COMB0101>> 300.0 (*COMB0101*)
    U235     = U235  5.105E-5 1 IRSET PTSL 1
    U238     = U238  2.037E-2 1 IRSET PTSL 1
    PU238    = Pu238 4.669E-5 1 IRSET PTSL 1
    PU239    = Pu239 1.465E-3 1 IRSET PTSL 1
    PU240    = Pu240 5.691E-4 1 IRSET PTSL 1
    PU241    = Pu241 2.713E-4 1 IRSET PTSL 1
    PU242    = Pu242 1.413E-4 1 IRSET PTSL 1
    AM241    = Am241 3.028E-5 1 IRSET PTSL 1
    O16      = O16   4.588E-2
 MIX <<COMB0102>> COMB <<COMB0101>> 1.0 (*COMB0102*)
 MIX <<COMB0103>> COMB <<COMB0101>> 1.0 (*COMB0103*)
 MIX <<COMB0104>> COMB <<COMB0101>> 1.0 (*COMB0104*)
 MIX <<COMB0105>> COMB <<COMB0101>> 1.0 (*COMB0105*)
 MIX <<COMB0106>> COMB <<COMB0101>> 1.0 (*COMB0106*)
 MIX <<GAIN1>> 300.0 (*GAIN1*)
    ZR      = Zr0   3.88E-2 2 IRSET PTSL 1
 MIX <<MODE1>> 293.0 (*MODE1*)
    H1      = H1_H2O 4.744E-2
    O16     = O16 2.372E-2
 MIX <<MODE2>> 293.0 (*MODE2*)
    H1      = H1_H2O 4.744E-2
    O16     = O16 2.372E-2
 ;
LIBRARY2 := USS: LIBRARY TRACK :: EDIT 1 TRAN PASS 3 GRMIN 45 ;
CP := ASM: LIBRARY2 TRACK :: PIJ ;
FLUX := FLU: CP LIBRARY2 TRACK :: TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.216642 ;
LIBRARY LIBRARY2 CP FLUX := DELETE: LIBRARY LIBRARY2 CP FLUX ;
*----
* Ribon extended with correlation
*----
LIBRARY := LIB: ::
 EDIT 1
 NMIX 9    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 ADED 4 NELAS N4N N2N N3N
 PTSL      (*CALENDF TYPE PROBABILITY TABLES*)
*
 MIXS LIB: DRAGON FIL: DLIB_J2
 MIX <<COMB0101>> 300.0 (*COMB0101*)
    PU240    = Pu240 5.691E-4 CORR 1 IRSET PTSL 1
 MIXS LIB: DRAGON FIL: DLIB_J2
 MIX <<COMB0101>> 300.0 (*COMB0101*)
    U235     = U235  5.105E-5 1 IRSET PTSL 1
    U238     = U238  2.037E-2 CORR 1 IRSET PTSL 1
    PU238    = Pu238 4.669E-5 1 IRSET PTSL 1
    PU239    = Pu239 1.465E-3 1 IRSET PTSL 1
    PU241    = Pu241 2.713E-4 1 IRSET PTSL 1
    PU242    = Pu242 1.413E-4 1 IRSET PTSL 1
    AM241    = Am241 3.028E-5 1 IRSET PTSL 1
    O16      = O16   4.588E-2
 MIX <<COMB0102>> COMB <<COMB0101>> 1.0 (*COMB0102*)
 MIX <<COMB0103>> COMB <<COMB0101>> 1.0 (*COMB0103*)
 MIX <<COMB0104>> COMB <<COMB0101>> 1.0 (*COMB0104*)
 MIX <<COMB0105>> COMB <<COMB0101>> 1.0 (*COMB0105*)
 MIX <<COMB0106>> COMB <<COMB0101>> 1.0 (*COMB0106*)
 MIX <<GAIN1>> 300.0 (*GAIN1*)
    ZR      = Zr0   3.88E-2 2 IRSET PTSL 1
 MIX <<MODE1>> 293.0 (*MODE1*)
    H1      = H1_H2O 4.744E-2
    O16     = O16 2.372E-2
 MIX <<MODE2>> 293.0 (*MODE2*)
    H1      = H1_H2O 4.744E-2
    O16     = O16 2.372E-2
 ;
LIBRARY2 := USS: LIBRARY TRACK :: EDIT 1 TRAN PASS 4 GRMIN 45 ;
CP := ASM: LIBRARY2 TRACK :: PIJ ;
FLUX := FLU: CP LIBRARY2 TRACK :: TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.218970 ;
LIBRARY LIBRARY2 CP FLUX := DELETE: LIBRARY LIBRARY2 CP FLUX ;
ECHO "test TCDR32 completed" ;
END: ;
