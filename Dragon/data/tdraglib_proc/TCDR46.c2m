*----
*  TEST CASE TRDR46
*  15 X 15 PWR ASSEMBLY WITHOUT POISON
*  172-GROUP JEF2.2 DRAGLIB
*  SPH EQUIVALENCE WITH SELENGUT NORMALIZATION
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST LIBRARY LIBRARY_SS CP FLUX DISCR DISCR_SS EDITION
  CELLS EIGHTH ASSMB ASSMB_SS MACDIR ;
MODULE
  LIB: GEO: SYBILT: USS: ASM: FLU: EDI: SPH: DELETE: END: ;
PROCEDURE assertS ;
*----
*  Microscopic cross sections from Jef 2.2 Draglib
*----
LIBRARY := LIB: ::
  NMIX 103 CTRA APOL SUBG
  MIXS LIB: DRAGON FIL: DLIB_J2
  MIX 10 579.9
    H1H2O    = H1_H2O   4.71676E-2   O16H2O   = O16      2.35838E-2
    B10      = B10      4.66791E-6   B11      = B11      1.89074E-5
  MIX 20 579.9
    Zr90     = Zr0     4.18621E-2 2
  MIX 100 933.6                      O16      = O16      4.49355E-2
    U235     = U235     7.39237E-4 1
    U238     = U238     2.17285E-2 1
  MIX 101 COMB 100 1.0
  MIX 102 COMB 100 1.0
  MIX 103 COMB 100 1.0
  ;
*----
*  Geometry : a 15 X 15 normal PWR assembly
*  contains   T : guide thimble tube
*            C1 : normal fuel cell
*            C2 : peripheral cell
*            C3 : corner cell
*----
REAL Rfuel := 0.4646 ;
REAL Rfuel1 := Rfuel .50 SQRT * ;
REAL Rfuel2 := Rfuel .80 SQRT * ;
REAL Rfuel3 := Rfuel .95 SQRT * ;
CELLS := GEO: ::
  ::: T  := GEO: CARCEL 2
    MESHX  0. 1.43
    MESHY  0. 1.43
    RADIUS 0. 0.55 0.57
    MIX     10    20   10 ;
  ::: C1 := GEO: CARCEL 5
    MESHX  0. 1.43
    MESHY  0. 1.43
    RADIUS 0. <<Rfuel1>> <<Rfuel2>> <<Rfuel3>> <<Rfuel>> 0.53594
    MIX     100        101        102        103       20       10 ;
  ::: C2 := GEO: C1  MESHX 0. 1.48 MIX 100 101 102 103 20 10 ;
  ::: C3 := GEO: C2  MESHY 0. 1.48 MIX 100 101 102 103 20 10 ;
;
EIGHTH := GEO: CELLS :: CAR2D 8 8
X- DIAG X+ REFL
Y- SYME Y+ DIAG ;
ASSMB := GEO: EIGHTH ::
CELL T  C1 C1 C1 T  C1 C1 C2
        C1 C1 C1 C1 C1 C1 C2
           C1 C1 C1 T  C1 C2
              T  C1 C1 C1 C2
                 C1 C1 C1 C2
                    T  C1 C2
                       C1 C2
                          C3 ;
ASSMB_SS := GEO: ASSMB ::
MERGE 1  2  2  2  1  2  2  4
         2  2  2  2  2  2  4
            2  2  2  1  2  4
               1  2  2  2  4
                  2  2  2  4
                     1  2  4
                        2  4
                           3 ;
*----
*  Self-Shielding calculation SYBIL
*  Transport calculation      SYBIL
*  Flux calculation for B1 homogeneous leakage
*----
DISCR     := SYBILT: ASSMB    :: MAXZ 1000000 MAXR 1000000
  QUA2 3 3 GAUSS DP01 ;
DISCR_SS  := SYBILT: ASSMB_SS :: MAXZ 1000000 MAXR 1000000
  QUA2 3 3 GAUSS DP01 ;
LIBRARY_SS := USS: LIBRARY DISCR_SS ;
CP := ASM: LIBRARY_SS DISCR :: ARM ;
FLUX := FLU: CP LIBRARY_SS DISCR :: EDIT 2 TYPE B B1 SIGS ;
assertS FLUX :: 'K-INFINITY' 1 1.279648 ;
EDITION := EDI: LIBRARY_SS DISCR FLUX ASSMB ::
  EDIT 4 SAVE MICR RES MERGE COMP COND 0.625 JOUT ;
EDITION := SPH: EDITION ::  EDIT 9 ALBS SELE_ALB ;
*
MACDIR := EDITION :: STEP UP 'REF-CASE0001' STEP UP MACROLIB
          STEP UP GROUP STEP AT 1 ;
assertS MACDIR :: 'NTOT0'  1 5.305629E-01 ;
assertS MACDIR :: 'NUSIGF' 1 6.686473E-03 ;
assertS MACDIR :: 'DIFF'   1 1.400524E+00 ;
assertS MACDIR :: 'SIGW00' 1 5.050910E-01 ;
MACDIR := DELETE: MACDIR ;
MACDIR := EDITION :: STEP UP 'REF-CASE0001' STEP UP MACROLIB
          STEP UP GROUP STEP AT 2 ;
assertS MACDIR :: 'NTOT0'  1 1.359428E+00 ;
assertS MACDIR :: 'NUSIGF' 1 1.365678E-01 ;
assertS MACDIR :: 'DIFF'   1 4.055927E-01 ;
assertS MACDIR :: 'SIGW00' 1 1.271332E+00 ;
MACDIR := DELETE: MACDIR ;
ECHO "test TCDR46 completed" ;
END: ;
QUIT "LIST" .
