*----
*  TEST CASE TCDR34
*  PWR UOX pin cell cylindrical / 293K / uss-6c / cylindrical
*  heterogeneous leakage / isotropic streaming
*  gamma(B,Sigma)=1
*  critical B square
*----
LINKED_LIST LIBRARY LIBRARY2 C3 TRACK SYS FLUX EDIT MACDIR ;
MODULE GEO: SYBILT: USS: MAC: LIB: ASM: FLU: EDI: END: ;
INTEGER COMB0101 COMB0102 COMB0103 COMB0104 COMB0105 COMB0106 GAIN1
        MODE1 MODE2 := 1 2 3 4 5 6 7 8 9 ; (* 6 RINGS *)
REAL RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 ;
PROCEDURE assertS ;
*
LIBRARY := LIB: ::
 EDIT 1
 NMIX 9    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 SUBG      (*COMPUTE SUBGROUPS*)
*
 MIXS LIB: DRAGON FIL: DLIB_J2 (*JEF2.2*)
 MIX <<COMB0101>> 293.0 (*COMB0101*)
    O16     = O16    4.6624E-2
    U235    = U235   7.0803E-4 1 IRSET 0.0 81
    U238    = U238   2.2604E-2 1 IRSET 0.0 81
 MIX <<COMB0102>> COMB <<COMB0101>> 1.0 (*COMB0102*)
 MIX <<COMB0103>> COMB <<COMB0101>> 1.0 (*COMB0103*)
 MIX <<COMB0104>> COMB <<COMB0101>> 1.0 (*COMB0104*)
 MIX <<COMB0105>> COMB <<COMB0101>> 1.0 (*COMB0105*)
 MIX <<COMB0106>> COMB <<COMB0101>> 1.0 (*COMB0106*)
 MIX <<GAIN1>> 293.0 (*GAIN1*)
    Zr0     = Zr0    4.3241E-2 2 IRSET 0.0 81
 MIX <<MODE1>> 293.0 (*MODE1*)
    H1      = H1_H2O 6.6988E-2
    O16     = O16    3.3494E-2
 MIX <<MODE2>> 293.0 (*MODE2*)
    H1      = H1_H2O 6.6988E-2
    O16     = O16    3.3494E-2
 ;
EVALUATE RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 := 0.2529822 
0.334664 0.3577709 0.3794733 0.3898718 0.40 ;
C3 := GEO: :: TUBE 9
      R+ REFL
      RADIUS 0.0 <<RCOMB1>> <<RCOMB2>> <<RCOMB3>> <<RCOMB4>>
                 <<RCOMB5>> <<RCOMB6>> 0.45 0.5748331 0.6770275
      MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
          <<COMB0105>> <<COMB0106>> <<GAIN1>> <<MODE1>> <<MODE2>>
          ;
TRACK := SYBILT: C3 ::
     EDIT 1
     MAXR 20  (*MAXIMUM OF 9 REGIONS*)
     QUA1 5 TITLE 'SIMPLE WIGNER-SEITZ CELL' ;
LIBRARY2 := USS: LIBRARY TRACK :: EDIT 0 TRAN PASS 2 GRMIN 45 ;
SYS := ASM: LIBRARY2 TRACK :: PIJ ECCO ;
FLUX := FLU: SYS LIBRARY2 TRACK :: TYPE B P1 ECCO ;
assertS FLUX :: 'K-INFINITY' 1 1.378385 ;
EDIT := EDI: FLUX LIBRARY2 TRACK :: EDIT 3
      COND 75 99 MERGE COMP
      SAVE ON 'EDITCDAT   1' ;
MACDIR := EDIT :: STEP UP 'EDITCDAT   1' STEP UP MACROLIB
          STEP UP GROUP STEP AT 2 ;
assertS MACDIR :: 'DIFF' 1 0.649 ;
ECHO "test TCDR34 completed" ;
END: ;
