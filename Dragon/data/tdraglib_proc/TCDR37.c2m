*--------------------------------------------------------------------
*  TEST CASE TCDR37
*  172-GROUP JEF2.2 DRAGLIB
*  DB-VHTR Fuel Block Lattice calculation with NXT and BIHET
*--------------------------------------------------------------------
*
*--------------------------------------------------------------------
*  Define STRUCTURES and MODULES used
*-------------------------------------------------------------------- 
LINKED_LIST  LIBRARY LIBRARY2 GEOM TRACK SYS FLUX
             ausgabe EDIT DATABASE sphgeom mtrack ;
SEQ_BINARY   TRACK_FIL mtrack_fil ;
SEQ_ASCII    Fig_mix.ps Fig_reg.ps ;
MODULE       GEO: LIB: ASM: FLU: SHI: NXT: END: PSP: ;
PROCEDURE    assertS ;

*--------------------------------------------------------------------
* CREATION OF THE LIBRARY 
*--------------------------------------------------------------------
LIBRARY := LIB: ::
  EDIT 0
  NMIX 8 CTRA APOL ANIS 2
  MIXS LIB: DRAGON FIL: DLIB_J2

*----
* Mixtures for fuel cell F0
*----
* Graphite matrix
  MIX  1 293.6
    GRAB      = 'C0_GR'  8.524E-02
* Fuel Kernel
  MIX  2 293.6
    O16       = 'O16'     4.436E-02
    NP27      = 'Np237'   1.522E-03 1
    PU38      = 'Pu238'   6.464E-04
    PU39      = 'Pu239'   1.099E-02 1
    PU40      = 'Pu240'   5.084E-03 1
    PU41      = 'Pu241'   1.937E-03 1
    PU42      = 'Pu242'   1.074E-03 1
* Porous C buffer
  MIX  3 293.6
    GRAB      = 'C0_GR'   5.265E-02
* IPyC
  MIX  4 293.6
    GRAB      = 'C0_GR'   9.526E-02
* SiC coating layer
  MIX  5 293.6
     SI0      = 'Si0'     4.402E-02
     C12      = 'C0'      4.772E-02
* OPyC
  MIX  6 293.6
    GRAB      = 'C0_GR' 9.526E-02
* Helium Coolant surrounding fuel
  MIX 7 293.6
    HE4      = 'He4'     7.0000E-04
* Block Graphite (full density surrounding fuel)
  MIX 8 293.6
    GRAB     = 'C0_GR' 8.774E-02
;

*--------------------------------------------------------
*  GEOMETRY
*--------------------------------------------------------
* ----
* F = Fuel cell
* C = coolant hole cell (large)
* K = coolant hole cell (small)
* G = Full density graphite cell
* J = Reduced density graphite cell
* ----
*orig+
GEOM := GEO: :: HEX 7
  EDIT 1
  HBC COMPLETE REFL
  CELL F0 F1 F1 F1 F1 F1 F1
 ::: F0 := GEO: HEXT 1
     SIDE 1.08542 
     MIX   8 8 8 8 8 8
     CLUSTER FF0
     ::: FF0 := GEO: TUBE 2 NPIN 1 RPIN 0.0 APIN 0.0
       RADIUS 0.0 0.6 0.63
       MIX     9   7  ;
 ;    
 ::: F1 := GEO: HEXT 1
     SIDE 1.08542 
     MIX  8 8 8 8 8 8 
     CLUSTER FF1
     ::: FF1 := GEO: TUBE 2 NPIN 1 RPIN 0.0 APIN 0.0
       RADIUS 0.0 0.6 0.63
       MIX     8  7 ;
 ;
*---------------------------------------------------------
* SPHERICAL MICRO-STRUCTURES *
* NMISTR = NUMBER OF MICRO STRUCTURE TYPES IN REGION
* NMILG  = NUMBER OF MICRO STRUCTURE REGIONS
* NS     = ARRAY OF SUB REGIONS IN THE MICRO STRUCTURES; LEN=NMILG
* RS     = RADIUS OF MICROS STRUCTURES; LEN=NS(I); I=1,NMISTR
* milie  = COMPOSITION OF EACH shell OF MICRO STRUCTURE; LEN=NMISTR;
*          !!!!!!!! milie #'s ARE > THAN MIX #'S !!!!!!!!
* mixdil = BASE COMPOSITION OF EACH REGION; LEN=NMILG
* fract  = MICROSTRUCTURE TYPE VOLUME FRACTION IN REGION LEN=NMILG
* mixgr  = LIBRARY MIXTURES FOR EACH MICROSTRUCTURE SHELL; LEN=NS(I)
* NMISTR, NMILG
  BIHET SPHE  1  1
  (* NS     *) 5
  (* RS     *) 0.0 0.0100 0.0220 0.0260 0.0295 0.0335
  (* milie  *)  9 
  (* mixdil *)  1  
  (* loop over NMISTR *)
  (* fract  *)  0.02
  (* mixgr  *)  2 3 4 5 6 
;

*--------------------------------------------------------------------
*  Use NXT, SHI, ASM and FLU for flux solution
*--------------------------------------------------------------------

***
* Tracking NXT
***
TRACK TRACK_FIL := NXT: GEOM ::
    EDIT 1
    MAXR 50000
    TRAK TISO 4 10.0
;

*---
*  Self Shielding calculation
*---
LIBRARY := SHI: LIBRARY TRACK TRACK_FIL :: 
  EDIT 0 
;

*---
*  Multigroup Flux Calculation
*---
 SYS := ASM: LIBRARY TRACK TRACK_FIL :: 
  EDIT 0 PNOR HELI ;

 FLUX := FLU: SYS LIBRARY TRACK TRACK_FIL ::
  EDIT 1 
  TYPE K P1 EXTE 300 1E-5 THER 10 1E-5 ;
 assertS FLUX :: K-EFFECTIVE 1 1.082078 ;

*--------------------------------------------------------
*  Graphik
*--------------------------------------------------------
*
Fig_reg.ps := PSP: TRACK ::
  EDIT 5
  FILL CMYK NOCONTOUR
  TYPE REGION
;

Fig_mix.ps := PSP: TRACK ::
  EDIT 5
  FILL CMYK NOCONTOUR
  TYPE MIXTURE
;

ECHO "test TCDR37 completed" ;
END: ;
QUIT "LIST" .
