*---
* Regression test 2 for MCCG: and MOCC:
*---
* TEST CASE TCDR41
* ROWLANDS PINCELL BENCHMARK 1
* DRAGLIB JEF2.2 XMAS 172 GROUPS LIBRARY FILE DLIB_J2
*---

*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST LIBRARY LIBRARY2 CELL TRACK SYS FLUX EDIT TEMP ;
MODULE GEO: EXCELT: MCCGT: LIB: USS: ASM: FLU: EDI: DELETE: END: ;
SEQ_BINARY TRACK_FIL ;
CHARACTER  PolarAng := "LCMD" ;
REAL densH := 0.066988 ;
REAL densO := 0.033414 ;
REAL temp1 := 293.0    ;
REAL temp2 := 293.0    ;
REAL temp3 := 293.0    ;
REAL Buckl := 0.0      ;
PROCEDURE assertS ;
*----
*  Microscopic cross sections
*----
LIBRARY := LIB: ::
EDIT 1 NMIX 9 PTSL                                               
    CTRA APOL                                                    
    MIXS LIB: DRAGON FIL: DLIB_J2        
    MIX 1 <<temp1>> (*fuel*)                                   
       U238 = U238 0.022604 1 IRSET PTSL 1
       U235 = U235 7.0803E-4 1 IRSET PTSL 1
       O16 = O16 0.046624 NOEV                                     
    MIX 2 COMB 1 1.0
    MIX 3 COMB 1 1.0
    MIX 4 COMB 1 1.0
    MIX 5 COMB 1 1.0
    MIX 6 COMB 1 1.0
    MIX 7 <<temp2>> (*clad*)                                      
       ZR = Zr0 0.043241 2 IRSET PTSL NONE NOEV        
    MIX 8 <<temp3>> (*water1*)                                     
       H1 = H1_H2O <<densH>> NOEV                               
       O16 = O16 <<densO>> NOEV                                   
    MIX 9 <<temp3>> (*water2*)                                     
       H1 = H1_H2O <<densH>> NOEV                                 
       O16 = O16 <<densO>> NOEV                            
    ;
*----
*  Geometry CELL : a single UO2 pincell with 6 annuli in fuel
*----
CELL := GEO: ::
CARCEL 8 EDIT 1                                             
    X- REFL X+ REFL Y- REFL Y+ REFL                         
    MESHX 0.0 1.2                                           
    MESHY 0.0 1.2                                           
    RADIUS 0.0 0.2529822 0.334664 0.3577709 0.3794733 0.3898718 0.4  
    0.45 0.5748331                  
    MIX 1 2 3 4 5 6 7 8 9 ; 
*----
*  MCCG (isotropic BC)
*----
*----
*  Tracking calculation
*  Self-Shielding calculation
*----
TRACK TRACK_FIL := EXCELT: CELL ::
    EDIT 1 ANIS 1
    MAXR 40
    TITLE 'UO2 Rowlands benchmark 1 with MCCG'
    TRAK TISO 12 12.0 NOSY ;
TEMP := TRACK ;
TRACK := MCCGT: TRACK TRACK_FIL ::
    EDIT 1 <<PolarAng>> 2 
    AAC 1 SCR 0 EPSI 1E-5 STIS 1
    MAXI 100 KRYL 30 HDD 0.0 ;
LIBRARY2 := USS: LIBRARY TRACK TRACK_FIL ::
    EDIT 1 ARM
    CALC REGI W1 U235 ALL
         REGI W1 ZR ALL
         REGI W1 U238 1
         REGI W2 U238 2
         REGI W3 U238 3
         REGI W4 U238 4
         REGI W5 U238 5
         REGI W6 U238 6
    ENDC ;
*---
*  Multigroup Flux Calculation
*  Edition
*--
TRACK := DELETE: TRACK ;
TRACK := TEMP ;
TRACK := MCCGT: TRACK TRACK_FIL ::
    EDIT 1 <<PolarAng>> 2 
    AAC 80 TMT SCR 0 EPSI 1E-5 STIS 1
    MAXI 1 KRYL 0 HDD 0.0 ;
SYS := ASM: LIBRARY2 TRACK TRACK_FIL :: 
    EDIT 2 ARM ;
FLUX := FLU: LIBRARY2 TRACK SYS TRACK_FIL ::
    EDIT 1 TYPE K B0 SIGS BUCK <<Buckl>> ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.392635 ;
EDIT := EDI: FLUX LIBRARY2 TRACK CELL :: 
EDIT 2
COND 27 59 95 ;
*---
* Memory Release
*---
TRACK LIBRARY2 SYS FLUX TRACK_FIL EDIT TEMP := DELETE:
TRACK LIBRARY2 SYS FLUX TRACK_FIL EDIT TEMP ;
*----
*  MOCC (cyclic tracking)
*----
*----
*  Tracking calculation
*  Self-Shielding calculation
*----
TRACK TRACK_FIL := EXCELT: CELL ::
    EDIT 1 ANIS 1
    MAXR 40
    TITLE 'UO2 Rowlands benchmark 1 with MOCC'
    TRAK TSPC 7 12.0 NOSY ;
TEMP := TRACK ;
TRACK := MCCGT: TRACK TRACK_FIL ::
    EDIT 1 <<PolarAng>> 2 
    AAC 1 TMT SCR 0 EPSI 1E-5
    MAXI 100 KRYL 30 HDD 100000.0 ;
LIBRARY2 := USS: LIBRARY TRACK TRACK_FIL ::
    EDIT 1 ARM
    CALC REGI W1 U235 ALL
         REGI W1 ZR ALL
         REGI W1 U238 1
         REGI W2 U238 2
         REGI W3 U238 3
         REGI W4 U238 4
         REGI W5 U238 5
         REGI W6 U238 6
    ENDC ;
*---
*  Multigroup Flux Calculation
*  Edition
*--
!TRACK TRACK_FIL := DELETE: TRACK TRACK_FIL ;
!TRACK TRACK_FIL := EXCELT: CELL ::
!    EDIT 1 ANIS 1
!    MAXR 40
!    TITLE 'UO2 Rowlands benchmark 1 with MOCC'
!    TRAK TSPC 7 12.0 NOSY ;
TRACK := DELETE: TRACK ;
TRACK := TEMP ;
TRACK := MCCGT: TRACK TRACK_FIL ::
    EDIT 1 <<PolarAng>> 2 
    AAC 80 TMT SCR 0 EPSI 1E-5
    MAXI 1 KRYL 0 HDD 100000.0 ;
SYS := ASM: LIBRARY2 TRACK TRACK_FIL :: 
    EDIT 2 ARM ;
FLUX := FLU: LIBRARY2 TRACK SYS TRACK_FIL ::
    EDIT 1 TYPE K B0 SIGS BUCK <<Buckl>> ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.393138 ;
EDIT := EDI: FLUX LIBRARY2 TRACK CELL :: 
EDIT 2
COND 27 59 95 ;
*---
* Memory Release
*---
CELL TRACK LIBRARY LIBRARY2 SYS FLUX TRACK_FIL EDIT := DELETE:
CELL TRACK LIBRARY LIBRARY2 SYS FLUX TRACK_FIL EDIT ;
ECHO "test TCDR41 completed" ;
END: ;
QUIT "LIST" .
