*----
*  TEST CASE TCDR14
*  17 X 17 PWR ASSEMBLY WITHOUT POISON
*  DEPLETION WITH CPO AND MULTICOMPO DATABASE
*  172-GROUP JEF2.2 DRAGLIB
*
*  REF: none
*
*----
*  Define STRUCTURES and MODULES used
*----
PARAMETER  saCOMPO :: ::: SEQ_ASCII saCOMPO ; ;
LINKED_LIST
  ASSMB DISCR LIBRARY CP CALC OUT COMPO MCOMPO ISOT ASSMB_HOM BURNUP
  RECORD MTRACK ;
MODULE
  LIB: GEO: SYBILT: SHI: ASM: FLU: EDI: CPO: COMPO: DELETE: END: EVO:
  BIVACT: SPH: ;
PROCEDURE assertS ;
INTEGER ISTEP ;
STRING HSTEP ;
REAL
  Power    Delt   Timec   Timei Timef :=
  31.9713  1.0    300.0   0.0   0.0 ;
*----
* MULTICOMPO
*----
MCOMPO := COMPO: ::
  EDIT 3
  MAXCAL 3
  COMM 'generated for TINST test case by TCDR14' ENDC
  PARA 'BURN' IRRA
  INIT
  ;
*----
*  Microscopic cross sections from Jef 2.2 Draglib
*----
LIBRARY := LIB: ::
  NMIX 8 CTRA WIMS
  DEPL LIB: DRAGON FIL: DLIB_J2
  MIXS LIB: DRAGON FIL: DLIB_J2
  MIX 1 579.9  NOEV
    H1H2O    = H1_H2O   4.76690E-2   O16H2O   = O16      2.38345E-2
    B10      = B10      4.71444E-6   B11      = B11      1.90959E-5
  MIX 2 579.9  NOEV                  O16      = O16      3.06711E-4
    Cr52     = Cr52     7.54987E-5   Fe56     = Fe56     1.47624E-4
    Zr91     = Zr0      4.18621E-2
  MIX 3 579.9  NOEV
    H1H2O    = H1_H2O   4.65292E-2   O16H2O   = O16      2.32646E-2
                                     Cr52     = Cr52     4.79927E-5
    Fe56     = Fe56     4.45845E-5   Ni58     = Ni58     1.13521E-4
                                     MoNat    = Mo95     4.03755E-6
                                     Al27     = Al27     2.35231E-6
    Mn55     = Mn55     4.15901E-7
    B10      = B10      4.60867E-6   B11      = B11      1.86674E-5
    Zr91     = Zr0      8.92427E-4
  MIX 4 579.9  NOEV                  O16      = O16      2.87335E-4
    Cr52     = Cr52     7.07291E-5   Fe56     = Fe56     1.38298E-4
    Zr91     = Zr0      3.92175E-2
  MIX 5 579.9  NOEV
    H1H2O    = H1_H2O   4.71346E-2   O16H2O   = O16      2.35673E-2
                                     Cr52     = Cr52     2.24991E-5
    Fe56     = Fe56     2.09013E-5   Ni58     = Ni58     5.32188E-5
                                     MoNat    = Mo95     1.89281E-6
                                     Al27     = Al27     1.10277E-6
    Mn55     = Mn55     1.94976E-7
    B10      = B10      4.66484E-6   B11      = B11      1.88950E-5
    Zr91     = Zr0      4.18372E-4
  MIX 6 579.9  NOEV
    H1H2O    = H1_H2O   4.71676E-2   O16H2O   = O16      2.35838E-2
                                     Cr52     = Cr52     2.11122E-5
    Fe56     = Fe56     1.96130E-5   Ni58     = Ni58     4.99383E-5
                                     MoNat    = Mo95     1.77614E-6
                                     Al27     = Al27     1.03479E-6
    Mn55     = Mn55     1.82957E-7
    B10      = B10      4.66791E-6   B11      = B11      1.89074E-5
    Zr91     = Zr0      3.92583E-4
  MIX 7 579.9  NOEV
    H1H2O    = H1_H2O   4.72020E-2   O16H2O   = O16      2.36010E-2
                                     Cr52     = Cr52     1.96591E-5
    Fe56     = Fe56     1.82630E-5   Ni58     = Ni58     4.65011E-5
                                     MoNat    = Mo95     1.65389E-6
                                     Al27     = Al27     9.63569E-7
    Mn55     = Mn55     1.70365E-7
    B10      = B10      4.67110E-6   B10      = B10      1.89203E-5
    Zr91     = Zr0      3.65562E-4
  MIX 8 933.6                        O16      = O16      4.49355E-2
    U235     = U235     7.39237E-4 1 
    U238     = U238     2.17285E-2 1 
  ;
*----
*  Geometry ASSMB : a 17 X 17 normal PWR assembly
*  contains  C1 : cell without fuel
*            C2 : normal fuel cell
*            C3 : peripheral cell
*            C4 : corner cell
*----
ASSMB := GEO: :: CAR2D 9 9
  X- DIAG X+ REFL Y- SYME Y+ DIAG
  CELL C1 C2 C2 C1 C2 C2 C1 C2 C3
          C2 C2 C2 C2 C2 C2 C2 C3
             C2 C2 C2 C2 C2 C2 C3
                C1 C2 C2 C1 C2 C3
                   C2 C2 C2 C2 C3
                      C1 C2 C2 C3
                         C2 C2 C3
                            C2 C3
                               C4
  MERGE 1  3 12 11 12 12 11 12 15
           4  6  5  6  6  5  6  8
             13  5  6  6  5  6  8
                 2  5  5 10  5  8
                   13  5  5  6  8
                       2  5  7  8
                         13  7  8
                            14  8
                                9
  TURN  H  H  B  H  H  B  H  H  A
           H  G  G  H  G  G  H  A
              A  E  E  F  E  E  A
                 H  H  F  H  H  A
                    H  E  G  H  A
                       H  H  A  A
                          H  A  A
                             A  A
                                A
  ::: C1 := GEO: CARCEL 2
    MESHX 0.0 1.26472 MESHY 0.0 1.26472
    RADIUS 0.0 0.572435 0.613142  MIX 1 2 3 ;
  ::: C2 := GEO: C1  RADIUS 0.0 0.412660 0.474364  MIX 8 4 5 ;
  ::: C3 := GEO: C2  MESHX 0.0 1.31472 MIX 8 4 6 ;
  ::: C4 := GEO: C3  MESHY 0.0 1.31472  MIX 8 4 7 ;
  ;
ASSMB_HOM := GEO: :: CAR2D 9 9
  X- DIAG X+ REFL Y- SYME Y+ DIAG
  MIX  1 2 2 1 2 2 1 2 2
         2 2 2 2 2 2 2 2
           2 2 2 2 2 2 2
             1 2 2 1 2 2
               2 2 2 2 2
                 1 2 2 2
                   2 2 2
                     2 2
                       2
  MESHX 0.0 1.26472 2.52944 3.79416 5.05888 6.3236 7.58832 8.85304
        10.11776 11.43248
  MESHY 0.0 1.26472 2.52944 3.79416 5.05888 6.3236 7.58832 8.85304
        10.11776 11.43248
  ;
*----
*  Self-Shielding calculation SYBIL
*  Transport calculation      SYBIL
*  OUT calculation for B1 homogeneous leakage
*  Editing using SPH model for transport-diffusion
*----
DISCR := SYBILT: ASSMB ::
  TITLE 'TCDR14: 17 X 17 MULTICELL PWR BENCHMARK WITHOUT POISON'
  MAXR 400 ;
LIBRARY := SHI: LIBRARY DISCR :: LJ ;
CP := ASM: LIBRARY DISCR ;
CALC := FLU: CP LIBRARY DISCR ::
  TYPE B B1 PNL ;
assertS CALC :: 'K-INFINITY' 1 1.256372 ;

OUT := EDI: CALC LIBRARY DISCR ASSMB_HOM ::
  EDIT 3 UPS SAVE MICR ALL MERGE MIX 1 1 1 2 2 2 2 2
  COND 4.0 MGEO ASSMB_HOM
  ;
MTRACK := BIVACT: ASSMB_HOM :: DUAL 1 1 EDIT 0 ;
OUT := SPH: OUT MTRACK ;
MTRACK := DELETE: MTRACK ;
BURNUP LIBRARY := EVO: LIBRARY CALC DISCR ::
  EDIT 0
  SAVE <<Timei>> DAY POWR <<Power>>
;
MCOMPO := COMPO: MCOMPO OUT BURNUP ::
  EDIT 3
  MACRO
  SET <<Timei>> DAY
  ;
*----
*  172 group Burnup
*----
EVALUATE Timec := 1.0 ;
WHILE Timei Timec < DO
  EVALUATE Timef := Timei Delt + ;
  BURNUP LIBRARY := EVO: BURNUP LIBRARY CALC DISCR ::
    EDIT 3 NOEX DEPL <<Timei>> <<Timef>> DAY POWR <<Power>> ;
  CP := DELETE: CP ;
  CP := ASM: LIBRARY DISCR ;
  CALC := FLU: CALC CP LIBRARY DISCR ::
    TYPE B B1 PNL ;
  OUT := EDI: OUT CALC LIBRARY DISCR ASSMB_HOM ::
     EDIT 3 UPS SAVE MICR ALL MERGE MIX 1 1 1 2 2 2 2 2
     COND 4.0 MGEO ASSMB_HOM
  ;
  MTRACK := BIVACT: ASSMB_HOM :: DUAL 1 1 EDIT 0 ;
  OUT := SPH: OUT MTRACK ;
  MTRACK := DELETE: MTRACK ;
  BURNUP LIBRARY := EVO: BURNUP LIBRARY CALC DISCR ::
    EDIT 0
    SAVE <<Timef>> DAY POWR <<Power>>
  ;
  MCOMPO := COMPO: MCOMPO OUT BURNUP ::
    EDIT 3
    MACRO
    SET <<Timef>> DAY
  ;
*----
*  change delta t for burnup and final time if required
*----
  IF Timef Timec = THEN
    IF Timec 5.0 = THEN
      EVALUATE Delt Timec := 5.0 10.0 ;
    ENDIF ;
    IF Timec 1.0 = THEN
      EVALUATE Delt Timec := 4.0 5.0 ;
    ENDIF ;
  ENDIF ;
  EVALUATE Timei := Timef ;
ENDWHILE ;
assertS CALC :: 'K-INFINITY' 1 1.211175 ;

COMPO := CPO: OUT BURNUP ::  
  BURNUP REF-CASE NAME COMPO  ;

RECORD := COMPO :: STEP UP 'COMPO      2' ;
assertS RECORD :: 'BURNUP' 4 3.197132E+02 ;
*----
*  2 group Burnup initialization
*----
EVALUATE Delt := 1.0 ;
EVALUATE Timei := 0.0 ;
CALC CP DISCR LIBRARY BURNUP := DELETE: CALC CP DISCR LIBRARY BURNUP ;
EVALUATE HSTEP := "REF-CASE0001" ;
ECHO "-----> Perform micro-depletion at hstep=" HSTEP ;
LIBRARY :=  OUT :: STEP UP <<HSTEP>> ;
DISCR := BIVACT: ASSMB_HOM ::
  DUAL 1 1 EDIT 0 ;
CP := ASM: LIBRARY DISCR :: ARM ;
CALC := FLU: CP LIBRARY DISCR ::
  TYPE K LKRD IDEM ;
assertS CALC :: 'K-INFINITY' 1 1.256390 ;
*----
*  2 group Burnup
*----
EVALUATE Timec := 1.0 ;
EVALUATE ISTEP := 1 ;
WHILE Timei Timec < DO
  ECHO "-----> Perform micro-depletion at hstep=" HSTEP ;
  EVALUATE Timef := Timei Delt + ;
  IF Timei 0.0 = THEN
    BURNUP LIBRARY := EVO: LIBRARY CALC DISCR ::
      EDIT 3 DEPL <<Timei>> <<Timef>> DAY POWR <<Power>> ;
  ELSE
    BURNUP LIBRARY := EVO: BURNUP LIBRARY CALC DISCR ::
      EDIT 3 NOEX DEPL <<Timei>> <<Timef>> DAY POWR <<Power>> ;
  ENDIF ;
  EVALUATE ISTEP := ISTEP 1 + ;
  IF ISTEP 10 < THEN
    EVALUATE HSTEP := "REF-CASE000" ISTEP I_TO_S + ;
  ELSEIF ISTEP 100 < THEN
    EVALUATE HSTEP := "REF-CASE00" ISTEP I_TO_S + ;
  ELSEIF ISTEP 10000 < THEN
    EVALUATE HSTEP := "REF-CASE0" ISTEP I_TO_S + ;
  ENDIF ;
  LIBRARY := DELETE: LIBRARY ;
  LIBRARY :=  OUT :: STEP UP <<HSTEP>> ;
  BURNUP LIBRARY := EVO: BURNUP LIBRARY DISCR ::
    EDIT 2 NOSA SET <<Timef>> DAY ;
  CP := DELETE: CP ;
  CP := ASM: LIBRARY DISCR :: ARM ;
  CALC := FLU: CALC CP LIBRARY DISCR ::
    TYPE K LKRD IDEM ;
*----
*  change delta t for burnup and final time if required
*----
  IF Timef Timec = THEN
    IF Timec 5.0 = THEN
      EVALUATE Delt Timec := 5.0 10.0 ;
    ENDIF ;
    IF Timec 1.0 = THEN
      EVALUATE Delt Timec := 4.0 5.0 ;
    ENDIF ;
  ENDIF ;
  EVALUATE Timei := Timef ;
ENDWHILE ;
saCOMPO := MCOMPO ;
assertS CALC :: 'K-INFINITY' 1 1.211098 ;

ECHO "test TCDR14 completed" ;
END: ; 
QUIT "LIST" .
