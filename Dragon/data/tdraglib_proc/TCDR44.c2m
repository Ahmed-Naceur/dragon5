*----
*
*  TEST CASE TCDR44 WITH MONTE CARLO MODULE
*  FIRST UO2 ROWLAND'S BENCHMARK
*  DISTRIBUTED SELF-SHIELDING
*  172-GROUP JEF2.2 DRAGLIB
*
*  Author: A. Hebert
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM TRACKSS TRACK LIBRARY LIBRARY2 FLUX ;
MODULE LIB: GEO: NXT: USS: FLU: GREP: MC: ABORT: END: ;
SEQ_BINARY TRKFIL ;
INTEGER COMB0101 COMB0102 COMB0103 COMB0104 COMB0105 COMB0106 GAIN1
MODE1 MODE2 := 
1 2 3 4 5 6 7 8 9 ; (* DISTRIBUTED SELF-SHIELDING *)
REAL RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 ;
REAL VALUE REFVALUE DELTA ;
*
EVALUATE RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 := 0.2529822 
0.334664 0.3577709 0.3794733 0.3898718 0.40 ;

GEOM := GEO: :: CAR2D 1 1
X- REFL X+ REFL
Y- REFL Y+ REFL
 CELL C1
 ::: C1 := GEO: CARCEL 8
       MESHX 0.0 1.20
       MESHY 0.0 1.20
       RADIUS 0.0 <<RCOMB1>> <<RCOMB2>> <<RCOMB3>> <<RCOMB4>>
                  <<RCOMB5>> <<RCOMB6>> 0.45 0.5
       MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
           <<COMB0105>> <<COMB0106>> <<GAIN1>> <<MODE1>> <<MODE2>>
;
;

TRACKSS TRKFIL := NXT: GEOM ::
 TITLE 'FIRST UO2 ROWLAND S BENCHMARK (USS)'
 EDIT 0 TISO 20 5.0
 ;

TRACK := NXT: GEOM ::
 TITLE 'FIRST UO2 ROWLAND S BENCHMARK (MC)'
 EDIT 0 MC ;

LIBRARY := LIB: ::
EDIT 0
NMIX 9    (*MAXIMUM OF MATERIAL MIXTURES*)
ANIS 2
ADED 4 NELAS N4N N2N N3N
CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
SUBG      (*HELIOS TYPE PROBABILITY TABLES*)
*
MIXS LIB: DRAGON FIL: DLIB_J2
MIX <<COMB0101>> 293.0 (*COMB0101*)
      O16        = O16      4.6624E-2
      U235       = U235     7.0803E-4 1 IRSET 0.0 81
      U238       = U238     2.2604E-2 1 IRSET 0.0 81
MIX <<COMB0102>> COMB <<COMB0101>> 1.0 (*COMB0102*)
MIX <<COMB0103>> COMB <<COMB0101>> 1.0 (*COMB0103*)
MIX <<COMB0104>> COMB <<COMB0101>> 1.0 (*COMB0104*)
MIX <<COMB0105>> COMB <<COMB0101>> 1.0 (*COMB0105*)
MIX <<COMB0106>> COMB <<COMB0101>> 1.0 (*COMB0106*)
MIX <<GAIN1>> 293.0 (*GAIN1*)
      Zr0         = Zr0       4.3241E-2 2 IRSET 0.0 81
MIX <<MODE1>> 293.0 (*MODE1*)
      H1          = H1_H2O    6.6988E-2
      O16         = O16       3.3494E-2
MIX <<MODE2>> 293.0 (*MODE2*)
      H1          = H1_H2O    6.6988E-2
      O16         = O16       3.3494E-2
;

LIBRARY2 := USS: LIBRARY TRACKSS TRKFIL ::
  EDIT 0 TRAN PASS 2 GRMIN 45 MAXST 50 ;

FLUX := MC: TRACK LIBRARY2 ::
  EDIT 2 KCODE 1000 25 50 SEED 23412 ;
*
*  Non regression assert
*
EVALUATE REFVALUE := 1.395400 ;
*
GREP: FLUX :: GETVAL K-EFFECTIVE 1 >>VALUE<< ;
EVALUATE DELTA := VALUE REFVALUE - REFVALUE / ABS ;
IF DELTA 1.0E-2 < THEN
  PRINT "TEST SUCCESSFUL; DELTA=" DELTA ;
ELSE
  PRINT "------------" ;
  PRINT "TEST FAILURE" ;
  PRINT "------------" ;
  PRINT "REFERENCE=" REFVALUE " CALCULATED=" VALUE ;
  ABORT: ;
ENDIF ;
ECHO "test TCDR44 completed" ;
END: ;
