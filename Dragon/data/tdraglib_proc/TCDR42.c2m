*----
*
*  TEST CASE TCDR42
*  TEST SHI: WITH FLUX-CURRENT EURYDICE ITERATIONS
*  DISTRIBUTED SELF-SHIELDING
*  172-GROUP JEF2.2 DRAGLIB
*
*  Author: A. Hebert
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM TRACK LIBRARY LIBRARY2 CP FLUX ;
MODULE LIB: GEO: SYBILT: SHI: ASM: FLU: DELETE: END: ;
INTEGER COMB0101 COMB0102 COMB0103 COMB0104 COMB0105 GAIN1 MODE1 :=
        1 2 3 4 5 6 7 ; (* DISTRIBUTED SELF-SHIELDING *)
REAL RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 ;
PROCEDURE assertS ;
*
EVALUATE RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 := 0.2529822 0.334664
0.3577709 0.3794733 0.40 ;
*
GEOM := GEO: :: CAR2D 5 5 (*ASSEMBLAGE 10 X 10*)
          X- DIAG X+ REFL
          Y- REFL Y+ DIAG
          CELL C1 C3 C2 C3 C4
                  C3 C3 C3 C4
                     C2 C3 C4
                        C3 C4
                           C5
          MERGE 1  2  3  4  5
                   6  7  8  9
                     10 11  9
                        12  9
                           13
          ::: C1 := GEO: CARCEL 1 (*CELLULE SANS COMBUSTIBLE*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 4.12660E-01
                MIX <<MODE1>> <<MODE1>>
          ;
          ::: C2 := GEO: CARCEL 3 (*CELLULE EMPOISONNEE*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 3.25296E-01 4.60039E-01 5.63430E-01
                MIX <<GAIN1>> <<GAIN1>> <<GAIN1>> <<MODE1>>
          ;
          ::: C3 := GEO: CARCEL 7 (*CELLULE NORMALE*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 <<RCOMB1>> <<RCOMB2>> <<RCOMB3>> <<RCOMB4>>
                <<RCOMB5>> 0.45 0.5748331
                MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
                <<COMB0105>> <<GAIN1>> <<MODE1>> <<MODE1>>
          ;
          ::: C4 := GEO: C3 (*CELLULE PERIPHERIQUE*)
                MESHX 0.0 1.322082
          ;
          ::: C5 := GEO: C4 (*CELLULE DE COIN*)
                MESHY 0.0 1.322082
          ;
          ;
TRACK := SYBILT: GEOM ::
  MAXR 100 MAXZ 100000
  TITLE 'TCDR42: MULTICELL 10 X 10 PWR BENCHMARK WITH POISON'
  DP00 ;
*----
* MAKE MICROLIB
*----
LIBRARY := LIB: ::
 EDIT 1
 NMIX 9    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 ADED 4 NELAS N4N N2N N3N
*
 MIXS LIB: DRAGON FIL: DLIB_J2
 MIX <<COMB0101>> 900.0 (*COMB0101*)
    O16     = O16    4.6624E-2
    U235    = U235   7.0803E-4 1 IRSET 0.0 81
    U238    = U238   2.2604E-2 1 IRSET 0.0 81
 MIX <<COMB0102>> COMB <<COMB0101>> 1.0 (*COMB0102*)
 MIX <<COMB0103>> COMB <<COMB0101>> 1.0 (*COMB0103*)
 MIX <<COMB0104>> COMB <<COMB0101>> 1.0 (*COMB0104*)
 MIX <<COMB0105>> COMB <<COMB0101>> 1.0 (*COMB0105*)
 MIX <<GAIN1>> 600.0 (*GAIN1*)
    Zr0     = Zr0    4.3241E-2 2 IRSET 0.0 81
 MIX <<MODE1>> 574.0 (*MODE1*)
    H1      = H1_H2O 4.6892E-2
    O16     = O16    2.3446E-2
;
*----
* SHI LEVEL 0 WITH PIJ
*----
LIBRARY2 := SHI: LIBRARY TRACK ::
  EDIT 1 LJ GRMIN 45 LEVEL 0 MXIT 3 PIJ ;
CP := ASM: LIBRARY2 TRACK :: PIJ ;
FLUX := FLU: CP LIBRARY2 TRACK :: TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.342790 ;
LIBRARY2 CP FLUX := DELETE: LIBRARY2 CP FLUX ;
*----
* SHI LEVEL 0 WITH ARM (FLUX-CURRENT EURYDICE ITERATIONS)
*----
LIBRARY2 := SHI: LIBRARY TRACK ::
  EDIT 1 LJ GRMIN 45 LEVEL 0 MXIT 3 ARM ;
CP := ASM: LIBRARY2 TRACK :: ARM ;
FLUX := FLU: CP LIBRARY2 TRACK :: TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.342790 ;
LIBRARY2 CP FLUX := DELETE: LIBRARY2 CP FLUX ;
*----
* SHI LEVEL 1 WITH PIJ
*----
LIBRARY2 := SHI: LIBRARY TRACK ::
  EDIT 1 LJ GRMIN 45 LEVEL 1 MXIT 3 PIJ ;
CP := ASM: LIBRARY2 TRACK :: PIJ ;
FLUX := FLU: CP LIBRARY2 TRACK :: TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.345739 ;
LIBRARY2 CP FLUX := DELETE: LIBRARY2 CP FLUX ;
*----
* SHI LEVEL 1 WITH ARM (FLUX-CURRENT EURYDICE ITERATIONS)
*----
LIBRARY2 := SHI: LIBRARY TRACK ::
  EDIT 1 LJ GRMIN 45 LEVEL 1 MXIT 3 ARM ;
CP := ASM: LIBRARY2 TRACK :: ARM ;
FLUX := FLU: CP LIBRARY2 TRACK :: TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.345739 ;
*
ECHO "test TCDR42 completed" ;
END: ;
