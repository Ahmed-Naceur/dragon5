*----
*
*  TEST CASE TCDR20
*  1D SN CASE WITH SPH/P5 EQUIVALENCE
*  172-GROUP JEF2.2 DRAGLIB
*
*  Author: J. Ortensi INL
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST LIBRARY LIBRARY2 SYS GEOM SPHGEOM MACRO EDIT
            TRACK SPHTRACK FLUX ;
MODULE LIB: GEO: SNT: USS: BIVACT: ASM: FLU: EDI: DELETE:
       SPH: END: ;
SEQ_ASCII MAC_P3SPH :: FILE 'MAC_P3SPH.txt' ;
REAL Tfuel NB10 NB11 :=  300.0 1.4495E-07 5.8343E-07  ;
PROCEDURE   assertS ;
*----
LIBRARY := LIB: ::
EDIT 0
NMIX 5
DEPL LIB: DRAGON FIL: DLIB_J2
CTRA NONE
ANIS 2
SUBG
MIXS LIB: DRAGON FIL: DLIB_J2
*-- Fuel
MIX 1 <<Tfuel>>
GRA   =   'C0_GR'   3.5353E-02
C0    =   'C0'      5.0874E-02
B10   =   'B10'     <<NB10>>
B11   =   'B11'     <<NB11>>
U235   =   'U235'   8.6849E-06 1 IRSET 0.0 81
U238   =   'U238'   6.2967E-07 1 IRSET 0.0 81
Fe54   =   'Fe54'   6.5286E-07
Fe56   =   'Fe56'   1.0239E-05
Fe57   =   'Fe57'   2.3659E-07
Fe58   =   'Fe58'   3.1248E-08
O16   =   'O16'   1.8623E-05
*-- Zircaloy Clad
MIX 2 <<Tfuel>>
Sn115   =   'Sn115'   3.1545E-07
Sn116   =   'Sn116'   1.3490E-05
Sn117   =   'Sn117'   7.1255E-06
Sn118   =   'Sn118'   2.2471E-05
Sn119   =   'Sn119'   7.9698E-06
Sn120   =   'Sn120'   3.0228E-05
Sn122   =   'Sn122'   4.2957E-06
Sn124   =   'Sn124'   5.3720E-06
Zr0    =   'Zr0'    4.2049E-02  2 IRSET 0.0 81
Fe54   =   'Fe54'   1.1659E-05
Fe56   =   'Fe56'   1.8285E-04
Fe57   =   'Fe57'   4.2252E-06
Fe58   =   'Fe58'   5.5804E-07
B10   =   'B10'   1.3751E-07
B11   =   'B11'   5.9006E-07
Cr50   =   'Cr50'   9.8706E-08
Cr52   =   'Cr52'   1.9013E-06
Cr53   =   'Cr53'   2.1556E-07
Cr54   =   'Cr54'   5.3551E-08
Ni58   =   'Ni58'   2.2803E-07
Ni60   =   'Ni60'   8.7821E-08
Ni61   =   'Ni61'   3.8183E-09
Ni62   =   'Ni62'   2.2207E-08
Ni64   =   'Ni64'   3.1149E-09
Cd110   =   'Cd110'   8.7398E-10
Cd111   =   'Cd111'   8.9567E-10
Cd112   =   'Cd112'   1.6885E-09
Cd113   =   'Cd113'   8.5508E-10
Cd114   =   'Cd114'   2.0104E-09
Cd116   =   'Cd116'   5.2411E-10
*-- Air
MIX 3 <<Tfuel>>
C0     = 'C0'      7.5811E-09
N14    = 'N14'     3.9484E-05
O16    = 'O16'     1.0608E-05
*-- Reflector
MIX 4 <<Tfuel>>
GRA   =   'C0_GR'   4.936E-02
C0   =   'C0'   3.430E-02
B10   =   'B10'   3.517E-08
B11   =   'B11'   1.509E-07
Fe54   =   'Fe54'   1.054E-06
Fe56   =   'Fe56'   1.652E-05
Fe57   =   'Fe57'   3.818E-07
Fe58   =   'Fe58'   5.043E-08
*-- Aluminum
MIX 5 <<Tfuel>>
Al27   =   'Al27'   5.9477E-02
Fe54   =   'Fe54'   2.5591E-05
Fe56   =   'Fe56'   4.0136E-04
Fe57   =   'Fe57'   9.2739E-06
Fe58   =   'Fe58'   1.2249E-06
;

************************************************************************
*             build and track geometry
************************************************************************
*      Fuel	    Zr	 void	 Fuel   ref	   Al	   void  ref
* DX  26.913	1.402	5.732	26.913 26.642 2.789 4.886 26.642
************************************************************************

*-- Single block model
GEOM := GEO: :: CAR1D 8
X- REFL X+ REFL
MESHX 0.0 26.913 28.315 34.047 60.960 87.602 90.391 95.278 121.920
MIX   1 2 3 1 4 5 3 4
SPLITX 20 1 5 20 20 2 4 20 ;

*-- Single block model
SPHGEOM := GEO: :: CAR1D 8
X- REFL X+ REFL
MESHX 0.0 26.913 28.315 34.047 60.960 87.602 90.391 95.278 121.920
MIX   1 2 3 1 4 5 3 4
SPLITX 8 1 1 8 8 1 1 8 ;

TRACK := SNT: GEOM ::
 EDIT 0
 MAXR 15000
 DIAM 0
 SN 12
 SCAT 2
;

SPHTRACK := BIVACT: SPHGEOM ::
EDIT 2
SPN 5
* SPH with SCAT > 1 not implemented for PN
SCAT 1
MAXR 500
DUAL (*IELEM=*) 1 (*ICOL=*) 1 ;

************************************************************************

LIBRARY2 := USS: LIBRARY TRACK ::
  EDIT 0
  PASS 2
  MAXST 100
  ARM ;

SYS := ASM: LIBRARY2 TRACK ::
  EDIT 0 ARM ;

FLUX := FLU: SYS LIBRARY2 TRACK ::
  EDIT 1 
  TYPE K P1 ;
SYS := DELETE: SYS ;

EDIT := EDI: FLUX LIBRARY2 TRACK GEOM ::
  EDIT 1
  MERG MIX 1 2 3 4 5
  COND 2.E+05 9.5E+02 2.3E+00 6.0E-01 3.0E-01
  P0W MICRO RES
  SAVE ;
FLUX := DELETE: FLUX ;

MAC_P3SPH := EDIT :: STEP UP 'REF-CASE0001' STEP UP MACROLIB ;

EDIT := SPH: EDIT SPHTRACK ::
  EDIT 2
  MAXNB 200
  ITER 400 1E-5 ;
*
**************************
* Verification calculation
**************************
*
MACRO :=  EDIT :: STEP UP 'REF-CASE0001' STEP UP MACROLIB ;
SYS := ASM: MACRO SPHTRACK :: ARM ;
FLUX := FLU: MACRO SPHTRACK SYS :: TYPE K EXTE 100 5.0E-8 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.503346 ;
ECHO "test TCDR20 completed" ;
END: ;
QUIT "LIST" .
