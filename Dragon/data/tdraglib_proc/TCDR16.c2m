*--------------------------------------------------------------------
*  TEST CASE TCDR16
*  DB-VHTR Fuel Block Lattice calculation
*  Fresh fuel TRU loading
*  Author : V. Descotes
*  Core corner block(6,1), no BP, no CR.
*--------------------------------------------------------------------
*
*--------------------------------------------------------------------
*  Define STRUCTURES and MODULES used
*-------------------------------------------------------------------- 
LINKED_LIST  LIBRARY LIBRARY2 GEOM TRACK SYS FLUX ;
SEQ_BINARY   TRACK_FIL ;
MODULE       GEO: LIB: ASM: FLU: USS: EXCELT: MCCGT: END: ;
PROCEDURE    assertS ;

*--------------------------------------------------------
*  GEOMETRY
*--------------------------------------------------------
* ----
* F = Fuel cell
* C = coolant hole cell (large)
* K = coolant hole cell (small)
* G = Full density graphite cell
* J = Reduced density graphite cell
* ----
GEOM := GEO: :: HEX 1
  EDIT 1
  HBC COMPLETE REFL
  CELL F0
 ::: F0 := GEO: HEXCEL 2
     SIDE 1.08542 
     RADIUS 0.0 0.6225 0.6350
     SECT -1 
     MIX  9  9  9  9  9  9 
          7  7  7  7  7  7 
          8  8  8  8  8  8 ;
*
*---------------------------------------------------------
* SPHERICAL MICRO-STRUCTURES *
* NMISTR = NUMBER OF MICRO STRUCTURE TYPES IN REGION
* NMILG  = NUMBER OF MICRO STRUCTURE REGIONS
* NS     = ARRAY OF SUB REGIONS IN THE MICRO STRUCTURES; LEN=NMILG
* RS     = RADIUS OF MICROS STRUCTURES; LEN=NS(I); I=1,NMISTR
* milie  = COMPOSITION OF EACH shell OF MICRO STRUCTURE; LEN=NMISTR;
*          !!!!!!!! milie #'s ARE > THAN MIX #'S !!!!!!!!
* mixdil = BASE COMPOSITION OF EACH REGION; LEN=NMILG
* fract  = MICROSTRUCTURE TYPE VOLUME FRACTION IN REGION LEN=NMILG
* mixgr  = LIBRARY MIXTURES FOR EACH MICROSTRUCTURE SHELL; LEN=NS(I)

* NMISTR, NMILG
  BIHET SPHE  1  1
  (* NS     *) 5
  (* RS     *) 0.0 0.0100 0.0220 0.0260 0.0295 0.0335
  (* milie  *)  9 
  (* mixdil *)  1  
  (* loop over NMISTR *)
  (* fract  *)  0.175016
  (* mixgr  *)  2 3 4 5 6 
;

*--------------------------------------------------------------------
*  Use EXCELT-MCCGT, USS, ASM and FLU for flux solution
*--------------------------------------------------------------------

***
* Tracking EXCELT-MCCGT
***
TRACK TRACK_FIL := EXCELT: GEOM ::
    EDIT 1
    MAXR 50000
    ANIS 1
    ALLG
    QUAB 5 HEBE
    PISO  
    TRAK TISO 4 10.0
;

TRACK := MCCGT: TRACK TRACK_FIL GEOM ::     
     EDIT 0  LCMD 3
     EPSI 5E-5
     MAXI 100 KRYL 10 SCR 1 AAC 1 TMT
;

*--------------------------------------------------------------------
* CREATION OF THE LIBRARY 
*--------------------------------------------------------------------
LIBRARY := LIB: ::
  EDIT 0
  NMIX 8 CTRA APOL ANIS 2
  SUBG
  MIXS LIB: DRAGON FIL: DLIB_J2

*----
* Mixtures for fuel cell F0
*----
* Graphite matrix
  MIX  1 293.6
    GRAB      = 'C0_GR'  8.524E-02
* Fuel Kernel
  MIX  2 293.6
    O16       = 'O16'     4.436E-02
    NP27      = 'Np237'   1.522E-03 1
    PU38      = 'Pu238'   6.464E-04 1
    PU39      = 'Pu239'   1.099E-02 1
    PU40      = 'Pu240'   5.084E-03 1
    PU41      = 'Pu241'   1.937E-03 1
    PU42      = 'Pu242'   1.074E-03 1
    AM41      = 'Am241'   6.163E-04 1
    AM42      = 'Am242m'  4.385E-06 1
    AM43      = 'Am243'   3.057E-04 1
* Porous C buffer
  MIX  3 293.6
    GRAB      = 'C0_GR'   5.265E-02
* IPyC
  MIX  4 293.6
    GRAB      = 'C0_GR'   9.526E-02
* SiC coating layer
  MIX  5 293.6
     SI0      = 'Si0'     4.402E-02
     C12      = 'C0'      4.772E-02
* OPyC
  MIX  6 293.6
    GRAB      = 'C0_GR' 9.526E-02
* Helium Coolant surrounding fuel
  MIX 7 293.6
    HE4      = 'He4'     7.0000E-04
* Block Graphite (full density surrounding fuel)
  MIX 8 293.6
    GRAB     = 'C0_GR' 8.774E-02
;

*---
*  Self Shielding calculation
*---
LIBRARY2 := USS: LIBRARY TRACK TRACK_FIL :: 
  EDIT 0 PASS 2 ARM ;

*---
*  Multigroup Flux Calculation
*---
 SYS := ASM: LIBRARY2 TRACK TRACK_FIL :: 
  EDIT 0 ARM PNOR HELI ;

 FLUX := FLU: SYS LIBRARY2 TRACK TRACK_FIL ::
  EDIT 1 
  TYPE K P1 EXTE 300 1E-5 THER 10 1E-5 ;

assertS FLUX :: 'K-EFFECTIVE' 1 1.144677 ;
ECHO "test TCDR16 completed" ;
END: ;
QUIT "LIST" .
