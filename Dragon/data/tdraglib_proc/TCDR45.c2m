*----
*  TEST CASE TRDR45
*  MIXTURES DEFINED WITH COMB INSTRUCTION
*  172-GROUP JEF2.2 DRAGLIB
*
*  REF: none
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST LIBRARY ;
MODULE LIB: END: ;
PROCEDURE assertS ;
*----
*  Concentrations given to LIB:
*----
REAL Ni58   := 3.9171E-3 ;
REAL O16    := 2.3954E-2 ;
REAL H1_H2O := O16 2.0 * ;
REAL B10    := 4.7892E-6 ;
REAL B11    := 1.9157E-5 ;

*----
*  Using COMB instruction to define MIX 3
*----
LIBRARY := LIB: ::
  NMIX 3
  MIXS LIB: DRAGON FIL: DLIB_J2
  MIX 1 579.9
    Ni58     = Ni58     <<Ni58>>
  MIX 2 579.9
    O16      = O16      <<O16>>
    H1H2O    = H1_H2O   <<H1_H2O>>
    B10      = B10      <<B10>>
    B11      = B11      <<B11>>
  MIX 3
    COMB 1 0.01
         2 0.99
;
*----
*  Out-of-LIB: dilution
*----
REAL Ni58dil   := Ni58   0.01 * ;
REAL O16dil    := O16    0.99 * ;
REAL H1_H2Odil := H1_H2O 0.99 * ;
REAL B10dil    := B10    0.99 * ;
REAL B11dil    := B11    0.99 * ;
*
assertS LIBRARY :: 'ISOTOPESDENS' 1  <<Ni58>>      ;
assertS LIBRARY :: 'ISOTOPESDENS' 2  <<O16>>       ;
assertS LIBRARY :: 'ISOTOPESDENS' 3  <<H1_H2O>>    ;
assertS LIBRARY :: 'ISOTOPESDENS' 4  <<B10>>       ;
assertS LIBRARY :: 'ISOTOPESDENS' 5  <<B11>>       ;
assertS LIBRARY :: 'ISOTOPESDENS' 6  <<Ni58dil>>   ;
assertS LIBRARY :: 'ISOTOPESDENS' 7  <<O16dil>>    ;
assertS LIBRARY :: 'ISOTOPESDENS' 8  <<H1_H2Odil>> ;
assertS LIBRARY :: 'ISOTOPESDENS' 9  <<B10dil>>    ;
assertS LIBRARY :: 'ISOTOPESDENS' 10 <<B11dil>>    ;
*
ECHO "test TCDR45 completed" ;
END: ; 
QUIT "LIST" .
