*--------------------------------------------------------------------    
* TEST CASE TCDR19
* VHTRC Fuel Cell B-2  
* JAVIER ORTENSI
* CP METHOD 
* Material Temp 300 K
* ENDFB-VII  
* XMAS172 Group Structure
* Based on design data from
* NEA/NSC VHTRC-GCR-EXP-001
*--------------------------------------------------------------------    
*  Define STRUCTURES and MODULES used    
*--------------------------------------------------------------------    
LINKED_LIST    
  GEOMETRY LIBRARY LIBRARY2 SYS TRACK FLUX ;   
SEQ_BINARY TRACK_FIL ;
MODULE
  GEO: EXCELT: MCCGT: LIB: ASM: FLU: EDI: END: USS: ; 
PROCEDURE assertS ;
*----
*  Mix number assignments
*   1 = fuel kernel material (UO2)
*   2 = 1st coating
*   3 = 2nd coating
*   4 = compact matrix
*   5 = graphite sheath
*   6 = air
*   7 = block graphite
*   8 = fuel compact mixture
*----
LIBRARY := LIB: ::
  NMIX 7 
  CTRA NONE
  SUBG
  MIXS LIB: DRAGON FIL: DLIB_J2 
* Fuel Kernel
  MIX  1 300.       
    U234      = 'U234'    3.19004E-06 1
    U235      = 'U235'    4.70582E-04 1
    U236      = 'U236'    3.74868E-07 1
    U238      = 'U238'    2.27638E-02 1
    O16       = 'O16'     4.65455E-02
    B10       = 'B10'     1.84810E-08 
    B11       = 'B11'     7.43882E-08 
* 1st coating
  MIX  2 300.  
    C12       = 'C0_GR'   5.96649E-02
    B10       = 'B10'     2.11059E-09
    B11       = 'B11'     8.49539E-09
* 2nd coating
  MIX  3 300.  
    C12       = 'C0_GR'   9.37591E-02
    B10       = 'B10'     3.31664E-09
    B11       = 'B11'     1.33499E-08
* Graphite matrix (Compact)
  MIX 4  300.         
    C12       = 'C0_GR'   8.65629E-02
    H1        = 'H1'      3.23272E-05
    O16       = 'O16'     1.61635E-05  
    B10       = 'B10'     3.06284E-09
    B11       = 'B11'     1.23284E-08
* Graphite sheath
  MIX 5 300.         
    C12       = 'C0_GR'   8.39319E-02 
    B10       = 'B10'     1.29894E-09
    B11       = 'B11'     5.22841E-09
    H1        = 'H1'      1.23109E-05
    O16       = 'O16'     8.77944E-06
    N14       = 'N14'     9.77961E-06
* air
  MIX 6 300.
    O16       = 'O16'     1.029640E-5 
    N14       = 'N14'     3.837430E-5 
* Block graphite
  MIX 7 300.         
    C12       = 'C0_GR'   8.34807E-02 
    B10       = 'B10'     1.29196E-09
    B11       = 'B11'     5.20030E-09
    H1        = 'H1'      1.22447E-05
    O16       = 'O16'     8.78735E-06
    N14       = 'N14'     9.93241E-06    
;
*--------------------------------------------------------------------    
*  Geometry:  GEOMETRY - Infinite hexagonal lattice of fuel pin  
*--------------------------------------------------------------------    
GEOMETRY := GEO: :: HEX 1    
  HBC COMPLETE REFL   
  CELL B2     

* SPHERICAL MICRO-STRUCTURES *
* NMISTR = NUMBER OF MICRO STRUCTURE TYPES IN REGION
* NMILG  = NUMBER OF MICRO STRUCTURE REGIONS
* NS     = ARRAY OF SUB REGIONS IN THE MICRO STRUCTURES; LEN=NMILG
* RS     = RADIUS OF MICROS STRUCTURES; LEN=NS(I); I=1,NMISTR
* milie  = COMPOSITION OF EACH shell OF MICRO STRUCTURE; LEN=NMISTR; 
*          !!!!!!!! milie #'s ARE > THAN MIX #'S !!!!!!!!
* mixdil = BASE COMPOSITION OF EACH REGION; LEN=NMILG
* fract  = MICROSTRUCTURE TYPE VOLUME FRACTION IN REGION LEN=NMILG
* mixgr  = LIBRARY MIXTURES FOR EACH MICROSTRUCTURE SHELL; LEN=NS(I)

* NMISTR, NMILG
  BIHET SPHE  1  1
  (* NS     *) 3
  (* RS     *) 0.0 .0301 .0380 .04590 
  (* milie  *)  8
  (* mixdil *)  4
  (* loop over NMISTR *)
  (* fract  *)  0.2977
  (* mixgr  *)  1 2 3 

 ::: B2 := GEO: HEXCEL 5
     SIDE 3.75278
     RADIUS 0.0 0.8975 1.7925 1.8250 2.3400 2.3600  
     MIX 6 8 6 5 6 7 ;
;  
*----
*  
*  Use EXCELT, MCCGT, USS, ASM and FLU for flux solution
*----
TRACK TRACK_FIL := EXCELT: GEOMETRY ::
    EDIT 1
    TITLE 'VHTRC B2 FUEL PIN WITH REPRESENTATIVE GRAPHITE '
    MAXR 500000
    ANIS 1
    ALLG
    QUAB 5 SAPO
    PISO  
    TRAK TISO 8 20.0
;      
TRACK := MCCGT: TRACK TRACK_FIL GEOMETRY ::     
     EDIT 1 LCMD 3
     EPSI 5E-5
     MAXI 100 KRYL 10 SCR 1 AAC 1 TMT
;
*---
*  Self Shielding calculation
*---
LIBRARY2 := USS: LIBRARY TRACK TRACK_FIL  :: 
  EDIT 2
  PASS 2
  ARM
; 
*---
*  Multigroup Flux Calculation
*---
 SYS := ASM: LIBRARY2 TRACK TRACK_FIL :: 
  EDIT 1 
  ARM
  ;
 FLUX := FLU: SYS LIBRARY2 TRACK TRACK_FIL ::
  EDIT 1 
  TYPE K
  EXTE 500 1E-5 
  THER 10 1E-5 ;
assertS FLUX :: 'K-INFINITY' 1 1.208958 ;
ECHO "test TCDR19 completed" ;
END:  ;
QUIT "LIST" .
