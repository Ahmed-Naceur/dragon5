*----
*  TEST CASE TCWW08
*  HOMOGENEOUS DEPLETION CASE
*  WIMS-E Library
*
*  REF: None
*
*----
*  Define variables
*     Burnup paremeters
*     a) Power
*        =  600.0 kw/kg for    0.0 to 1000.0 days
*        =    0.0 kw/kg for 1000.0 to 2000.0 days
*     b) Burnup time interval Delt
*        =    10 days for    0 to   50 days
*        =    50 days for   50 to  500 days
*        =   100 days for  500 to 1000 days
*        =  1000 days for 1000 to 2000 days
*     c) Editing time Timec
*        = 0.0, 50.0, 500.0, 1000.0 and 2000.0 days
*     d) Burnup control time variables Timei, Timef, TotalTime
*        Timei = initial time
*        Timef = final time
*        TotalTime = Final time reached
*     d) Print variable Iprint
*        = 1 reduced print
*        = 3 full print
*----
REAL
  Power  Delt  Timec Timei Timef TotalTime :=
  600.0  10.0  50.0  0.0   0.0   2000.0 ;
INTEGER
  Iprint :=  1 ;
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  LIBRARY HOM TRACK PIJ FLUX BURNUP EDITION ;
MODULE
  GEO: SYBILT: LIB: SHI: ASM: FLU: EVO: EDI: DELETE: END: ;
PROCEDURE assertS ;
*----
*  Depletion data from file WIMS-E format WIMSE
*  Microscopic cross sections from file WIMS-E format WIMSE
*----
LIBRARY := LIB: ::
  NMIX 1 CTRA WIMS
  DEPL LIB: WIMSE FIL: WIMS-E
  MIXS LIB: WIMSE FIL: WIMS-E
  MIX 1 300.0                        H1       = '1001'       2.00000E+1
    U235     = '92235'     1.0        1
    U236     = '92238'     0.0        1
  ;
*----
*  Geometry HOM : Homogeneous geometry
*----
HOM := GEO: :: HOMOGE
  MIX 1 ;
*----
*  Self-Shielding calculation SYBIL
*  Transport calculation      SYBIL
*  Flux calculation for keff
*----
TRACK := SYBILT: HOM  ::
  TITLE 'TCWW08: HOMOGENEOUS BENCHMARK WITH BURNUP' ;
LIBRARY := SHI: LIBRARY TRACK :: EDIT 0 NOLJ ;
PIJ := ASM: LIBRARY TRACK ;
FLUX := FLU: PIJ LIBRARY TRACK ::
  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.843108 ;
EDITION := EDI: LIBRARY TRACK FLUX ::
  COND 4.0 MERGE COMP SAVE ;
*----
*  Burnup loop: for first step BURNUP is created
*  while for other steps it is modified
*----
WHILE Timei TotalTime < DO
  EVALUATE Timef := Timei Delt + ;
  IF Timef Timec = THEN
    EVALUATE Iprint := 3 ;
  ELSE
    EVALUATE Iprint := 1 ;
  ENDIF ;
  IF Timei 0.0 = THEN
    BURNUP LIBRARY := EVO: LIBRARY FLUX TRACK ::
      EDIT <<Iprint>> DEPL <<Timei>> <<Timef>> DAY
      POWR <<Power>> ;
  ELSE
    BURNUP LIBRARY := EVO: BURNUP LIBRARY FLUX TRACK ::
      EDIT <<Iprint>> NOEX DEPL <<Timei>> <<Timef>> DAY
      POWR <<Power>> ;
  ENDIF ;
  LIBRARY := SHI: LIBRARY TRACK :: EDIT 0 NOLJ ;
  PIJ := DELETE: PIJ ;
  PIJ := ASM: LIBRARY TRACK ;
  FLUX := FLU: FLUX PIJ LIBRARY TRACK ::
    TYPE K ;
  IF Iprint  3 = THEN
    EDITION := EDI: EDITION LIBRARY TRACK FLUX ::
      SAVE ;
  ENDIF ;
*----
*  change delta t for burnup, final time and power if required
*----
  IF Timef Timec = THEN
    IF Timec 1000.0 = THEN
      EVALUATE Power Delt Timec := 0.0 1000.0 2000.0 ;
    ENDIF ;
    IF Timec 500.0 = THEN
      EVALUATE Delt Timec := 100.0 1000.0 ;
    ENDIF ;
    IF Timec 50.0 = THEN
      EVALUATE Delt Timec := 50.0 500.0 ;
    ENDIF ;
  ENDIF ;
  EVALUATE Timei := Timef ;
ENDWHILE ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.322846 ;
ECHO "test TCWE08 completed" ;
END: ;
QUIT "LIST" .
