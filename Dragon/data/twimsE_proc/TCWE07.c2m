*----
*  TEST CASE TCWE07
*  CANDU-6 CARTESIAN CELL
*  WIMS-E Library
*  TEST VARIOUS LEAKAGE OPTIONS
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  LIBRARY CANDU6S CANDU6T CANDU6SV CANDU6TV TRACK
  SYS FLUX EDITION ;
MODULE
  GEO: EXCELT: LIB: SHI: ASM: FLU: EDI: DELETE: END: ;
SEQ_BINARY
  INTLIN ;
PROCEDURE assertS ;
*----
*  Depletion data from file WIMS-E format WIMSE
*  Microscopic cross sections from file WIMS-E format WIMSE
*----
PROCEDURE TCWE05Lib ;
INTEGER   iedit := 1 ;
LIBRARY := TCWE05Lib :: <<iedit>> ;
*----
*  Geometry CANDU6S : GEOMETRY FOR SELF-SHIELDING  (NO VOID)
*           CANDU6F : GEOMETRY FOR TRANSPORT       (NO VOID)
*           CANDU6FV: GEOMETRY FOR TRANSPORT       (COOLANT VOID)
*           CANDU6FV: GEOMETRY FOR TRANSPORT       (COOLANT VOID)
*----
CANDU6S := GEO: :: CARCEL  5
  X+ REFL X- REFL MESHX -14.2875 14.2875
  Y+ REFL Y- REFL MESHY -14.2875 14.2875
  RADIUS  0.00000 5.16890 5.60320 6.44780 6.58750 14.00
  MIX     1       2       3       4       5       5
  CLUSTER ROD1 ROD2 ROD3 ROD4
  ::: ROD1 := GEO: TUBE 2 MIX  6 10 NPIN  1 RPIN 0.0000 APIN 0.0000
    RADIUS 0.00000 0.6122 0.6540 ;
  ::: ROD2 := GEO: ROD1   MIX  7 10 NPIN  6 RPIN 1.4885 APIN 0.0000   ;
  ::: ROD3 := GEO: ROD1   MIX  8 10 NPIN 12 RPIN 2.8755 APIN 0.261799 ;
  ::: ROD4 := GEO: ROD1   MIX  9 10 NPIN 18 RPIN 4.3305 APIN 0.0      ;
  ;
CANDU6T := GEO: CANDU6S :: SPLITR  6 1 1  1 10
  ::: ROD1 := GEO: ROD1 SPLITR 2 1 ;
  ::: ROD2 := GEO: ROD2 SPLITR 2 1 ;
  ::: ROD3 := GEO: ROD3 SPLITR 2 1 ;
  ::: ROD4 := GEO: ROD4 SPLITR 2 1 ;
  ;
CANDU6SV := GEO: CANDU6S  :: MIX 0 2 3 4 5 5 ;
CANDU6TV := GEO: CANDU6SV :: SPLITR  6 1 1  1 10
  ::: ROD1 := GEO: ROD1 SPLITR 2 1 ;
  ::: ROD2 := GEO: ROD2 SPLITR 2 1 ;
  ::: ROD3 := GEO: ROD3 SPLITR 2 1 ;
  ::: ROD4 := GEO: ROD4 SPLITR 2 1 ;
  ;
*----
*  CASE WITH NO VOID
*  Self-Shielding calculation EXCEL
*  Transport calculation      EXCEL
*  Flux TYPE K AND B WITH VARIOUS LEAKAGE OPTIONS
*----
TRACK INTLIN := EXCELT: CANDU6S ::
  TITLE 'TCWE07: CANDU-6 CARTESIAN FUEL TEMP= 941.29'
  EDIT 0 MAXR 14 TRAK TISO 7 20.0 SYMM 4 ;
LIBRARY := SHI: LIBRARY TRACK INTLIN :: EDIT 0 NOLJ ;
TRACK INTLIN := DELETE: TRACK INTLIN ;
TRACK INTLIN := EXCELT: CANDU6T ::
  TITLE 'TCWE07: CANDU-6 CARTESIAN FUEL TEMP= 941.29'
  EDIT 0 MAXR 32 ANIS 2 TRAK TISO 7 20.0 SYMM 4 ;
SYS := ASM: LIBRARY TRACK INTLIN ::
  EDIT 0 PIJK ;
FLUX := FLU: SYS LIBRARY TRACK ::
  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.124517 ;
EDITION := EDI: LIBRARY TRACK FLUX ::
  EDIT 3 SAVE COND 4.0 TAKE REGI 1 4 7 10 16 24 ;
FLUX := FLU: FLUX SYS LIBRARY TRACK ::
  TYPE B B1 PNL ;
assertS FLUX :: 'K-INFINITY' 1 1.116083 ;
EDITION := EDI: EDITION LIBRARY TRACK FLUX ::
  EDIT 3 SAVE COND 4.0  TAKE REGI 1 4 7 10 16 24 ;
FLUX := FLU: FLUX SYS LIBRARY TRACK ::
  TYPE B B1 HETE ;
assertS FLUX :: 'K-INFINITY' 1 1.116019 ;
EDITION := EDI: EDITION LIBRARY TRACK FLUX ::
  EDIT 3 SAVE COND 4.0 TAKE REGI 1 4 7 10 16 24 ;
FLUX SYS := DELETE: FLUX SYS ;
SYS := ASM: LIBRARY TRACK INTLIN ::
  EDIT 0 PIJ ECCO ;
FLUX := FLU: SYS LIBRARY TRACK ::
  TYPE B B1 ECCO ;
assertS FLUX :: 'K-INFINITY' 1 1.116024 ;
EDITION := EDI: EDITION LIBRARY TRACK FLUX ::
  EDIT 3 SAVE COND 4.0  TAKE REGI 1 4 7 10 16 24 ;
FLUX SYS TRACK INTLIN := DELETE: FLUX SYS TRACK INTLIN ;
*----
*  CASE WITH COOLANT VOIDED
*  Self-Shielding calculation EXCEL
*  Transport calculation      EXCEL
*  Flux TYPE K AND B WITH VARIOUS LEAKAGE OPTIONS
*----
TRACK INTLIN := EXCELT: CANDU6SV ::
  TITLE 'TCWE07: CANDU-6 CARTESIAN FUEL TEMP= 941.29'
  EDIT 0 MAXR 14 TRAK TISO 7 20.0 SYMM 4 ;
LIBRARY := SHI: LIBRARY TRACK INTLIN :: EDIT 0 NOLJ ;
TRACK INTLIN := DELETE: TRACK INTLIN ;
TRACK INTLIN := EXCELT: CANDU6TV ::
  TITLE 'TCWE07: CANDU-6 CARTESIAN FUEL TEMP= 941.29'
  EDIT 0 MAXR 32 ANIS 2 TRAK TISO 7 20.0 SYMM 4 ;
SYS := ASM: LIBRARY TRACK INTLIN ::
  EDIT 0 PIJK ;
FLUX := FLU: SYS LIBRARY TRACK ::
  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.144657 ;
EDITION := EDI: EDITION LIBRARY TRACK FLUX ::
  EDIT 3 SAVE COND 4.0 TAKE REGI 1 4 7 10 16 24 ;
FLUX := FLU: FLUX SYS LIBRARY TRACK ::
  TYPE B B1 PNL ;
assertS FLUX :: 'K-INFINITY' 1 1.136610 ;
EDITION := EDI: EDITION LIBRARY TRACK FLUX ::
  EDIT 3 SAVE COND 4.0  TAKE REGI 1 4 7 10 16 24 ;
FLUX := FLU: FLUX SYS LIBRARY TRACK ::
  TYPE B B1 HETE ;
assertS FLUX :: 'K-INFINITY' 1 1.136823 ;
EDITION := EDI: EDITION LIBRARY TRACK FLUX ::
  EDIT 3 SAVE COND 4.0 TAKE REGI 1 4 7 10 16 24 ;
FLUX SYS := DELETE: FLUX SYS ;
SYS := ASM: LIBRARY TRACK INTLIN ::
  EDIT 0 PIJ ECCO ;
FLUX := FLU: SYS LIBRARY TRACK ::
  TYPE B B1 ECCO ;
assertS FLUX :: 'K-INFINITY' 1 1.136833 ;
EDITION := EDI: EDITION LIBRARY TRACK FLUX ::
  EDIT 3 SAVE COND 4.0  TAKE REGI 1 4 7 10 16 24 ;
FLUX SYS TRACK INTLIN := DELETE: FLUX SYS TRACK INTLIN ;
ECHO "test TCWE07 completed" ;
END: ;   
QUIT "LIST" .
