*----
*  TEST CASE TCWE01
*  MOSTELLER BENCHMARK: 1-D ANNULAR CELL AND 2-D CARTESIAN CELL
*  WIMSE 69 GROUPS LIBRARY FILE WIMS-E
*
*  REF: R. Mosteller et al. Nucl. Sci. Eng. 107, 265 (1991)
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  MOSTELA MOSTELC DISCR LIBRARY CP CALC OUT ;
SEQ_BINARY
  TRKSPC ;
MODULE
  LIB: GEO: SYBILT: EXCELT: SHI: ASM: FLU: EDI: DELETE: END: ;
PROCEDURE assertS ;
REAL P_Zr90     P_Zr91     P_Zr92    P_Zr94    P_Zr96    :=
     51.45E-02  11.22E-02  17.15E-02 17.38E-02 2.80E-02  ;
REAL N_Zr0 := 3.83243E-2 ;
REAL N_Zr90       N_Zr91       N_Zr92       N_Zr94       N_Zr96    :=
     N_Zr0 P_Zr90 *  N_Zr0 P_Zr91 *  N_Zr0 P_Zr92 *  N_Zr0 P_Zr94 *
     N_Zr0 P_Zr96 *  ;

REAL P_B10 P_B11    :=    0.199 0.801 ;
REAL N_B0 := 1.02133E-5 ;
REAL N_B10 N_B11    :=    N_B0 P_B10 *  N_B0 P_B11 * ;
*----
*  Microscopic cross sections from file WIMS-E format WIMSE
*----
LIBRARY := LIB: ::
  NMIX 3 CTRA WIMS
  MIXS LIB: WIMSE FIL: WIMS-E
  MIX 1 600.0                        O16      = '8016'     4.61309E-2
    U235     = '92235'   1.66078E-4 1 
    U238     = '92238'   2.28994E-2 1 
  MIX 2 600.0
    Zr90    = '40090'  <<N_Zr90>>
    Zr91    = '40091'  <<N_Zr91>> 2
    Zr92    = '40092'  <<N_Zr92>>
    Zr94    = '40094'  <<N_Zr94>>
    Zr96    = '40096'  <<N_Zr96>>
  MIX 3 600.0
    H1H2O    = '1001'   4.42326E-2   O16H2O   = '8016'     2.21163E-2
    B10      = '5010'   <<N_B10>>    B11      = '5011'    <<N_B11>>
  ;
*----
*  Geometry MOSTELA : annular 3 region geometry
*           MOSTELC : Cartesian 3 region geometry
*----
MOSTELA := GEO: :: TUBE 3
  R+ REFL RADIUS 0.0 0.39306 0.45802 0.71206 SPLITR 2 1 1
  MIX 1 2 3 ;
MOSTELC := GEO: :: CARCEL 2
  X- REFL X+ REFL MESHX 0.0 1.26209
  Y- REFL Y+ REFL MESHY 0.0 1.26209
  RADIUS 0.0 0.39306 0.45802 SPLITR 2 1
  MIX  1 2 3 ;
*----
*  Case 1 -- annular
*  Self-Shielding calculation SYBIL
*  Transport calculation      SYBIL
*  Flux calculation for K no leakage
*----
DISCR := SYBILT: MOSTELA ::
  TITLE 'TCWE01: MOSTELLER BENCHMARK (SYBIL / SYBIL)'
  MAXR 4 QUA1 5 ;
LIBRARY := SHI: LIBRARY DISCR :: EDIT 0 NOLJ ;
CP := ASM: LIBRARY DISCR ;
CALC := FLU: CP LIBRARY DISCR ::
  TYPE K ;
assertS CALC :: 'K-EFFECTIVE' 1 0.8289803 ;
OUT := EDI: LIBRARY DISCR CALC ::
  EDIT 4 MERG MIX 1 2 3 COND 4.0 SAVE ;
DISCR CP := DELETE: DISCR CP ;
*----
*  Case 2 -- Cartesian
*  Self-Shielding calculation SYBIL
*  Transport calculation      SYBIL
*  Flux calculation for K no leakage
*----
DISCR := SYBILT: MOSTELC ::
  TITLE 'TCWE01: MOSTELLER BENCHMARK (SYBIL / SYBIL)'
  MAXR 4 QUA1 5 QUA2 6 5 ;
LIBRARY := SHI: LIBRARY DISCR :: EDIT 0 NOLJ ;
CP := ASM: LIBRARY DISCR ;
CALC := FLU: CALC CP LIBRARY DISCR ::
  TYPE K ;
assertS CALC :: 'K-EFFECTIVE' 1 0.8290998 ;
OUT := EDI: OUT LIBRARY DISCR CALC ::
  EDIT 1 MERG MIX 1 2 3 COND 4.0 STAT ALL REFE 1 ;
DISCR CP := DELETE: DISCR CP ;
*----
*  Case 3 -- annular
*  Self-Shielding calculation EXCEL-ISO
*  Transport calculation      EXCEL-ISO
*  Flux calculation for K no leakage
*----
DISCR TRKSPC := EXCELT: MOSTELC ::
  TITLE 'TCWE01: MOSTELLER BENCHMARK (EXCELL)'
  MAXR 4 TRAK TISO 12 20.0 ;
LIBRARY := SHI: LIBRARY DISCR TRKSPC :: EDIT 0 NOLJ ;
CP := ASM: LIBRARY DISCR TRKSPC ;
CALC := FLU: CALC CP LIBRARY DISCR ::
  TYPE K ;
assertS CALC :: 'K-EFFECTIVE' 1 0.8302385 ;
OUT := EDI: OUT LIBRARY DISCR CALC ::
  EDIT 1 MERG MIX 1 2 3 COND 4.0 STAT ALL REFE 1 ;
DISCR TRKSPC CP := DELETE: DISCR TRKSPC CP ;
*----
*  Case 4 -- Cartesian
*  Self-Shielding calculation EXCEL-SPC
*  Transport calculation      EXCEL-SPC
*  Flux calculation for K no leakage
*----
DISCR TRKSPC := EXCELT: MOSTELC ::
  TITLE 'TCWE01: MOSTELLER BENCHMARK (EXCELL)'
  MAXR 4 TRAK TSPC 12 20.0 ;
LIBRARY := SHI: LIBRARY DISCR TRKSPC :: EDIT 0 NOLJ ;
CP := ASM: LIBRARY DISCR TRKSPC ;
CALC := FLU: CALC CP LIBRARY DISCR ::
  TYPE K ;
assertS CALC :: 'K-EFFECTIVE' 1 0.8325990 ;
OUT := EDI: OUT LIBRARY DISCR CALC ::
  EDIT 1 MERG MIX 1 2 3 COND 4.0 STAT ALL REFE 1 ;
DISCR TRKSPC CP := DELETE: DISCR TRKSPC CP ;
ECHO "test TCWE01 completed" ;
END: ;
QUIT "LIST" .
