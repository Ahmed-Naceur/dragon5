*---
* TEST CASE RegtestCNG_mccg
* CANDU-NG UNIT CELL WITH NXT:
*---
MODULE
  NXT: MCCGT: FLU: USS: SHI: ASM: EDI: END: DELETE: ; 
PROCEDURE
  assertS
  CHB_NXT
  LCNG_U2_nDf
  ;
LINKED_LIST  
  GEOMF GEOMS
  GEOFLXC GEOSHIC
  Biblio LIBC PIJ FLUXC EDIT
  ;
SEQ_BINARY 
  TRKFLXC TRKSHIC
  ;
STRING    
  state SolveurSS SolveurFL polquad
  ;
STRING mtype := "PIJ" ;
INTEGER grmin ;
INTEGER
  splitc splitm nban2d nban3d nban2dss
  ;
REAL
  density densityss
  ;
INTEGER
   CF11  CF12  CF13  CF14 
   CF21i CF22i CF23i CF24i
   CF31i CF32i CF33i CF34i
   CF41i CF42i CF43i CF44i
   CF21e CF22e CF23e CF24e
   CF31e CF32e CF33e CF34e
   CF41e CF42e CF43e CF44e
   VF11  VF12  VF13  VF14 
   VF21i VF22i VF23i VF24i
   VF31i VF32i VF33i VF34i
   VF41i VF42i VF43i VF44i
   VF21e VF22e VF23e VF24e
   VF31e VF32e VF33e VF34e
   VF41e VF42e VF43e VF44e
   COOL  PT    HEG   CT
   MOD   SH    VOID := 
          1     2     3     4
          5     6     7     8
          9    10    11    12
         13    14    15    16
         17    18    19    20
         21    22    23    24
         25    26    27    28
         29    30    31    32
         33    34    35    36
         37    38    39    40
         41    42    43    44
         45    46    47    48
         49    50    51    52
         53    54    55    56
         57    58    59    60
         61    63    62
  ; 
*
* temperatures
*
REAL
  TempComb  TempCalo 
  TempPres  TempMode  TempCala
  := 
  687.      300.6
  300.      50.6      80.
  ;
*
* geometrical parameters
*
EVALUATE state      := "Chboard"  ; ! Cooled / Voided / Chboard
EVALUATE splitc    := 12 ;
EVALUATE splitm    := 5 ;

EVALUATE nban2dss  := 8  ;
EVALUATE densityss := 20.0 ;

EVALUATE nban2d    := 12 ;
EVALUATE density   := 50.0 ;
EVALUATE polquad   := "LCMD" ;
EVALUATE nban3d    := 2 ;
*
* execution parameters
*
EVALUATE SolveurSS := "PIJ" ; ! PIJ / MCCG
EVALUATE SolveurFL := "MCCG" ; ! PIJ / MCCG
EVALUATE grmin     := 0     ; ! if USS / SUBG
STRING LibName := "DLIB_J2" ;
*
*  geometries
*
GEOMF GEOMS := CHB_NXT ::
  <<state>>
  <<splitc>> <<splitm>>
  <<CF11>>  <<CF12>>  <<CF13>>  <<CF14>> 
  <<CF21i>> <<CF22i>> <<CF23i>> <<CF24i>>
  <<CF31i>> <<CF32i>> <<CF33i>> <<CF34i>>
  <<CF41i>> <<CF42i>> <<CF43i>> <<CF44i>>
  <<CF21e>> <<CF22e>> <<CF23e>> <<CF24e>>
  <<CF31e>> <<CF32e>> <<CF33e>> <<CF34e>>
  <<CF41e>> <<CF42e>> <<CF43e>> <<CF44e>>
  <<VF11>>  <<VF12>>  <<VF13>>  <<VF14>> 
  <<VF21i>> <<VF22i>> <<VF23i>> <<VF24i>>
  <<VF31i>> <<VF32i>> <<VF33i>> <<VF34i>>
  <<VF41i>> <<VF42i>> <<VF43i>> <<VF44i>>
  <<VF21e>> <<VF22e>> <<VF23e>> <<VF24e>>
  <<VF31e>> <<VF32e>> <<VF33e>> <<VF34e>>
  <<VF41e>> <<VF42e>> <<VF43e>> <<VF44e>>
  <<COOL>>  <<PT>>    <<HEG>>   <<CT>>
  <<MOD>>   <<SH>>    <<VOID>>
 ;
*
* tracking
*
* self-shielding tracking 
GEOSHIC TRKSHIC := NXT: GEOMS ::
  ALLG
  TISO <<nban2dss>> <<densityss>>
 ;
*
* flux tracking
GEOFLXC TRKFLXC := NXT: GEOMF ::
  ALLG
  TISO <<nban2d>> <<density>>
 ;
*
* library treatment
*
Biblio := LCNG_U2_nDf ::
  <<TempComb>> <<TempCalo>> <<TempPres>>
  <<TempMode>> <<TempCala>> <<LibName>>
  <<CF11>>  <<CF12>>  <<CF13>>  <<CF14>> 
  <<CF21i>> <<CF22i>> <<CF23i>> <<CF24i>>
  <<CF31i>> <<CF32i>> <<CF33i>> <<CF34i>>
  <<CF41i>> <<CF42i>> <<CF43i>> <<CF44i>>
  <<CF21e>> <<CF22e>> <<CF23e>> <<CF24e>>
  <<CF31e>> <<CF32e>> <<CF33e>> <<CF34e>>
  <<CF41e>> <<CF42e>> <<CF43e>> <<CF44e>>
  <<VF11>>  <<VF12>>  <<VF13>>  <<VF14>> 
  <<VF21i>> <<VF22i>> <<VF23i>> <<VF24i>>
  <<VF31i>> <<VF32i>> <<VF33i>> <<VF34i>>
  <<VF41i>> <<VF42i>> <<VF43i>> <<VF44i>>
  <<VF21e>> <<VF22e>> <<VF23e>> <<VF24e>>
  <<VF31e>> <<VF32e>> <<VF33e>> <<VF34e>>
  <<VF41e>> <<VF42e>> <<VF43e>> <<VF44e>>
  <<COOL>>  <<PT>>    <<HEG>>   <<CT>>
  <<MOD>>   <<SH>>    <<VOID>>
  ;
*
* self-shielding
*
IF SolveurSS "MCCG" = THEN
   GEOSHIC := MCCGT: GEOSHIC TRKSHIC GEOMS ::
   EDIT 1 <<polquad>> <<nban3d>> AAC 1 TMT EPSI 1E-5
   MAXI 20 KRYL 30 SCR 0 HDD 0.
   ;
   EVALUATE mtype := "ARM" ;
ENDIF
 ;
LIBC := USS: Biblio GEOSHIC TRKSHIC :: 
EDIT 2 GRMIN <<grmin>> <<mtype>>
 ;
*
* assembly
*
IF SolveurFL "MCCG" = THEN
  GEOFLXC := MCCGT: GEOFLXC TRKFLXC GEOMF ::
  EDIT 1 <<polquad>> <<nban3d>>
  MAXI 1 EPSI 1E-4 HDD 0. MCU 400000
  KRYL 0 SCR 0 AAC 100 TMT
  ;
  EVALUATE mtype := "ARM" ;
ENDIF
  ;
PIJ := ASM: LIBC GEOFLXC TRKFLXC  ::
<<mtype>> EDIT 1
  ;
* 
* flux calculation
*
FLUXC := FLU: PIJ LIBC GEOFLXC TRKFLXC ::
EDIT 3 TYPE K THER 5 5E-5
 ; 
assertS FLUXC :: 'K-EFFECTIVE' 1 1.323817 ;
*
* edition
*
EDIT := EDI: FLUXC LIBC GEOFLXC ::
EDIT 3
COND 172
MERGE MIX
* fuel in cooled cells
*1   2  3  4 : center
*5   6  7  8 : r=1.73
*9  10 11 12 : r=3.075
*13 14 15 16 : r=4.384
* fuel in voided cells
*17 18 19 20 : center
*21 22 23 24 : r=1.73
*25 26 27 28 : r=3.075
*29 39 31 32 : r=4.384
 1     2     3     4
 5     6     7     8
 9    10    11    12
13    14    15    16
 5     6     7     8
 9    10    11    12
13    14    15    16
17    18    19    20
21    22    23    24
25    26    27    28
29    30    31    32
21    22    23    24
25    26    27    28
29    30    31    32
33    34    35    36
37    38    39
!   CF11  CF12  CF13  CF14 
!   CF21i CF22i CF23i CF24i
!   CF31i CF32i CF33i CF34i
!   CF41i CF42i CF43i CF44i
!   CF21e CF22e CF23e CF24e
!   CF31e CF32e CF33e CF34e
!   CF41e CF42e CF43e CF44e
!   VF11  VF12  VF13  VF14 
!   VF21i VF22i VF23i VF24i
!   VF31i VF32i VF33i VF34i
!   VF41i VF42i VF43i VF44i
!   VF21e VF22e VF23e VF24e
!   VF31e VF32e VF33e VF34e
!   VF41e VF42e VF43e VF44e
!   COOL  PT    HEG   CT
!   MOD   SH    VOID
SAVE
;
*
* delete files
*
GEOFLXC TRKFLXC GEOSHIC TRKSHIC 
LIBC PIJ FLUXC GEOMF GEOMS EDIT
  := DELETE: 
GEOFLXC TRKFLXC GEOSHIC TRKSHIC 
LIBC PIJ FLUXC GEOMF GEOMS EDIT
  ; 
*  
* end
*
ECHO "test RegtestCNG_mccg completed" ;
QUIT "LIST" .
