*----
*  TEST CASE TCXA01
*  MOSTELLER BENCHMARK: 1-D ANNULAR CELL AND 2-D CARTESIAN CELL
*  MATXS7A 69 GROUPS LIBRARY FILE MATXS7A (BASED ON ENDF-B/V)
*
*  REF: R. Mosteller et al. Nucl. Sci. Eng. 107, 265 (1991)
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  MOSTELA MOSTELC DISCR LIBRARY CP CALC OUT ;
SEQ_BINARY
  TRKSPC ;
MODULE
  LIB: GEO: SYBILT: EXCELT: SHI: ASM: FLU: EDI: DELETE: END: ;
PROCEDURE assertS ;
*----
*  Microscopic cross sections from file MATXS7A format MATXS
*----
LIBRARY := LIB: ::
  NMIX 3 CTRA OLDW
  MIXS LIB: MATXS FIL: MATXS7A
  MIX 1 600.0
    O16      = O16      4.61309E-2    THER 42 FREE
    U235     = U235     1.66078E-4 1  THER 42 FREE
    U238     = U238     2.28994E-2 1  THER 42 FREE
  MIX 2 600.0                        
    Zr91     = ZRNAT    3.83243E-2    THER 42 FREE
  MIX 3 600.0
    H1H2O    = H1       4.42326E-2    THER 42 H2O
    O16H2O   = O16      2.21163E-2    THER 42 FREE
    BNat     = B10      2.03245E-6    THER 42 FREE
  ;
*----
*  Geometry MOSTELA : annular 3 region geometry
*           MOSTELC : Cartesian 3 region geometry
*----
MOSTELA := GEO: :: TUBE 3
  R+ REFL RADIUS 0.0 0.39306 0.45802 0.71206 SPLITR 2 1 1
  MIX 1 2 3 ;
MOSTELC := GEO: :: CARCEL 2
  X- REFL X+ REFL MESHX 0.0 1.26209
  Y- REFL Y+ REFL MESHY 0.0 1.26209
  RADIUS 0.0 0.39306 0.45802 SPLITR 2 1
  MIX  1 2 3 ;
*----
*  Case 1 -- annular
*  Self-Shielding calculation SYBIL
*  Transport calculation      SYBIL
*  Flux calculation for K no leakage
*----
DISCR := SYBILT: MOSTELA ::
  TITLE 'TCWM01: MOSTELLER BENCHMARK (SYBIL / SYBIL)'
  MAXR 4 QUA1 5 ;
LIBRARY := SHI: LIBRARY DISCR :: EDIT 1 NOGC NOLJ
  GRMIN 5 GRMAX 27 ;
CP := ASM: LIBRARY DISCR ::
  PIJ ;
CALC := FLU: CP LIBRARY DISCR ::
  TYPE K ;
assertS CALC :: 'K-EFFECTIVE' 1 0.8214942 ;
OUT := EDI: LIBRARY DISCR CALC ::
  EDIT 4 MERG MIX 1 2 3 COND 27 69 SAVE ;
DISCR CP := DELETE: DISCR CP ;
*----
*  Case 2 -- Cartesian
*  Self-Shielding calculation SYBIL
*  Transport calculation      SYBIL
*  Flux calculation for K no leakage
*----
DISCR := SYBILT: MOSTELC ::
  TITLE 'TCWM01: MOSTELLER BENCHMARK (SYBIL / SYBIL)'
  MAXR 4 QUA1 5 QUA2 6 5 ;
LIBRARY := SHI: LIBRARY DISCR :: EDIT 1 NOGC NOLJ
  GRMIN 5 GRMAX 27 ;
CP := ASM: LIBRARY DISCR ::
  PIJ ;
CALC := FLU: CALC CP LIBRARY DISCR ::
  TYPE K ;
assertS CALC :: 'K-EFFECTIVE' 1 0.8212590 ;
OUT := EDI: OUT LIBRARY DISCR CALC ::
  EDIT 1 MERG MIX 1 2 3 COND 27 69 STAT ALL REFE 1 ;
DISCR CP := DELETE: DISCR CP ;
*----
*  Case 3 -- annular
*  Self-Shielding calculation EXCEL-ISO
*  Transport calculation      EXCEL-ISO
*  Flux calculation for K no leakage
*----
DISCR TRKSPC := EXCELT: MOSTELC ::
  TITLE 'TCWM01: MOSTELLER BENCHMARK (EXCELL)'
  MAXR 4 TRAK TISO 12 20.0 ;
LIBRARY := SHI: LIBRARY DISCR TRKSPC :: EDIT 1 NOGC NOLJ
  GRMIN 5 GRMAX 27 ;
CP := ASM: LIBRARY DISCR TRKSPC ::
  PIJ ;
CALC := FLU: CALC CP LIBRARY DISCR ::
  TYPE K ;
assertS CALC :: 'K-EFFECTIVE' 1 0.8220187 ;
OUT := EDI: OUT LIBRARY DISCR CALC ::
  EDIT 1 MERG MIX 1 2 3 COND 27 69 STAT ALL REFE 1 ;
DISCR TRKSPC CP := DELETE: DISCR TRKSPC CP ;
*----
*  Case 4 -- Cartesian
*  Self-Shielding calculation EXCEL-SPC
*  Transport calculation      EXCEL-SPC
*  Flux calculation for K no leakage
*----
DISCR TRKSPC := EXCELT: MOSTELC ::
  TITLE 'TCWM01: MOSTELLER BENCHMARK (EXCELL)'
  MAXR 4 TRAK TSPC 12 20.0 ;
LIBRARY := SHI: LIBRARY DISCR TRKSPC :: EDIT 1 NOGC NOLJ
  GRMIN 5 GRMAX 27 ;
CP := ASM: LIBRARY DISCR TRKSPC ::
  PIJ ;
CALC := FLU: CALC CP LIBRARY DISCR ::
  TYPE K ;
assertS CALC :: 'K-EFFECTIVE' 1 0.8238403 ;
OUT := EDI: OUT LIBRARY DISCR CALC ::
  EDIT 1 MERG MIX 1 2 3 COND 27 69 STAT ALL REFE 1 ;
DISCR TRKSPC CP := DELETE: DISCR TRKSPC CP ;
ECHO "test TCXA01 completed" ;
END: ;
