*----
*
*  TEST CASE uo2_1c_case4
*  FOURTH UO2 ROWLAND'S BENCHMARK
*  UNIFORM SELF-SHIELDING
*  172-GROUP JEF2.2 DRAGLIB
*
*  Author: A. Hebert
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM TRACK LIBRARY LIBRARY2 CP FLUX ;
MODULE LIB: GEO: SYBILT: SHI: USS: ASM: FLU: DELETE: END: ;
PROCEDURE assertS ;
*
GEOM := GEO: :: TUBE 3
  R+ REFL
  RADIUS 0.0 0.4 0.45 0.6770275
  SPLITR 2 1 1 
  MIX 1 2 3 ;
TRACK := SYBILT: GEOM ::
  TITLE 'FOURTH UO2 ROWLAND S BENCHMARK'
  MAXR 20 QUA1 5 ;
*----
* SHI
*----
LIBRARY := LIB: ::
   EDIT 1
   NMIX 3    (*MAXIMUM OF MATERIAL MIXTURES*)
   CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
   ANIS 2
   ADED 4 NELAS N4N N2N N3N
*
  MIXS LIB: DRAGON FIL: DLIB_J2
  MIX 1 574.0
    O16     = O16    4.6624E-2
    U235    = U235   7.0803E-4 1
    U238    = U238   2.2604E-2 1
  MIX 2 574.0
    Zr0     = Zr0    4.3241E-2 2
  MIX 3 574.0
    H1      = H1_H2O 4.6892E-2
    O16     = O16    2.3446E-2
  ;
LIBRARY := SHI: LIBRARY TRACK :: EDIT 1 LJ GRMIN 45 ;
CP := ASM: LIBRARY TRACK :: PIJ ;
FLUX := FLU: CP LIBRARY TRACK :: TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.310755 ;
LIBRARY CP FLUX := DELETE: LIBRARY CP FLUX ;
*----
* USS
*----
LIBRARY := LIB: ::
  EDIT 1
  NMIX 3    (*MAXIMUM OF MATERIAL MIXTURES*)
  CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
  ANIS 2
  ADED 4 NELAS N4N N2N N3N
  SUBG      (*HELIOS TYPE PROBABILITY TABLES*)
*
  MIXS LIB: DRAGON FIL: DLIB_J2
  MIX 1 574.0
    O16     = O16    4.6624E-2
    U235    = U235   7.0803E-4 1 IRSET 0.0 81
    U238    = U238   2.2604E-2 1 IRSET 0.0 81
  MIX 2 574.0
    Zr0     = Zr0    4.3241E-2 2 IRSET 0.0 81
  MIX 3 574.0
    H1      = H1_H2O 4.6892E-2
    O16     = O16    2.3446E-2
  ;
LIBRARY2 := USS: LIBRARY TRACK ::
            EDIT 1 TRAN PASS 2 GRMIN 45 ;
CP := ASM: LIBRARY2 TRACK :: PIJ ;
FLUX := FLU: CP LIBRARY2 TRACK ::
TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.314775 ;
LIBRARY LIBRARY2 CP FLUX := DELETE: LIBRARY LIBRARY2 CP FLUX ;
*----
* Ribon extended
*----
LIBRARY := LIB: ::
  EDIT 1
  NMIX 3    (*MAXIMUM OF MATERIAL MIXTURES*)
  CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
  ANIS 2
  ADED 4 NELAS N4N N2N N3N
  PTSL      (*CALENDF TYPE PROBABILITY TABLES*)
*
  MIXS LIB: DRAGON FIL: DLIB_J2
  MIX 1 574.0
    O16     = O16    4.6624E-2
    U235    = U235   7.0803E-4 1 IRSET PTSL 1
    U238    = U238   2.2604E-2 1 IRSET PTSL 1
  MIX 2 574.0
    Zr0     = Zr0    4.3241E-2 2 IRSET PTSL 1
  MIX 3 574.0
    H1      = H1_H2O 4.6892E-2
    O16     = O16    2.3446E-2
  ;
LIBRARY2 := USS: LIBRARY TRACK ::
            EDIT 1 TRAN PASS 2 GRMIN 45 ;
CP := ASM: LIBRARY2 TRACK :: PIJ ;
FLUX := FLU: CP LIBRARY2 TRACK ::
TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.313890 ;
LIBRARY LIBRARY2 CP FLUX := DELETE: LIBRARY LIBRARY2 CP FLUX ;
*----
* Ribon extended with correlation
*----
LIBRARY := LIB: ::
  EDIT 1
  NMIX 3    (*MAXIMUM OF MATERIAL MIXTURES*)
  CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
  ANIS 2
  ADED 4 NELAS N4N N2N N3N
  PTSL      (*CALENDF TYPE PROBABILITY TABLES*)
*
  MIXS LIB: DRAGON FIL: DLIB_J2
  MIX 1 574.0
    O16     = O16    4.6624E-2
    U235    = U235   7.0803E-4 1 CORR 1 IRSET PTSL 1
    U238    = U238   2.2604E-2 1 CORR 1 IRSET PTSL 1
  MIX 2 574.0
    Zr0     = Zr0    4.3241E-2 2 IRSET PTSL 1
  MIX 3 574.0
    H1      = H1_H2O 4.6892E-2
    O16     = O16    2.3446E-2
  ;
LIBRARY2 := USS: LIBRARY TRACK ::
            EDIT 1 TRAN PASS 4 GRMIN 45 ;
CP := ASM: LIBRARY2 TRACK :: PIJ ;
FLUX := FLU: CP LIBRARY2 TRACK ::
TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.313668 ;
LIBRARY LIBRARY2 CP FLUX := DELETE: LIBRARY LIBRARY2 CP FLUX ;
ECHO "test uo2_1c_case4 completed" ;
END: ;
