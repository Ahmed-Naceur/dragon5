*----
*
*  TEST CASE mox_1c_case3
*  THIRD MOX ROWLAND'S BENCHMARK
*  UNIFORM SELF-SHIELDING
*  172-GROUP JEF2.2 DRAGLIB
*
*  Author: A. Hebert
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST LIBRARY LIBRARY2 GEOM TRACK MACRO CP FLUX ;
MODULE LIB: GEO: SYBILT: SHI: USS: ASM: FLU: DELETE: END: ;
INTEGER COMB0101 GAIN1 MODE := 1 2 3 ;
PROCEDURE assertS ;
*
GEOM := GEO: :: TUBE 4
  R+ REFL
  RADIUS 0.0 0.41 0.475 0.6045552 0.710879
  SPLITR 2 1 1 1
  MIX 1 2 3 3 ;
TRACK := SYBILT: GEOM ::
  EDIT 1
  TITLE 'THIRD MOX ROWLAND S BENCHMARK'
  MAXR 20 QUA1 5 ;
*----
* SHI
*----
LIBRARY := LIB: ::
 EDIT 1
 NMIX 3    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 ADED 4 NELAS N4N N2N N3N
*
 MIXS LIB: DRAGON FIL: DLIB_J2
 MIX <<COMB0101>> 300.0 (*COMB0101*)
    U235     = U235  5.118E-5 1
    U238     = U238  2.042E-2 1
    PU239    = Pu239 1.972E-3 1
    PU238    = Pu238 2.714E-5 1
    PU240    = Pu240 4.256E-4 1
    PU241    = Pu241 3.577E-5 1
    PU242    = Pu242 1.234E-5 1
    AM241    = Am241 1.406E-5 1
    O16      = O16   4.588E-2
 MIX <<GAIN1>> 300.0 (*GAIN1*)
    ZR      = Zr0  3.88E-2 2
 MIX <<MODE>> 293.0 (*MODE*)
    H1      = H1_H2O 4.744E-2
    O16     = O16 2.372E-2
 ;
LIBRARY := SHI: LIBRARY TRACK :: EDIT 1 LJ GRMIN 45 ;
CP := ASM: LIBRARY TRACK :: PIJ ;
FLUX := FLU: CP LIBRARY TRACK :: TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.254774 ;
LIBRARY CP FLUX := DELETE: LIBRARY CP FLUX ;
*----
* USS
*----
LIBRARY := LIB: ::
 EDIT 1
 NMIX 3    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 ADED 4 NELAS N4N N2N N3N
 SUBG      (*HELIOS TYPE PROBABILITY TABLES*)
*
 MIXS LIB: DRAGON FIL: DLIB_J2
 MIX <<COMB0101>> 300.0 (*COMB0101*)
    U235     = U235  5.118E-5 1 IRSET 0.0 81
    U238     = U238  2.042E-2 1 IRSET 0.0 81
    PU239    = Pu239 1.972E-3 1 IRSET 0.0 81
    PU238    = Pu238 2.714E-5 1 IRSET 0.0 81
    PU240    = Pu240 4.256E-4 1 IRSET 0.0 81
    PU241    = Pu241 3.577E-5 1 IRSET 0.0 81
    PU242    = Pu242 1.234E-5 1 IRSET 0.0 81
    AM241    = Am241 1.406E-5 1 IRSET 0.0 81
    O16      = O16   4.588E-2
 MIX <<GAIN1>> 300.0 (*GAIN1*)
    ZR      = Zr0  3.88E-2 2 IRSET 0.0 81
 MIX <<MODE>> 293.0 (*MODE*)
    H1      = H1_H2O 4.744E-2
    O16     = O16 2.372E-2
 ;
LIBRARY2 := USS: LIBRARY TRACK ::
            EDIT 1 TRAN PASS 3 GRMIN 45 ;
CP := ASM: LIBRARY2 TRACK :: PIJ ;
FLUX := FLU: CP LIBRARY2 TRACK ::
TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.257776 ;
LIBRARY LIBRARY2 CP FLUX := DELETE: LIBRARY LIBRARY2 CP FLUX ;
*----
* Ribon extended
*----
LIBRARY := LIB: ::
 EDIT 1
 NMIX 6    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 ADED 4 NELAS N4N N2N N3N
 PTSL      (*CALENDF TYPE PROBABILITY TABLES*)
*
 MIXS LIB: DRAGON FIL: DLIB_J2
 MIX <<COMB0101>> 300.0 (*COMB0101*)
    U235     = U235  5.118E-5 1 IRSET PTSL 1
    U238     = U238  2.042E-2 1 IRSET PTSL 1
    PU239    = Pu239 1.972E-3 1 IRSET PTSL 1
    PU238    = Pu238 2.714E-5 1 IRSET PTSL 1
    PU240    = Pu240 4.256E-4 1 IRSET PTSL 1
    PU241    = Pu241 3.577E-5 1 IRSET PTSL 1
    PU242    = Pu242 1.234E-5 1 IRSET PTSL 1
    AM241    = Am241 1.406E-5 1 IRSET PTSL 1
    O16      = O16   4.588E-2
 MIX <<GAIN1>> 300.0 (*GAIN1*)
    ZR      = Zr0  3.88E-2 2 IRSET PTSL 1
 MIX <<MODE>> 293.0 (*MODE*)
    H1      = H1_H2O 4.744E-2
    O16     = O16 2.372E-2
 ;
LIBRARY2 := USS: LIBRARY TRACK ::
            EDIT 1 TRAN PASS 3 GRMIN 45 ;
CP := ASM: LIBRARY2 TRACK :: PIJ ;
FLUX := FLU: CP LIBRARY2 TRACK ::
TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.257338 ;
LIBRARY LIBRARY2 CP FLUX := DELETE: LIBRARY LIBRARY2 CP FLUX ;
*----
* Ribon extended with correlation
*----
LIBRARY := LIB: ::
 EDIT 1
 NMIX 3    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 ADED 4 NELAS N4N N2N N3N
 PTSL      (*CALENDF TYPE PROBABILITY TABLES*)
*
 MIXS LIB: DRAGON FIL: DLIB_J2
 MIX <<COMB0101>> 300.0 (*COMB0101*)
    PU240    = Pu240 4.256E-4 CORR 1 IRSET PTSL 1
 MIXS LIB: DRAGON FIL: DLIB_J2
 MIX <<COMB0101>> 300.0 (*COMB0101*)
    U235     = U235  5.118E-5 1 IRSET PTSL 1
    U238     = U238  2.042E-2 CORR 1 IRSET PTSL 1
    PU238    = Pu238 2.714E-5 1 IRSET PTSL 1
    PU239    = Pu239 1.972E-3 1 IRSET PTSL 1
    PU241    = Pu241 3.577E-5 1 IRSET PTSL 1
    PU242    = Pu242 1.234E-5 1 IRSET PTSL 1
    AM241    = Am241 1.406E-5 1 IRSET PTSL 1
    O16      = O16   4.588E-2
 MIX <<GAIN1>> 300.0 (*GAIN1*)
    ZR      = Zr0  3.88E-2 2 IRSET PTSL 1
 MIX <<MODE>> 293.0 (*MODE*)
    H1      = H1_H2O 4.744E-2
    O16     = O16 2.372E-2
 ;
LIBRARY2 := USS: LIBRARY TRACK ::
            EDIT 1 TRAN PASS 4 GRMIN 45 ;
CP := ASM: LIBRARY2 TRACK :: PIJ ;
FLUX := FLU: CP LIBRARY2 TRACK ::
TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.259420 ;
LIBRARY LIBRARY2 CP FLUX := DELETE: LIBRARY LIBRARY2 CP FLUX ;
ECHO "test mox_1c_case3 completed" ;
END: ;
