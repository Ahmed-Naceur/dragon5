*--------------------------------------------------------------------- 
*  Electron Source on 1D Slab Geometry
*  CEPXSR library
*  Author: Ahmed Naceur
*---------------------------------------------------------------------

* --- Define STRUCTURES and MODULES used

MODULE       GEO: LIB: MAC: SNT: ASM: FLU: HEAT: OUT: DELETE: 
             UTL: END: ;
PROCEDURE    assertS ;
LINKED_LIST   MACRO DEPOS MICRO OLDMACRO GEOM TRACK SYSTEM FLUX 
              EEDIT ;
SEQ_ASCII edep.txt :: FILE './edep.txt' ;


* --- Geometry 

GEOM := GEO: :: CAR1D 2
           X- VOID  X+ VOID
           MIX  1 2
           MESHX 0.0 1.0 2.0
           SPLITX 30 70
           ;

* --- Tracking       

TRACK := SNT: GEOM ::
             TITLE 'Water-Slab:Cepxsr-Dragon'
             EDIT 2 MAXR 1000 DIAM 1 NLIVO SN 12 SCAT 12 EPSI 1.E-4
             QUAD 1 MAXI 300 BFPL ;

* --- Library access

MICRO := LIB: :: 
      EDIT 99
      NMIX 1 
      ANIS 8
      MIXS LIB: MATXS2 FIL: _matxs_O_cepxsr_80Ne_80Ng
      MIX  1  300.0  1.0 NOGAS
      Onat = o 1.0
;

* --- Library structuring 

MACRO := MICRO :: STEP UP MACROLIB ;
UTL: MACRO :: STEP UP GROUP STEP AT 1 DIR IMPR ESTOPW * ;
OLDMACRO := MACRO ;
MACRO := MAC: MACRO OLDMACRO :: MIX 2 1 OLDL ;
OLDMACRO := DELETE: OLDMACRO ;

* --- Specify fixed particle source density   

REAL qsour := 1.E10 2.0 / $Pi_R / ; ! [cm-3 s-1]
MACRO := MAC: MACRO ::
      READ INPUT
      MIX 1 FIXE
      <<qsour>> 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
      0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
      0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
      0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
      0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
      0.0 0.0 0.0 0.0 0.0
      ;


* ---- Electron flux

SYSTEM := ASM: MACRO TRACK :: EDIT 99 ARM ;
FLUX  := FLU: MACRO TRACK SYSTEM :: EDIT 0 TYPE S EXTE 100 ;

* --- Recover flux and reaction rates

EEDIT := OUT: FLUX TRACK MACRO GEOM :: EDIT 2 COND INTG NONE ;
    
* ---- Energy deposition

*DEPOS := HEAT: EEDIT :: EDIT 99 ;

*edep.txt := EEDIT ; 

EEDIT FLUX SYSTEM MACRO TRACK GEOM := DELETE: 
  EEDIT FLUX SYSTEM MACRO TRACK GEOM ; 

END: ;

QUIT "LIST" .


