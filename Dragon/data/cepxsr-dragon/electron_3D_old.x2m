*--------------------------------------------------------------------- 
*  Electron Source on 1D Slab Geometry
*  CEPXS-BFP library
*  Author: Ahmed Naceur, Sep.2021
*---------------------------------------------------------------------
*  Define STRUCTURES and MODULES used
*--------------------------------------
MODULE       GEO: FMAC: MAC: SNT: ASM: FLU: OUT: DELETE: END: ;
PROCEDURE    assertS ;
LINKED_LIST  MACRO MACRO2 GEOM TRACK SYSTEM FLUX EEDITS GWT0 ;
SEQ_ASCII    FMAC_M :: FILE './Al_1MeV_CSD_P7.txt' ;

*--------------------------------------
*  Set the GEOMETRY
*--------------------------------------
* lx,ly,lz  : number of subdivisions along x,y and z
* VOID      : bc-zero re-entrant flux
* MIX       : mixture by subdivisions 
* MESH      : spatial mesh along x (in cm)
* SPLIT     : mesh splitting 
--------------------------------------
GEOM := GEO: :: CAR3D 3 2 1
           X- VOID  X+ VOID
           Y- VOID  Y+ VOID
           Z- VOID  Z+ VOID
           MIX  1 2 2
                2 2 2
           MESHX 0.0 0.04 0.1 0.3
           MESHY 0.0 0.02 0.2
           MESHZ 0.0 0.2
           SPLITX 5 10 10
           SPLITY 5 10
           SPLITZ 10
           ;

*--------------------------------------
*  Track the GEOMETRY
*--------------------------------------
* MAXR  : 
* NLIVO : disable Livolant acceleration of scattering iteration
* SN    : angular approximation order of the flux
* SCAT  : limit of anistropy of the scattering source (=SN)
* EPSI  : convergence criterion of inner iterations (def. =1.0E-5)
* MAXI  : maximum number of inner iterations (def.=100)
* QUAD  : angular quadrature type
*          =1:  Lathrop-Carlson level-symmetric quadrature
*          =2:  optimized level-symmetric quadrature (def. 2D, 3D)
*          =3:  Snow-code level-symmetric quadrature
*          =4:  Legendre-Chebyshev quadrature
*          =5:  symmetric Legendre-Chebyshev quadrature
*          =6:  quadruple range (QR) quadrature
*          =10: product of Gauss-Legendre and Gauss- Chebyshev quadrature
* BFPL  : solution of the BFP with Przybylski and Ligou energy propagation factors
--------------------------------------              
TRACK := SNT: GEOM ::
      TITLE 'HOMOGENIZED Al_1MeV_CSD_P7 BENCHMARK WITH FMAC-M'
      EDIT 2 MAXR 13000 NLIVO SN 8 SCAT 8 EPSI 1.E-4 MAXI 300
      QUAD 1 BFPL ;
*----
*  Compute the electron flux
*----
MACRO := FMAC: FMAC_M ::
  EDIT 2
  PARTICLE B
;
MACRO2 := MACRO ;
MACRO := MAC: MACRO MACRO2 ::
     MIX 2 1 OLDL
     ;
MACRO2 := DELETE: MACRO2 ;
REAL qsour := 1.E10 2.0 / $Pi_R / ;
ECHO "qsour=" qsour ;
MACRO := MAC: MACRO ::
     READ INPUT
     MIX 1 FIXE
     <<qsour>> 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     ;
SYSTEM := ASM: MACRO TRACK :: EDIT 1 ARM ;
FLUX := FLU: MACRO TRACK SYSTEM :: EDIT 1 TYPE S EXTE 100 ;
EEDITS := OUT: FLUX TRACK MACRO GEOM ::
    EDIT 2 COND SOUR 0.3703703 INTG NONE ! 1/(\ro_{Al} g/cm^{3})
    ;
*----
*  Asserts
*----
GWT0 := EEDITS :: STEP UP GROUP STEP AT 1 ;
assertS GWT0 :: 'FLUX-INTG' 1 1.636638E-5 ;
assertS GWT0 :: 'FLUX-INTG' 200 8.830625E-10 ;
assertS GWT0 :: 'FLUX-INTG' 500 -1.251768E-9 ;
assertS GWT0 :: 'FLUX-INTG' 2000 -1.032750E-9 ;
*
ECHO "test TCM69 completed" ;
END: ;
