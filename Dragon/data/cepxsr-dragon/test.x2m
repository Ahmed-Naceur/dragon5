*--------------------------------------------------------------------- 
*  Electron Source on 1D Slab Geometry
*  CEPXS-BFP library
*  Author: Ahmed Naceur, Sep.2021
*---------------------------------------------------------------------
* --- Define STRUCTURES and MODULES used
*---------------------------------------------------------------------

MODULE       GEO: LIB: MAC: SNT: ASM: FLU: HEAT: OUT: DELETE: END: ;
PROCEDURE    assertS ;

LINKED_LIST   eMACRO eDEPOS MICRO OLDeMACRO GEOM eTRACK SYSTEM eFLUX 
              eEDITS1 ;
SEQ_ASCII filewater :: FILE './matxsWater.txt' ;             
SEQ_ASCII edep.txt  :: FILE './edep.txt' ;

*---------------------------------------------------------------------
* --- Set the GEOMETRY
*---------------------------------------------------------------------
* lx=2  : number of subdivisions along x
* VOID  : bc-zero re-entrant flux
* MIX   : mixture by subdivisions (=lx)
* MESHX : spatial mesh along x (in cm)
* SPLITX: mesh splitting 
*---------------------------------------------------------------------
GEOM := GEO: :: CAR1D 2
           X- VOID  X+ VOID
           MIX  1 2
           MESHX 0.0 0.01 2.0
           SPLITX 30 70
           ;

*---------------------------------------------------------------------
* --- TRACK the GEOMETRY
*---------------------------------------------------------------------
* MAXR  :
* DIAM  : spatial approximation order
*          =0: (constant) HODD
*          =1: (linear)   Disc. Galerkin
*          =2: (parabolic) 
*          =3: (cubic) Disc. Galerkin
* NLIVO : disable Livolant acceleration of scattering iteration
* SN    : angular approximation order of the flux
* SCAT  : limit of anistropy of the scattering source (=SN)
* EPSI  : convergence criterion of inner iterations (def. =1.0E-5)
* QUAD  : type of the angular quadrature (1,2,3,4,5,10)
* MAXI  : maximum number of inner iterations (def.=100)
* BFPL  : solution of the BFP with Przybylski and Ligou energy  
*         propagation factors
*---------------------------------------------------------------------          
eTRACK := SNT: GEOM ::
      TITLE 'Water-Slab:Cepxsr-Dragon'
      EDIT 2 MAXR 1000 DIAM 1 NLIVO SN 64 SCAT 64 EPSI 1.E-4
      QUAD 1 MAXI 300 BFPL ;
*-----------------------------------------------------------------------
* --- Library
*---------------------------------------------------------------------
* LIB: Access MATXS2 file generated by CEPXSR module
* MAC:
*   MACRO is the MACROLIB updated
*   MIX to be updated (2 is created, while 1 is the mixture in the old
*                      macrolib mixture)
*   OLDL says take cross-sections associated with 1 from OLDMACRO
*
* MAC: 
*   READ  to specify the input file format
*   INPUT to specify that mixture cross sections will be read on the input
*         stream
*   MIX   to specify that the macro. cross.sect. associated with a new 
*         mixture are to be read.
*      1: identifier for the next mixture to be read
*   FIXE  to specify fixed particle source density
*         <<qsour>> 0.0 ... 
*          multigroup array of fixed particle source densiy in [cm-3 s-1]
*---------------------------------------------------------------------
MICRO := LIB: :: 
      EDIT 99
      NMIX 2 
      ANIS 8
      MIXS LIB: MATXS2 FIL: filewater
      MIX  1  300.0  1.0 NOGAS
      Hnat = h 0.111894 
      Onat = o 0.888106
;

END: ;
eMACRO := MICRO :: STEP UP MACROLIB ;
OLDeMACRO := eMACRO ;
eMACRO := MAC: eMACRO OLDeMACRO :: MIX 2 1 OLDL ;
OLDeMACRO := DELETE: OLDeMACRO ;

REAL qsour := 1.E10 2.0 / $Pi_R / ;
ECHO "qsour=" qsour ;

eMACRO := MAC: eMACRO ::
     READ INPUT
     MIX 1 FIXE
     <<qsour>> 
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0
     ;

*---------------------------------------------------------------------
* ---- Electron flux
*---------------------------------------------------------------------
* ASM: assembly module for matrices required by the flux solution module
*   ARM: assembly calculation without building the full collision 
*        probability matrices
* FLU: solve the BFP equation
*   TYPE S: fixed source problem
*   EXTE:   control parameters for the external iteration
*      100: maximum number of external iterations
*---------------------------------------------------------------------

SYSTEM := ASM: eMACRO eTRACK :: EDIT 2 ARM ;
eFLUX  := FLU: eMACRO eTRACK SYSTEM :: EDIT 2 TYPE S EXTE 100 ;



eEDITS1 := OUT: eFLUX eTRACK eMACRO GEOM ::
    EDIT 2 INTG MIX ;
    
SYSTEM := DELETE: SYSTEM ; 

* ---- Energy deposition
* SOUR: nsumb number of source particles used for flux nomalization 

eDEPOS := HEAT: eEDITS1 :: EDIT 2 SOUR 1.00 ; ! 1/(\ro_{WATER} g/cm^{3})

edep.txt := eDEPOS ; 

END: ;

QUIT .

*eEDITS1 := OUT: eFLUX eTRACK eMACRO GEOM ::
*    EDIT 2 COND SOUR 1.00 INTG NONE ;
*
*GEDITS := OUT: GFLUX TRACK GMACRO GEOM ::
*    EDIT 2 INTG MIX
*    ;
*
*EEDITS := OUT: EFLUX ETRACK EMACRO GEOM ::
*    EDIT 2 COND INTG NONE
*    ;    

EOF

