*--------------------------------------------------------------------- 
*  Electron Source on 1D Slab Geometry
*  CEPXSR library
*  Author: Ahmed Naceur
*---------------------------------------------------------------------

* --- Define STRUCTURES and MODULES used

MODULE       GEO: LIB: MAC: SNT: ASM: FLU: HEAT: OUT: DELETE: 
             UTL: END: ;
PROCEDURE    assertS ;
LINKED_LIST   MACRO DEPOS MICRO OLDMACRO GEOM TRACK SYSTEM FLUX 
              EEDIT ;
SEQ_ASCII edep.txt :: FILE './edep.txt' ;


* --- Geometry 

GEOM := GEO: :: CAR3D 2 1 1
           EDIT 0
           X- VOID  X+ VOID
           Y- VOID  Y+ VOID
           Z- VOID  Z+ VOID
           MIX  1 2 
           MESHX 0.0 0.1 2.0
           MESHY 0.0 2.0
           MESHZ 0.0 2.0 
           SPLITX 10 90
           SPLITY 20
           SPLITZ 20 
           ;

* --- Tracking       

TRACK := SNT: GEOM ::
             TITLE 'Water-3DSlab:Cepxsr-Dragon'
             EDIT 1 MAXR 50000 DIAM 1 NLIVO SN 12 SCAT 12 EPSI 1.E-4
             QUAD 1 MAXI 300 BFPL ;

* --- Library access

MICRO := LIB: :: 
      EDIT 1
      NMIX 1 
      ANIS 8
      MIXS LIB: MATXS2 FIL: _cepxsr_80Ne_80Ng
      MIX  1  300.0  1.0 NOGAS
      Hnat = h 0.111894 
      Onat = o 0.888106
;

* --- Library structuring 

MACRO := MICRO :: STEP UP MACROLIB ;
*UTL: MACRO :: STEP UP GROUP STEP AT 1 DIR IMPR ESTOPW * ;
OLDMACRO := MACRO ;
MACRO := MAC: MACRO OLDMACRO :: MIX 2 1 OLDL ;
OLDMACRO := DELETE: OLDMACRO ;

* --- Specify fixed particle source density   

REAL qsour := 1.E10 2.0 / $Pi_R / ; ! [cm-3 s-1]
MACRO := MAC: MACRO ::
      READ INPUT
      MIX 1 FIXE
      <<qsour>> 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
      0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0
      ;


* ---- Electron flux

SYSTEM := ASM: MACRO TRACK :: EDIT 0 ARM ;
FLUX  := FLU: MACRO TRACK SYSTEM :: EDIT 1 TYPE S EXTE 200 ;

* --- Recover flux and reaction rates

EEDIT := OUT: FLUX TRACK MACRO GEOM :: EDIT 2 COND INTG NONE ;
    
* ---- Energy deposition

*DEPOS := HEAT: EEDIT :: EDIT 99 ;

*edep.txt := EEDIT ; 

EEDIT FLUX SYSTEM MACRO TRACK GEOM := DELETE: 
  EEDIT FLUX SYSTEM MACRO TRACK GEOM ; 

END: ;

QUIT "LIST" .


