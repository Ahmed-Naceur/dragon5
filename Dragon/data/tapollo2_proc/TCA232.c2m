*----
*
*  TEST CASE TCA232
*  FIRST MOX ROWLAND'S BENCHMARK
*  DISTRIBUTED SELF-SHIELDING
*  99-GROUP APOLIB-2
*
*  Author: A. Hebert
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST LIBRARY LIBRARY2 C3 TRACK1 TRACK2 MACRO SYS FLUX ;
MODULE GEO: SYBILT: USS: LIB: ASM: FLU: UTL: END: ;
INTEGER COMB0101 COMB0102 COMB0103 COMB0104 COMB0105 COMB0106 GAIN1
        MODE1 MODE2 := 
        1 2 3 4 5 6 7 8 9 ; (* DISTRIBUTED SELF-SHIELDING *)
REAL RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 ;
PROCEDURE assertS ;
*
LIBRARY := LIB: ::
 EDIT 3
 NMIX 9    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 SUBG      (*COMPUTE SUBGROUPS*)
*
 MIXS LIB: APLIB2 FIL: CEA93V4
 MIX <<COMB0101>> 500.0 (*COMB0101*)
    U235     = U235_4  5.105E-5 1 SHIB U235SS_4  IRSET 0.0 44
    U238     = U238_4  2.037E-2 1 SHIB U238SS_3  IRSET 0.0 44
    PU238    = PU238_4 4.669E-5 1 SHIB PU238SS_4 IRSET 0.0 44
    PU239    = PU239_4 1.465E-3 1 SHIB PU239SS_4 IRSET 0.0 44
    PU240    = PU240_4 5.691E-4 SHIB PU240SS_4
    PU241    = PU241_4 2.713E-4 1 SHIB PU241SS_3 IRSET 0.0 44
    PU242    = PU242_3 1.413E-4 1 SHIB PU242SS_3 IRSET 0.0 44
    AM241    = AM241_1 3.028E-5 1 SHIB AM241SS_1 IRSET 0.0 44
    O16      = O16_6   4.588E-2
 MIX <<COMB0102>> COMB <<COMB0101>> 1.0 (*COMB0102*)
 MIX <<COMB0103>> COMB <<COMB0101>> 1.0 (*COMB0103*)
 MIX <<COMB0104>> COMB <<COMB0101>> 1.0 (*COMB0104*)
 MIX <<COMB0105>> COMB <<COMB0101>> 1.0 (*COMB0105*)
 MIX <<COMB0106>> COMB <<COMB0101>> 1.0 (*COMB0106*)
 MIX <<GAIN1>> 300.0 (*GAIN1*)
    ZR      = ZR_4  3.88E-2 2 SHIB ZRSS_4 IRSET 0.0 44
 MIX <<MODE1>> 300.0 (*MODE1*)
    H2O     = H2O_3_P5 2.372E-2
 MIX <<MODE2>> 300.0 (*MODE2*)
    H2O     = H2O_3_P5 2.372E-2
 ;
EVALUATE RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 := 0.259307 
0.343031 0.366715 0.388960 0.3996186 0.41 ;
C3 := GEO: :: TUBE 9
      R+ REFL
      RADIUS 0.0 <<RCOMB1>> <<RCOMB2>> <<RCOMB3>> <<RCOMB4>>
                 <<RCOMB5>> <<RCOMB6>> 0.475 0.6045552 0.710879
      MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
          <<COMB0105>> <<COMB0106>> <<GAIN1>> <<MODE1>> <<MODE2>>
          ;
TRACK2 := SYBILT: C3 ::
     EDIT 1
     MAXR 20
     QUA1 5 TITLE 'FIRST MOX ROWLAND S BENCHMARK' ;
LIBRARY2 := USS: LIBRARY TRACK2 :: EDIT 1 TRAN PASS 2 ;
SYS := ASM: LIBRARY2 TRACK2 :: PIJ ;
FLUX := FLU: SYS LIBRARY2 TRACK2 :: TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.194064 ;
ECHO "test TCA232 completed" ;
END: ;
