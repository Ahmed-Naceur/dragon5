*----
*
*  TEST CASE TCA233
*  9 X 9 PWR ASSEMBLY WITH NXT
*  99-GROUP APOLIB-2
*
*  Author: A. Hebert
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST LIBRARY ASSMB TRACK SYS FLUX EDIT SPHGEOM MTRACK ;
MODULE GEO: NXT: BIVACT: MAC: LIB: ASM: FLU: EDI: SPH: DELETE:
       END: ;
SEQ_BINARY TRACK_FIL ;
PROCEDURE assertS ;
*
LIBRARY := LIB: ::
     EDIT 2
     ANIS 2
     NMIX 3    (*MAXIMUM OF 3 MATERIAL MIXTURES*)
*
 MIXS LIB: APLIB2 FIL: CEA93V4
 MIX 1 300.16
  H2O      = H2O_3_P5 2.3934E-02
 MIX 2 579.9
  U238     = U238_4 2.2089E-02 1 SHIB U238SS_3 IRSET 0.0 38
  U235     = U235_4 8.6623E-04 1 SHIB U235SS_4 IRSET 0.0 38
  O16      = O16_6  4.5910E-02
 MIX 3 300.16
  AL27     = AL27_4 3.9222E-02
     ;
ASSMB := GEO: :: CAR2D 5 5 (*ASSEMBLAGE 9 X 9*)
          X- DIAG X+ REFL
          Y- SYME Y+ DIAG
          CELL C1 C3 C2 C3 C4
                  C3 C3 C3 C4
                     C2 C3 C4
                        C3 C4
                           C5
*          MERGE 1  2  3  4  5
*                   6  7  8  9
*                     10 11  9
*                        12  9
*                           13
          ::: C1 := GEO: CARCEL 1 (*CELLULE SANS COMBUSTIBLE*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 4.12660E-01
                MIX 1 1
          ;
          ::: C2 := GEO: CARCEL 3 (*CELLULE EMPOISONNEE*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 3.25296E-01 4.60039E-01 5.63430E-01
                MIX 3 3 3 1
          ;
          ::: C3 := GEO: CARCEL 1 (*CELLULE NORMALE*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 4.12660E-01
                MIX 2 1
          ;
          ::: C4 := GEO: C3 (*CELLULE PERIPHERIQUE*)
                MESHX 0.0 1.322082
                MIX 2 1
          ;
          ::: C5 := GEO: C4 (*CELLULE DE COIN*)
                MESHY 0.0 1.322082
                MIX 2 1
          ;
          ;
TRACK TRACK_FIL := NXT: ASSMB ::
     EDIT 1
     TITLE 'MULTICELL 9 X 9 PWR BENCHMARK WITH POISON'
     TISO 12 12.0 ;
*
SYS := ASM: LIBRARY TRACK TRACK_FIL :: PIJ PNOR HELI ;
*
FLUX := FLU: LIBRARY TRACK SYS ::
  EDIT 2 TYPE K B0 SIGS BUCK 0.0 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.065848 ;
*
*---
* MACRO-GEOMETRY TRACKING IN BIVAC
*---
SPHGEOM := GEO: :: CAR2D 5 5 (*ASSEMBLAGE 9 X 9*)
        X- DIAG X+ REFL
        Y- SYME Y+ DIAG
        MESHX 0.0 1.26208198 2.52416396 3.78624582 5.04832792 6.37040997
        MESHY 0.0 1.26208198 2.52416396 3.78624582 5.04832792 6.37040997
        MIX 1  2  3  4  5
               6  7  8  9
                 10 11 12
                    13 14
                       15
;
MTRACK := BIVACT: SPHGEOM :: DUAL (*IELEM=*) 2 (*ICOL=*) 2 ;

EDIT := EDI: FLUX LIBRARY TRACK ASSMB :: EDIT 3
*    EDITION FOR HETEROGENEOUS DIFFUSION CALCULATIONS
*    CELL-BY-CELL HOMOGENIZATION OF THE ASSEMBLY
     EDIT 3 SAVE COND 74 99 MERG CELL
     ;
EDIT := SPH: EDIT MTRACK ;
*
TRACK TRACK_FIL SYS FLUX EDIT := DELETE: TRACK TRACK_FIL SYS FLUX EDIT ;
*---
* IN-LINE TRACKING WITH NXT
*---
TRACK := NXT: ASSMB ::
     EDIT 1
     TITLE 'MULTICELL 9 X 9 PWR BENCHMARK WITH POISON'
     TISO 12 12.0 ;
*
SYS := ASM: LIBRARY TRACK :: PIJ PNOR HELI ;
*
FLUX := FLU: LIBRARY TRACK SYS ::
  EDIT 2 TYPE K B0 SIGS BUCK 0.0 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.065848 ;
*
EDIT := EDI: FLUX LIBRARY TRACK ASSMB :: EDIT 3
*    EDITION FOR HETEROGENEOUS DIFFUSION CALCULATIONS
*    CELL-BY-CELL HOMOGENIZATION OF THE ASSEMBLY
     EDIT 3 SAVE COND 74 99 MERG CELL
     ;
EDIT := SPH: EDIT MTRACK ;
SPHGEOM MTRACK := DELETE: SPHGEOM MTRACK ;

ECHO "test TCA233 completed" ;
END: ;
QUIT "LIST" .
