*---
* Regression test 3 for MCCG: and MOCC:
*---
*  TEST CASE TCA241
*  9 x 9 PWR SIMPLIFIED ASSEMBLY WITH POISON
*  CEA93V4 99 GROUPS LIBRARY FILE CEA93V4
*---

*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST LIBRARY ASSMB TRACK SYS FLUX EDIT ;
SEQ_BINARY TRACK_FIL ;
MODULE GEO: EXCELT: MCCGT: MAC: LIB: ASM: FLU: EDI: END: DELETE: ;
PROCEDURE assertS ;
*----
*  Microscopic cross sections
*----
LIBRARY := LIB: ::
  EDIT 0 ANIS 2 NMIX 3 
  MIXS LIB: APLIB2 FIL: CEA93V4
  MIX 1 300.16
    H2O      = H2O_3_P5 2.3934E-02
  MIX 2 579.9
    U238     = U238_4 2.2089E-02 1 SHIB U238SS_3 IRSET 0.0 38
    U235     = U235_4 8.6623E-04 1 SHIB U235SS_4 IRSET 0.0 38
    O16      = O16_6  4.5910E-02
  MIX 3 300.16
    AL27     = AL27_4 3.9222E-02
     ;
*----
* Geometry ASSMB : a 9 X 9 PWR ASSEMBLY
*  contains  C1 : cell without fuel
*            C2 : poisoned cell
*            C3 : normal fuel cell
*            C4 : peripheral cell
*            C5 : corner cell
*----
ASSMB := GEO: :: CAR2D 5 5
  X- DIAG X+ REFL Y- SYME Y+ DIAG
  CELL C1 C3 C2 C3 C4
          C3 C3 C3 C4
             C2 C3 C4
                C3 C4
                   C5
          ::: C1 := GEO: CARCEL 1
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 4.12660E-01
                SPLITR 4
                MIX 1 1
          ;
          ::: C2 := GEO: CARCEL 3
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 3.25296E-01 4.60039E-01 5.63430E-01
                MIX 3 3 3 1
          ;
          ::: C3 := GEO: C1
                MIX 2 1
          ;
          ::: C4 := GEO: C3
                MESHX 0.0 1.322082
          ;
          ::: C5 := GEO: C4
                MESHY 0.0 1.322082
          ;
  ;
*----
*  MCCG (isotropic BC)
*----
*----
*  Tracking calculation
*----
TRACK TRACK_FIL := EXCELT: ASSMB ::
   EDIT 1 MAXR 100
   TITLE 'MULTICELL 9 X 9 PWR BENCHMARK WITH POISON'
   ANIS 2 TRAK TISO 12 20.0
;
*---
*  Multigroup Flux Calculation
*  Edition
*---
TRACK := MCCGT: TRACK TRACK_FIL ASSMB ::
  EDIT 1 CACB 4
  AAC 80 TMT EPSI 1E-5 STIS 1
  MAXI 1 KRYL 0 SCR 0 HDD 100000.0 ;
SYS := ASM: LIBRARY TRACK TRACK_FIL ::
  ARM EDIT 1 ;
FLUX := FLU: LIBRARY TRACK SYS TRACK_FIL ::
  EDIT 1 TYPE K THER 5 5E-5 EXTE 100 1E-5 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.050948 ;
EDIT := EDI: FLUX LIBRARY TRACK ASSMB ::
  EDIT 2 COND 14 27 52 75 89 99
  MERGE MIX 1 2 3  ;
*---
* Memory Release
*---
TRACK TRACK_FIL SYS FLUX EDIT := DELETE: 
TRACK TRACK_FIL SYS FLUX EDIT ;
*----
*  MOCC (cyclic tracking)
*----
*----
*  Tracking calculation
*----
TRACK TRACK_FIL := EXCELT: ASSMB ::
   EDIT 1 MAXR 100
   TITLE 'MULTICELL 9 X 9 PWR BENCHMARK WITH POISON'
   ANIS 2 TRAK TSPC 7 20.0
;
*---
*  Multigroup Flux Calculation
*  Edition
*---
TRACK := MCCGT: TRACK TRACK_FIL ASSMB ::
  EDIT 1 CACB 4
  AAC 80 TMT EPSI 1E-5
  MAXI 1 KRYL 0 SCR 0 HDD 0.0 ;
SYS := ASM: LIBRARY TRACK TRACK_FIL ::
  ARM EDIT 2 ;
FLUX := FLU: LIBRARY TRACK SYS TRACK_FIL ::
  EDIT 1 TYPE K THER 5 5E-5 EXTE 100 1E-5 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.071877 ;
EDIT := EDI: FLUX LIBRARY TRACK ASSMB ::
  EDIT 2 COND 14 27 52 75 89 99
  MERGE MIX 1 2 3  ;
*---
* Memory Release
*---
TRACK TRACK_FIL LIBRARY ASSMB SYS FLUX EDIT := DELETE: 
TRACK TRACK_FIL LIBRARY ASSMB SYS FLUX EDIT ;
ECHO "test TCA241 completed" ;
END: ;
QUIT "LIST" .
