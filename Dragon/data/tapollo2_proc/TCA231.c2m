*----
*
*  TEST CASE TCA231
*  SIMPLE 9 X 9 PWR ASSEMBLY WITH SYBIL
*  CEA93V4 LIBRARY APOLLIB-2 FORMAT
*  MULTI-PARAMETER COMPO WITH INTERPOLATION
*
*  Author: A. Hebert
*
*----
*  Define STRUCTURES and MODULES used
*----
REAL POW := 3.016E17 ; (*flux normalization factor in Mev/(s*cm) *)
REAL evobeg evoend VOL_ASS NORM_FCT1 NORM_FCT2 COTE LAME B2 ;
INTEGER istep := 1 ;
REAL step2 step3 step4 step5 step6 := 9.375 18.75 37.5 75.0 500.0 ;
REAL BoronCont := 600.0E-6 ;
MODULE GEO: LIB: SYBILT: ASM: FLU: SHI: UTL: EVO: EDI:
       DELETE: COMPO: BIVACT: GREP: SPH: END: ;
LINKED_LIST LIBRARY ASSMB TRACK MACRO SYS FLUX EDIT BURNUP CPO
            CPONEW MACRO2 SPHGEOM MTRACK ;
INTEGER WATER FUEL1 FUEL2 CLAD := 1 2 3 4 ;
PROCEDURE assertS ;
*
LIBRARY := LIB: ::
 EDIT 2
 DEPL LIB: APLIB2 FIL: CEA93V4 CHAIN
 U234     FROM N2N      1.0000E+00 U235
 U235     FROM NG       1.0000E+00 U234
 U236     FROM NG       1.0000E+00 U235
 U238
 NP237    FROM NG       1.0000E+00 U236
 PU238    FROM NG       1.0000E+00 NP237    DECAY    1.0000E+00 CM242
 PU239    FROM NG       1.0000E+00 PU238
               DECAY    1.0000E+00 CM243    NG       1.0000E+00 U238
 PU240    FROM NG       1.0000E+00 PU239    DECAY    1.0000E+00 CM244
 PU241    FROM NG       1.0000E+00 PU240
 PU242    FROM NG       1.0000E+00 PU241    NG       1.4160E-01 AM241
 AM241    FROM DECAY    1.0000E+00 PU241
 AM242M   FROM NG       1.1500E-01 AM241
 AM243    FROM NG       1.0000E+00 PU242
 CM242    FROM NG       7.4340E-01 AM241
 CM243    FROM NG       1.0000E+00 CM242
 CM244    FROM NG       1.0000E+00 CM243    NG       1.0000E+00 AM243

 I135PF
 XE135PF  FROM DECAY    1.0000E+00 I135PF
 ND143PF
 ND144PF  FROM NG       1.0000E+00 ND143PF
 ND145PF  FROM NG       1.0000E+00 ND144PF
 ND146PF  FROM NG       1.0000E+00 ND145PF
 ND147PF  FROM NG       1.0000E+00 ND146PF
 ND148PF  FROM NG       1.0000E+00 ND147PF
 PM147PF  FROM DECAY    1.0000E+00 ND147PF
 PM148PF  FROM NG       5.3000E-01 PM147PF
 PM148MPF FROM NG       4.7000E-01 PM147PF
 PM149PF  FROM NG       1.0000E+00 PM148PF  NG       1.0000E+00 PM148MPF
 SM149PF  FROM DECAY    1.0000E+00 PM149PF
 SM150PF  FROM NG       1.0000E+00 SM149PF
 SM151PF  FROM NG       1.0000E+00 SM150PF
 SM152PF  FROM NG       1.0000E+00 SM151PF
 EU153PF  FROM NG       1.0000E+00 SM152PF
 EU154PF  FROM NG       1.0000E+00 EU153PF
 EU155PF  FROM NG       1.0000E+00 EU154PF
 MO95PF TC99PF RH103PF RH105PF
 AG109PF XE131PF CS133PF
 PSU5U PSU8U PSP9U PSP0U PSP1U PSP2U
 ENDCHAIN
*
 ANIS 2
 NMIX 4    (*MAXIMUM OF 4 MATERIAL MIXTURES*)
 CTRA APOL
*
 MIXS LIB: APLIB2 FIL: CEA93V4
 MIX <<WATER>> 300.16
  H2O      = H2O_3_P5 2.3934E-02
 MIX <<FUEL1>> 579.9
  U238     = U238_4 2.2089E-02 1 SHIB U238SS_3 IRSET 0.0 38
  U235     = U235_4 8.6623E-04 1 SHIB U235SS_4 IRSET 0.0 38
  PU239    = PU239_4 0.0 1 SHIB PU239SS_4 IRSET 0.0 38
  PU240    = PU240_4 0.0 1 SHIB PU240SS_4 IRSET 0.0 38
  O16      = O16_6  4.5910E-02
 MIX <<FUEL2>> COMB <<FUEL1>> 1.0
 MIX <<CLAD>> 300.16
  AL27     = AL27_4 3.9222E-02
     ;
ASSMB := GEO: :: CAR2D 5 5 (*9 X 9 ASSEMBLY*)
          X- DIAG X+ REFL
          Y- SYME Y+ DIAG
          CELL C1 C3 C2 C3 C4
                  C3 C3 C3 C4
                     C2 C3 C4
                        C3 C4
                           C5
          MERGE 1  2  3  2  6
                   2  2  4  6
                      5  4  6
                         4  6
                            7
          ::: C1 := GEO: CARCEL 2 (*WATER CELL - NO FUEL*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 3.0E-01 4.1266E-01
                MIX <<WATER>> <<WATER>> <<WATER>>
          ;
          ::: C2 := GEO: CARCEL 3 (*POISON CELL*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 3.25296E-01 4.60039E-01 5.63430E-01
                MIX <<CLAD>> <<CLAD>> <<CLAD>> <<WATER>>
          ;
          ::: C3 := GEO: C1 (*ORDINARY CELL*)
                MIX <<FUEL1>> <<FUEL2>> <<WATER>>
          ;
          ::: C4 := GEO: C3 (*SURFACE CELL*)
                MESHX 0.0 1.322082
                MIX <<FUEL1>> <<FUEL2>> <<WATER>>
          ;
          ::: C5 := GEO: C4 (*CORNER CELL*)
                MESHY 0.0 1.322082
                MIX <<FUEL1>> <<FUEL2>> <<WATER>>
          ;
          ;
TRACK := SYBILT: ASSMB ::
     EDIT 1
     MAXR 40  (*MAXIMUM OF 40 REGIONS*)
     TITLE 'MULTICELL 9 X 9 PWR BENCHMARK WITH POISON'
     ;
*
CPO := COMPO: ::
     EDIT 5
     COMM  'First line of comment'
           'Second line of comment'
     ENDC
     PARA  'BCON' VALU REAL
     PARA  'FTMP' TEMP LIBRARY <<FUEL1>>
     PARA  'WTMP' TEMP LIBRARY <<WATER>>
     PARA  'BURN' IRRA
     PARA  'FLUB' FLUB
     PARA  'PUIS' POWR
     PARA  'XE1'  CONC XE135PF LIBRARY <<FUEL1>>
     PARA  'XE2'  CONC XE135PF LIBRARY <<FUEL2>>

     LOCA  'burn' IRRA
     LOCA  'flug' FLUG
     LOCA  'mass' MASL
     LOCA  'xe'   CONC XE135PF
     LOCA  'mtmp' TEMP
     
     INIT
;
*
LIBRARY := SHI: LIBRARY TRACK :: EDIT 2 LJ ;
*
SYS := ASM: LIBRARY TRACK :: PIJ ECCO ;
FLUX := FLU: TRACK LIBRARY SYS :: TYPE B B1 ECCO ;
*
EVALUATE COTE := 1.262082 ;
EVALUATE LAME := 1.322082 ;
EVALUATE VOL_ASS := COTE 7.0 * LAME 2.0 * + ;
EVALUATE VOL_ASS := VOL_ASS VOL_ASS * ;
EVALUATE NORM_FCT1 := POW 1.60207E-13 * VOL_ASS / ;

EVALUATE NORM_FCT2 := NORM_FCT1 2.651005 / 1.00115 / ;
PRINT 'assembly volume=' VOL_ASS 'cm**3  in-fuel power=' NORM_FCT2
      'MW/tonne=' ;
PRINT 'normalization power=' NORM_FCT1 'W/CC' ;
EVALUATE evoend := 0.0 ;
WHILE evoend step2 NORM_FCT2 / < DO

  EVALUATE evobeg := evoend ;
  EVALUATE evoend := step2 NORM_FCT2 / ;
  PRINT 'Burnup step' istep 'between' evobeg 'and' evoend 'day:' ;
  IF istep 1 = THEN
    BURNUP LIBRARY := EVO: LIBRARY FLUX TRACK :: EDIT 3 RUNG
         DEPL <<evobeg>> <<evoend>> DAY W/CC <<NORM_FCT1>>
         EPS2 100.0 EXPM 1.0 RUNG SAT NODI ;
  ELSE
    BURNUP LIBRARY := EVO: BURNUP LIBRARY FLUX TRACK :: EDIT 2
         DEPL <<evobeg>> <<evoend>> DAY W/CC <<NORM_FCT1>>
         EPS2 100.0 EXPM 1.0E15 RUNG EXTR NSAT NODI ;
  ENDIF ;

  PRINT 'Self-shielding calculation' istep 'at' evoend 'DAY:' ;
  LIBRARY := SHI: LIBRARY TRACK :: EDIT 2 LJ ;
  SYS := DELETE: SYS ;
  SYS := ASM: LIBRARY TRACK :: PIJ ECCO ;
  FLUX := FLU: FLUX TRACK LIBRARY SYS :: TYPE B B1 ECCO ;
  IF istep 3 = THEN
     GREP: FLUX :: GETVAL "B2  B1HOM" 1 >>B2<< ;
  ENDIF ;
  PRINT 'step2=' step2 'evoend=' evoend ;

  PRINT 'Edition at' evoend 'DAY  burnup=' 
          step2 'MWday/tonne=' ;

  EDIT := EDI: FLUX LIBRARY TRACK ASSMB :: EDIT 1
*    EDITION FOR HETEROGENEOUS DIFFUSION CALCULATIONS
*    CELL-BY-CELL HOMOGENIZATION OF THE ASSEMBLY
     SAVE COND 74 99 MERG CELL
     MICR 9 U235 U238 PU239 PU240 PU241 PU242 AM241 AM242M XE135PF
     SAVE ON 'EDITCDAT   1'
     ;
  SPHGEOM := EDIT :: STEP UP 'MACRO-GEOM' ;
  MTRACK := BIVACT: SPHGEOM :: DUAL (*IELEM=*) 1 (*ICOL=*) 2 ;
  EDIT := SPH: EDIT MTRACK ;
  SPHGEOM MTRACK := DELETE: SPHGEOM MTRACK ;
*
  BURNUP LIBRARY := EVO: BURNUP LIBRARY FLUX TRACK :: EDIT 2
                       SAVE <<evoend>> DAY W/CC <<NORM_FCT1>> ;

  CPO := COMPO: CPO EDIT BURNUP FLUX LIBRARY ::
      EDIT 3
      SET <<evoend>> DAY
      BCON <<BoronCont>>
      ;
  EDIT := DELETE: EDIT ;

  EVALUATE step2 step3 step4 step5 step6 := step3 step4 step5 step6
  step2 ;

  EVALUATE istep := istep 1 + ;

ENDWHILE ;
assertS FLUX :: K-INFINITY 1 1.346755 ;
*
* CONCATENATION
*
CPONEW := COMPO: ::
     EDIT 5
     COMM  'First line of comment(new)'
           'Second line of comment'
     ENDC
     PARA  'BCON' VALU REAL
     PARA  'FTMP' TEMP LIBRARY <<FUEL1>>
     PARA  'WTMP' TEMP LIBRARY <<WATER>>
     PARA  'BURN' IRRA
     PARA  'FLUB' FLUB
     PARA  'PUIS' POWR
     PARA  'CELL' VALU CHAR
     PARA  'XE1'  CONC XE135PF LIBRARY <<FUEL1>>
     PARA  'XE2'  CONC XE135PF LIBRARY <<FUEL2>>

     LOCA  'burn' IRRA
     LOCA  'flug' FLUG
     LOCA  'mass' MASL
     LOCA  'xe'   CONC XE135PF
     LOCA  'mtmp' TEMP

     INIT
;

CPONEW := COMPO: CPONEW CPO :: CELL '2D' ;
CPO := DELETE: CPO ;
*
* MULTICOMPO ACCESS
MACRO2 := SPH: CPONEW ::
   EDIT 1
   STEP UP default STEP AT 3
   MACRO LEAK <<B2>> OFF
   ;
UTL: MACRO2 :: DIR ;
*
* VERIFICATION CALCULATION AT THIRD BURNUP
ASSMB TRACK SYS FLUX := DELETE: ASSMB TRACK SYS FLUX ;
ASSMB := CPONEW :: STEP UP default STEP UP 'GEOMETRIES' STEP AT 1 ;
TRACK := BIVACT: ASSMB :: DUAL (*IELEM=*) 1 (*ICOL=*) 2 ;
SYS := ASM: MACRO2 TRACK :: ARM ;
FLUX := FLU: MACRO2 TRACK SYS :: TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.0 ;
ECHO "test TCA231 completed" ;
*
END: ;
