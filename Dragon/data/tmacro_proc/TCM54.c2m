*----
*  TEST CASE TCM54
*  HEXAGONAL CASES IN EXCELT
*  ONE-GROUP MACROLIB-DEFINED CROSS SECTIONS
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST ASSMBH TRACK MACRO FLUX SYS EDIT MACRO2 ASSMB2
            TRACK2 ;
MODULE GEO: EXCELT: BIVACT: MAC: ASM: FLU: EDI: SPH: DELETE:
       END: ;
SEQ_BINARY TRK_FIL ;
PROCEDURE assertS ;
*
MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 4 NIFI 1
     READ INPUT
     MIX 1 (*VIDE*)
       TOTAL 0.0 SCAT 1 1 0.0
     MIX 2 (*MIDI*)
       TOTAL 1.0 SCAT 1 1 0.9
     MIX 3 (*CREU*)
       TOTAL 1.0E-4 SCAT 1 1 0.0
     MIX 4 (*MIFI*)
       TOTAL 1.0 SCAT 1 1 0.8
       NUSIGF 1.1 CHI 1.0
;
*
ASSMBH := GEO: :: HEX 1
          EDIT 2
          HBC COMPLETE REFL
          MIX HEXFGM
          ::: HEXFGM := GEO: HEXCEL 1
               SIDE 1.12045
               RADIUS 0.0 0.4
               MIX 4
                   2
          ;
          ;
TRACK TRK_FIL := EXCELT: ASSMBH ::
       TITLE 'SIMPLE HEXAGONAL CASE'
       MAXR 2000
       EDIT 2 ANIS 2 GAUS 4 TRAK TISO 12 10.0 ;
*
SYS := ASM: MACRO TRACK TRK_FIL :: EDIT 1 PIJ ;
FLUX := FLU: SYS MACRO TRACK :: TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.779603 ;
*
ASSMBH TRACK TRK_FIL SYS FLUX := DELETE: ASSMBH TRACK TRK_FIL SYS FLUX ;
ASSMBH := GEO: :: HEX 1
          EDIT 2
          HBC COMPLETE REFL
          MIX HEXFGM
          ::: HEXFGM := GEO: HEXCEL 1
               SIDE 1.12045
               RADIUS 0.0 0.4
               SECT -1
               MIX 4 4 4 4 4 4
                   2 2 2 2 2 2
          ;
          ;
TRACK TRK_FIL := EXCELT: ASSMBH ::
       TITLE 'SIMPLE HEXAGONAL CASE'
       MAXR 2000
       EDIT 2 ANIS 2 GAUS 4 TRAK TISO 12 10.0 ;
SYS := ASM: MACRO TRACK TRK_FIL :: EDIT 1 PIJ ;
FLUX := FLU: SYS MACRO TRACK :: TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.791653 ;
*
ASSMBH TRACK TRK_FIL SYS FLUX := DELETE: ASSMBH TRACK TRK_FIL SYS FLUX ;
ASSMBH := GEO: :: HEX 7
          EDIT 2
          HBC COMPLETE REFL
          MIX HEXVGV HEXVGV HEXFGM HEXCREU HEXCREU HEXVGV HEXNOIR
          MERGE  1  2  3  4  4  5  6
          ::: HEXFGM := GEO: HEXCEL 2
               SIDE 1.12045
               RADIUS 0.0 0.2 1.01946
               SECT -1
               MIX 4 4 4 4 4 4
                   2 2 2 2 2 2
                   2 2 2 2 2 2
          ;
          ::: HEXVGV := GEO: HEXCEL 2
               SIDE 1.12045
               RADIUS 0.0 0.2 0.4
               SECT -1
               MIX 1 1 1 1 1 1
                   2 2 2 2 2 2
                   2 2 2 2 2 2
          ;
          ::: HEXCREU := GEO: HEXCEL 0
               SIDE 1.12045
               MIX 3
          ;
          ::: HEXNOIR := GEO: HEXCEL 1
               SIDE 1.12045
               RADIUS 0.0 0.4
               SECT -1
               MIX 4 4 4 4 4 4
                   2 2 2 2 2 2
          ;
          ;
TRACK TRK_FIL := EXCELT: ASSMBH ::
       TITLE 'SIMPLE HEXAGONAL CASE'
       MAXR 2000
       EDIT 2 ANIS 2 GAUS 4 TRAK TISO 12 10.0 ;
*
SYS := ASM: MACRO TRACK TRK_FIL :: EDIT 1 PIJ ;
FLUX := FLU: SYS MACRO TRACK :: TYPE K B0 SIGS BUCK 0.0 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.000711 ;
*----
* TRANSPORT-DIFFUSION EQUIVALENCE
*----
*    EDITION FOR HETEROGENEOUS DIFFUSION CALCULATIONS
*    CELL-BY-CELL SPH HOMOGENIZATION OF THE ASSEMBLY
EDIT := EDI: FLUX MACRO TRACK ASSMBH ::
     EDIT 3 SAVE MERG CELL ;
ASSMB2 := EDIT :: STEP UP 'MACRO-GEOM' ;
ASSMB2 := GEO: ASSMB2 :: SPLITL 2 ;
TRACK2 := BIVACT: ASSMB2 :: MAXR 100 DUAL (*IELEM=*) 2 (*ICOL=*) 2 ;
EDIT := SPH: EDIT TRACK2 :: ITER 1.0E-5 ;
ASSMB2 TRACK2 := DELETE: ASSMB2 TRACK2 ;
MACRO2 := EDIT :: STEP UP 'REF-CASE0001' STEP UP MACROLIB
                  STEP UP GROUP STEP AT 1 ;
assertS MACRO2 :: 'NSPH' 3 0.8897515 ;
*
ECHO "test TCM54 completed" ;
END: ;
QUIT "LIST" .
