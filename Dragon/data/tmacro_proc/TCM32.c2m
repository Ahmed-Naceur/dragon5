*----
*  TEST CASE TCM32
*  9 X 9 PWR CASE WITH SYBIL AND MCCG SPH EQUIVALENCE
*  MACROLIB-DEFINED CROSS SECTIONS
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST ASSMB TRACK MACRO SYS FLUX FLUX2 EDIT ASSMB2 TRACK2 MACRO2 ;
MODULE GEO: SYBILT: MAC: ASM: FLU: EDI: BIVACT: NXT: MCCGT: SNT: SPH:
       DELETE: END: ;
SEQ_BINARY TRK_FIL2 ;
PROCEDURE assertS ;
*
MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 3 NIFI 1 ANIS 2
     READ INPUT
     MIX 1 (*EAU*)
        TOTAL 0.3683
        SCAT 1 1 0.3661 1 1 0.1212
     MIX 2 (*COMBUSTIBLE*)
        TOTAL 0.36522
        SCAT 1 1 0.3234 1 1 0.1100
        NUSIGF 0.09564      CHI 1.0
     MIX 3 (*ARGENT*)
        TOTAL 0.8453
        SCAT 1 1 0.5216 1 1 0.4212
     ;
ASSMB := GEO: :: CAR2D 5 5 (*ASSEMBLAGE 9 X 9*)
          X- DIAG X+ REFL
          Y- REFL Y+ DIAG
          CELL C1 C3 C2 C3 C4
                  C3 C3 C3 C4
                     C2 C3 C4
                        C3 C4
                           C5
          MERGE 1  2  3  4  5
                   6  7  8  9
                     10 11  9
                        12  9
                           13
          ::: C1 := GEO: CARCEL 1 (*CELLULE SANS COMBUSTIBLE*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 4.12660E-01
                MIX 1 1
          ;
          ::: C2 := GEO: CARCEL 3 (*CELLULE EMPOISONNEE*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 3.25296E-01 4.60039E-01 5.63430E-01
                MIX 3 3 3 1
          ;
          ::: C3 := GEO: CARCEL 1 (*CELLULE NORMALE*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 4.12660E-01
                MIX 2 1
          ;
          ::: C4 := GEO: C3 (*CELLULE PERIPHERIQUE*)
                MESHX 0.0 1.322082
                MIX 2 1
          ;
          ::: C5 := GEO: C4 (*CELLULE DE COIN*)
                MESHY 0.0 1.322082
                MIX 2 1
          ;
          ;
TRACK := SYBILT: ASSMB ::
     EDIT 1
     MAXR 60  (*MAXIMUM OF 40 REGIONS*)
     TITLE 'TCM32: MULTICELL 9 X 9 PWR BENCHMARK WITH POISON'
     DP00
     ;
*
SYS := ASM: MACRO TRACK :: ECCO SKIP ;
FLUX := FLU: SYS MACRO TRACK ::
  TYPE B B1 ECCO BUCK 1.5E-03 ;
assertS FLUX :: 'K-INFINITY' 1 0.8888339 ;
SYS FLUX := DELETE: SYS FLUX ;
*
SYS := ASM: MACRO TRACK :: ECCO ;
FLUX := FLU: SYS MACRO TRACK ::
  TYPE B B1 ECCO BUCK 1.5E-03 ;
assertS FLUX :: 'K-INFINITY' 1 0.8888339 ;
*
*----
* TRANSPORT-TRANSPORT EQUIVALENCE
*----
EDIT := EDI: FLUX MACRO TRACK ASSMB ::
     EDIT 3 P0W MERG CELL NXT SAVE ;
ASSMB2 := EDIT :: STEP UP 'MACRO-GEOM' ;
TRACK2 TRK_FIL2 := NXT: ASSMB2 :: TISO 12 12.0 ;
TRACK2 := MCCGT: TRACK2 TRK_FIL2 :: DIFC TMT
          EPSI 1E-8 MAXI 100 KRYL 0 SCR 1 HDD 0.0 ;
EDIT := SPH: EDIT TRACK2 TRK_FIL2 ;
ASSMB2 TRACK2 TRK_FIL2 := DELETE: ASSMB2 TRACK2 TRK_FIL2 ;

MACRO2 := EDIT :: STEP UP 'REF-CASE0001' STEP UP MACROLIB
                  STEP UP GROUP STEP AT 1 ;
assertS MACRO2 :: 'NSPH' 3 0.8144096 ;
EDIT MACRO2 := DELETE: EDIT MACRO2 ;
*----
* TRANSPORT-SPN EQUIVALENCE
*----
EDIT := EDI: FLUX MACRO TRACK ASSMB :: EDIT 3
     P1W_L SAVE MICR ALL MERG CELL ;
ASSMB2 := EDIT :: STEP UP 'MACRO-GEOM' ;
TRACK2 := BIVACT: ASSMB2 ::
     DUAL (*IELEM=*) 2 (*ICOL=*) 1 SPN 5 SCAT 1 ;
EDIT := SPH: EDIT TRACK2 ;
ASSMB2 TRACK2 := DELETE: ASSMB2 TRACK2 ;

MACRO2 := EDIT :: STEP UP 'REF-CASE0001' STEP UP MACROLIB
                  STEP UP GROUP STEP AT 1 ;
assertS MACRO2 :: 'NSPH' 3 0.9258092 ;
EDIT MACRO2 := DELETE: EDIT MACRO2 ;
*----
* TRANSPORT-SN EQUIVALENCE
*----
EDIT := EDI: FLUX MACRO TRACK ASSMB :: EDIT 3
     P1W_L SAVE MICR ALL MERG CELL ;
ASSMB2 := EDIT :: STEP UP 'MACRO-GEOM' ;
TRACK2 := SNT: ASSMB2 ::
     EDIT 1 SN 6 SCAT 1 QUAD 1 ;
EDIT := SPH: EDIT TRACK2 ;
ASSMB2 TRACK2 := DELETE: ASSMB2 TRACK2 ;

MACRO2 := EDIT :: STEP UP 'REF-CASE0001' STEP UP MACROLIB
                  STEP UP GROUP STEP AT 1 ;
assertS MACRO2 :: 'NSPH' 3 0.8991457 ;
SYS FLUX EDIT MACRO2 := DELETE: SYS FLUX EDIT MACRO2 ;
*
SYS := ASM: MACRO TRACK :: PIJ ;
FLUX := FLU: MACRO TRACK SYS :: TYPE K B0 SIGS BUCK 0.0 ;
assertS FLUX :: K-INFINITY 1 0.8934618 ;
SYS := DELETE: SYS ;
*----
* TRANSPORT-TRANSPORT EQUIVALENCE
*----
EDIT := EDI: FLUX MACRO TRACK ASSMB ::
     EDIT 3 SAVE MERG CELL NXT ;
ASSMB2 := EDIT :: STEP UP 'MACRO-GEOM' ;
TRACK2 TRK_FIL2 := NXT: ASSMB2 :: TISO 12 12.0 ;
EDIT := SPH: EDIT TRACK2 TRK_FIL2 :: EDIT 2 SN BELL ;
ASSMB2 TRACK2 TRK_FIL2 := DELETE: ASSMB2 TRACK2 TRK_FIL2 ;
*
* VERIFICATION CALCULATION
ASSMB2 := EDIT :: STEP UP 'MACRO-GEOM' ;
MACRO2 := EDIT :: STEP UP 'REF-CASE0001' STEP UP 'MACROLIB' ;
TRACK2 TRK_FIL2 := NXT: ASSMB2 :: TISO 12 12.0 ;
SYS := ASM: MACRO2 TRACK2 TRK_FIL2 :: PIJ ;
FLUX2 := FLU: MACRO2 TRACK2 TRK_FIL2 SYS :: TYPE K LKRD BUCK 0.0 ;
assertS FLUX2 :: K-EFFECTIVE 1 0.8934032 ;
FLUX2 MACRO2 TRACK2 EDIT ASSMB2 SYS := DELETE:
        FLUX2 MACRO2 TRACK2 EDIT ASSMB2 SYS ;
*----
* TRANSPORT-DIFFUSION EQUIVALENCE
*----
*    EDITION FOR HETEROGENEOUS DIFFUSION CALCULATIONS
*    CELL-BY-CELL SPH HOMOGENIZATION OF THE ASSEMBLY
EDIT := EDI: FLUX MACRO TRACK ASSMB ::
     EDIT 3 SAVE MERG CELL ;
ASSMB2 := EDIT :: STEP UP 'MACRO-GEOM' ;
TRACK2 := BIVACT: ASSMB2 ::
     DUAL (*IELEM=*) 1 (*ICOL=*) 2 ;
EDIT := SPH: EDIT TRACK2 :: EDIT 9 ITER 1.0E-5 ;
ASSMB2 TRACK2 := DELETE: ASSMB2 TRACK2 ;
*
* VERIFICATION CALCULATION
ASSMB2 := EDIT :: STEP UP 'MACRO-GEOM' ;
MACRO2 := EDIT :: STEP UP 'REF-CASE0001' STEP UP 'MACROLIB' ;
TRACK2 := BIVACT: ASSMB2 :: DUAL (*IELEM=*) 1 (*ICOL=*) 2 ;
SYS := ASM: MACRO2 TRACK2 :: ARM ;
FLUX2 := FLU: MACRO2 TRACK2 SYS :: TYPE K LKRD BUCK 0.0 ;
assertS FLUX2 :: K-EFFECTIVE 1 0.8934604 ;
ECHO "test TCM32 completed" ;
END: ;
QUIT "LIST" .
