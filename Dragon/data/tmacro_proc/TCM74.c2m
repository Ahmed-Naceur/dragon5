*----
*  TEST CASE TCM74
*  Multi-slab air-water-bone-lung-water coupled electron-photon benchmark with 
*  3 MeV monodirectional photon boundary source.
*----
*----
*  Define STRUCTURES and MODULES used
*----
MODULE GEO: FMAC: MAC: SNT: ASM: FLU: PSOUR: OUT: DELETE: END: UTL:
       HEAT: ABORT: ;
PROCEDURE assertS ;
LINKED_LIST  GMACRO GMACRO2 EMACRO EMACRO2 GEOM GTRACK ETRACK SYSTEM
       GFLUX EFLUX GSOUR ESOUR GEDITS EEDITS DEPOS ;
REAL esum DELTA ;
SEQ_ASCII FMAC_M :: FILE './air_water_bone_lung.txt' ;
*----
*  Set and track the geometry
*----
GEOM := GEO: :: CAR1D 5
           X- VOID  X+ VOID
           MIX 1 2 3 4 2
           MESHX 0.0 1.0 4.0 6.0 13.0 31.0
           SPLITX 4 12 8 28 72
           ;
GTRACK := SNT: GEOM ::
      TITLE 'Monodirectional photon sources on tissues.'
      EDIT 2 MAXR 1000000 SCHM 1 DIAM 1 NLIVO SN 4 SCAT 4 EPSI 1.E-5
      MAXI 300 BTE QUAD 10 ;
ETRACK := GTRACK ;
ETRACK := SNT: ETRACK GEOM :: EDIT 2 BFPG ;
*----
*  Compute the electron flux
*----
GMACRO := FMAC: FMAC_M ::
  EDIT 2
  PARTICLE GAMA
;
GMACRO2 := GMACRO ;
GMACRO := MAC: GMACRO GMACRO2 ::
     MIX 1 1 OLDL
     MIX 2 2 OLDL
     MIX 3 3 OLDL
     MIX 4 4 OLDL
     ;
GMACRO2 := DELETE: GMACRO2 ;
GSOUR := PSOUR: GMACRO GTRACK GEOM ::
     MONO 1
     X-
     INTG 1 1.0
     DIR 1.0 0.0 0.0
     ;
SYSTEM := ASM: GMACRO GTRACK :: EDIT 2 ARM ;
GFLUX := FLU: GMACRO GTRACK SYSTEM GSOUR ::
     EDIT 2 TYPE S EXTE 100
     ;
GEDITS := OUT: GFLUX GTRACK GMACRO GEOM ::
     EDIT 2 COND INTG NONE
     ;
SYSTEM := DELETE: SYSTEM ;

*----
*  Set the photon source
*----
EMACRO := FMAC: FMAC_M ::
  EDIT 2
  PARTICLE BETA
;

EMACRO2 := EMACRO ;
EMACRO := MAC: EMACRO EMACRO2 ::
     MIX 1 1 OLDL
     MIX 2 2 OLDL
     MIX 3 3 OLDL
     MIX 4 4 OLDL
     ;
ESOUR := PSOUR: EMACRO ETRACK GFLUX ::
   EDIT 1
   PARTICLE GAMA
   ;
*----
*  Compute the photon flux
*----
SYSTEM := ASM: EMACRO ETRACK :: EDIT 1 ARM ;
EFLUX := FLU: EMACRO ETRACK SYSTEM ESOUR ::
    EDIT 2 TYPE S EXTE 100
    ;
EEDITS := OUT: EFLUX ETRACK EMACRO GEOM ::
    EDIT 2 COND INTG NONE
    ;
SYSTEM := DELETE: SYSTEM ;
*----
*  Compute the primary energy deposition
*----
DEPOS := HEAT: GEDITS EEDITS ::
    EDIT 1 NORM 1.20427E-3 1.0 1.85 1.05
    ;
GEDITS EEDITS GFLUX GSOUR := DELETE: GEDITS EEDITS GFLUX GSOUR ;
*----
*  Compute the secondary electron source
*----
GSOUR := PSOUR: GMACRO GTRACK EFLUX ::
   EDIT 1
   PARTICLE BETA
   ;
*----
*Compute the secondary electron flux
*----
SYSTEM := ASM: GMACRO GTRACK :: EDIT 1 ARM ;
GFLUX := FLU: GMACRO GTRACK SYSTEM GSOUR ::
    EDIT 2 TYPE S EXTE 100
    ;
GEDITS := OUT: GFLUX GTRACK GMACRO GEOM ::
    EDIT 2 COND INTG NONE
    ;
SYSTEM ESOUR EFLUX := DELETE: SYSTEM ESOUR EFLUX ;
*----
*  Compute the secondary photon source
*----
ESOUR := PSOUR: EMACRO ETRACK GFLUX ::
   EDIT 1
   PARTICLE GAMA
   ;
*----
*  Compute the secondary photon flux
*----
SYSTEM := ASM: EMACRO ETRACK :: EDIT 1 ARM ;
EFLUX := FLU: EMACRO ETRACK SYSTEM ESOUR ::
    EDIT 2 TYPE S EXTE 100
    ;
EEDITS := OUT: EFLUX ETRACK EMACRO GEOM ::
    EDIT 2 COND INTG NONE
    ;
SYSTEM GSOUR := DELETE: SYSTEM GSOUR ;
*----
*  Compute the secondary total energy deposition
*----
DEPOS := HEAT: DEPOS GEDITS EEDITS ::
    EDIT 1 NORM 1.20427E-3 1.0 1.85 1.05
    ;

assertS DEPOS :: 'EDEPOS_TOT' 1 1.9662E-03 ; 
assertS DEPOS :: 'EDEPOS_TOT' 16 7.1653E-02 ; 
assertS DEPOS :: 'EDEPOS_TOT' 27 6.2861E-02 ; 
*
ECHO "Test TCM74 completed" ;
END: ;
