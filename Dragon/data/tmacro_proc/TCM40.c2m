*----
*  TEST CASE TCM40
*  9 X 9 PWR CASE WITH SYBIL-TRIVAC SPH EQUIVALENCE
*  MACROLIB-DEFINED CROSS SECTIONS
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST ASSMB TRACK MACRO SYS FLUX EDIT1 EDIT2 ASSMB2 TRACK2
            MACRO2 ;
MODULE GEO: SYBILT: MAC: ASM: FLU: EDI: TRIVAT: SPH: DELETE: END: ;
PROCEDURE assertS ;
*
MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 3 NIFI 1 ANIS 2
     READ INPUT
     MIX 1 (*EAU*)
        TOTAL 0.3683
        SCAT 1 1 0.3661 1 1 0.1212
     MIX 2 (*COMBUSTIBLE*)
        TOTAL 0.36522
        SCAT 1 1 0.3234 1 1 0.1100
        NUSIGF 0.09564      CHI 1.0
     MIX 3 (*ARGENT*)
        TOTAL 0.8453
        SCAT 1 1 0.5216 1 1 0.4212
     ;
ASSMB := GEO: :: CAR2D 5 5 (*ASSEMBLAGE 9 X 9*)
          X- DIAG X+ REFL
          Y- REFL Y+ DIAG
          CELL C1 C3 C2 C3 C4
                  C3 C3 C3 C4
                     C2 C3 C4
                        C3 C4
                           C5
          MERGE 1  2  3  4  5
                   6  7  8  9
                     10 11  9
                        12  9
                           13
          ::: C1 := GEO: CARCEL 1 (*CELLULE SANS COMBUSTIBLE*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 4.12660E-01
                MIX 1 1
          ;
          ::: C2 := GEO: CARCEL 3 (*CELLULE EMPOISONNEE*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 3.25296E-01 4.60039E-01 5.63430E-01
                MIX 3 3 3 1
          ;
          ::: C3 := GEO: CARCEL 1 (*CELLULE NORMALE*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 4.12660E-01
                MIX 2 1
          ;
          ::: C4 := GEO: C3 (*CELLULE PERIPHERIQUE*)
                MESHX 0.0 1.322082
                MIX 2 1
          ;
          ::: C5 := GEO: C4 (*CELLULE DE COIN*)
                MESHY 0.0 1.322082
                MIX 2 1
          ;
          ;
TRACK := SYBILT: ASSMB ::
     EDIT 1
     MAXR 60  (*MAXIMUM OF 40 REGIONS*)
     TITLE 'TCM40: MULTICELL 9 X 9 PWR BENCHMARK WITH POISON'
     DP00
     ;
*
SYS := ASM: MACRO TRACK :: PIJ ;
FLUX := FLU: MACRO TRACK SYS :: TYPE K B0 SIGS BUCK 0.0 ;
assertS FLUX :: K-INFINITY 1 0.8934618 ;
*----
* TRANSPORT-DIFFUSION EQUIVALENCE
*----
*    EDITION FOR HETEROGENEOUS DIFFUSION CALCULATIONS
*    CELL-BY-CELL SPH HOMOGENIZATION OF THE ASSEMBLY
EDIT1 := EDI: FLUX MACRO TRACK ASSMB ::
     EDIT 3 MERG CELL SAVE P0W ;
ASSMB2 := EDIT1 :: STEP UP 'MACRO-GEOM' ;
TRACK2 := TRIVAT: ASSMB2 :: DUAL (*IELEM=*) 1 (*ICOL=*) 2 ;
EDIT1 := SPH: EDIT1 TRACK2 :: ITER 1.0E-5 ;
ASSMB2 TRACK2 := DELETE: ASSMB2 TRACK2 ;
*----
* TRANSPORT-SPN EQUIVALENCE
*----
*    EDITION FOR HETEROGENEOUS DIFFUSION CALCULATIONS
*    CELL-BY-CELL SPH HOMOGENIZATION OF THE ASSEMBLY
EDIT2 := EDI: FLUX MACRO TRACK ASSMB ::
     EDIT 3 MERG CELL SAVE P0W ;
ASSMB2 := EDIT2 :: STEP UP 'MACRO-GEOM' ;
TRACK2 := TRIVAT: ASSMB2 :: DUAL (*IELEM=*) 1 (*ICOL=*) 2 SPN 5
     SCAT 1 ;
EDIT2 := SPH: EDIT2 TRACK2 :: ITER 1.0E-5 ;
ASSMB2 TRACK2 := DELETE: ASSMB2 TRACK2 ;
*----
* DIFFUSION VERIFICATION CALCULATION
*----
ASSMB TRACK SYS FLUX := DELETE: ASSMB TRACK SYS FLUX ;
ASSMB := EDIT1 :: STEP UP 'MACRO-GEOM' ;
MACRO2 := EDIT1 :: STEP UP 'REF-CASE0001' STEP UP 'MACROLIB' ;
TRACK := TRIVAT: ASSMB :: DUAL (*IELEM=*) 1 (*ICOL=*) 2 ;
SYS := ASM: MACRO2 TRACK :: ARM ;
FLUX := FLU: MACRO2 TRACK SYS :: EDIT 1 TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 0.8934618 ;
*----
* SPN VERIFICATION CALCULATION
*----
ASSMB MACRO2 TRACK SYS FLUX := DELETE: ASSMB MACRO2 TRACK SYS FLUX ;
ASSMB := EDIT2 :: STEP UP 'MACRO-GEOM' ;
MACRO2 := EDIT2 :: STEP UP 'REF-CASE0001' STEP UP 'MACROLIB' ;
TRACK := TRIVAT: ASSMB :: DUAL (*IELEM=*) 1 (*ICOL=*) 2 SPN 5 SCAT 1 ;
SYS := ASM: MACRO2 TRACK :: ARM ;
FLUX := FLU: MACRO2 TRACK SYS :: EDIT 1 TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 0.8934618 ;
ECHO "test TCM40 completed" ;
END: ;
QUIT "LIST" .
