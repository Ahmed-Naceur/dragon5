*----
* TEST CASE TCM53
* MACROSCOPIC CROSS SECTIONS
* MONTE-CARLO MULTIGROUP METHOD
* CANDU-NG UNIT CELL WITH NXT:
*
*----
*  Define STRUCTURES and MODULES used
*----
MODULE
  NXT: MAC: DELETE: MC: GREP: ABORT: SYBILT: ASM: FLU: GEO: END:
  ; 
REAL VALUE REFVALUE DELTA ;
PROCEDURE
  CHB_NXT_MC
  ;
LINKED_LIST  
  GEOMF
  MACRO
  TRAC2
  OUTMC
  MACRO2 GEOM2 TRACK2 SYS FLUX
  ;
SEQ_ASCII
  plot.ps
  ;
STRING    
  state SolveurSS SolveurFL polquad 
  Library 
  ;
INTEGER
  splitc splitm nban2d nban3d nban2dss
  ;
REAL
  density densityss
  ;
INTEGER
   CF11  CF12  CF13  CF14 
   CF21i CF22i CF23i CF24i
   CF31i CF32i CF33i CF34i
   CF41i CF42i CF43i CF44i
   CF21e CF22e CF23e CF24e
   CF31e CF32e CF33e CF34e
   CF41e CF42e CF43e CF44e
   VF11  VF12  VF13  VF14 
   VF21i VF22i VF23i VF24i
   VF31i VF32i VF33i VF34i
   VF41i VF42i VF43i VF44i
   VF21e VF22e VF23e VF24e
   VF31e VF32e VF33e VF34e
   VF41e VF42e VF43e VF44e
   COOL  PT    HEG   CT
   MOD   SH    VOID := 
          1     1     1     1
          2     2     2     2
          3     3     3     3
          4     4     4     4
          2     2     2     2
          3     3     3     3
          4     4     4     4
          1     1     1     1
          2     2     2     2
          3     3     3     3
          4     4     4     4
          2     2     2     2
          3     3     3     3
          4     4     4     4
          6     6     5     5
          6     5     5
  ; 
*
* geometrical parameters
*
EVALUATE state     := "Voided"   ; ! Cooled/Voided/Chboard
EVALUATE splitc    := 1 ;
EVALUATE splitm    := 1 ;

EVALUATE nban2dss  := 8  ;
EVALUATE densityss := 20.0 ;

EVALUATE nban2d    := 12 ;
EVALUATE density   := 50.0 ;
EVALUATE polquad   := "LCMD" ;
EVALUATE nban3d    := 2 ;
*
*  geometries
*
GEOMF := CHB_NXT_MC ::
  <<state>>
  <<splitc>> <<splitm>>
  <<CF11>>  <<CF12>>  <<CF13>>  <<CF14>> 
  <<CF21i>> <<CF22i>> <<CF23i>> <<CF24i>>
  <<CF31i>> <<CF32i>> <<CF33i>> <<CF34i>>
  <<CF41i>> <<CF42i>> <<CF43i>> <<CF44i>>
  <<CF21e>> <<CF22e>> <<CF23e>> <<CF24e>>
  <<CF31e>> <<CF32e>> <<CF33e>> <<CF34e>>
  <<CF41e>> <<CF42e>> <<CF43e>> <<CF44e>>
  <<VF11>>  <<VF12>>  <<VF13>>  <<VF14>> 
  <<VF21i>> <<VF22i>> <<VF23i>> <<VF24i>>
  <<VF31i>> <<VF32i>> <<VF33i>> <<VF34i>>
  <<VF41i>> <<VF42i>> <<VF43i>> <<VF44i>>
  <<VF21e>> <<VF22e>> <<VF23e>> <<VF24e>>
  <<VF31e>> <<VF32e>> <<VF33e>> <<VF34e>>
  <<VF41e>> <<VF42e>> <<VF43e>> <<VF44e>>
  <<COOL>>  <<PT>>    <<HEG>>   <<CT>>
  <<MOD>>   <<SH>>    <<VOID>>
 ;
*
* geometry tracking 
*
 TRAC2 := NXT: GEOMF ::
 EDIT 1 ANIS 1 MC PSPC
 ;
*
*  macrolib
*
MACRO := MAC: ::
 EDIT 0 NGRO 2 NMIX 6 NIFI 1 
 READ INPUT
 MIX     1
     TOTAL  2.222E-01  8.3333E-01
    NUSIGF  0.000E+00  2.3600E-01
       CHI  1.000E+00  0.0000E+00
      SCAT  1 1 0.1921 2 2 0.7533 0.02
 MIX     2
     TOTAL  2.222E-01  8.3333E-01
    NUSIGF  0.000E+00  2.3600E-01
       CHI  1.000E+00  0.0000E+00
      SCAT  1 1 0.1921 2 2 0.7483 0.02
 MIX     3
     TOTAL  2.222E-01  7.5333E-01
    NUSIGF  0.000E+00  2.36000E-01
       CHI  1.000E+00  0.00000E+00
      SCAT  1 1 0.1921 2 2 0.7033 0.02
 MIX     4
     TOTAL  2.322E-01  7.3333E-01
    NUSIGF  0.000E+00  2.36000E-01
       CHI  1.000E+00  0.00000E+00
      SCAT  1 1 0.2121 2 2 0.7033 0.02
 MIX     5
     TOTAL  0.0  0.0
      SCAT  1 1 0.0 2 2 0.0 0.0
 MIX     6
     TOTAL  0.1677  1.1211
      SCAT  1 1 0.1265 2 2 1.1011 0.04
 ;
*
*  Monte-Carlo calculation
*
OUTMC := MC: TRAC2 MACRO ::
 KCODE 300 400 6000
 SEED 23412
 EDIT 1 
 TALLY
   MERG COMP
   COND NONE
 ENDT
 ;
MACRO2 := OUTMC :: STEP UP 'MACROLIB' ;
GEOM2 := GEO: :: CARCEL 0
           X- REFL X+ REFL Y- REFL Y+ REFL
           MIX  1
           MESHX 0.0 1.0
           MESHY 0.0 1.0
           ;
TRACK2 := SYBILT: GEOM2 ::
  TITLE 'INFINITE MEDIUM'
  EDIT 1 MAXR 5 QUA1 5 ;
SYS := ASM: MACRO2 TRACK2 ;
FLUX := FLU: SYS MACRO2 TRACK2 :: TYPE K ;
*
*  Non regression assert
*
EVALUATE REFVALUE := 1.0088 ;
*
GREP: FLUX :: GETVAL K-EFFECTIVE 1 >>VALUE<< ;
EVALUATE DELTA := VALUE REFVALUE - REFVALUE / ABS ;
IF DELTA 5.0E-3 < THEN
  PRINT "TEST SUCCESSFUL; DELTA=" DELTA ;
ELSE
  PRINT "------------" ;
  PRINT "TEST FAILURE" ;
  PRINT "------------" ;
  PRINT "REFERENCE=" REFVALUE " CALCULATED=" VALUE ;
  ABORT: ;
ENDIF ;

ECHO "test TCM53 completed" ;
END: ;
QUIT "LIST" .
