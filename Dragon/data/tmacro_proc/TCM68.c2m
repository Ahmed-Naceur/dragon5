*----
*  TEST CASE TCM68
*  FMAC-M generated homogeneous macrolib
*  2D plate sn electron testcase from Kaskad_S_serial
*----
*  Define STRUCTURES and MODULES used
*----
MODULE GEO: FMAC: MAC: SNT: ASM: FLU: OUT: DELETE: END: ;
PROCEDURE assertS ;
LINKED_LIST  MACRO MACRO2 GEOM TRACK SYSTEM FLUX EEDITS GWT0 ;
SEQ_ASCII FMAC_M :: FILE './Al_1MeV_CSD_P7.txt' ;
*----
*  Set and track the geometry
*----
GEOM := GEO: :: CAR2D 3 2
           X- VOID  X+ VOID
           Y- VOID  Y+ VOID
           MIX  1 2 2
                2 2 2
           MESHX 0.0 0.04 0.1 0.3
           MESHY 0.0 0.02 0.2
           SPLITX 10 40 40
           SPLITY 20 80
           ;
TRACK := SNT: GEOM ::
      TITLE 'HOMOGENIZED Al_1MeV_CSD_P7 BENCHMARK WITH FMAC-M'
      EDIT 2 MAXR 10000 NLIVO SN 8 SCAT 8 EPSI 1.E-4 MAXI 300
      QUAD 1 BFPL ;
*----
*  Compute the electron flux
*----
MACRO := FMAC: FMAC_M ::
  EDIT 2
  PARTICLE B
;
MACRO2 := MACRO ;
MACRO := MAC: MACRO MACRO2 ::
     MIX 2 1 OLDL
     ;
MACRO2 := DELETE: MACRO2 ;
REAL qsour := 1.E10 2.0 / $Pi_R / ;
ECHO "qsour=" qsour ;
MACRO := MAC: MACRO ::
     READ INPUT
     MIX 1 FIXE
     <<qsour>> 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     ;
SYSTEM := ASM: MACRO TRACK :: EDIT 1 ARM ;
FLUX := FLU: MACRO TRACK SYSTEM :: EDIT 1 TYPE S EXTE 100 ;
EEDITS := OUT: FLUX TRACK MACRO GEOM ::
    EDIT 2 COND SOUR 0.3703703 INTG NONE ! 1/(\ro_{Al} g/cm^{3})
    ;
*----
*  Asserts
*----
GWT0 := EEDITS :: STEP UP GROUP STEP AT 1 ;
assertS GWT0 :: 'FLUX-INTG' 1 2.170554E-5 ;
assertS GWT0 :: 'FLUX-INTG' 1000 3.434623E-5 ;
assertS GWT0 :: 'FLUX-INTG' 2000 9.597430E-6 ;
assertS GWT0 :: 'FLUX-INTG' 3000 5.226816E-6 ;
assertS GWT0 :: 'FLUX-INTG' 4000 2.983444E-6 ;
assertS GWT0 :: 'FLUX-INTG' 5000 1.335913E-6 ;
*
ECHO "test TCM68 completed" ;
END: ;
