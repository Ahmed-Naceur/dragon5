*----
*  TEST CASE TCM34
*  ENE6101 HOMEWORK 1D SLAB/CYLINDRICAL/SPHERICAL PROBLEMS
*  ONE-GROUP MACROLIB-DEFINED CROSS SECTIONS
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM MACRO TRACK SYSTEM FLUX EDIT ;
MODULE GEO: MAC: SYBILT: SNT: BIVACT: ASM: FLU: DELETE: END: ;
INTEGER s := 2 ;
INTEGER d := 2 ;
REAL api pi r1 r2 r3 r4 r5 ;
PROCEDURE assertS ;
*
GEOM := GEO: :: CAR1D  5
           X- TRAN X+ TRAN
           MIX  1 2 3 4 5
           MESHX 0.5 0.9 1.6 2.0 3.3 5.4
           SPLITX <<s>> <<s>> <<s>> <<s>> <<s>>
           ;
MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 5 NIFI 1
 READ INPUT
 MIX     1
     TOTAL  0.2
      SCAT  1 1 0.05
     NUSIGF 1.4      CHI 1.0
 MIX     2
     TOTAL  0.0
      SCAT  1 1 0.0
 MIX     3
     TOTAL  0.5
      SCAT  1 1 0.05
 MIX     4
     TOTAL  0.0
      SCAT  1 1 0.0
 MIX     5
     TOTAL  0.3
      SCAT  1 1 0.05
 ;
*----
* PIJ (slab)
*----
TRACK := SYBILT: GEOM ::
      TITLE 'ENE6101 BENCHMARK IN 1D SLAB GEOMETRY'
      MAXR 50 ;
SYSTEM := ASM: MACRO TRACK :: EDIT 1 PIJ ;
FLUX := FLU: MACRO TRACK SYSTEM :: TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.305803 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
*----
* SN (slab) - HIGH ORDER DIAMOND DIFFERENCE
*----
TRACK := SNT: GEOM ::
      TITLE 'ENE6101 BENCHMARK IN 1D SLAB GEOMETRY'
      EDIT 2 MAXR 2500 DIAM <<d>> SN 18 SCAT 1 ;
SYSTEM := ASM: MACRO TRACK ::
      EDIT 1 ARM ;
FLUX :=  FLU: MACRO TRACK SYSTEM ::
      EDIT 2 TYPE K EXTE 100 5.0E-8 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.203569 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
*----
* SN (slab) - DISCONTINUOUS GALERKIN
*----
TRACK := SNT: GEOM ::
      TITLE 'ENE6101 BENCHMARK IN 1D SLAB GEOMETRY'
      EDIT 2 MAXR 2500 SCHM 2 DIAM 3 SN 18 SCAT 1 ;
SYSTEM := ASM: MACRO TRACK ::
      EDIT 1 ARM ;
FLUX :=  FLU: MACRO TRACK SYSTEM ::
      EDIT 2 TYPE K EXTE 100 5.0E-8 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.203574 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
*----
* PN (slab)
*----
EVALUATE s := 10 ;
GEOM := GEO: GEOM :: SPLITX <<s>> <<s>> <<s>> <<s>> <<s>> ;
TRACK := BIVACT: GEOM ::
      TITLE 'ANISOTROPIC NSE BENCHMARK 1D, 1 GROUP'
      EDIT 2 MAXR 2500 DUAL 1 3 SPN 17 SCAT 1 ;
SYSTEM := ASM: MACRO TRACK ::
      EDIT 1 ARM ;
FLUX :=  FLU: MACRO TRACK SYSTEM ::
      EDIT 2 TYPE K EXTE 100 5.0E-8 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.202620 ;
GEOM TRACK SYSTEM FLUX := DELETE: GEOM TRACK SYSTEM FLUX ;
*----
* PIJ (cylindrical)
*----
EVALUATE pi := 3.141592654 ;
EVALUATE r1 := 0.4 pi / SQRT ;
EVALUATE r2 := 1.1 pi / SQRT ;
EVALUATE r3 := 1.5 pi / SQRT ;
EVALUATE r4 := 2.8 pi / SQRT ;
EVALUATE r5 := 4.9 pi / SQRT ;
ECHO 'R =' r1 r2 r3 r4 r5 ;
GEOM := GEO: :: TUBE  5
           R+ REFL
           MIX  1 2 3 4 5
           RADIUS 0.0 <<r1>> <<r2>> <<r3>> <<r4>> <<r5>>
           ;
TRACK := SYBILT: GEOM ::
      TITLE 'NSE6101 BENCHMARK IN 1D CYLINDRICAL GEOMETRY'
      QUA1 5 MAXR 50 ;
SYSTEM := ASM: MACRO TRACK :: EDIT 1 PIJ ;
FLUX := FLU: MACRO TRACK SYSTEM :: TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.169899 ;
GEOM TRACK SYSTEM FLUX := DELETE: GEOM TRACK SYSTEM FLUX ;
*----
* PIJ (spherical)
*----
EVALUATE api := 3.141592654 4.0 * 3.0 / ;
EVALUATE r1 := 0.4 api / 0.333333333 ** ;
EVALUATE r2 := 1.1 api / 0.333333333 ** ;
EVALUATE r3 := 1.5 api / 0.333333333 ** ;
EVALUATE r4 := 2.8 api / 0.333333333 ** ;
EVALUATE r5 := 4.9 api / 0.333333333 ** ;
ECHO 'R =' r1 r2 r3 r4 r5 ;
GEOM := GEO: :: SPHERE  5
           R+ REFL
           MIX  1 2 3 4 5
           RADIUS 0.0 <<r1>> <<r2>> <<r3>> <<r4>> <<r5>>
           ;
TRACK := SYBILT: GEOM ::
      TITLE 'ENE6101 BENCHMARK IN 1D SPHERICAL GEOMETRY'
      QUA1 5 MAXR 50 ;
SYSTEM := ASM: MACRO TRACK :: EDIT 1 PIJ ;
FLUX := FLU: MACRO TRACK SYSTEM :: TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.094737 ;
GEOM TRACK SYSTEM FLUX := DELETE: GEOM TRACK SYSTEM FLUX ;
*----
* SN (cylindrical)
*----
EVALUATE pi := 3.141592654 ;
EVALUATE r1 := 0.4 pi / SQRT ;
EVALUATE r2 := 1.1 pi / SQRT ;
EVALUATE r3 := 1.5 pi / SQRT ;
EVALUATE r4 := 2.8 pi / SQRT ;
EVALUATE r5 := 4.9 pi / SQRT ;
ECHO 'R =' r1 r2 r3 r4 r5 ;
GEOM := GEO: :: TUBE  5
           R+ REFL
           MIX  1 2 3 4 5
           RADIUS 0.0 <<r1>> <<r2>> <<r3>> <<r4>> <<r5>>
           ;
TRACK := SNT: GEOM ::
      TITLE 'NSE6101 BENCHMARK IN 1D CYLINDRICAL GEOMETRY'
      EDIT 2 MAXR 2500 DIAM 0 SN 18 SCAT 1 ;
SYSTEM := ASM: MACRO TRACK :: EDIT 1 ARM ;
FLUX := FLU: MACRO TRACK SYSTEM :: TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.189361 ;
GEOM TRACK SYSTEM FLUX := DELETE: GEOM TRACK SYSTEM FLUX ;
*----
* SN (spherical)
*----
EVALUATE api := 3.141592654 4.0 * 3.0 / ;
EVALUATE r1 := 0.4 api / 0.333333333 ** ;
EVALUATE r2 := 1.1 api / 0.333333333 ** ;
EVALUATE r3 := 1.5 api / 0.333333333 ** ;
EVALUATE r4 := 2.8 api / 0.333333333 ** ;
EVALUATE r5 := 4.9 api / 0.333333333 ** ;
ECHO 'R =' r1 r2 r3 r4 r5 ;
GEOM := GEO: :: SPHERE  5
           R+ REFL
           MIX  1 2 3 4 5
           RADIUS 0.0 <<r1>> <<r2>> <<r3>> <<r4>> <<r5>>
           ;
TRACK := SNT: GEOM ::
      TITLE 'ENE6101 BENCHMARK IN 1D SPHERICAL GEOMETRY'
      EDIT 2 MAXR 2500 DIAM 0 SN 18 SCAT 1 ;
SYSTEM := ASM: MACRO TRACK :: EDIT 1 ARM ;
FLUX := FLU: MACRO TRACK SYSTEM :: TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.567753 ;
GEOM TRACK SYSTEM FLUX := DELETE: GEOM TRACK SYSTEM FLUX ;
*----
* SN  (R-Z geometry)
*----
EVALUATE pi := 3.141592654 ;
EVALUATE r1 := 0.4 pi / SQRT ;
EVALUATE r2 := 1.1 pi / SQRT ;
EVALUATE r3 := 1.5 pi / SQRT ;
EVALUATE r4 := 2.8 pi / SQRT ;
EVALUATE r5 := 4.9 pi / SQRT ;
ECHO 'R =' r1 r2 r3 r4 r5 ;
GEOM := GEO: :: TUBEZ  5 1
           R+ REFL Z- VOID Z+ VOID
           MIX  1 2 3 4 5
           RADIUS 0.0 <<r1>> <<r2>> <<r3>> <<r4>> <<r5>>
           MESHZ 0.0 10.0
           SPLITZ 200
           ;
TRACK := SNT: GEOM ::
      TITLE 'ENE6101 BENCHMARK IN 2D R-Z GEOMETRY'
      EDIT 2 MAXR 2500 DIAM 0 SN 18 SCAT 1 ;
SYSTEM := ASM: MACRO TRACK :: EDIT 1 ARM ;
FLUX := FLU: MACRO TRACK SYSTEM :: TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.844769 ;
GEOM TRACK SYSTEM FLUX := DELETE: GEOM TRACK SYSTEM FLUX ;
*
ECHO "test TCM34 completed" ;
END: ;
QUIT "LIST" .
