*---
* Regression test for TIBERE
*---
* TEST CASE TCM59
* 2-D cell calculation on small cell geometry
* Fission source problem solved with MoC
*---
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST  GEOCELL TRACK MACRO SYS FLUX EDITION ;
SEQ_BINARY  TRKFIL ;
MODULE  GEO: MAC: NXT: MCCGT: ASM: FLU: EDI: DELETE: END: ;
PROCEDURE assertS ;
*----
* Macroscopic Cross sections
*----
MACRO := MAC: ::
NGRO 2 ANIS 2 NMIX 3 NIFI 1
READ INPUT
MIX 1 TOTAL 0.392175 0.622581
SCAT 2 2 0.001451 0.361893 2 2 0.358282 0.000715
2 2 0.0 0.0 2 2 0.0 0.0
NUSIGF 0.022141 0.496970 CHI 1.0 0.0
MIX 2 TOTAL 0.276383 0.278610
SCAT 2 2 0.000774 0.274505 2 2 0.273807 0.000288
2 2 0.0 0.0 2 2 0.0 0.0
MIX 3 TOTAL 0.439812 1.35565
SCAT 2 2 0.002672 0.411998 2 2 1.33517 0.02128
1 1 0.2 2 2 0.5 0.006    ;
*----
* Geometry definition
*----
GEOCELL := GEO: :: CARCEL 4
X- REFL X+ REFL MESHX 0.0 1.2647
Y- REFL Y+ REFL MESHY 0.0 1.2647
RADIUS 0.0 0.205 0.41 0.47 0.57
MIX 1 1 2 3 3
;
*----
* Isotropic tracking 
*----
TRACK TRKFIL := NXT: GEOCELL ::
  MAXR 1000 GAUS 4 TISO 8 250.0   ;
TRACK := MCCGT: TRACK TRKFIL :: 
  AAC 1 SCR 0 MAXI 1000 GAUS KRYL 30 HDD 0.0 STIS 1 ;
SYS := ASM: MACRO TRACK TRKFIL :: 
  ARM HETE ;
FLUX := FLU: MACRO TRACK SYS TRKFIL :: 
  EDIT 1 TYPE B B1 HETE ;
assertS FLUX :: 'K-INFINITY' 1 1.062726 ;
EDITION := EDI: MACRO TRACK FLUX ::
  EDIT 3 SAVE ;
TRACK TRKFIL SYS FLUX := DELETE: TRACK TRKFIL SYS FLUX ;
*----
* Specular tracking 
*----
TRACK TRKFIL := NXT: GEOCELL ::
  MAXR 1000 GAUS 4 TSPC 8 250.0   ;
TRACK := MCCGT: TRACK TRKFIL :: 
  AAC 1 SCR 0 MAXI 1000 GAUS KRYL 30 HDD 0.0 STIS 1 ;
SYS := ASM: MACRO TRACK TRKFIL :: 
  ARM HETE ;
FLUX := FLU: MACRO TRACK SYS TRKFIL :: 
  TYPE B B1 HETE ;
assertS FLUX :: 'K-INFINITY' 1 1.061762 ;
EDITION := EDI: EDITION MACRO TRACK FLUX ::
  EDIT 3 SAVE ;
*----
* CLEAN-UP
*----
TRACK TRKFIL SYS FLUX := DELETE: TRACK TRKFIL SYS FLUX ;
ECHO "test TCM59 completed" ;
END: ;
QUIT "LIST" .
