*----
*  TEST CASE TCM49
*  NSE 2D CARTESIAN PROBLEM MODIFIED FOR 3D SN REGRESSION TEST
*  ONE-GROUP MACROLIB-DEFINED CROSS SECTIONS
*
*  Ref: A. Hebert, "The Search for Superconvergence in Spherical
*  Harmonics Approximations", Nucl. Sci. Eng, 2006.
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM MACRO TRACK SYSTEM FLUX EDIT ;
MODULE GEO: MAC: SNT: ASM: FLU: DELETE: END: ;
INTEGER s := 2 ;
PROCEDURE assertS ;
*
*--------
*GEOMETRY
*--------
GEOM := GEO: :: CAR3D  5 5 1
           X- REFL X+ VOID
           Y- REFL Y+ VOID
           Z- VOID Z+ VOID
           MIX  1 1 2 2 3
                1 1 2 2 3
                2 2 2 2 3
                2 2 2 3 3
                3 3 3 3 0
           MESHX 0.0 40.0 80.0 120.0 160.0 200.0
           MESHY 0.0 40.0 80.0 120.0 160.0 200.0
           MESHZ 0.0 1000.0
           SPLITX <<s>> <<s>> <<s>> <<s>> <<s>>
           SPLITY <<s>> <<s>> <<s>> <<s>> <<s>>
           ;
*--------
*MACROLIB
*--------
MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 3 NIFI 1 ANIS 2
 READ INPUT
 MIX     1
     TOTAL  0.025
    NUSIGF  0.0155
       CHI  1.0
      SCAT  1 1 0.013
            1 1 0.0
 MIX     2
     TOTAL  0.025
      SCAT  1 1 0.024
            1 1 0.006
 MIX     3
     TOTAL  0.075
      SCAT  1 1 0.0
            1 1 0.0
 ;
*--------
*TRACKING
*--------
*----
* HIGH-ORDER DIAMOND DIFFERENCE
*----
!
!FLAT ORDER
TRACK := SNT: GEOM ::
      TITLE '3D ANISOTROPIC NSE BENCHMARK, 1 GROUP'
      EDIT 2 MAXR 5000 DIAM 0 SN 6 SCAT 2 QUAD 1 DSA 10 0 ;
SYSTEM := ASM: MACRO TRACK ::
      EDIT 1 ARM ;
FLUX :=  FLU: MACRO TRACK SYSTEM ::
      EDIT 2 TYPE K EXTE 100 5.0E-8 ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.9178295 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
!
!LINEAR ORDER
TRACK := SNT: GEOM ::
      TITLE '3D ANISOTROPIC NSE BENCHMARK, 1 GROUP'
      EDIT 2 MAXR 5000 DIAM 1 SN 6 SCAT 2 QUAD 1 DSA 10 1 ;
SYSTEM := ASM: MACRO TRACK ::
      EDIT 1 ARM ;
FLUX :=  FLU: MACRO TRACK SYSTEM ::
      EDIT 2 TYPE K EXTE 100 5.0E-8 ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.9817208 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
!
!PARABOLIC ORDER
TRACK := SNT: GEOM ::
      TITLE '3D ANISOTROPIC NSE BENCHMARK, 1 GROUP'
      EDIT 2 MAXR 5000 DIAM 2 SN 6 SCAT 2 QUAD 1 DSA 10 2 ;
SYSTEM := ASM: MACRO TRACK ::
      EDIT 1 ARM ;
FLUX :=  FLU: MACRO TRACK SYSTEM ::
      EDIT 2 TYPE K EXTE 100 5.0E-8 ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.9829153 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
*----
* DISCONTINUOUS GALERKIN
*----
!
!LINEAR ORDER
TRACK := SNT: GEOM ::
      TITLE '3D ANISOTROPIC DG LINEAR BENCHMARK, 1 GROUP'
      EDIT 2 MAXR 5000 SCHM 2 DIAM 1 SN 6 SCAT 2 QUAD 1 DSA 10 1 ;
SYSTEM := ASM: MACRO TRACK ::
      EDIT 1 ARM ;
FLUX :=  FLU: MACRO TRACK SYSTEM ::
      EDIT 2 TYPE K EXTE 100 5.0E-8 ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.9509456 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
!
!PARABOLIC ORDER
TRACK := SNT: GEOM ::
      TITLE '3D ANISOTROPIC DG PARABOLIC BENCHMARK, 1 GROUP'
      EDIT 2 MAXR 5000 SCHM 2 DIAM 2 SN 6 SCAT 2 QUAD 1 DSA 10 2 ;
SYSTEM := ASM: MACRO TRACK ::
      EDIT 1 ARM ;
FLUX :=  FLU: MACRO TRACK SYSTEM ::
      EDIT 2 TYPE K EXTE 100 5.0E-8 ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.9830175 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
!
!CUBIC ORDER
TRACK := SNT: GEOM ::
      TITLE '3D ANISOTROPIC DG CUBIC BENCHMARK, 1 GROUP'
      EDIT 2 MAXR 5000 SCHM 2 DIAM 3 SN 4 SCAT 2 QUAD 1 ;
SYSTEM := ASM: MACRO TRACK ::
      EDIT 1 ARM ;
FLUX :=  FLU: MACRO TRACK SYSTEM ::
      EDIT 2 TYPE K EXTE 100 5.0E-8 ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.9827218 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;

ECHO "test TCM49 completed" ;
END: ;
QUIT "LIST" .
