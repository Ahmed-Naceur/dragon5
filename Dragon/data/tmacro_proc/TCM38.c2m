*----
*  TEST CASE TCM38
*  EURYDICE HEXAGONAL CASES WITH VARIOUS MULTICELL APPRIXIMATIONS
*  ONE-GROUP MACROLIB-DEFINED CROSS SECTIONS
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST ASSMBH TRACK MACRO FLUX SYS EDIT ASSMB2 TRACK2 MACRO2 ;
MODULE GEO: SYBILT: MAC: ASM: FLU: EDI: BIVACT: SPH: DELETE: END: ;
PROCEDURE assertS ;
*
MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 5 NIFI 1
     READ INPUT
     MIX 1 (*VIDE*)
       TOTAL 0.0 SCAT 1 1 0.0
     MIX 2 (*MIDI*)
       TOTAL 1.0 SCAT 1 1 0.9
     MIX 3 (*CREU*)
       TOTAL 1.0E-4 SCAT 1 1 0.0
     MIX 4 (*MIFI*)
       TOTAL 1.0 SCAT 1 1 0.8
       NUSIGF 1.1 CHI 1.0
     MIX 5 (*NOIR*)
       TOTAL 1.0E5 SCAT 1 1 0.0
;
*
ASSMBH := GEO: :: HEX 1
          EDIT 2
          HBC COMPLETE REFL
          MIX HEXFGM
          ::: HEXFGM := GEO: HEXCEL 1
               SIDE 1.12045
               RADIUS 0.0 0.4
               MIX 4
                   2
          ;
          ;
TRACK := SYBILT: ASSMBH ::
       TITLE 'CAS MULTICELL SIMPLE'
       EDIT 6 MAXR 110 MAXJ 510 MAXZ 100000
       QUA2 12 6
       DP01
       ;
*
SYS := ASM: MACRO TRACK :: EDIT 1 PIJ ;
FLUX := FLU: SYS MACRO TRACK :: TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.717908 ;
*
ASSMBH TRACK SYS FLUX := DELETE: ASSMBH TRACK SYS FLUX ;
ASSMBH := GEO: :: HEX 1
          EDIT 2
          HBC COMPLETE REFL
          MIX HEXFGM
          ::: HEXFGM := GEO: HEXCEL 1
               SIDE 1.12045
               RADIUS 0.0 0.4
               SECT -1
               MIX 4 4 4 4 4 4
                   2 5 2 2 2 2
          ;
          ;
TRACK := SYBILT: ASSMBH ::
       TITLE 'CAS MULTICELL SIMPLE'
       EDIT 6 MAXR 110 MAXJ 510 MAXZ 100000
       QUA2 12 12
       DP01
       ;
SYS := ASM: MACRO TRACK :: EDIT 1 PIJ ;
FLUX := FLU: SYS MACRO TRACK :: TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.9242665 ;
*
ASSMBH TRACK SYS FLUX := DELETE: ASSMBH TRACK SYS FLUX ;
ASSMBH := GEO: :: HEX 4
          EDIT 2
          HBC S30 SYME
          MIX HEXFGM HEXFGM HEXNOI HEXFGM
          ::: HEXFGM := GEO: HEXCEL 1
               SIDE 1.12045
               RADIUS 0.0 0.4
               MIX 4
                   2
          ;
          ::: HEXNOI := GEO: HEXCEL 1
               SIDE 1.12045
               RADIUS 0.0 0.4
               SECT -1
               MIX 4 4 4 4 4 4
                   2 5 2 2 5 2
          ;
          ;
TRACK := SYBILT: ASSMBH ::
       TITLE 'CAS MULTICELL SIMPLE'
       EDIT 6 MAXR 110 MAXJ 510 MAXZ 100000
       QUA2 12 6
       DP01
       ;
*
SYS := ASM: MACRO TRACK :: EDIT 1 PIJ ;
FLUX := FLU: SYS MACRO TRACK :: TYPE K ;
assertS FLUX :: 'K-EFFECTIVE ' 1 1.097246 ;
*
ASSMBH TRACK SYS FLUX := DELETE: ASSMBH TRACK SYS FLUX ;
ASSMBH := GEO: :: HEX 7
          EDIT 2
          HBC COMPLETE REFL
          MIX HEXVGV HEXVGV HEXFGM HEXCREU HEXCREU HEXVGV HEXNOIR
          TURN   A  A  A  A  B  A  A 
          MERGE  1  2  3  4  4  5  6
          ::: HEXFGM := GEO: HEXCEL 2
               SIDE 1.12045
               RADIUS 0.0 0.2 1.01946
               SECT -1
               MIX 4 4 4 4 4 4
                   2 2 2 2 2 2
                   2 2 2 2 2 2
          ;
          ::: HEXVGV := GEO: HEXCEL 2
               SIDE 1.12045
               RADIUS 0.0 0.2 0.4
               SECT -1
               MIX 1 1 1 1 1 1
                   2 2 2 2 2 2
                   1 1 1 1 1 1
          ;
          ::: HEXCREU := GEO: HEXCEL 0
               SIDE 1.12045
               (*SECT -999*)
               MIX 3
          ;
          ::: HEXNOIR := GEO: HEXCEL 1 (*CELL WITH BLACK SECTOR*)
               SIDE 1.12045
               RADIUS 0.0 0.4
               SECT -1 1
               MIX 4
                   5 2 2 2 2 2
          ;
          ;
TRACK := SYBILT: ASSMBH ::
       TITLE 'CAS MULTICELL SIMPLE'
       EDIT 6 MAXR 110 MAXJ 510 MAXZ 100000
       QUA2 12 6
       DP01
       ;
*
SYS := ASM: MACRO TRACK :: EDIT 1 PIJ ;
FLUX := FLU: SYS MACRO TRACK :: TYPE K B0 SIGS BUCK 0.0 ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.8268365 ;
*----
* TRANSPORT-DIFFUSION EQUIVALENCE
*----
*    EDITION FOR HETEROGENEOUS DIFFUSION CALCULATIONS
*    CELL-BY-CELL SPH HOMOGENIZATION OF THE ASSEMBLY
EDIT := EDI: FLUX MACRO TRACK ASSMBH ::
     EDIT 3 MERG CELL SAVE ;
ASSMB2 := EDIT :: STEP UP 'MACRO-GEOM' ;
ASSMB2 := GEO: ASSMB2 :: SPLITL 2 ;
TRACK2 := BIVACT: ASSMB2 :: MAXR 100 DUAL (*IELEM=*) 2 (*ICOL=*) 2 ;
EDIT := SPH: EDIT TRACK2 ;
ASSMB2 TRACK2 := DELETE: ASSMB2 TRACK2 ;
MACRO2 := EDIT :: STEP UP 'REF-CASE0001' STEP UP MACROLIB
                  STEP UP GROUP STEP AT 1 ;
assertS MACRO2 :: 'NSPH' 3 0.95815 ;
*
ECHO "test TCM38 completed" ;
END: ;
QUIT "LIST" .
