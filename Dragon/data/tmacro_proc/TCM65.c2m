*----
*  TEST CASE TCM65
*  FMAC-M generated homogeneous macrolib
*  1D slab sn testcase from ROZ6
*  1D slab sn neutron-photon coupled testcase from ROZ6
*----
*  Define STRUCTURES and MODULES used
*----
MODULE GEO: FMAC: MAC: SNT: ASM: FLU: PSOUR: OUT: DELETE: END: ;
PROCEDURE assertS ;
LINKED_LIST  MACRO GEOM TRACK SYSTEM FLUX GMACRO GSOUR GFLUX GEDITS
             GWT0 ;
SEQ_ASCII FMAC_M :: FILE './caskmix_FMAC-M.txt' ;
*----
*  Set and track the geometry
*----
GEOM := GEO: :: CAR1D 5
           X- VOID  X+ VOID
           MIX  1 2 3 4 5
           MESHX 0.0 10.0 20.0 30.0 40.0 50.0
           SPLITX 5 5 5 5 5
           ;
TRACK := SNT: GEOM ::
      TITLE 'HOMOGENIZED CASKMIX BENCHMARK WITH FMAC-M'
      EDIT 2 MAXR 1000 SN 16 SCAT 4 ;
*----
*  Compute the neutron flux
*----
MACRO := FMAC: FMAC_M ::
  EDIT 2
  PARTICLE N
;
MACRO := MAC: MACRO ::
     READ INPUT
     MIX 1 FIXE
     1.0 1.0 1.0 1.0 1.0 1.0 1.0 1.0 1.0 1.0 1.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0
     ;
SYSTEM := ASM: MACRO TRACK :: EDIT 1 ARM ;
FLUX := FLU: MACRO TRACK SYSTEM :: EDIT 1 TYPE S EXTE 100 ;
SYSTEM := DELETE: SYSTEM ;
*----
*  Set the gamma source
*----
GMACRO := FMAC: FMAC_M ::
  EDIT 2
  PARTICLE G
;
GSOUR := PSOUR: GMACRO TRACK FLUX ::
   EDIT 1
   PARTICLE N
   ;
*----
*  Compute the gamma flux
*----
SYSTEM := ASM: GMACRO TRACK :: EDIT 1 ARM ;
GFLUX := FLU: GMACRO TRACK SYSTEM GSOUR :: EDIT 1 TYPE S EXTE 100 ;
GEDITS := OUT: GFLUX TRACK GMACRO GEOM ::
    EDIT 2 INTG MIX
    ;
*----
*  Asserts
*----
GWT0 := GEDITS :: STEP UP GROUP STEP AT 18 ;
assertS GWT0 :: 'FLUX-INTG' 1 7.89299488E-1 ;
assertS GWT0 :: 'FLUX-INTG' 2 8.66148663E+0 ;
assertS GWT0 :: 'FLUX-INTG' 3 5.81171036E-1 ;
assertS GWT0 :: 'FLUX-INTG' 4 1.09355543E-2 ;
assertS GWT0 :: 'FLUX-INTG' 5 3.78631288E-3 ;
*
ECHO "test TCM65 completed" ;
END: ;
