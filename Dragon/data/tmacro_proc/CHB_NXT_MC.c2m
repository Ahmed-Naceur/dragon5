*----------
* Definition des parametres de la procedure
* 
PARAMETER
  GEOMF 
  ::
    EDIT 0 
    ::: LINKED_LIST GEOMF ;
  ;
*
*  Lire pas de reseau
*
STRING
  state
  ;
INTEGER
  splitc splitm
  ;
INTEGER
   CF11  CF12  CF13  CF14 
   CF21i CF22i CF23i CF24i
   CF31i CF32i CF33i CF34i
   CF41i CF42i CF43i CF44i
   CF21e CF22e CF23e CF24e
   CF31e CF32e CF33e CF34e
   CF41e CF42e CF43e CF44e
   VF11  VF12  VF13  VF14 
   VF21i VF22i VF23i VF24i
   VF31i VF32i VF33i VF34i
   VF41i VF42i VF43i VF44i
   VF21e VF22e VF23e VF24e
   VF31e VF32e VF33e VF34e
   VF41e VF42e VF43e VF44e
   COOL  PT    HEG   CT
   MOD   SH    VOID
  ; 
::
  >>state<<
  >>splitc<< >>splitm<< 
  >>CF11<<  >>CF12<<  >>CF13<<  >>CF14<< 

  >>CF21i<< >>CF22i<< >>CF23i<< >>CF24i<<
  >>CF31i<< >>CF32i<< >>CF33i<< >>CF34i<<
  >>CF41i<< >>CF42i<< >>CF43i<< >>CF44i<<

  >>CF21e<< >>CF22e<< >>CF23e<< >>CF24e<<
  >>CF31e<< >>CF32e<< >>CF33e<< >>CF34e<<
  >>CF41e<< >>CF42e<< >>CF43e<< >>CF44e<<

  >>VF11<<  >>VF12<<  >>VF13<<  >>VF14<< 

  >>VF21i<< >>VF22i<< >>VF23i<< >>VF24i<<
  >>VF31i<< >>VF32i<< >>VF33i<< >>VF34i<<
  >>VF41i<< >>VF42i<< >>VF43i<< >>VF44i<<

  >>VF21e<< >>VF22e<< >>VF23e<< >>VF24e<<
  >>VF31e<< >>VF32e<< >>VF33e<< >>VF34e<<
  >>VF41e<< >>VF42e<< >>VF43e<< >>VF44e<<

  >>COOL<<  >>PT<<    >>HEG<<   >>CT<<
  >>MOD<<   >>SH<<    >>VOID<<
  ;  
* 
*  Structures de donnees locales et
*  modules et parametres
*
MODULE
  GEO:
  ;
INTEGER splitc1 splitc2 ;
INTEGER  cool1 cool2 ;
IF state "Cooled" = THEN
   EVALUATE splitc1 splitc2 := splitc splitc ;
   EVALUATE cool1 cool2 := COOL COOL ;
ELSEIF state "Voided" = THEN
   EVALUATE splitc1 splitc2 := 1 1 ;
   EVALUATE cool1 cool2 := VOID VOID ;
ELSE
   EVALUATE splitc1 splitc2 := 1 splitc ;
   EVALUATE cool1 cool2 := VOID COOL ;
ENDIF ;
*
*  Rayon Final =  PasReseau * sqrt(1/Pi)
*
REAL  
  PasReseau Rtpi    Rtpo     Rgapo    Rtco    := 
  22.0      5.16890 5.7689   7.5      7.8
  ;
INTEGER splitfx := 1 ;
* circular discretization of the moderator
REAL Diag := PasReseau 0.5 * Rtco -  ;
INTEGER splitm2 := splitm 1 + ;
REAL deltar := Diag splitm2 I_TO_R / ;
REAL temp := splitm I_TO_R deltar * ;
REAL RayonFinal := temp Rtco +  ;
* carteisan meshes in the moderator
REAL halfpas := PasReseau 0.5 * ;
REAL modx := halfpas Rtco - ;
REAL deltat := Rtco Rgapo - 0.5 * ;
INTEGER splitmx :=  modx deltar / R_TO_I ;
REAL deltax := modx splitmx I_TO_R / ;
REAL mesh1 := deltax 0.5 * ;
REAL mesh2 := mesh1 modx + deltax - ; 
EVALUATE splitmx := splitmx 1 - ;
REAL mesh3 := modx deltat + ;
REAL mesh4 := PasReseau mesh3 - ;
REAL mesh5 := PasReseau mesh2 - ;
REAL mesh6 := PasReseau mesh1 - ;
REAL 
  Rci   Rgi   Rce   Rge   
  sub1
  sub2
  sub3
  subis1
  subis2
  subis3
  subis4
  subis5
  :=
  0.627 0.675 0.533 0.575
  0.70710681186550
  0.89442719099992
  0.97467943448090
  0.63245553203368
  0.83666002653408
  0.89442719099992
  0.94868329805051
  0.97467943448090
  ;
* pin splitting
REAL hRgi hRge := 0.5 Rgi * 0.5 Rge * ;
REAL
  Rci1 Rci2 Rci3 Rce1 Rce2 Rce3
  Rcib1 Rcib2 Rcib3 Rcib4 Rcib5
  ;
EVALUATE
  Rci1 Rci2 Rci3 Rce1 Rce2 Rce3 
  Rcib1 Rcib2 Rcib3 Rcib4 Rcib5 :=
  Rci sub1 *
  Rci sub2 *
  Rci sub3 *
  Rce sub1 *
  Rce sub2 *
  Rce sub3 *
  Rci subis1 *
  Rci subis2 *
  Rci subis3 *
  Rci subis4 *
  Rci subis5 *
  ;
*
*  Geometry for flux calculation
*
GEOMF := GEO: ::  CAR2D 2 2
       X- REFL X+ REFL 
       Y- REFL Y+ REFL 
       CELL C1 C2
            C2 C1
*  voided cell
   ::: C1 := GEO: CARCEL 4 1 1 
       MESHX 0.0 <<PasReseau>> 
       MESHY 0.0 <<PasReseau>> 
       RADIUS  0.0  <<Rtpi>> <<Rtpo>> <<Rgapo>> <<Rtco>>
       MIX   <<cool1>>
             <<PT>>  <<HEG>>  <<CT>>    <<MOD>> 
       CLUSTER ROD1 ROD2 ROD3 ROD4
       ::: ROD1 := GEO: TUBE 5
            MIX <<VF11>>  <<VF12>>  <<VF13>>  <<VF14>> <<SH>> 
            RADIUS 0.00000 <<Rci1>> <<Rci2>> <<Rci3>>
                                     <<Rci>> <<Rgi>>
            NPIN  1 RPIN 0.0000 APIN 0.0000    ;
       ::: ROD2 := GEO: TUBE 5 1 2
            MESHY 0.0 <<hRgi>> <<Rgi>>
            MESHX 0.0 <<Rgi>>
            MIX <<VF21i>>  <<VF22i>>  <<VF23i>>  <<VF24i>> <<SH>>
                <<VF21e>>  <<VF22e>>  <<VF23e>>  <<VF24e>> <<SH>>
            RADIUS 0.00000 <<Rci1>> <<Rci2>> <<Rci3>>
                                    <<Rci>> <<Rgi>>
            NPIN  7 RPIN 1.7340 APIN 0.0000    ;
       ::: ROD3 := GEO: TUBE 5 1 2
            MESHY 0.0 <<hRge>> <<Rge>>
            MESHX 0.0 <<Rge>>
            MIX <<VF31i>>  <<VF32i>>  <<VF33i>>  <<VF34i>> <<SH>>
                <<VF31e>>  <<VF32e>>  <<VF33e>>  <<VF34e>> <<SH>>
            RADIUS 0.00000  <<Rce1>> <<Rce2>> <<Rce3>>
                                    <<Rce>> <<Rge>>
            NPIN 14 RPIN 3.0750 APIN 0.224399475  ;
       ::: ROD4 := GEO: TUBE 5 1 2
            MESHY 0.0 <<hRge>> <<Rge>>
            MESHX 0.0 <<Rge>>
            MIX <<VF41i>>  <<VF42i>>  <<VF43i>>  <<VF44i>> <<SH>>
                <<VF41e>>  <<VF42e>>  <<VF43e>>  <<VF44e>> <<SH>>
            RADIUS 0.00000  <<Rce1>> <<Rce2>> <<Rce3>>
                                    <<Rce>> <<Rge>>
            NPIN 21 RPIN 4.3840 APIN 0.0       ;
    ;
*   cooled cell
    ::: C2 := GEO: CARCEL 4 1 1 
       MESHX 0.0 <<PasReseau>> 
       MESHY 0.0 <<PasReseau>> 
       RADIUS  0.0 <<Rtpi>> 
               <<Rtpo>> <<Rgapo>> <<Rtco>> 
       MIX   <<cool2>>
             <<PT>>  <<HEG>>  <<CT>>  <<MOD>>
       CLUSTER ROD1 ROD2 ROD3 ROD4
       ::: ROD1 := GEO: TUBE 5
            MIX <<CF11>>  <<CF12>>  <<CF13>>  <<CF14>> <<SH>> 
            RADIUS 0.00000 <<Rci1>> <<Rci2>> <<Rci3>>
                                     <<Rci>> <<Rgi>>
            NPIN  1 RPIN 0.0000 APIN 0.0000    ;
       ::: ROD2 := GEO: TUBE 5 1 2
            MESHY 0.0 <<hRgi>> <<Rgi>>
            MESHX 0.0 <<Rgi>>
            MIX <<CF21i>>  <<CF22i>>  <<CF23i>>  <<CF24i>> <<SH>> 
                <<CF21e>>  <<CF22e>>  <<CF23e>>  <<CF24e>> <<SH>> 
            RADIUS 0.00000 <<Rci1>> <<Rci2>> <<Rci3>>
                                    <<Rci>> <<Rgi>>
            NPIN  7 RPIN 1.7340 APIN 0.0000    ;
       ::: ROD3 := GEO: TUBE 5 1 2
            MESHY 0.0 <<hRge>> <<Rge>>
            MESHX 0.0 <<Rge>>
            MIX <<CF31i>>  <<CF32i>>  <<CF33i>>  <<CF34i>> <<SH>> 
                <<CF31e>>  <<CF32e>>  <<CF33e>>  <<CF34e>> <<SH>>
            RADIUS 0.00000  <<Rce1>> <<Rce2>> <<Rce3>>
                                    <<Rce>> <<Rge>>
            NPIN 14 RPIN 3.0750 APIN 0.224399475  ;
       ::: ROD4 := GEO: TUBE 5 1 2
            MESHY 0.0 <<hRge>> <<Rge>>
            MESHX 0.0 <<Rge>>
            MIX <<CF41i>>  <<CF42i>>  <<CF43i>>  <<CF44i>> <<SH>> 
                <<CF41e>>  <<CF42e>>  <<CF43e>>  <<CF44e>> <<SH>> 
            RADIUS 0.00000  <<Rce1>> <<Rce2>> <<Rce3>>
                                    <<Rce>> <<Rge>>
            NPIN 21 RPIN 4.3840 APIN 0.0       ;
    ;
 ;
*  
*  terminer execution
*
QUIT "LIST" .
