*----
*  TEST CASE TCM15
*  VVER Assembly of central water hole with 6 identical fuel cells
*  NXT flux
*  Coarse mesh
*----
MODULE
  MAC: GEO: NXT: ASM: FLU: DELETE: END: ;
LINKED_LIST
  MACRO GEOMF TRACK PIJMAT FLUX ;
SEQ_BINARY 
  TRACK_LGT ;
PROCEDURE assertS ;
*----
* Macroscopic XS
*----
MACRO := MAC: ::
 NGRO 2 NMIX 4 NIFI 1
 READ INPUT
 MIX 1  TOTAL     2.60458171E-1 3.77224326E-1
        SCAT 2 2  5.98954648E-5 2.49342978E-1
             2 2  3.77127469E-1 1.11155845E-2
 MIX 2  TOTAL     1.49818063E-1 1.59792125E-1
        SCAT 2 2  7.40572286E-5 1.47693634E-1
             2 2  1.57371104E-1 1.30506000E-3
 MIX 3  TOTAL     3.22798014E-1 3.81341100E-1
        NUSIGF    5.46564534E-3 7.17375278E-2
        CHI       1.0           0.0
        SCAT 2 2  3.13575147E-4 3.11233580E-1
             2 2  3.24143648E-1 2.19577667E-3
 MIX 4  TOTAL     2.60458171E-1 3.77224326E-1
        SCAT 2 2  5.98954648E-5 2.49342978E-1
             2 2  3.77127469E-1 1.11155845E-2
 ;
*----
*  Geometries NXT
*----
GEOMF := GEO: :: HEX 7
  EDIT 2
  HBC COMPLETE REFL
  CELL CT  CFI CFI CFI CFI CFI CFI
  ::: CT := GEO: HEXTCEL 2 1
    SIDE 0.736121593
    SPLITR 3 1
    MIX 1 2 1
    RADIUS 0.0 0.386 0.445
    ;
  ::: CFI := GEO: HEXTCEL 2 1
    SIDE 0.736121593
    SPLITR 3 1
    MIX 3 2 1
    RADIUS 0.0 0.386 0.445
    ;
  ;
*----
*  Transport calculation
*  Flux calculation
*----
TRACK TRACK_LGT := NXT: GEOMF ::
  EDIT 3  TITL 'VVER-NXT: Flux'  
  TISO 50 100.0 ;
PIJMAT := ASM: MACRO TRACK TRACK_LGT ::
  EDIT 1 ;
FLUX := FLU: PIJMAT MACRO TRACK ::
  EDIT 1  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.055674 ;
TRACK TRACK_LGT GEOMF := DELETE: TRACK TRACK_LGT GEOMF ;
PIJMAT FLUX := DELETE: PIJMAT FLUX ;
MACRO := DELETE: MACRO ;
ECHO "test TCM15 completed" ;
END: ;
QUIT "LIST" .
