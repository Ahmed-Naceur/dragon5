*----
*  TEST CASE TCM66
*  FMAC-M generated homogeneous macrolib
*  1D slab sn electron testcase from ROZ6
*----
*  Define STRUCTURES and MODULES used
*----
MODULE GEO: FMAC: MAC: SNT: ASM: FLU: OUT: DELETE: END: ;
PROCEDURE assertS ;
LINKED_LIST  MACRO MACRO2 GEOM TRACK SYSTEM FLUX EEDITS1 GWT0 ;
SEQ_ASCII FMAC_M :: FILE './W_6MeV_e_CSD.txt' ;
*----
*  Set and track the geometry
*----
GEOM := GEO: :: CAR1D 2
           X- VOID  X+ VOID
           MIX  1 2
           MESHX 0.0 0.3 4.0
           SPLITX 30 70
           ;
TRACK := SNT: GEOM ::
      TITLE 'HOMOGENIZED W_6Mev_e_CSD BENCHMARK WITH FMAC-M'
      EDIT 2 MAXR 1000 DIAM 1 NLIVO SN 16 SCAT 16 EPSI 1.E-4
      MAXI 300 BFPL ;
*----
*  Compute the electron flux
*----
MACRO := FMAC: FMAC_M ::
  EDIT 2
  PARTICLE B
;
MACRO2 := MACRO ;
MACRO := MAC: MACRO MACRO2 ::
     MIX 2 1 OLDL
     ;
MACRO2 := DELETE: MACRO2 ;
REAL qsour := 1.E10 2.0 / $Pi_R / ;
ECHO "qsour=" qsour ;
MACRO := MAC: MACRO ::
     READ INPUT
     MIX 1 FIXE
     <<qsour>> 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0
     ;
SYSTEM := ASM: MACRO TRACK :: EDIT 2 ARM ;
FLUX := FLU: MACRO TRACK SYSTEM :: EDIT 1 TYPE S EXTE 100 ;
EEDITS1 := OUT: FLUX TRACK MACRO GEOM ::
    EDIT 2 COND SOUR 0.0518134 INTG NONE ! 1/(\ro_{W} g/cm^{3})
    ;
*----
*  Asserts
*----
GWT0 := EEDITS1 :: STEP UP GROUP STEP AT 1 ;
assertS GWT0 :: 'FLUX-INTG' 1 1.649578E-4 ;
assertS GWT0 :: 'FLUX-INTG' 5 3.512070E-4 ;
assertS GWT0 :: 'FLUX-INTG' 10 4.093370E-4 ;
assertS GWT0 :: 'FLUX-INTG' 15 4.156484E-4 ;
assertS GWT0 :: 'FLUX-INTG' 20 4.124526E-4 ;
assertS GWT0 :: 'FLUX-INTG' 25 3.760881E-4 ;
assertS GWT0 :: 'FLUX-INTG' 30 2.358792E-4 ;
*
ECHO "test TCM66 completed" ;
END: ;
