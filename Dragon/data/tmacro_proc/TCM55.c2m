*----
*  TEST CASE TCM55
*  9 X 9 PWR CASE WITH SYBIL AND ALBS-TYPE SPH EQUIVALENCE
*  MACROLIB-DEFINED CROSS SECTIONS
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST ASSMB TRACK MACRO SYS FLUX EDIT ASSMB2 MACRO2 TRACK2
            SYS2 FLUX2  ;
MODULE GEO: SYBILT: MAC: ASM: FLU: EDI: BIVACT: SPH: DELETE: END: ;
PROCEDURE assertS ;
*
MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 3 NIFI 1 ANIS 2
     READ INPUT
     MIX 1 (*EAU*)
        TOTAL 0.3683
        SCAT 1 1 0.3661 1 1 0.1212
     MIX 2 (*COMBUSTIBLE*)
        TOTAL 0.36522
        SCAT 1 1 0.3234 1 1 0.1100
        NUSIGF 0.09564      CHI 1.0
     MIX 3 (*ARGENT*)
        TOTAL 0.8453
        SCAT 1 1 0.5216 1 1 0.4212
     ;
ASSMB := GEO: :: CAR2D 6 6 (*ASSEMBLAGE 9 X 9*)
          X- DIAG X+ VOID
          Y- SYME Y+ DIAG
          CELL C1 C3 C2 C3 C3 C4
                  C3 C3 C3 C3 C4
                     C2 C3 C3 C4
                        C3 C3 C4
                           C3 C4
                              C5
          MERGE 1  2  3  4  5 14
                   6  7  8  9 15
                     10 11  9 15
                        12  9 15
                           13 16
                              17
          ::: C1 := GEO: CARCEL 1 (*CELLULE SANS COMBUSTIBLE*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 4.12660E-01
                MIX 1 1
          ;
          ::: C2 := GEO: CARCEL 3 (*CELLULE EMPOISONNEE*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 3.25296E-01 4.60039E-01 5.63430E-01
                MIX 3 3 3 1
          ;
          ::: C3 := GEO: CARCEL 1 (*CELLULE NORMALE*)
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                RADIUS 0.0 4.12660E-01
                MIX 2 1
          ;
          ::: C4 := GEO: CARCEL 0 (*LAME PERIPHERIQUE*)
                MESHX 1.262082 1.322082
                MESHY 0.0 1.262082
                MIX 1
          ;
          ::: C5 := GEO: CARCEL 0 (*LAME DE COIN*)
                MESHX 1.262082 1.322082
                MESHY 1.262082 1.322082
                MIX 1
          ;
          ;
TRACK := SYBILT: ASSMB ::
     EDIT 10
     MAXR 60  (*MAXIMUM OF 40 REGIONS*)
     TITLE 'TCM32: MULTICELL 9 X 9 PWR BENCHMARK WITH POISON'
     DP00
     ;
*
SYS := ASM: MACRO TRACK :: PIJ ALBS ;
FLUX := FLU: SYS MACRO TRACK ::
  TYPE B B1 ;
assertS FLUX :: 'K-INFINITY' 1 0.9951068 ;
*----
* TRANSPORT-DIFFUSION EQUIVALENCE (ALBS-TYPE SELENGUT)
*----
*    EDITION FOR HETEROGENEOUS DIFFUSION CALCULATIONS
*    CELL-BY-CELL SPH HOMOGENIZATION OF THE ASSEMBLY
ASSMB2 := GEO: :: CAR2D 5 5 (*HOMOGENIZED ASSEMBLY 9 X 9*)
          X- DIAG X+ REFL
          Y- SYME Y+ DIAG
          MIX 1  2  3  4  5
                 6  7  8  9
                   10 11  9
                      12  9
                         13
          MESHX 0.0  1.262082 2.524164 3.786246 5.048328 6.37041
          MESHY 0.0  1.262082 2.524164 3.786246 5.048328 6.37041
          ;
EDIT := EDI: FLUX MACRO TRACK ASSMB2 SYS ::
     EDIT 9 SAVE MERG REGI 1 1   2 2    3 3 3 3    4 4    5 5    6 6
                           7 7   8 8    9 9     10 10 10 10     11 11
                           12 12    13 13     5   9   13 13
     MGEO ASSMB2 ALBS
     ;
TRACK2 := BIVACT: ASSMB2 :: DUAL (*IELEM=*) 1 (*ICOL=*) 2 ;
EDIT := SPH: EDIT TRACK2 ::
     EDIT 9 SELE_ALB ITER 1.0E-5
     ;
*
* VERIFICATION CALCULATION (ALBS-TYPE SELENGUT)
ASSMB2 := DELETE: ASSMB2 ;
ASSMB2 := EDIT :: STEP UP 'MACRO-GEOM' ;
MACRO2 := EDIT :: STEP UP 'REF-CASE0001' STEP UP 'MACROLIB' ;
SYS2 := ASM: MACRO2 TRACK2 :: ARM ;
FLUX2 := FLU: MACRO2 TRACK2 SYS2 :: TYPE K LKRD BUCK 0.0 ;
assertS FLUX2 :: K-EFFECTIVE 1 0.9951417 ;
ASSMB2 MACRO2 TRACK2 EDIT SYS2 FLUX2
    := DELETE: ASSMB2 MACRO2 TRACK2 SYS2 FLUX2 EDIT ;
ECHO "test TCM55 completed" ;
END: ;
QUIT "LIST" .
