*----
*  TEST CASE TCM04
*  MACROSCOPIC CROSS SECTIONS
*  FISSION SOURCE PROBLEM
*  CANDU 3-D ADJUSTER ROD 1/8-ASSEMBLY
*
*  REF: none
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  BC TRACK  MACRO SYS  FLUX  EDITION 
     TRACK2       SYS2 FLUX2 EDITION2    ;
SEQ_BINARY
  BCTRK ;
MODULE
  GEO: EXCELT: MAC: ASM: FLU: EDI: DELETE: END: ;
PROCEDURE assertS ;
*----
* Macroscopic XS
*----
MACRO := MAC: ::
 NGRO 2 NMIX 4 NIFI 1
 READ INPUT
 MIX 1  TOTAL     3.22798014E-1 3.81341100E-1
        NUSIGF    5.46564534E-3 7.17375278E-2
        CHI       1.0           0.0
        SCAT 2 2  3.13575147E-4 3.11233580E-1
             2 2  3.24143648E-1 2.19577667E-3
 MIX 2  TOTAL     1.49818063E-1 1.59792125E-1
        SCAT 2 2  7.40572286E-5 1.47693634E-1
             2 2  1.57371104E-1 1.30506000E-3
 MIX 3  TOTAL     2.60458171E-1 3.77224326E-1
        SCAT 2 2  5.98954648E-5 2.49342978E-1
             2 2  3.77127469E-1 1.11155845E-2
 MIX 4  TOTAL     2.60458171E-1 3.77224326E-1
        SCAT 2 2  5.98954648E-5 2.49342978E-1
             2 2  3.77127469E-1 1.11155845E-2
 ;
*----
*  Geometry : BC - 3D Cartesian assembly with annular regions
*  Tracking : 1) EXCELT ALLG
*             2) EXCELT XCLL
*----
BC := GEO: :: CAR3D 3 2 2
  X- REFL X+ SYME Y- REFL Y+ SYME Z- REFL Z+ SYME
  CELL  M   MX  MX MX  FXY MXY   M   MX  BX MX  FXY BXY
  TURN  A   A   A  F   A   A     A   A   A  F   A   A
  ::: M := GEO: CAR3D 1 1 1
    MESHX  0.0 7.14375 MESHY 0.0 7.14375 MESHZ -8.25500 +8.25500
    SPLITZ 2 MIX 3
    ;
  ::: MX := GEO: M
    MESHX -7.14375 +7.14375 SPLITX 2
    ;
  ::: MXY := GEO: MX
    MESHY -7.14375 +7.14375 SPLITY 2
    ;
  ::: BX := GEO: CARCELY 2 1
    MESHX -7.14375 +7.14375 SPLITX  2
    MESHY  0.0     +7.14375
    MESHZ -8.25500 +8.25500 SPLITZ  2
    RADIUS 0.0 3.5100 3.8100
    MIX       3      4      3
    ;
  ::: BXY := GEO: BX
    MESHY -7.14375 +7.14375 SPLITY 2
    ;
  ::: FXY := GEO: CARCELZ 2 1
    MESHX -7.14375 +7.14375 SPLITX  2
    MESHY -7.14375 +7.14375 SPLITY  2
    MESHZ -8.25500 +8.25500 SPLITZ  2
    RADIUS 0.0 5.16890 6.58750
    MIX       1       2       3
    ;
  ;
TRACK BCTRK := EXCELT: BC ::
  TITLE 'TCM04: TWO GROUPS CANDU 3-D ADJUSTER ROD ASSEMBLY '
  MAXR 40 ALLG TRAK TISO 4 2.5
  ;
SYS := ASM: MACRO TRACK BCTRK ;

TRACK2 := EXCELT: BC ::
  TITLE 'TCM04: TWO GROUPS CANDU 3-D ADJUSTER ROD ASSEMBLY '
  MAXR 40 XCLL TRAK TISO 4 2.5
  ;
SYS2 := ASM: MACRO TRACK2 ;
*---- 
*  Solution : K-EFFECTIVE
*  Editing  : Compute reference reaction rates
*----
FLUX := FLU: SYS MACRO TRACK ::
  TYPE K
  ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.103927 ;
EDITION := EDI: MACRO TRACK FLUX ::
  EDIT 3 UPS  MERG COMP SAVE ON 'NOROD'
  ;
FLUX := FLU: FLUX SYS2 MACRO TRACK2 ::
  TYPE K
  ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.103921 ;
EDITION := EDI: EDITION MACRO TRACK2 FLUX ::
  EDIT 3 UPS  MERG COMP STAT ALL REFE 'NOROD'
  ;
EDITION2 := EDI: MACRO TRACK2 FLUX ::
  EDIT 3 UPS  MERG COMP SAVE ON 'NOROD'
  ;
SYS SYS2 := DELETE: SYS SYS2 ;
*----
*  Modify Macrolib for adjuster rod material
*  Solution : K-EFFECTIVE
*  Editing  : Compute Delta-Sigma
*----
MACRO := MAC: MACRO ::
  READ INPUT
  MIX 4  TOTAL     6.96358740E-1 1.12379551E+0
         SCAT 2 2  2.55611958E-4 6.77430272E-1
              2 2  9.55488145E-1 3.16311372E-3
  ;
SYS := ASM: MACRO TRACK BCTRK ;
SYS2 := ASM: MACRO TRACK2 ;
FLUX := FLU: FLUX SYS MACRO TRACK ::
  TYPE K
  ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.016438 ;
EDITION := EDI: EDITION MACRO TRACK FLUX ::
  EDIT 3 UPS  MERG COMP STAT DELS REFE 'NOROD'
  ;
FLUX := FLU: FLUX SYS2 MACRO TRACK2 ::
  TYPE K
  ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.016686 ;
EDITION2 := EDI: EDITION2 MACRO TRACK2 FLUX ::
  EDIT 3 UPS  MERG COMP STAT DELS REFE 'NOROD'
  ;
BCTRK := DELETE: BCTRK ;
ECHO "test TCM04 completed" ;
END: ;
QUIT "LIST" .
