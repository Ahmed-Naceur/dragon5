*----
*  TEST CASE TCM07
*  MACROSCOPIC CROSS SECTIONS
*  FISSION SOURCE PROBLEM
*  2-D CARTESIAN CELL 
*  REFLECTIVE AND VOID BOUNDARY CONDITIONS 
*
*  REF: none 
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST 
  MACRO LATGEOR LATREGR SYSR FLUXR EDITR 
        LATGEOV LATREGV SYSV FLUXV EDITV ;
SEQ_BINARY
  TRKR TRKV ;
MODULE
  MAC: GEO: EXCELT:  ASM: FLU: EDI: DELETE: END: ;
PROCEDURE assertS ;
*----
* Macroscopic XS
*----
MACRO := MAC: ::
  NGRO 1 NMIX 2 NIFI 1
  READ INPUT 
  MIX  1 TOTAL 0.75 SCAT 1 1 0.50 NUSIGF 1.00  CHI 1.0                
  MIX  2 TOTAL 0.75 SCAT 1 1 0.50                
  ;
*----
*  Geometry : LATGEOR - Cartesian 2D with reflection BC
*             LATGEOR - Cartesian 2D with void BC
*  Tracking : EXCELT 
*----
LATGEOR  := GEO: :: CAR2D 2 2
  X- REFL X+ REFL MESHX 0.00 1.0 2.00 SPLITX  4 4  
  Y- REFL Y+ REFL MESHY 0.00 1.0 2.00 SPLITY  4 4
  MIX  1  2  2  2  ;
LATGEOV  := GEO: LATGEOR  ::
  X- VOID X+ VOID Y- VOID Y+ VOID  ;
LATREGR TRKR := EXCELT: LATGEOR ::
  TITLE 'LATHROP *** P1 ANISOTROPE '
  MAXR 64 TRAK TISO 49 20.0  ;
LATREGV TRKV := EXCELT: LATGEOV ::
  TITLE 'LATHROP *** P1 ANISOTROPE '
  MAXR 64 TRAK TISO 49 20.0  ;
*---- 
*  Solution : TYPE K
*----
SYSR := ASM: MACRO LATREGR TRKR ;
FLUXR := FLU: SYSR MACRO LATREGR :: 
  TYPE K ACCE 3 0 ;
assertS FLUXR :: 'K-EFFECTIVE' 1 1.687038 ;
EDITR := EDI: MACRO LATREGR FLUXR ::
  EDIT 1 ;
SYSV := ASM: MACRO LATREGV TRKV ;
FLUXV := FLU: SYSV MACRO LATREGV :: 
  TYPE K ACCE 3 0 ;
assertS FLUXV :: 'K-EFFECTIVE' 1 0.6779595 ;
EDITV := EDI: MACRO LATREGV FLUXV ::
  EDIT 1 ;
LATGEOR LATREGR SYSR FLUXR EDITR TRKR
   LATGEOV LATREGV SYSV FLUXV EDITV TRKV := DELETE: 
LATGEOR LATREGR SYSR FLUXR EDITR TRKR
   LATGEOV LATREGV SYSV FLUXV EDITV TRKV ;
MACRO := DELETE: MACRO ;
ECHO "test TCM07 completed" ;
END: ;
QUIT "LIST" .
