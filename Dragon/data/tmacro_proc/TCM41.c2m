*---
* Regression test 1 for MCCG: and MOCC:
*---
* TEST CASE TCM41
* 2D ANISOTROPIC SCATTERING BENCHMARK WITH VOID
*---

*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM MACRO TRACK SYSTEM FLUX EDIT ;
SEQ_BINARY TRK_FIL ;
MODULE GREP: GEO: MAC: EXCELT: MCCGT: ASM: FLU: DELETE: END: ;
PROCEDURE assertS ;
INTEGER s := 2 ;
REAL kaniso kiso ;
REAL hdd ;
INTEGER stis ;
*---
* Geometry Geom
*---
GEOM := GEO: :: CAR2D  5 5
           X- REFL X+ VOID
           Y- REFL Y+ VOID
           MIX  1 1 2 2 3
                1 1 2 2 3
                2 2 2 2 3
                2 2 2 3 3
                3 3 3 3 4
           MESHX 0.0 40.0 80.0 120.0 160.0 200.0
           MESHY 0.0 40.0 80.0 120.0 160.0 200.0
           SPLITX <<s>> <<s>> <<s>> <<s>> <<s>>
           SPLITY <<s>> <<s>> <<s>> <<s>> <<s>>
           ;
*---
*  Macrolib
*--
MACRO := MAC: ::
 EDIT 0 NGRO 1 NMIX 4 NIFI 1 ANIS 2
 READ INPUT
 MIX     1
     TOTAL  0.025
    NUSIGF  0.0155
       CHI  1.0
      SCAT  1 1 0.013
            1 1 0.0
 MIX     2
     TOTAL  0.025
     SCAT   1 1 0.024
            1 1 0.006
 MIX     3
     TOTAL  0.075
     SCAT   1 1 0.0
            1 1 0.0
 MIX     4
     TOTAL  1E-8
     SCAT   1 1 0.0
            1 1 0.0
 ;

EVALUATE stis := -1 ;
WHILE stis 2 < DO 
EVALUATE hdd := 10.0 ;
WHILE hdd -1.0 > DO
*----
*  MCCG (isotropic BC)
*----
**************************
* ANISOTROPIC CALCULATION
**************************
*----
*  Tracking calculation 
*----
TRACK TRK_FIL := EXCELT: GEOM ::
  TITLE '2D ANISOTROPIC NSE BENCHMARK, 1 GROUP'
  EDIT 0 MAXR 2500 ANIS 2 TRAK TISO 8 5.0 ;
*---
*  Flux Calculation
*---
TRACK := MCCGT: TRACK TRK_FIL ::
  EDIT 1 DGAU 4
  EPSI 1E-6 HDD <<hdd>> STIS <<stis>>
  MAXI 10 KRYL 0 SCR 1 AAC 0 ;
SYSTEM := ASM: MACRO TRACK TRK_FIL ::
  EDIT 0 ARM ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYSTEM ::
  EDIT 2 TYPE K EXTE 100 1E-6 ;
IF hdd 0.0 > THEN
assertS FLUX :: 'K-EFFECTIVE' 1 1.000700 ; ! DD scheme
ELSE
assertS FLUX :: 'K-EFFECTIVE' 1 9.981245E-01 ; ! SC scheme
ENDIF ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>kaniso<< ; 
TRACK TRK_FIL SYSTEM FLUX := DELETE:
TRACK TRK_FIL SYSTEM FLUX ;

************************
* ISOTROPIC CALCULATION
************************
*----
*  Tracking calculation 
*----
TRACK TRK_FIL := EXCELT: GEOM ::
  TITLE '2D ISOTROPIC NSE BENCHMARK, 1 GROUP'
  EDIT 0 MAXR 2500 ANIS 1 TRAK TISO 8 5.0 ;
*---
*  Flux Calculation
*---
TRACK := MCCGT: TRACK TRK_FIL ::
  EDIT 1 DGAU 4
  EPSI 1E-6 HDD <<hdd>> STIS <<stis>>
  MAXI 10 KRYL 0 SCR 0 AAC 1 TMT  ;
SYSTEM := ASM: MACRO TRACK TRK_FIL ::
  EDIT 0 ARM ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYSTEM ::
  EDIT 2 TYPE K EXTE 100 1E-6 ;
IF hdd 0.0 > THEN
assertS FLUX :: 'K-EFFECTIVE' 1 1.023443 ; ! DD scheme
ELSE
assertS FLUX :: 'K-EFFECTIVE' 1 1.020310 ; ! SC scheme
ENDIF ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>kiso<< ; 
ECHO "**********MCCG***********" ;
ECHO "DELTA = K_ANISO - K_ISO ="
kaniso "-" kiso "=" kaniso kiso - 1E3 * "mk" ;
ECHO "FOR INTEGRATION STRATEGY:" stis ;
ECHO "FOR HDD PARAMETER:" hdd ;
ECHO "*************************" ;
EVALUATE hdd  := hdd  10.0 - ;
*---
* Memory Release
*---
TRACK TRK_FIL SYSTEM FLUX := DELETE:
TRACK TRK_FIL SYSTEM FLUX ;
ENDWHILE ; 
EVALUATE stis := stis 1 + ;
ENDWHILE ; 

EVALUATE stis := -1 ;
WHILE stis 2 < DO 
EVALUATE hdd := 10.0 ;
WHILE hdd -1.0 > DO
*----
*  MOCC (cyclic tracking)
*----
**************************
* ANISOTROPIC CALCULATION
**************************
*----
*  Tracking calculation 
*----
TRACK TRK_FIL := EXCELT: GEOM ::
  TITLE '2D ANISOTROPIC NSE BENCHMARK, 1 GROUP'
  EDIT 0 MAXR 2500 ANIS 2 TRAK TSPC 8 5.0 ;
*---
*  Flux Calculation
*---
TRACK := MCCGT: TRACK TRK_FIL ::
  EDIT 1 DGAU 4
  EPSI 1E-6 HDD <<hdd>> STIS <<stis>>
  MAXI 10 KRYL 10 SCR 1 AAC 0  ;
SYSTEM := ASM: MACRO TRACK TRK_FIL ::
  EDIT 0 ARM ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYSTEM ::
  EDIT 2 TYPE K EXTE 100 1E-6 ;
IF hdd 0.0 > THEN
assertS FLUX :: 'K-EFFECTIVE' 1 9.845409E-01 ;
ELSE
assertS FLUX :: 'K-EFFECTIVE' 1 9.814614E-01 ;
ENDIF ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>kaniso<< ; 
TRACK TRK_FIL SYSTEM FLUX := DELETE:
TRACK TRK_FIL SYSTEM FLUX ;

************************
* ISOTROPIC CALCULATION
************************
*----
*  Tracking calculation 
*----
TRACK TRK_FIL := EXCELT: GEOM ::
  TITLE '2D ISOTROPIC NSE BENCHMARK, 1 GROUP'
  EDIT 0 MAXR 2500 ANIS 1 TRAK TSPC 8 5.0 ;
*---
*  Flux Calculation
*---
TRACK := MCCGT: TRACK TRK_FIL ::
  EDIT 1 DGAU 4
  EPSI 1E-6 HDD <<hdd>> STIS <<stis>>
  MAXI 10 KRYL -10 SCR 0 AAC 1 TMT  ;
SYSTEM := ASM: MACRO TRACK TRK_FIL ::
  EDIT 0 ARM ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYSTEM ::
  EDIT 2 TYPE K EXTE 100 1E-6 ;
IF hdd 0.0 > THEN
assertS FLUX :: 'K-EFFECTIVE' 1 1.010862 ;
ELSE
assertS FLUX :: 'K-EFFECTIVE' 1 1.007189 ;
ENDIF ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>kiso<< ; 
ECHO "**********MOCC***********" ;
ECHO "DELTA = K_ANISO - K_ISO ="
kaniso "-" kiso "=" kaniso kiso - 1E3 * "mk" ;
ECHO "FOR INTEGRATION STRATEGY:" stis ;
ECHO "FOR HDD PARAMETER:" hdd ;
ECHO "*************************" ;
EVALUATE hdd  := hdd  10.0 - ;
*---
* Memory Release
*---
TRACK TRK_FIL SYSTEM FLUX := DELETE:
TRACK TRK_FIL SYSTEM FLUX ;
ENDWHILE ; 
EVALUATE stis := stis 1 + ;
ENDWHILE ; 

GEOM MACRO := DELETE: GEOM MACRO ;
ECHO "test TCM41 completed" ;
END: ;
