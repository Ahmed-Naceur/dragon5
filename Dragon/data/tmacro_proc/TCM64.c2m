*----
*  TEST CASE TCM64
*  FMAC-M generated homogeneous macrolib
*  1D spherical sn testcase from ROZ6
*----
*  Define STRUCTURES and MODULES used
*----
MODULE GEO: FMAC: SNT: ASM: FLU: END: ;
PROCEDURE assertS ;
LINKED_LIST  MACRO GEOM TRACK SYSTEM FLUX ;
SEQ_ASCII FMAC_M :: FILE './Godiva_2001_Bugle96_47n.txt' ;
*----
*  Set and track the geometry
*----
GEOM := GEO: :: SPHERE  1
           R+ VOID
           MIX  1
           RADIUS 0.0 8.7407
           SPLITR 35
           ;
TRACK := SNT: GEOM ::
      TITLE 'HOMOGENIZED GODIVA BENCHMARK WITH FMAC-M'
      EDIT 2 MAXR 100 SN 16 SCAT 6 ;
*----
*  Compute the neutron flux
*----
MACRO := FMAC: FMAC_M ::
  EDIT 2
  PARTICLE N
;
SYSTEM := ASM: MACRO TRACK :: EDIT 1 ARM ;
FLUX := FLU: MACRO TRACK SYSTEM :: TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.9963791 ;
*
ECHO "test TCM64 completed" ;
END: ;
