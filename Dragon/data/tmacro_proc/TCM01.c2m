*----
* TEST CASE TCM01
* MACROSCOPIC CROSS SECTIONS
* FISSION SOURCE PROBLEM
* 1-D ANNULAR CELL
*
* REF: none
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST 
  MACRO ANGEO TRACK SYS FLUX EDITION ;
SEQ_ASCII
  res ;
MODULE
  GEO: SYBILT: MAC: ASM: FLU: EDI: DELETE: END: ;
PROCEDURE assertS ;
*----
*  Macroscopic XS
*----
MACRO := MAC: ::
  NGRO 2 NMIX 2 NIFI 1
  READ INPUT
  MIX 1   TOTAL 0.222222 0.833333
    SCAT   1 1  0.19222          2 2 0.75333 0.02
    NUSIGF  0.0  0.135 CHI 1.0 0.0
  MIX 2   TOTAL 0.166667 1.111111
    SCAT   2 2  0.00015 0.126667 2 2 1.10111 0.04 
  ;
*----
*  Geometry :  ANGEO - Annular 2 regions
*----
ANGEO := GEO: :: TUBE 2
  R+ REFL
  RADIUS 0.0 0.19653 1.0
  MIX        1       2
  SPLITR     1       4    ;
*---- 
*  Tracking : SYBILT
*  Solution : PIJ
*    1- KEFF WITHOUT BUCKLING
*    2- BUCKLING WITH KEFF=1
*    3- LEAKAGE WITH KEFF=1
*----
TRACK := SYBILT: ANGEO :: 
  TITLE 'TCM01: ANNULAR GEOMETRY WITH MACROSCOPIC XS (SYBIL)'
  EDIT 1 MAXR 5 QUA1 5 ;
SYS := ASM: MACRO TRACK ;
FLUX := FLU:      SYS MACRO TRACK :: 
  TYPE K ;
assertS FLUX :: 'K-INFINITY' 1 4.048651E-01 ;
EDITION := EDI: MACRO TRACK FLUX ::
  EDIT 3 SAVE ;
FLUX := FLU: FLUX SYS MACRO TRACK ::
  TYPE B B0 ;
assertS FLUX :: 'K-INFINITY' 1 4.060840E-01 ;
EDITION := EDI: EDITION MACRO TRACK FLUX ::
  EDIT 3 SAVE ;
FLUX := FLU: FLUX SYS MACRO TRACK ::
  TYPE L B0 ;
assertS FLUX :: 'K-INFINITY' 1 4.060870E-01 ;
EDITION := EDI: EDITION MACRO TRACK FLUX ::
  EDIT 3 SAVE ;
FLUX SYS TRACK := DELETE: FLUX SYS TRACK ;
ECHO "test TCM01 completed" ;
END: ;  
QUIT "LIST" .
