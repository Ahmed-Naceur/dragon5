*----
*  TEST CASE TCM71
*  DG1 for Boltzmann Fokker-Planck
*----
*  Define STRUCTURES and MODULES used
*----
MODULE GEO: FMAC: MAC: SNT: ASM: FLU: OUT: DELETE: END: ;
PROCEDURE assertS ;
LINKED_LIST  MACRO MACRO2 GEOM TRACK SYSTEM FLUX EEDITS GWT0 ;
SEQ_ASCII FMAC_M :: FILE './Al_1MeV_CSD_P7.txt' ;
*----
*  Set and track the geometry
*----
GEOM := GEO: :: CAR1D 2
           X- VOID  X+ VOID
           MIX  1 2
           MESHX 0.0 0.03 0.1
           SPLITX 30 70
           ;
TRACK := SNT: GEOM ::
      TITLE 'HOMOGENIZED Al_1MeV_CSD_P7 BENCHMARK WITH FMAC-M'
      EDIT 2 MAXR 10000 NLIVO SN 4 SCAT 1 EPSI 1.E-4 MAXI 300
      BFPG DIAM 1 SCHM 2 ;
*----
*  Compute the electron flux
*----
MACRO := FMAC: FMAC_M ::
  EDIT 2
  PARTICLE B
;
MACRO2 := MACRO ;
MACRO := MAC: MACRO MACRO2 ::
     MIX 2 1 OLDL
     ;
MACRO2 := DELETE: MACRO2 ;
REAL qsour := 1.E10 2.0 / $Pi_R / ;
ECHO "qsour=" qsour ;
MACRO := MAC: MACRO ::
     READ INPUT
     MIX 1 FIXE
     <<qsour>> 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     ;
SYSTEM := ASM: MACRO TRACK :: EDIT 1 ARM ;
FLUX := FLU: MACRO TRACK SYSTEM :: EDIT 1 TYPE S EXTE 100 ;
EEDITS := OUT: FLUX TRACK MACRO GEOM ::
    EDIT 2 COND SOUR 0.3703703 INTG NONE ! 1/(\ro_{Al} g/cm^{3})
    ;
*----
*  Asserts
*----
GWT0 := EEDITS :: STEP UP GROUP STEP AT 1 ;
assertS GWT0 :: 'FLUX-INTG' 1 3.77170E-4 ;
assertS GWT0 :: 'FLUX-INTG' 37 7.70624E-4 ;
assertS GWT0 :: 'FLUX-INTG' 98 4.21483E-7 ;
*
ECHO "test TCM71 completed" ;
END: ;
