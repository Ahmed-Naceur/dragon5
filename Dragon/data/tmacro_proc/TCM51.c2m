*----
*  TEST CASE TCM51
*  3-REGION PIJ AND SN WITH DOUBLE HETEROGENEITY (BIHET) MIXTURES
*  ONE-GROUP MACROLIB-DEFINED CROSS SECTIONS
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST MACRO ASSMB TRACK SYS FLUX EDIT ;
MODULE GEO: SYBILT: SNT: NXT: MAC: ASM: FLU: UTL: DELETE:
       GREP:
       END: ;
REAL keff ;
SEQ_BINARY TRACK_FIL ;
PROCEDURE assertS ;
*
MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 4 NIFI 1
     READ INPUT
     MIX 1
        TOTAL 0.41       SCAT 1 1 0.3
        NUSIGF 0.1       CHI 1.0
     MIX 2
        TOTAL 0.45       SCAT 1 1 0.347
        NUSIGF 0.17      CHI 1.0
     MIX 3
        TOTAL 0.53       SCAT 1 1 0.5
     MIX 4
        TOTAL 0.3        SCAT 1 1 0.2
        NUSIGF 0.1       CHI 1.0
     ;
ASSMB := GEO: :: TUBE 3
          R+ REFL
          MIX 5 4 6
          RADIUS 0.0 1.0 3.0 5.0
     BIHET SPHE (*NG=*) 2 (* NMILG= *) 2 (* SPHERICAL MICRO-STRUCTURE *)
     (* NS= *) 1 1
     (* M-S-1 *) 0.0 0.3 (* M-S 2 *) 0.0 0.5
     (* COMPOSITE MIXTURES *)  5 6
     (* MIXTURES SURROUNDING M-S *) 3 1
     (* COMPOSITE MIXTURE 5 FRACT *) 0.4 0.0
     (* REAL MIXTURE CONTENT M-S-1 *) 3
     (* COMPOSITE MIXTURE 6 FRACT *) 0.0 0.3
     (* REAL MIXTURE CONTENT M-S-1 *) 2
          ;
*----
*  Collision probability method
*----
TRACK := SYBILT: ASSMB ::
     EDIT 1
     MAXR 20
     HEBE QUAB 7
     TITLE 'TEST'
     ;
*
SYS := ASM: MACRO TRACK :: EDIT 99 PIJ ;
FLUX := FLU: MACRO TRACK SYS :: TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.074210 ;
TRACK SYS FLUX := DELETE: TRACK SYS FLUX ;
*----
*  Discrete ordinates method HEBE
*----
TRACK := SNT: ASSMB ::
     EDIT 2 
     DIAM 0 SN 8 SCAT 1
     HEBE QUAB 7
     TITLE 'TEST'
     ;
*
SYS := ASM: MACRO TRACK :: EDIT 99 ARM ;
FLUX := FLU: MACRO TRACK SYS :: EDIT 99 TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.073958 ;
TRACK SYS FLUX := DELETE: TRACK SYS FLUX ;
*
*----
*  Collision probability method SAPO
*----
TRACK := SYBILT: ASSMB ::
     EDIT 1
     MAXR 20
     SAPO QUAB 7
     TITLE 'TEST'
     ;
*
SYS := ASM: MACRO TRACK :: EDIT 99 PIJ ;
FLUX := FLU: MACRO TRACK SYS :: TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.074108 ;
TRACK SYS FLUX := DELETE: TRACK SYS FLUX ;
*----
*  Discrete ordinates method SAPO
*----
TRACK := SNT: ASSMB ::
     EDIT 2 
     DIAM 0 SN 8 SCAT 1
     SAPO QUAB 7
     TITLE 'TEST'
     ;
*
SYS := ASM: MACRO TRACK :: EDIT 99 ARM ;
FLUX := FLU: MACRO TRACK SYS :: EDIT 99 TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.073781 ;
TRACK SYS FLUX := DELETE: TRACK SYS FLUX ;
*
*----
*  Collision probability method SLSI
*----
TRACK := SYBILT: ASSMB ::
     EDIT 1
     MAXR 20
     SLSI 0.65 QUAB -100
     TITLE 'TEST'
     ;
*
SYS := ASM: MACRO TRACK :: EDIT 99 PIJ ;
FLUX := FLU: MACRO TRACK SYS :: TYPE K ;
GREP: FLUX :: GETVAL 'K-INFINITY  ' 1 1 1 >>keff<< ;
ECHO 'K-INFINITY  ' keff ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.068906 ;
TRACK SYS FLUX := DELETE: TRACK SYS FLUX ;
*----
*  Discrete ordinates method SLSI
*----
TRACK := SNT: ASSMB ::
     EDIT 2 
     DIAM 0 SN 8 SCAT 1
     SLSI 0.65 QUAB -100
     TITLE 'TEST'
     ;
*
SYS := ASM: MACRO TRACK :: EDIT 99 ARM ;
FLUX := FLU: MACRO TRACK SYS :: EDIT 99 TYPE K ;
GREP: FLUX :: GETVAL 'K-INFINITY  ' 1 1 1 >>keff<< ;
ECHO 'K-INFINITY  ' keff ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.068608 ;
TRACK SYS FLUX := DELETE: TRACK SYS FLUX ;
ECHO "test TCM51 completed" ;
END: ;
QUIT "LIST" .
