*----
*  TEST CASE TCM56
*  CNS 2D CARTESIAN PROBLEM
*  ONE-GROUP MACROLIB-DEFINED CROSS SECTIONS
*
*  Ref: A. Calloo and A. Hebert, CNS 2019
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM MACRO TRACK SYSTEM FLUX EDIT ;
MODULE GEO: MAC: SNT: ASM: FLU: UTL: END: ;
PROCEDURE assertS ;
*
* s = region split
* sch = scheme parameter
* spa = spatial order
* and = sn order
* sca = scattering
*
INTEGER s := 6 ;
INTEGER sch := 2 ;
INTEGER spa := 3 ;
INTEGER ang := 18 ; 
INTEGER sca := 2 ;
*
GEOM := GEO: :: CAR2D  5 5
           X- REFL X+ VOID
           Y- REFL Y+ VOID
           MIX  1 1 2 2 3
                1 1 2 2 3
                2 2 2 2 3
                2 2 2 3 3
                3 3 3 3 0
           MESHX 0.0 40.0 80.0 120.0 160.0 200.0
           MESHY 0.0 40.0 80.0 120.0 160.0 200.0
           SPLITX <<s>> <<s>> <<s>> <<s>> <<s>>
           SPLITY <<s>> <<s>> <<s>> <<s>> <<s>>
           ;
MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 3 NIFI 1 ANIS 2
 READ INPUT
 MIX     1
     TOTAL  0.025
    NUSIGF  0.0155
       CHI  1.0
      SCAT  1 1 0.013
            1 1 0.0
 MIX     2
     TOTAL  0.025
      SCAT  1 1 0.024
            1 1 0.006
 MIX     3
     TOTAL  0.075
      SCAT  1 1 0.0
            1 1 0.0
 ;

TRACK := SNT: GEOM ::
      TITLE '2D ANISOTROPIC CNS DG BENCHMARK, 1 GROUP'
      EDIT 2 MAXR 25000
      SCHM <<sch>> 
      DIAM <<spa>> 
      SN <<ang>> 
      SCAT <<sca>> 
      MAXI 300 EPSI 1.E-5
      QUAD 2 (*Level-Symmetric*) ;

SYSTEM := ASM: MACRO TRACK :: EDIT 2 ARM ;

FLUX :=  FLU: MACRO TRACK SYSTEM ::
      EDIT 2 TYPE K EXTE 100 1E-6 ;

assertS FLUX :: 'K-EFFECTIVE' 1 9.922985E-01 ;

ECHO "test TCM56 completed" ;
END: ;
QUIT "LIST" .
