*---
* Regression test for MCCG:
*---
* TEST CASE TCM44
* PRISMATIC 3D ANISOTROPIC SCATTERING BENCHMARK WITH VOID
* MACROLIB-DEFINED CROSS SECTIONS
*---

*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM MACRO TRACK SYSTEM FLUX EDIT ;
SEQ_BINARY TRK_FIL ;
MODULE GREP: GEO: MAC: EXCELT: NXT: MCCGT: ASM: FLU: DELETE: END: ;
PROCEDURE assertS ;
REAL kaniso_n kiso_n kaniso_e kiso_e ;
INTEGER stis ;
*---
* Geometry Geom
*---
GEOM := GEO: :: CAR3D 5 5 2
           X- REFL X+ VOID
           Y- REFL Y+ VOID
           Z- REFL Z+ REFL
           CELL C1 C1 C2 C2 C3
                C1 C1 C2 C2 C3
                C2 C2 C2 C2 C3
                C2 C2 C2 C3 C3
                C3 C3 C3 C3 C0
                C1 C1 C2 C2 C3
                C1 C1 C2 C2 C3
                C2 C2 C2 C2 C3
                C2 C2 C2 C3 C3
                C3 C3 C3 C3 C0
        ::: C1 := GEO: CAR3D 1 1 1
                MESHX 0.0 4.0 
                MESHY 0.0 4.0 
                MESHZ 0.0 1.0
                MIX 1
            ;
        ::: C2 := GEO: C1
                MIX 2
            ;
        ::: C3 := GEO: C1
                MIX 3
            ;
        ::: C0 := GEO: C1
                MIX 4
            ;
           ;
*---
*  Macrolib
*--
MACRO := MAC: ::
 EDIT 0 NGRO 1 NMIX 4 NIFI 1 ANIS 2
 READ INPUT
 MIX     1
     TOTAL  0.25
    NUSIGF  0.155
       CHI  1.0
      SCAT  1 1 0.13
            1 1 0.07
 MIX     2
     TOTAL  0.25
     SCAT   1 1 0.24
            1 1 0.15
 MIX     3
     TOTAL  0.75
     SCAT   1 1 0.0
            1 1 0.0
 MIX     4
     TOTAL  1E-8
     SCAT   1 1 0.0
            1 1 0.0
 ;
EVALUATE stis := -1 ;

*****************************
* NXT ANISOTROPIC CALCULATION
*****************************
*----
*  Tracking calculation 
*----
TRACK TRK_FIL := NXT: GEOM ::
  TITLE '3D ANISOTROPIC BENCHMARK, 1 GROUP'
  EDIT 0 ANIS 2 PRIZ 2.0 GAUS 4 TISO 4 5.0 ;
*---
*  Flux Calculation
*---
TRACK := MCCGT: TRACK TRK_FIL ::
  EDIT 1
  EPSI 1E-6 HDD 0.0
  MAXI 10 KRYL 10 SCR 1 AAC 1 TMT STIS <<stis>> ;
SYSTEM := ASM: MACRO TRACK TRK_FIL ::
  EDIT 0 ARM ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYSTEM ::
  EDIT 2 TYPE K EXTE 100 1E-5 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.009687E+00 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>kaniso_n<< ; 
TRACK TRK_FIL SYSTEM FLUX := DELETE:
TRACK TRK_FIL SYSTEM FLUX ;

***************************
* NXT ISOTROPIC CALCULATION
***************************
*----
*  Tracking calculation 
*----
TRACK TRK_FIL := NXT: GEOM ::
  TITLE '3D ISOTROPIC BENCHMARK, 1 GROUP'
  EDIT 0 ANIS 1 PRIZ 2.0 GAUS 4 TISO 4 5.0 ;
*---
*  Flux Calculation
*---
TRACK := MCCGT: TRACK TRK_FIL ::
  EDIT 1
  EPSI 1E-6 HDD 0.0
  MAXI 10 KRYL -10 SCR 0 AAC 1 TMT STIS <<stis>> ;
SYSTEM := ASM: MACRO TRACK TRK_FIL ::
  EDIT 0 ARM ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYSTEM ::
  EDIT 2 TYPE K EXTE 100 1E-5 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.035233E+00 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>kiso_n<< ; 
TRACK TRK_FIL SYSTEM FLUX := DELETE:
TRACK TRK_FIL SYSTEM FLUX ;

********************************
* EXCELT ANISOTROPIC CALCULATION
********************************
*----
*  Tracking calculation 
*----
TRACK TRK_FIL := EXCELT: GEOM ::
  TITLE '3D ANISOTROPIC BENCHMARK, 1 GROUP'
  EDIT 0 MAXR 1000 ANIS 2 PRIZ 2.0 GAUS 4 TRAK TISO 8 5.0 ;
*---
*  Flux Calculation
*---
TRACK := MCCGT: TRACK TRK_FIL ::
  EDIT 1
  EPSI 1E-6 HDD 0.0
  MAXI 10 KRYL 10 SCR 1 AAC 1 TMT STIS <<stis>> ;
SYSTEM := ASM: MACRO TRACK TRK_FIL ::
  EDIT 0 ARM ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYSTEM ::
  EDIT 2 TYPE K EXTE 100 1E-5 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.010743E+00 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>kaniso_e<< ; 
TRACK TRK_FIL SYSTEM FLUX := DELETE:
TRACK TRK_FIL SYSTEM FLUX ;

******************************
* EXCELT ISOTROPIC CALCULATION
******************************
*----
*  Tracking calculation 
*----
TRACK TRK_FIL := EXCELT: GEOM ::
  TITLE '3D ISOTROPIC BENCHMARK, 1 GROUP'
  EDIT 0 MAXR 1000 ANIS 1 PRIZ 2.0 GAUS 4 TRAK TISO 8 5.0 ;
*---
*  Flux Calculation
*---
TRACK := MCCGT: TRACK TRK_FIL ::
  EDIT 1
  EPSI 1E-6 HDD 0.0
  MAXI 10 KRYL -10 SCR 0 AAC 1 TMT STIS <<stis>> ;
SYSTEM := ASM: MACRO TRACK TRK_FIL ::
  EDIT 0 ARM ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYSTEM ::
  EDIT 2 TYPE K EXTE 100 1E-5 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.036185E+00 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>kiso_e<< ; 

ECHO "***********NXT***********" ;
ECHO "DELTA = K_ANISO - K_ISO ="
kaniso_n "-" kiso_n "=" kaniso_n kiso_n - 1E3 * "mk" ;
ECHO "*************************" ;
ECHO "**********EXCELT*********" ;
ECHO "DELTA = K_ANISO - K_ISO ="
kaniso_e "-" kiso_e "=" kaniso_e kiso_e - 1E3 * "mk" ;
ECHO "*************************" ;
ECHO "FOR INTEGRATION STRATEGY:" stis ;
*---
* Memory Release
*---
TRACK TRK_FIL SYSTEM FLUX := DELETE:
TRACK TRK_FIL SYSTEM FLUX ;

GEOM MACRO := DELETE: GEOM MACRO ;
ECHO "test TCM44 completed" ;
END: ;
