*---
* Regression test for MCCG:
*---
* TEST CASE TCM45
* PRISMATIC 3D ANISOTROPIC SCATTERING BENCHMARK WITH VOID
* MACROLIB-DEFINED CROSS SECTIONS
*---
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST MACRO GEOM2D GEOMZ GEOMX GEOMY TRACK SYSTEM FLUX ;
SEQ_BINARY TRK_FIL ; 
MODULE MAC: ASM: FLU: GEO: EXCELT: MCCGT: GREP: DELETE: END: ;
PROCEDURE assertS ;
REAL k2D k3D kpriz kpriy kprix ;
*---
*  Macrolib
*--
MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 4 NIFI 1 ANIS 2
 READ INPUT
 MIX     1
     TOTAL  0.25
    NUSIGF  0.155
       CHI  1.0
      SCAT  1 1 0.13
            1 1 0.07
 MIX     2
     TOTAL  0.25
     SCAT   1 1 0.24
            1 1 0.15
 MIX     3
     TOTAL  0.75
     SCAT   1 1 0.0
            1 1 0.0
 MIX     4
     TOTAL  1E-10
     SCAT   1 1 0.0
            1 1 0.0
 ;
*---
* Geometries
*---
GEOM2D := GEO: :: CAR2D 3 3
  X- REFL X+ SYME
  Y- SYME Y+ REFL
  CELL C1 C1 C2
       C1 C1 C3
       C2 C3 C4
  ::: C1 := GEO: CAR2D 1 1
         MESHX 0.0 5.0 
         MESHY 0.0 5.0 
         MIX 1
      ;
  ::: C2 := GEO: CARCEL 1
         MESHX 0.0 5.0 
         MESHY 0.0 5.0 
         RADIUS 0.0 0.3
         MIX 2 1
     ;
  ::: C3 := GEO: C2
         MIX 3 1
      ;
  ::: C4 := GEO: C1
         MIX 4
      ;
;

GEOMZ := GEO: :: CAR3D 3 3 2
  X- DIAG X+ REFL
  Y- SYME Y+ DIAG
  Z- REFL Z+ REFL
  CELL C1 C1 C2
          C1 C3
             C4
       C5 C5 C2
          C1 C2
             C4
  ::: C1 := GEO: CAR3D 1 1 1
         MESHX 0.0 5.0 
         MESHY 0.0 5.0 
         MESHZ 0.0 5.0
         MIX 1
      ;
  ::: C2 := GEO: CARCELZ 1 1 1 1
         MESHX 0.0 5.0 
         MESHY 0.0 5.0 
         MESHZ 0.0 5.0
         RADIUS 0.0 0.3
         MIX 2 1
     ;
  ::: C3 := GEO: C2
         MIX 3 1
      ;
  ::: C4 := GEO: C1
         MIX 4
      ;
  ::: C5 := GEO: C1
         MIX 3
      ;
;

GEOMX := GEO: :: CAR3D 2 3 3
  X- REFL X+ REFL
  Y- SYME Y+ REFL
  Z- SYME Z+ REFL
  CELL C1 C5
       C1 C5
       C2 C2
      
       C1 C5
       C1 C1
       C3 C2

       C2 C2
       C3 C2
       C4 C4

  ::: C1 := GEO: CAR3D 1 1 1
         MESHX 0.0 5.0 
         MESHY 0.0 5.0 
         MESHZ 0.0 5.0
         MIX 1
      ;
  ::: C2 := GEO: CARCELX 1 1 1 1
         MESHX 0.0 5.0 
         MESHY 0.0 5.0 
         MESHZ 0.0 5.0
         RADIUS 0.0 0.3
         MIX 2 1
     ;
  ::: C3 := GEO: C2
         MIX 3 1
      ;
  ::: C4 := GEO: C1
         MIX 4
      ;
  ::: C5 := GEO: C1
         MIX 3
      ;
;

GEOMY := GEO: :: CAR3D 3 2 3
  X- SYME X+ REFL
  Y- REFL Y+ REFL
  Z- SYME Z+ REFL
  CELL C1 C1 C2
       C5 C5 C2

       C1 C1 C3
       C5 C1 C2
 
       C2 C3 C4
       C2 C2 C4

  ::: C1 := GEO: CAR3D 1 1 1
         MESHX 0.0 5.0 
         MESHY 0.0 5.0 
         MESHZ 0.0 5.0
         MIX 1
      ;
  ::: C2 := GEO: CARCELY 1 1 1 1
         MESHX 0.0 5.0 
         MESHY 0.0 5.0 
         MESHZ 0.0 5.0
         RADIUS 0.0 0.3
         MIX 2 1
     ;
  ::: C3 := GEO: C2
         MIX 3 1
      ;
  ::: C4 := GEO: C1
         MIX 4
      ;
  ::: C5 := GEO: C1
         MIX 3
      ;
;

***********************
* EXCELT 2D CALCULATION
***********************
TRACK TRK_FIL := EXCELT: GEOM2D ::
EDIT 1 MAXR 2500 TRAK TISO 8 2.0
 ;
TRACK := MCCGT: TRACK TRK_FIL ::
EDIT 2 GAUS 4
EPSI 1E-6 HDD 0.0 STIS -1
MAXI 10 KRYL 10 SCR 0 AAC 1 TMT
;
SYSTEM := ASM: MACRO TRACK TRK_FIL ::
EDIT 2 ARM
 ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYSTEM ::
EDIT 2 TYPE K EXTE 100 1E-6
 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.273445E+00 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>k2D<< ; 
TRACK TRK_FIL SYSTEM FLUX := DELETE:
TRACK TRK_FIL SYSTEM FLUX ;

****************************
* EXCELT 3D PRIX CALCULATION
****************************
TRACK TRK_FIL := EXCELT: GEOMX ::
EDIT 1 MAXR 2500 PRIX 2.0 GAUS 4 TRAK TISO 8 2.0
 ;
TRACK := MCCGT: TRACK TRK_FIL ::
EDIT 2
EPSI 1E-6 HDD 0.0 STIS -1
MAXI 10 KRYL 10 SCR 0 AAC 1 TMT
;
SYSTEM := ASM: MACRO TRACK TRK_FIL ::
EDIT 2 ARM
 ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYSTEM ::
EDIT 2 TYPE K EXTE 100 1E-6
 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.105478E+00 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>kprix<< ; 
TRACK TRK_FIL SYSTEM FLUX := DELETE:
TRACK TRK_FIL SYSTEM FLUX ;

****************************
* EXCELT 3D PRIZ CALCULATION
****************************
TRACK TRK_FIL := EXCELT: GEOMZ ::
EDIT 1 MAXR 2500 PRIZ 2.0 GAUS 4 TRAK TISO 8 2.0
 ;
TRACK := MCCGT: TRACK TRK_FIL ::
EDIT 2
EPSI 1E-6 HDD 0.0 STIS -1
MAXI 10 KRYL 10 SCR 0 AAC 1 TMT
;
SYSTEM := ASM: MACRO TRACK TRK_FIL ::
EDIT 2 ARM
 ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYSTEM ::
EDIT 2 TYPE K EXTE 100 1E-6
 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.105478E+00 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>kpriz<< ; 
TRACK TRK_FIL SYSTEM FLUX := DELETE:
TRACK TRK_FIL SYSTEM FLUX ;

****************************
* EXCELT 3D PRIY CALCULATION
****************************
TRACK TRK_FIL := EXCELT: GEOMY ::
EDIT 1 MAXR 2500 PRIY 2.0 GAUS 4 TRAK TISO 8 2.0
 ;
TRACK := MCCGT: TRACK TRK_FIL ::
EDIT 2
EPSI 1E-6 HDD 0.0 STIS -1
MAXI 10 KRYL 10 SCR 0 AAC 1 TMT
;
SYSTEM := ASM: MACRO TRACK TRK_FIL ::
EDIT 2 ARM
 ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYSTEM ::
EDIT 2 TYPE K EXTE 100 1E-6
 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.105478E+00 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>kpriy<< ; 
TRACK TRK_FIL SYSTEM FLUX := DELETE:
TRACK TRK_FIL SYSTEM FLUX ;

***********************
* EXCELT 3D CALCULATION
***********************
TRACK TRK_FIL := EXCELT: GEOMZ ::
EDIT 1 MAXR 2500 TRAK TISO 10 4.0
 ;
TRACK := MCCGT: TRACK TRK_FIL ::
EDIT 2
EPSI 1E-6 HDD 0.0 STIS -1
MAXI 10 KRYL 10 SCR 0 AAC 1 TMT
;
SYSTEM := ASM: MACRO TRACK TRK_FIL ::
EDIT 2 ARM
 ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYSTEM ::
EDIT 2 TYPE K EXTE 100 1E-6
 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.104729E+00 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>k3D<< ; 
TRACK TRK_FIL SYSTEM FLUX := DELETE:
TRACK TRK_FIL SYSTEM FLUX ;

ECHO "**********************" ;
ECHO "2D     " k2D ;
ECHO "3D PRIX" kprix ;
ECHO "2D PRIY" kpriy ;
ECHO "2D PRIZ" kpriz ;
ECHO "3D     " k3D ;
ECHO "**********************" ;

ECHO "test TCM45 completed" ;
END: ;
