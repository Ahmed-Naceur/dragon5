*                                                              
* TEST CASE TCM12                                             
* MACROSCOPIC CROSS SECTIONS                                   
* FISSION SOURCE PROBLEM
* 3D HEXAGONAL S30 ASSEMBLY WITH FUEL RODS        
*                                                              
LINKED_LIST GEOMETRY TRACKING ASSEMBLY MACLIB FLUX ;
MODULE MAC: GEO: EXCELT: ASM: FLU: TRIVAT: END: ;
SEQ_BINARY TRKSPC ;
PROCEDURE assertS ;

MACLIB := MAC: ::
 EDIT 2 NGRO 1 NMIX 4 NIFI 1
     READ INPUT
     MIX 1
        TOTAL 0.41       SCAT 1 1 0.3
        NUSIGF 0.1       CHI 1.0
     MIX 2
        TOTAL 0.53       SCAT 1 1 0.5
     MIX 3
        TOTAL 0.45       SCAT 1 1 0.347
        NUSIGF 0.17      CHI 1.0
     MIX 4
        TOTAL 0.3        SCAT 1 1 0.2
        NUSIGF 0.1       CHI 1.0
;

GEOMETRY := GEO: :: HEXZ 2 2
 EDIT 2
 HBC S30 REFL Z+ REFL Z- REFL
 MIX UDEPLETED_1 UDEPLETED_1 UDEPLETED_2 UDEPLETED_2
 ::: UDEPLETED_1 := GEO: HEXCELZ 4 1
    EDIT 2
    SIDE 2.804
    RADIUS 0.0 2.23 2.275 2.4 2.5
    MESHZ  0.0 0.7
    MIX 1 3 4  2 2 ;
 ::: UDEPLETED_2 := GEO: HEXCELZ 4 1
    EDIT 2
    SIDE 2.804
    RADIUS 0.0 2.23 2.275 2.4 2.5
    MESHZ  0.7 1.0
    MIX 4 3 1  2 2 ;
;

TRACKING TRKSPC := EXCELT: GEOMETRY ::
 EDIT 2
 MAXR 200 TRAK TISO 6 10 10. 5.
;

ASSEMBLY := ASM: MACLIB TRACKING TRKSPC :: EDIT 1 ;
FLUX := FLU: ASSEMBLY MACLIB TRACKING TRKSPC :: TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.9292231 ;
ECHO "test TCM12 completed" ;
END: ;
QUIT .
