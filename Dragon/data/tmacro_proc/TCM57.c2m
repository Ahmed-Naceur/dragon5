*----
*  TEST CASE TCM57
*  PHYSOR 2D HEXAGONAL PROBLEM -- BASED ON 2010 RAVIART-THOMAS 
*  HEBERT/LE TELLIER PROBLEM -- MULTIGROUP MACROLIB-DEFINED CROSS 
*  SECTIONS
*
*  Ref: A. A. Calloo and A. Hebert, PHYSOR 2020
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM MACRO TRACK SYSTEM FLUX EDIT ;
MODULE GEO: MAC: ASM: END: SNT: FLU: UTL: DELETE: ;
PROCEDURE assertS ;
*
* spl = region split
* sch = scheme parameter
* spa = spatial order
* and = sn order
* sca = scattering
*
INTEGER spl := 1 ;
INTEGER sch ;
INTEGER spa := 2 ;
INTEGER ang := 18 ; 
INTEGER sca := 2 ;
*
GEOM := GEO: :: HEX  127
       EDIT 2
       SPLITL <<spl>>
       HBC   COMPLETE  VOID
       SIDE 19.0
       MIX
       1
       1 1 1 1 1 1
       1 1 1 1 1 1 1 1 1 1 1 1 
       2 1 1 2 1 1 2 1 1 2 1 1 2 1 1 2 1 1
       2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2
       3 2 2 2 2 3 2 2 2 2 3 2 2 2 2 3 2 2 2 2 3 2 2 2 2 3 2 2 2 2  
       0 3 3 3 3 3 0 3 3 3 3 3 0 3 3 3 3 3 0 3 3 3 3 3 0 3 3 3 3 3
       0 3 3 3 3 3
       ;

MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 3 NIFI 1 ANIS 2
 READ INPUT
 MIX     1
     TOTAL  0.025
    NUSIGF  0.0155
       CHI  1.0
      SCAT  1 1 0.013
            1 1 0.0
 MIX     2
     TOTAL  0.025
      SCAT  1 1 0.024
            1 1 0.006
 MIX     3
     TOTAL  0.075
      SCAT  1 1 0.0
            1 1 0.0
 ;
*----
* DISCONTINUOUS GALERKIN
*----
EVALUATE sch := 2 ;
TRACK := SNT: GEOM ::
      TITLE '2D HEXA ANISOTROPIC PHYSOR DG BENCHMARK, 1 GROUP'
      EDIT 2 MAXR 25000
      SCHM <<sch>> 
      DIAM <<spa>> 
      SN <<ang>> 
      SCAT <<sca>> 
      MAXI 300 EPSI 1.E-5
      QUAD 2 (*Level-Symmetric*) ;
SYSTEM := ASM: MACRO TRACK :: EDIT 2 ARM ;
FLUX :=  FLU: MACRO TRACK SYSTEM ::
      EDIT 2 TYPE K EXTE 100 1E-6 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.002337 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
*----
* HIGH-ORDER DIAMOND DIFFERENCE
*----
EVALUATE sch := 1 ;
TRACK := SNT: GEOM ::
      TITLE '2D HEXA ANISOTROPIC PHYSOR HODD BENCHMARK, 1 GROUP'
      EDIT 2 MAXR 25000
      SCHM <<sch>> 
      DIAM <<spa>> 
      SN <<ang>> 
      SCAT <<sca>> 
      MAXI 300 EPSI 1.E-5
      QUAD 2 (*Level-Symmetric*) ;
SYSTEM := ASM: MACRO TRACK :: EDIT 2 ARM ;
FLUX :=  FLU: MACRO TRACK SYSTEM ::
      EDIT 2 TYPE K EXTE 100 1E-6 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.002337 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;

ECHO "test TCM57 completed" ;
END: ;
