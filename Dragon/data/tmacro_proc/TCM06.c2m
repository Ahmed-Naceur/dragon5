*----
*  TEST CASE TCM06
*  MACROSCOPIC CROSS SECTIONS
*  BUCKLING SEARCH PROBLEM WITHOUT FISSION SOURCE
*  HOMOGENEOUS GEOMETRY
*
*  REF: none
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  WATER TRACK MACRO SYS FLUX EDITION ;
MODULE
 GEO: SYBILT: MAC: ASM: FLU: EDI: END: ;
PROCEDURE assertS ;
*----
*  Macroscopic XS
*----
MACRO := MAC: ::
  EDIT 2 NGRO 1 ANIS 2 NMIX 1 NIFI 0
  READ INPUT
  MIX 1 TOTAL 3.59 SCAT 1 1 3.57 1 1 2.38
  ;
*----
*  Geometry :  WATER - Homogeneous geometry
*  Tracking : SYBILT
*----
WATER := GEO: :: HOMOGE
  MIX 1
  ;
TRACK := SYBILT: WATER ::
  TITLE 'TCM06: ENE6101 EXAM'
  MAXR 1
  ;
*---- 
*  Solution : TYPE  L
*  Leakage  : B0 PNL, P0 PNL, B1 PNL, P1 PNL
*----
SYS := ASM: MACRO TRACK ;
FLUX := FLU: SYS MACRO TRACK ::
  TYPE L B0 SIGS EXTE 5 1.0E-5 BUCK -0.07 ;
assertS FLUX :: 'B2  B1HOM' 1 -2.14440E-01 ;
EDITION := EDI: MACRO TRACK FLUX ::
  EDIT 3 SAVE ;
FLUX := FLU: FLUX SYS MACRO TRACK ::
  TYPE L P0 SIGS EXTE 5 1.0E-5 BUCK -0.07 ;
assertS FLUX :: 'B2  B1HOM' 1 -2.15400E-01 ;
EDITION := EDI: EDITION MACRO TRACK FLUX ::
  EDIT 3 SAVE ;
FLUX := FLU: FLUX SYS MACRO TRACK ::
  TYPE L B1 SIGS EXTE 5 1.0E-5 BUCK -0.07 ;
assertS FLUX :: 'B2  B1HOM' 1 -7.22773E-02 ;
EDITION := EDI: EDITION MACRO TRACK FLUX ::
  EDIT 3 SAVE ;
FLUX := FLU: FLUX SYS MACRO TRACK ::
  TYPE L P1 SIGS EXTE 5 1.0E-5 BUCK -0.07 ;
assertS FLUX :: 'B2  B1HOM' 1 -7.25999E-02 ;
EDITION := EDI: EDITION MACRO TRACK FLUX ::
  EDIT 3 SAVE ;
ECHO "test TCM06 completed" ;
END: ;
QUIT "LIST" .
