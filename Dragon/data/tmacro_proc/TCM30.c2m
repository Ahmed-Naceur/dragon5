*----
*  TEST CASE TCM30
*  NSE 2D HEXAGONAL PROBLEM
*  ONE-GROUP MACROLIB-DEFINED CROSS SECTIONS
*
*  Ref: A. Hebert, "Mixed-dual implementations of the of the simplified
*  Pn method", Ann. Nucl. Energy, 2010.
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM MACRO TRACK SYSTEM FLUX ;
MODULE GEO: MAC: EXCELT: MCCGT: ASM: FLU: END: ;
SEQ_BINARY TRK_FIL ;
PROCEDURE assertS ;
*
GEOM := GEO: :: HEX  16
       EDIT 2
       HBC   S30  VOID
       SIDE 19.0
       MIX
       1
       1
       1  1
       1  2
       2  2  2
       2  2  3
       3  3  3  4
       ;
MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 4 NIFI 1 ANIS 2
 READ INPUT
 MIX     1
     TOTAL  0.025
    NUSIGF  0.0155
       CHI  1.0
      SCAT  1 1 0.013
            1 1 0.0
 MIX     2
     TOTAL  0.025
      SCAT  1 1 0.024
            1 1 0.006
 MIX     3
     TOTAL  0.075
      SCAT  1 1 0.0
            1 1 0.0
 MIX     4
     TOTAL  0.0
      SCAT  1 1 0.0
            1 1 0.0
 ;
TRACK TRK_FIL := EXCELT: GEOM ::
  TITLE '2D ANISOTROPIC HEXAGONAL BENCHMARK, 1 GROUP'
  MAXR 200
  EDIT 99 ANIS 2 GAUS 4 TRAK TISO 6 5.0 ;
TRACK := MCCGT: TRACK TRK_FIL ::
  EDIT 1
  EPSI 1E-6 HDD 1.0E10 (*diamond scheme*)
  MAXI 10 STIS 0 LCMD 3 ;
SYSTEM := ASM: MACRO TRACK TRK_FIL ::
      EDIT 1 ARM ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYSTEM ::
      EDIT 2 TYPE K EXTE 100 5.0E-8 ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.9863732 ;
ECHO "test TCM30 completed" ;
END: ;
QUIT "LIST" .
