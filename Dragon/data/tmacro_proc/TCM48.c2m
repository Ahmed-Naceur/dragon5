*----
*  TEST CASE TCM48
*  9 X 9 PWR CASE WITH PRISMATIC MCCG-TRIVAC SPH EQUIVALENCE
*  MACROLIB-DEFINED CROSS SECTIONS
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST ASSMB TRACK MACRO SYS FLUX EDIT1 EDIT2 MACRO2 MTRACK
            MASSMB ;
MODULE GEO: NXT: MCCGT: MAC: ASM: FLU: EDI: TRIVAT: SPH: DELETE: END: ;
SEQ_BINARY TRK_FIL ;
PROCEDURE assertS ;
*
MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 3 NIFI 1 ANIS 2
     READ INPUT
     MIX 1 (*EAU*)
        TOTAL 0.3683
        SCAT 1 1 0.3661 1 1 0.1212
     MIX 2 (*COMBUSTIBLE*)
        TOTAL 0.36522
        SCAT 1 1 0.3234 1 1 0.1100
        NUSIGF 0.09564      CHI 1.0
     MIX 3 (*ARGENT*)
        TOTAL 0.8453
        SCAT 1 1 0.5216 1 1 0.4212
     ;
ASSMB := GEO: :: CAR3D 5 5 1
          X- DIAG X+ REFL
          Y- REFL Y+ DIAG
          Z- REFL Z+ REFL
          CELL C1 C3 C2 C3 C4
                  C3 C3 C3 C4
                     C2 C3 C4
                        C3 C4
                           C5
          ::: C1 := GEO: CARCELZ 1 2
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                MESHZ 0.0 2.0 7.0
                RADIUS 0.0 4.12660E-01
                MIX 1 1
                    1 1
          ;
          ::: C2 := GEO: CARCELZ 3 2
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                MESHZ 0.0 2.0 7.0
                RADIUS 0.0 3.25296E-01 4.60039E-01 5.63430E-01
                MIX 2 2 2 1
                    3 3 3 1
          ;
          ::: C3 := GEO: CARCELZ 1 2
                MESHX 0.0 1.262082
                MESHY 0.0 1.262082
                MESHZ 0.0 2.0 7.0
                RADIUS 0.0 4.12660E-01
                MIX 2 1
                    2 1
          ;
          ::: C4 := GEO: C3
                MESHX 0.0 1.322082
          ;
          ::: C5 := GEO: C4
                MESHY 0.0 1.322082
          ;
          ;
*----
*  Tracking calculation 
*----
TRACK TRK_FIL := NXT: ASSMB ::
  TITLE '3D ANISOTROPIC BENCHMARK, 1 GROUP'
  EDIT 0 ANIS 1 PRIZ 5.0 GAUS 4 TISO 4 5.0 ;
*---
*  Flux Calculation
*---
TRACK := MCCGT: TRACK TRK_FIL ::
  EDIT 1
  EPSI 1E-6 HDD 0.0
  MAXI 10 KRYL 10 SCR 1 AAC 1 TMT ;
*
SYS := ASM: MACRO TRACK TRK_FIL ::
  EDIT 0 ARM ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYS ::
  EDIT 2 TYPE K B0 SIGS BUCK 0.0 EXTE 100 1E-5 ;
assertS FLUX :: K-INFINITY 1 1.085109 ;
*----
* DEFINE THE MACRO-GEOMETRY
*----
MASSMB := GEO: :: CAR3D 5 5 2
          X- DIAG X+ REFL
          Y- REFL Y+ DIAG
          Z- REFL Z+ REFL
          MESHX 0.0 1.262082 2.524164 3.786246 5.048328 6.37041
          MESHY 0.0 1.262082 2.524164 3.786246 5.048328 6.37041
          MESHZ 0.0 2.0 7.0
          MIX  1  2  3  4  5
                  6  7  8  9
                    10 11 12
                       13 14
                          15
              16 17 18 19 20
                 21 22 23 24
                    25 26 27
                       28 29
                          30 ;
*----
* TRANSPORT-DIFFUSION EQUIVALENCE
*----
*    EDITION FOR HETEROGENEOUS DIFFUSION CALCULATIONS
*    CELL-BY-CELL SPH HOMOGENIZATION OF THE ASSEMBLY
EDIT1 := EDI: FLUX MACRO TRACK MASSMB ::
     EDIT 3 SAVE P0W MERG
     REGI 1 1 16 16 2 2 17 17 3 3 3 3 18 18 18 18 4 4 19 19 5 5 20 20
          6 6 21 21 7 7 22 22 8 8 23 23 9 9 24 24
          10 10 10 10 25 25 25 25 11 11 26 26 12 12 27 27
          13 13 28 28 14 14 29 29
          15 15 30 30
     MGEO MASSMB
     ;
MTRACK := TRIVAT: MASSMB :: DUAL (*IELEM=*) 1 (*ICOL=*) 2 ;
EDIT1 := SPH: EDIT1 MTRACK :: ITER 1.0E-5 ;
MTRACK := DELETE: MTRACK ;
*----
* TRANSPORT-SPN EQUIVALENCE
*----
*    EDITION FOR HETEROGENEOUS DIFFUSION CALCULATIONS
*    CELL-BY-CELL SPH HOMOGENIZATION OF THE ASSEMBLY
EDIT2 := EDI: FLUX MACRO TRACK MASSMB ::
     EDIT 3 SAVE P0W MERG
     REGI 1 1 16 16 2 2 17 17 3 3 3 3 18 18 18 18 4 4 19 19 5 5 20 20
          6 6 21 21 7 7 22 22 8 8 23 23 9 9 24 24
          10 10 10 10 25 25 25 25 11 11 26 26 12 12 27 27
          13 13 28 28 14 14 29 29
          15 15 30 30
     MGEO MASSMB
     ;
MTRACK := TRIVAT: MASSMB :: DUAL (*IELEM=*) 1 (*ICOL=*) 2 SPN 5 SCAT 1 ;
EDIT2 := SPH: EDIT2 MTRACK :: ITER 1.0E-5 ;
MTRACK := DELETE: MTRACK ;
*----
* DIFFUSION VERIFICATION CALCULATION
*----
ASSMB TRACK SYS FLUX := DELETE: ASSMB TRACK SYS FLUX ;
ASSMB := EDIT1 :: STEP UP 'MACRO-GEOM' ;
MACRO2 := EDIT1 :: STEP UP 'REF-CASE0001' STEP UP 'MACROLIB' ;
TRACK := TRIVAT: ASSMB :: DUAL (*IELEM=*) 1 (*ICOL=*) 2 ;
SYS := ASM: MACRO2 TRACK :: ARM ;
FLUX := FLU: MACRO2 TRACK SYS :: EDIT 1 TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.085109 ;
*----
* SPN VERIFICATION CALCULATION
*----
ASSMB MACRO2 TRACK SYS FLUX := DELETE: ASSMB MACRO2 TRACK SYS FLUX ;
ASSMB := EDIT2 :: STEP UP 'MACRO-GEOM' ;
MACRO2 := EDIT2 :: STEP UP 'REF-CASE0001' STEP UP 'MACROLIB' ;
TRACK := TRIVAT: ASSMB :: DUAL (*IELEM=*) 1 (*ICOL=*) 2 SPN 5 SCAT 1 ;
SYS := ASM: MACRO2 TRACK :: ARM ;
FLUX := FLU: MACRO2 TRACK SYS :: EDIT 1 TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.085109 ;
ECHO "test TCM48 completed" ;
END: ;
QUIT "LIST" .
