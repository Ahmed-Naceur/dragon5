*---
* Regression test for MCCG:
*---
* TEST CASE TCM46
* PRISMATIC 3D ANISOTROPIC SCATTERING BENCHMARK WITH PINS
* MACROLIB-DEFINED CROSS SECTIONS
*---
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST MACRO GEOM2D GEOMZ GEOMX GEOMY TRACK TEMP SYSTEM FLUX ;
SEQ_BINARY TRK_FIL ; 
SEQ_ASCII FIG.ps ;
MODULE MAC: ASM: FLU: GEO: MCCGT: NXT: PSP: GREP: DELETE: END: ;
PROCEDURE assertS ;
REAL k2D kpriz  ;
*---
*  Macrolib
*--
MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 3 NIFI 1 ANIS 2
 READ INPUT
 MIX     1
     TOTAL  0.35
    NUSIGF  0.255
       CHI  1.0
      SCAT  1 1 0.13
            1 1 0.07
 MIX     2
     TOTAL  0.25
     SCAT   1 1 0.24
            1 1 0.15
 MIX     3
     TOTAL  0.015
     SCAT   1 1 0.0
            1 1 0.0
 ;
*---
* Geometry Geom
*---
GEOM2D := := GEO: :: CAR2D 1 2
    X- REFL X+ REFL
    Y- REFL Y+ REFL
    CELL C1
         C2
::: C1 := GEO: CARCEL 2 1 1
    MESHX 0.0 1.0
    MESHY 0.0 1.0
    SPLITX 2
    SPLITY 2
    RADIUS 0.0 0.3 0.4
    MIX 2 3 2
    CLUSTER ROD1 ROD2
    ::: ROD1 := GEO: TUBE 2
        RADIUS 0.0 0.1 0.15
        NPIN 2 APIN 0.0 RPIN 0.2
        MIX  1 3
        ;
    ::: ROD2 := GEO: TUBE 1 1 2
        RADIUS 0.0 0.1
        SPLITR 2
        NPIN 1 APIN 1.0 RPIN 0.4
        MIX 1 3
        MESHY 0.0 0.5 1.0
        ;
    ;
    ::: C2 := GEO: CARCEL 2 1 1
    MESHX 0.0 1.0
    MESHY 0.0 1.0
    SPLITX 3
    SPLITY 3
    RADIUS 0.0 0.3 0.4
    MIX 2 3 2
    CLUSTER ROD1
    ::: ROD1 := GEO: TUBE 1
        RADIUS 0.0 0.1
        NPIN 6 APIN 0.0 RPIN 0.3
        MIX 1
        ;
    ;
;

GEOMZ := GEO: :: CAR3D 1 2 2
    X- REFL X+ REFL
    Y- REFL Y+ REFL
    Z- REFL Z+ REFL
    CELL C1
         C2
         C1
         C2
::: C1 := GEO: CARCELZ 2 1 1 1
    MESHX 0.0 1.0
    MESHY 0.0 1.0
    MESHZ 0.0 1.0
    SPLITX 2
    SPLITY 2
    !SPLITZ 2
    RADIUS 0.0 0.3 0.4
    MIX 2 3 2
    CLUSTER ROD1 ROD2
    ::: ROD1 := GEO: TUBEZ 2 1 1 1
        RADIUS 0.0 0.1 0.15
        MESHZ 0.0 1.0
        !SPLITZ 2
        NPIN 2 APIN 0.0 RPIN 0.2
        MIX  1 3
        ;
    ::: ROD2 := GEO: TUBEZ 1 1 2 1
        RADIUS 0.0 0.1
        SPLITR 2
        MESHZ 0.0 1.0
        !SPLITZ 2
        NPIN 1 APIN 1.0 RPIN 0.4
        MIX 1 3
        MESHY 0.0 0.5 1.0
        ;
    ;
    ::: C2 := GEO: CARCELZ 2 1 1 1
    MESHX 0.0 1.0
    MESHY 0.0 1.0
    MESHZ 0.0 1.0
    SPLITX 3
    SPLITY 3
    !SPLITZ 2
    RADIUS 0.0 0.3 0.4
    MIX 2 3 2
    CLUSTER ROD1
    ::: ROD1 := GEO: TUBEZ 1 1 1 1
        RADIUS 0.0 0.1
        MESHZ 0.0 1.0
        !SPLITZ 2
        NPIN 6 APIN 0.0 RPIN 0.3
        MIX  1
        ;
    ;
;
********************
* 2D NXT CALCULATION
********************
TRACK TRK_FIL := NXT: GEOM2D ::
EDIT 1 TISO 4 50.0
 ;
TRACK := MCCGT: TRACK TRK_FIL ::
EDIT 2 GAUS 4
EPSI 1E-6 HDD 0.0 STIS 1
MAXI 10 KRYL 0 SCR 0 AAC 1 TMT
 ;
SYSTEM := ASM: MACRO TRACK TRK_FIL ::
  EDIT 2 ARM
 ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYSTEM ::
  EDIT 2 TYPE K EXTE 100 1E-6
 ;
assertS FLUX :: 'K-EFFECTIVE' 1 8.741547E-01 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>k2D<< ; 
TRACK TRK_FIL SYSTEM FLUX := DELETE:
TRACK TRK_FIL SYSTEM FLUX ;

*************************
* 3D PRIZ NXT CALCULATION
*************************
TRACK TRK_FIL := NXT: GEOMZ ::
EDIT 1 PRIZ 50.0 GAUS 4 TISO 4 50.0 ;
TEMP := TRACK :: STEP UP PROJECTION ;
FIG.ps := PSP: TEMP
 ;
TRACK := MCCGT: TRACK TRK_FIL ::
EDIT 2
EPSI 1E-6 HDD 0.0 STIS 1
MAXI 10 KRYL 0 SCR 0 AAC 1 TMT
;
SYSTEM := ASM: MACRO TRACK TRK_FIL ::
  EDIT 2 ARM
 ;
FLUX :=  FLU: MACRO TRACK TRK_FIL SYSTEM ::
  EDIT 2 TYPE K EXTE 100 1E-6
 ;
assertS FLUX :: 'K-EFFECTIVE' 1 8.743929E-01 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>kpriz<< ; 
TRACK TRK_FIL SYSTEM FLUX := DELETE:
TRACK TRK_FIL SYSTEM FLUX ;

ECHO "**********************" ;
ECHO "2D     " k2D ;
ECHO "2D PRIZ" kpriz ;
ECHO "**********************" ;

ECHO "test TCM46 completed" ;
END: ;
QUIT .
