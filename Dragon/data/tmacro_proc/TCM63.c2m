*----
*  TEST CASE TCM63
*  First Takeda benchmark
*  SN solution with Domino swapping
*  Author: A. Calloo
*
*  Ref: T Takeda, H Ikeda, "3-D neutron transport benchmarks," Journal
*  of Nuclear Science and Technology, 1991
*----
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM MACRO TRACK SYSTEM FLUX EDIT ;
MODULE GEO: MAC: SNT: ASM: FLU: DELETE: END: ;
PROCEDURE assertS ;
*
* spl = region split
* sch = scheme parameter
* spa = spatial order
* ang = sn order
* sca = scattering
*
INTEGER spl := 2 ;
INTEGER sch ;
INTEGER spa := 1 ;
INTEGER ang := 8 ;
INTEGER sca := 1 ;
*
GEOM := GEO: :: CAR3D 5 5 5
           X- REFL X+ VOID
           Y- REFL Y+ VOID
           Z- REFL Z+ VOID
           MIX  1 1 1 3 2
                1 1 1 2 2
                1 1 1 2 2
                2 2 2 2 2
                2 2 2 2 2 

                1 1 1 3 2
                1 1 1 2 2
                1 1 1 2 2
                2 2 2 2 2
                2 2 2 2 2 
                
                1 1 1 3 2
                1 1 1 2 2
                1 1 1 2 2
                2 2 2 2 2
                2 2 2 2 2 

                2 2 2 3 2
                2 2 2 2 2
                2 2 2 2 2
                2 2 2 2 2
                2 2 2 2 2 

                2 2 2 3 2
                2 2 2 2 2
                2 2 2 2 2
                2 2 2 2 2
                2 2 2 2 2 
           MESHX 0.0 5.0 10.0 15.0 20.0 25.0
           MESHY 0.0 5.0 10.0 15.0 20.0 25.0
           MESHZ 0.0 5.0 10.0 15.0 20.0 25.0
           SPLITX <<spl>> <<spl>> <<spl>> <<spl>> <<spl>>
           SPLITY <<spl>> <<spl>> <<spl>> <<spl>> <<spl>>
           SPLITZ <<spl>> <<spl>> <<spl>> <<spl>> <<spl>>
           ;
MACRO := MAC: ::
 EDIT 2 NGRO 2 NMIX 3 NIFI 1 ANIS 1
 ENER 1.E7 0.68256 1.E-5
 READ INPUT
 MIX     1 (* CORE *)
   NUSIGF 9.09319E-03  2.90183E-01
      CHI 1.00000E+00  0.00000E+00  
    TOTAL 2.23775E-01  1.03864E+00
     SCAT 1 1                      (*1->1*) 1.92423E-01
          2 2 (*2->2*) 8.80439E-01 (*1->2*) 2.28253E-02
 MIX     2 (* REFLECTOR *)
    TOTAL 2.50367E-01  1.64482E+00
     SCAT 1 1                      (*1->1*) 1.93446E-01
          2 2 (*2->2*) 1.62452E-00 (*1->2*) 5.65042E-02
 MIX     3 (* EMPTY-VOID CASE 1 *)
    TOTAL 1.28407E-02  1.20676E-02
     SCAT 1 1                      (*1->1*) 1.27700E-02
          2 2 (*2->2*) 1.07387E-02 (*1->2*) 2.40997E-05
          ;

*----
* DISCONTINUOUS GALERKIN
*----
EVALUATE sch := 2 ;
TRACK := SNT: GEOM ::
      TITLE 'TAKEDA 3D MODEL 1'
      EDIT 2 MAXR 25000
      SCHM <<sch>> 
      DIAMA <<spa>> 
      SN <<ang>> 
      SCAT <<sca>> 
      MAXI 50 EPSI 1.0E-5
      NLIVO NDSA
      QUAD 2 (*Level-Symmetric*)
      ONEG DOON 3 ;

SYSTEM := ASM: MACRO TRACK :: EDIT 0 ARM ;

FLUX :=  FLU: MACRO TRACK SYSTEM ::
      EDIT 2 TYPE K EXTE 200 1.0E-5 ;
assertS FLUX :: K-EFFECTIVE 1 0.9799388 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
*----
* HIGH-ORDER DIAMOND DIFFERENCE
*----
EVALUATE sch := 1 ;
TRACK := SNT: GEOM ::
      TITLE 'TAKEDA 3D MODEL 1'
      EDIT 2 MAXR 25000
      SCHM <<sch>> 
      DIAMA <<spa>> 
      SN <<ang>> 
      SCAT <<sca>> 
      MAXI 50 EPSI 1.0E-5
      NLIVO NDSA
      QUAD 2 (*Level-Symmetric*)
      ONEG DOON 3 ;

SYSTEM := ASM: MACRO TRACK :: EDIT 0 ARM ;

FLUX :=  FLU: MACRO TRACK SYSTEM ::
      EDIT 2 TYPE K EXTE 200 1.0E-5 ;
assertS FLUX :: K-EFFECTIVE 1 0.9772558 ;

ECHO "test TCM63 completed" ;
END: ;
