*----
* 295-group eighth assembly in DRAGON
* one-level zero-burnup scheme
*----
SEQ_ASCII UOX_TBH :: FILE './UOX_TBH.dat' ;
LINKED_LIST GEOSS GEON1 TRACKSS TRACKN1 TRACKN2 LIBRARY LIBRARY2
            SYS FLUX EDITION LIBEQ ;
MODULE SYBILT: SALT: MCCGT: USS: ASM: FLU: EDI: END: ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 R_int_TG R_ext_TG R_int_TI R_ext_TI
     R_int_G R_ext_G Cote CoteL RmodeN1 ;
REAL dens_mod_0 pbore_0 N_U5_UOX densU_UOX temp_comb_0 temp_mode_0
     temp_clad_0 ;
SEQ_BINARY TF_EXC ;
INTEGER an2d := 8 ;
REAL densur := 20.0 ;
STRING Library := "DLIB_295" ;
PROCEDURE Geo_SS_32 Mix_UOX_32 MultLIBEQ_32 ;
PROCEDURE assertS ;
************************************************************************
*  BEGIN GEOMETRY                                                      *
************************************************************************
EVALUATE Rcomb1 := 0.288712 ;
EVALUATE Rcomb2 := 0.365195 ;
EVALUATE Rcomb3 := 0.397962 ;
EVALUATE Rcomb4 := 0.4083 ;
EVALUATE R_int_TG := 0.5691 ;
EVALUATE R_ext_TG := 0.6095 ;
EVALUATE R_int_TI := 0.5691 ;
EVALUATE R_ext_TI := 0.6095 ;
EVALUATE R_int_G := 0.4165 ;
EVALUATE R_ext_G := 0.4775 ;
EVALUATE Cote := 1.26 ;
EVALUATE CoteL := 1.302 ;
EVALUATE RmodeN1 := 0.670 ;
GEOSS := Geo_SS_32 :: <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
                      <<R_int_TG>> <<R_ext_TG>> <<R_int_TI>> 
                      <<R_ext_TI>> <<R_int_G>> <<R_ext_G>> <<Cote>>
                      <<CoteL>> ;

************************************************************************
*  END GEOMETRY                                                        *
************************************************************************
************************************************************************
*  BEGIN TRACKING                                                      *
************************************************************************
TRACKSS := SYBILT: GEOSS ::
  EDIT 0
  MAXR 500
  MAXZ  1000000
  TITLE 'TRACKING FOR ASSEMBLY SS'
  QUA2 20 3
  DP01  ;
TRACKN2 TF_EXC := SALT: UOX_TBH ::
  EDIT 3
  ALLG
  TSPC EQW2 <<an2d>> <<densur>> REND
;
TRACKN2 := MCCGT: TRACKN2 TF_EXC ::
  CACB 4 AAC 80 TMT EPSI 1E-5 MCU 10000 ;
************************************************************************
*  END TRACKING                                                        *
************************************************************************
************************************************************************
*  BEGIN LIBRARY READING                                               *
************************************************************************
EVALUATE dens_mod_0 := 1.002 ;
EVALUATE pbore_0 := 0.0 ;
EVALUATE N_U5_UOX := 0.03 ;
EVALUATE densU_UOX := 10.45 ;
EVALUATE temp_comb_0 := 1200.0 ;
EVALUATE temp_mode_0 := 600.0 ;
EVALUATE temp_clad_0 := 900.0 ;
LIBRARY := Mix_UOX_32 ::
   <<Library>> <<dens_mod_0>> <<pbore_0>> <<N_U5_UOX>> <<densU_UOX>>
   <<temp_comb_0>> <<temp_mode_0>> <<temp_clad_0>> ;
************************************************************************
*  END LIBRARY READING                                                 *
************************************************************************
*----
* USS
*----
LIBRARY2 := USS: LIBRARY TRACKSS :: EDIT 0 PASS 2 ARM ;
LIBRARY2 := MultLIBEQ_32 LIBRARY2 ;
SYS := ASM: LIBRARY2 TRACKN2 TF_EXC :: ARM EDIT 1 ;    
FLUX := FLU: LIBRARY2 SYS TRACKN2 TF_EXC :: 
     EDIT 1 TYPE K ;
************************************************************************
*  END SECOND LEVEL FLUX CALCULATION                                   *
************************************************************************
assertS FLUX :: K-EFFECTIVE 1 1.368096 ;
ECHO "test UOX_TBH_eighth_1level completed" ;
  END: ;
