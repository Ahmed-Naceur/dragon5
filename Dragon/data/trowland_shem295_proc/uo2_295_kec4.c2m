*----
*
*  TEST CASE uo2_295_kec4
*  UO2 ROWLAND'S BENCHMARK
*  DISTRIBUTED SELF-SHIELDING
*  295-GROUP JEF2.2 DRAGLIB
*
*  Author: A. Hebert
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM TRACK LIBRARY LIBRARY2 CP CALC ;
MODULE LIB: GEO: SYBILT: USS: ASM: FLU: ;
INTEGER COMB0101 COMB0102 COMB0103 COMB0104 COMB0105 COMB0106 GAIN1
        MODE1 MODE2 := 
        1 2 3 4 5 6 7 8 9 ; (* DISTRIBUTED SELF-SHIELDING *)
REAL RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 ;
PROCEDURE assertS ;
*
EVALUATE RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 := 0.2529822 
0.334664 0.3577709 0.3794733 0.3898718 0.40 ;
GEOM := GEO: :: TUBE 9
      R+ REFL
      RADIUS 0.0 <<RCOMB1>> <<RCOMB2>> <<RCOMB3>> <<RCOMB4>>
                 <<RCOMB5>> <<RCOMB6>> 0.45 0.5748331 0.6770275
      MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
          <<COMB0105>> <<COMB0106>> <<GAIN1>> <<MODE1>> <<MODE2>>
          ;
TRACK := SYBILT: GEOM ::
     TITLE 'UO2 ROWLAND S BENCHMARK'
     MAXR 20 QUA1 5 ;
LIBRARY := LIB: ::
 EDIT 1
 NMIX 9    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 ADED 4 NELAS N4N N2N N3N
 CALENDF 3 PT      (*CALENDF TYPE PROBABILITY TABLES*)
*
 MIXS LIB: DRAGON FIL: DLIB_295
 MIX <<COMB0101>> 574.0 (*COMB0101*)
    O16     = O16    4.6624E-2
    U235    = U235   7.0803E-4 CORR 1 IRSET PT 1
    U238    = U238   2.2604E-2 CORR 1 IRSET PT 1
 MIX <<COMB0102>> COMB <<COMB0101>> 1.0 (*COMB0102*)
 MIX <<COMB0103>> COMB <<COMB0101>> 1.0 (*COMB0103*)
 MIX <<COMB0104>> COMB <<COMB0101>> 1.0 (*COMB0104*)
 MIX <<COMB0105>> COMB <<COMB0101>> 1.0 (*COMB0105*)
 MIX <<COMB0106>> COMB <<COMB0101>> 1.0 (*COMB0106*)
 MIX <<GAIN1>> 574.0 (*GAIN1*)
    Zr0     = Zr0    4.3241E-2 2 IRSET PT 1
 MIX <<MODE1>> 573.6 (*MODE1*)
    H1      = H1_H2O 4.6892E-2
    O16     = O16    2.3446E-2
 MIX <<MODE2>> 573.6 (*MODE2*)
    H1      = H1_H2O 4.6892E-2
    O16     = O16    2.3446E-2
 ;
LIBRARY2 := USS: LIBRARY TRACK :: EDIT 1 TRAN PASS 2 GRMIN 56 ;
CP := ASM: LIBRARY2 TRACK :: EDIT 1 PIJ ;
CALC := FLU: CP LIBRARY2 TRACK :: EDIT 1 TYPE K ;
assertS CALC :: K-EFFECTIVE 1 1.316920 ;
ECHO "test uo2_295_kec4 completed" ;
QUIT "LIST" .
