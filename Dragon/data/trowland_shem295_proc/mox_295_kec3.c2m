*----
*
*  TEST CASE mox_295_kec3
*  MOX ROWLAND'S BENCHMARK
*  DISTRIBUTED SELF-SHIELDING
*  295-GROUP JEF2.2 DRAGLIB
*
*  Author: A. Hebert
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM TRACK LIBRARY LIBRARY2 CP CALC ;
MODULE LIB: GEO: SYBILT: USS: ASM: FLU: ;
INTEGER COMB0101 COMB0102 COMB0103 COMB0104 COMB0105 COMB0106 GAIN1
        MODE1 MODE2 := 
        1 2 3 4 5 6 7 8 9 ; (* DISTRIBUTED SELF-SHIELDING *)
REAL RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 ;
PROCEDURE assertS ;
*
EVALUATE RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 := 0.259307 
0.343031 0.366715 0.388960 0.3996186 0.41 ;
GEOM := GEO: :: TUBE 9
      R+ REFL
      RADIUS 0.0 <<RCOMB1>> <<RCOMB2>> <<RCOMB3>> <<RCOMB4>>
                 <<RCOMB5>> <<RCOMB6>> 0.475 0.6045552 0.710879
      MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
          <<COMB0105>> <<COMB0106>> <<GAIN1>> <<MODE1>> <<MODE2>>
          ;
TRACK := SYBILT: GEOM ::
     TITLE 'MOX ROWLAND S BENCHMARK'
     MAXR 20 QUA1 5 ;
LIBRARY := LIB: ::
 EDIT 1
 NMIX 9    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 ADED 4 NELAS N4N N2N N3N
 CALENDF 3 PT      (*CALENDF TYPE PROBABILITY TABLES*)
*
 MIXS LIB: DRAGON FIL: DLIB_295
 MIX <<COMB0101>> 300.0 (*COMB0101*)
    U235     = U235  5.118E-5 CORR 1 IRSET PT 1
    U238     = U238  2.042E-2 CORR 1 IRSET PT 1
    PU238    = Pu238 2.714E-5 CORR 1 IRSET PT 1
    PU239    = Pu239 1.972E-3 CORR 1 IRSET PT 1
    PU240    = Pu240 4.256E-4 CORR 1 IRSET PT 1
    PU241    = Pu241 3.577E-5 CORR 1 IRSET PT 1
    PU242    = Pu242 1.234E-5 CORR 1 IRSET PT 1
    AM241    = Am241 1.406E-5 CORR 1 IRSET PT 1
    O16      = O16   4.588E-2
 MIX <<COMB0102>> COMB <<COMB0101>> 1.0 (*COMB0102*)
 MIX <<COMB0103>> COMB <<COMB0101>> 1.0 (*COMB0103*)
 MIX <<COMB0104>> COMB <<COMB0101>> 1.0 (*COMB0104*)
 MIX <<COMB0105>> COMB <<COMB0101>> 1.0 (*COMB0105*)
 MIX <<COMB0106>> COMB <<COMB0101>> 1.0 (*COMB0106*)
 MIX <<GAIN1>> 300.0 (*GAIN1*)
    ZR      = Zr0   3.88E-2 2 IRSET PT 1
 MIX <<MODE1>> 293.6 (*MODE1*)
    H1      = H1_H2O 4.744E-2
    O16     = O16 2.372E-2
 MIX <<MODE2>> 293.6 (*MODE2*)
    H1      = H1_H2O 4.744E-2
    O16     = O16 2.372E-2
 ;
LIBRARY2 := USS: LIBRARY TRACK :: EDIT 9 TRAN PASS 2 GRMIN 56 ;
CP := ASM: LIBRARY2 TRACK :: EDIT 1 PIJ ;
CALC := FLU: CP LIBRARY2 TRACK :: EDIT 1 TYPE K ;
assertS CALC :: K-EFFECTIVE 1 1.261764 ;
ECHO "test mox_295_kec3 completed" ;
QUIT "LIST" .
