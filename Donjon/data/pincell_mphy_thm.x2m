***********************************************************
* Input file :  pincell_mphy_thm.x2m                      *
*                                                         *
* Test of a PWR single pincell                            *
*                                                         *
* The aim is to test the THM module of DONJON             *
***********************************************************
 
LINKED_LIST Geom Track Flux Cpo Matex Lib MicroF MacroF Macro2 Fmap
            System Power Burnup Thm DONNEES ;
MODULE      GEO: RESINI: TRIVAT: TRIVAA: GREP: FIND0: NCR: FLUD: FLPOW:
            MACINI: USPLIT: TINST: UTL: DELETE: ABORT: THM: END: ;

PROCEDURE   assertS ;
SEQ_ASCII   _ACompo ;
INTEGER     maxstep := 67 ;
REAL        Fuelpwr := 38.425 ; ! W/g ou kW/kg ou MW/t
REAL        Mass := 4.655149E-3 ; ! kg
*----
*  Calculation options
*----
STRING Dir := "EDI2B" ;
REAL   Tfuel := 318.20357 ; (*fuel temperature (K)*)
REAL   Tcool := 318.15044 ; (*coolant temperature (K)*)
REAL   Dfuel := 9.7413951 ; (*fuel density (g/cc)*)
REAL   dens_mod_0 := 0.65 ;
REAL   powi := Fuelpwr Mass * 1.E3 / ;
ECHO "total reactor power=" powi "MW" ;

*----
*  Recover the Multicompo
*----
Cpo := _ACompo ;
UTL: Cpo :: DIR ;

INTEGER MaxR := 10000 ;
INTEGER Iter := 1 ;
REAL keff11 keff12 keff1n ;
REAL Y1 Y2 ROOT YNEW ;
LOGICAL CONV ;

REAL maxh := 80.0 ;
REAL z1 := maxh 20.0 / ;
REAL z2 := maxh 20.0 / z1 + ;
REAL z3 := maxh 20.0 / z2 + ;
REAL z4 := maxh 20.0 / z3 + ;
REAL z5 := maxh 20.0 / z4 + ;
REAL z6 := maxh 20.0 / z5 + ;
REAL z7 := maxh 20.0 / z6 + ;
REAL z8 := maxh 20.0 / z7 + ;
REAL z9 := maxh 20.0 / z8 + ;
REAL z10 := maxh 20.0 / z9 + ;
REAL z11 := maxh 20.0 / z10 + ;
REAL z12 := maxh 20.0 / z11 + ;
REAL z13 := maxh 20.0 / z12 + ;
REAL z14 := maxh 20.0 / z13 + ;
REAL z15 := maxh 20.0 / z14 + ;
REAL z16 := maxh 20.0 / z15 + ;
REAL z17 := maxh 20.0 / z16 + ;
REAL z18 := maxh 20.0 / z17 + ;
REAL z19 := maxh 20.0 / z18 + ;
REAL z20 := maxh 20.0 / z19 + ;

REAL Cote      := 1.26  ;
Geom := GEO: :: CAR3D 1 1 20
   X- REFL X+ REFL    Y- REFL Y+ REFL    Z- REFL Z+ REFL
   MESHX 0.0 <<Cote>>
   MESHY 0.0 <<Cote>>
   MESHZ 0.0 <<z1>> <<z2>> <<z3>> <<z4>> <<z5>> <<z6>> <<z7>> <<z8>>
         <<z9>> <<z10>> <<z11>> <<z12>> <<z13>> <<z14>> <<z15>> <<z16>>
         <<z17>> <<z18>> <<z19>> <<z20>>
   MIX
   PLANE 1
      1
   PLANE 2  SAME 1
   PLANE 3  SAME 1
   PLANE 4  SAME 1
   PLANE 5  SAME 1
   PLANE 6  SAME 1
   PLANE 7  SAME 1
   PLANE 8  SAME 1
   PLANE 9  SAME 1
   PLANE 10 SAME 1
   PLANE 11 SAME 1
   PLANE 12 SAME 1
   PLANE 13 SAME 1
   PLANE 14 SAME 1
   PLANE 15 SAME 1
   PLANE 16 SAME 1
   PLANE 17 SAME 1
   PLANE 18 SAME 1
   PLANE 19 SAME 1
   PLANE 20 SAME 1
;

Geom Matex := USPLIT: Geom :: NGRP 2 MAXR <<MaxR>>
               NFUEL 1  FMIX  1
;

Track := TRIVAT: Geom ::
   EDIT 1 MAXR <<MaxR>> MCFD 1 ;

*--
* Fuel map definition
*--
Fmap Matex := RESINI: Matex ::
      ::: GEO: CAR3D 1 1 20
                EDIT  0
                X- REFL X+ REFL    Y- REFL Y+ REFL    Z- REFL Z+ REFL
   MESHX 0.0 <<Cote>>
   MESHY 0.0 <<Cote>>
   MESHZ 0.0 <<z1>> <<z2>> <<z3>> <<z4>> <<z5>> <<z6>> <<z7>> <<z8>>
         <<z9>> <<z10>> <<z11>> <<z12>> <<z13>> <<z14>> <<z15>> <<z16>>
         <<z17>> <<z18>> <<z19>> <<z20>>
   MIX
   PLANE 1
      1
   PLANE 2  SAME 1
   PLANE 3  SAME 1
   PLANE 4  SAME 1
   PLANE 5  SAME 1
   PLANE 6  SAME 1
   PLANE 7  SAME 1
   PLANE 8  SAME 1
   PLANE 9  SAME 1
   PLANE 10 SAME 1
   PLANE 11 SAME 1
   PLANE 12 SAME 1
   PLANE 13 SAME 1
   PLANE 14 SAME 1
   PLANE 15 SAME 1
   PLANE 16 SAME 1
   PLANE 17 SAME 1
   PLANE 18 SAME 1
   PLANE 19 SAME 1
   PLANE 20 SAME 1
;
!
NXNAME '01' NYNAME  'A'
NCOMB 1
B-ZONE 1

ADD-PARAM PNAME 'T-FUEL' PARKEY 'TFA' GLOBAL
ADD-PARAM PNAME 'T-COOL' PARKEY 'TCA' GLOBAL
ADD-PARAM PNAME 'D-FUEL' PARKEY 'DFA' GLOBAL
ADD-PARAM PNAME 'D-COOL' PARKEY 'DCA' GLOBAL
BTYPE INST-BURN
INST-BVAL CHAN 0.0
REACTOR-POW <<powi>> AXIAL-PFORM 1.0 1.0 1.0 1.0 1.0 1.0 1.0 1.0 1.0 1.0
                                 1.0 1.0 1.0 1.0 1.0 1.0 1.0 1.0 1.0 1.0
SET-PARAM 'T-FUEL' <<Tfuel>>
SET-PARAM 'T-COOL' <<Tcool>>
SET-PARAM 'D-FUEL' <<Dfuel>>
SET-PARAM 'D-COOL' <<dens_mod_0>>
FUEL WEIGHT <<Mass>>
;

UTL: Fmap :: STEP UP PARAM STEP AT 4 DIR IMPR P-NAME * ;
*--
* THM single-stage calculation
*--
Thm Fmap := THM: Fmap ::
    EDIT 20
    FPUISS 0.93
    INLET 330000.0 (*Pa*) 318.15 (*K*)
    INLET-Q 7.9770045E-5 (*m2*) 0.375 (*inlet mass flow rate kg/s*)
    CRITFL 8.5E6 (* W/m^2 *)
    ASSMB 1 0
    RADIUS 4.55063E-3 4.60169E-3 5.00184E-3 5.00184E-3 (* m *)
    RODMESH 15 20
    MONO
;

*--
* Dump THM object
*--
UTL: Thm :: DIR DUMP ;

*--
* Cross-section database interpolation
*--
MicroF := NCR: Cpo Fmap ::
       EDIT 2
       MICRO LINEAR
       TABLE Cpo <<Dir>> 'burnup'
         MIX 1 INST-BURN
               SET LINEAR 'burnup' MAP
               SET CUBIC 'DCA' <<dens_mod_0>>
               SET CUBIC 'DCAH' <<dens_mod_0>>
               ADD 'DCA' <<dens_mod_0>> MAP
                        REF 'burnup' SAMEASREF
                        ENDREF
         ENDMIX
  ;
MacroF := MicroF :: STEP UP 'MACROLIB' ;
  
Macro2 Matex := MACINI: Matex MacroF :: FUEL ;
  
*--
* Steady-state diffusion calculation
*--
System := TRIVAA: Macro2 Track ;

Flux := FLUD: System Track :: EDIT 1 ADI 4 ACCE 5 3 ;
System MacroF Macro2 := DELETE: System MacroF Macro2 ;

GREP: Flux :: GETVAL 'K-EFFECTIVE' 1 >>keff11<< ;
ECHO "+++ Burnup= 0.0 Keff=" keff11 ;

assertS Flux :: 'K-EFFECTIVE' 1 1.354165 ;

ECHO "test pincell_mphy_thm.x2m completed" ;
END: ;
