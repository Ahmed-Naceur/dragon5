!********************************************************************
!                                                                    
!      MODELE 4x4x4 POUR SUIVI DE COEUR COUPLE AVEC DRAGON
!                                                                    
!********************************************************************
*----
* INITIALISATION DONJON
*----
 PROCEDURE    PgeoCell PresCell Pmac MainDraG2 GeoCellG2 assertS ;
 LINKED_LIST  G23D2 MACRO INDEX MACRO2 SYSTEM FLUX TRACK
              RESEAU MACRES POWER ;
 MODULE       TRIVAT: TRIVAA: FLUD: FLPOW: MACINI: CRE: TINST:
              DELETE: END: GREP: ;
 REAL         B1 K1 ;
 REAL         Precf := 1.E-4  ;
*----
* RECOVER INPUT ASCII FILES
*----
 SEQ_ASCII    RESCV4 :: FILE 'RESCV4' ;
 SEQ_ASCII    BckHst1 :: FILE 'BckHst1V4'  ;
*
 G23D2 INDEX  := PgeoCell ;
 MACRO := Pmac ;
*
* IMPORT DE RESEAU A PARTIR D'UN CALCUL D'AGE PRECEDENT.
* 
 EVALUATE B1 := 15000.  ;
 RESEAU INDEX := PresCell INDEX :: <<B1>> ;
 RESEAU := DELETE: RESEAU ;
*
 RESEAU := RESCV4  ;
*
*----
* INITIALISATION DRAGON
*----
LINKED_LIST  GeoS    GeoF                               ;
SEQ_BINARY   IntLinS IntLinF                            ;
XSM_FILE     History                                    ;
History := BckHst1 ;
*----
*  Definir le type de geometrie et de bibliotheque
*  Modele        : Modele de cellule considere
*                  "NG2C"  -  > Nouvelle cellule G-2 avec
*                               frontiere Cartesienne
*                  "NG2A"    -> Nouvelle cellule G-2 avec
*                               frontiere annulaire
*                  "IGE227"  -> Geometrie du rapport
*                               IGE-227 avec  frontiere annulaire
*                               (defaut)
*                  "IGE227C" -> Geometrie du rapport
*                               IGE-227 avec  frontiere cartesiennes
*                  "Simple"  -> Geometrie de test simplifiee
*  RefLib        : Type de bibliotheque
*                  "Winfrith" -> Bibliotheque Winfrith WIMS-AECL
*                  "ENDFB5"   -> Bibliotheque ENDF/B-V WIMS-AECL
*                               (defaut)ASM.f
*                  "ENDFB6"   -> Bibliotheque ENDF/B-VI WIMS-AECL
*----
STRING       Modele   RefLib                            ;
EVALUATE     Modele   RefLib                            :=
            "IGE227" "Winfrith"                         ;
*----
*  Definir les options locales pour les geometries
*----
STRING       Calo     Auto    Flux                      ;
EVALUATE     Calo     Auto    Flux                      := 
            "Present" "Auto" "Flux"                     ;
*----
*  Definir les parametrees d'impression 
*----
INTEGER      NivImp := 2                            ;
*----
*  Obtenir les Geometries 
*----
GeoS IntLinS := GeoCellG2                               ::
  <<Modele>> <<Calo>> <<Auto>> <<NivImp>>           ;
GeoF IntLinF := GeoCellG2                               ::
  <<Modele>> <<Calo>> <<Flux>> <<NivImp>>           ; 
*
* Parametres de parallelisation
* ibgncha numero du premier canal a calculer sur ce CPU
* iendcha numero du dernier canal a calculer sur ce CPU
*----
 INTEGER    ibgncha iendcha := 1 16 ;
 INTEGER    ncha  nbun  := 16 4 ;
 REAL       ppm purmod := 0.0 0.0 ;
*
**************************************************************************
*                                                                       
*  SET EVOLUTION AND REFUELING                                          
*                                                                       
 RESEAU := TINST: RESEAU ::                                            
    EDIT 5                                                              
     TIME  0.5  DAY
     REFUEL CHAN A1  2
     TIME  1.5  DAY
     REFUEL CHAN C4  2
     TIME  1.0  DAY   ;
*----
* CALCULATION OF THE NEW XS (CELL CALCULATION)  
*----
 History MACRES := MainDraG2 History RESEAU 
                      GeoS GeoF IntLinS IntLinF ::
  <<ncha>> <<nbun>> <<ibgncha>> <<iendcha>> 
  <<RefLib>> <<NivImp>> <<ppm>> <<purmod>> ;

*----
* CALCULATION OF THE NEW POWER DISTRIBUTION
*----
 MACRO2 INDEX := MACINI: INDEX MACRO MACRES ;                                 
  TRACK := TRIVAT: G23D2 ::  EDIT 1 TITLE '4*4*4 CELL CALCULATION'
                            MAXR 64 MCFD 1 ;
 SYSTEM := TRIVAA: MACRO2 TRACK :: EDIT 1 ; 
 FLUX := FLUD: SYSTEM TRACK ::  EDIT 1  ADI 1 EXTE <<Precf>>   ; 
                                                                        
 POWER := FLPOW: RESEAU FLUX TRACK INDEX ::  EDIT 5 PTOT 40. ;    
                                                                        
 GREP: FLUX ::  GETVAL K-EFFECTIVE 1 >>K1<< ; 

 ECHO "REACTIVITE INITIALE " K1 ;

History      := DELETE: History                         ;
GeoS IntLinS := DELETE: GeoS IntLinS                    ;
GeoF IntLinF := DELETE: GeoF IntLinF                    ;

assertS FLUX :: 'K-EFFECTIVE' 1 0.9799650 ;

END: ;
QUIT .
