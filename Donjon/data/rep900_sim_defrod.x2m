*----
*  Name          : rep900_sim_defrod.x2m
*  Type          : DONJON procedure
*  Author        : V. Salino and G. Tixier (IRSN), basis by A. Hebert
*  Date          : 01/11/2014
*
*  Compute a simple step with rod insertion
*----
*  References
*
*    Loading pattern freely inspired from public data:
*      Exploitation des coeurs REP (p.36),
*      N. Kerkar, P. Paulin,
*      EDP Sciences, 2008.
*----
*  STRUCTURES, MODULES and PROCEDURES definitions
*----
LINKED_LIST GeoRes Track MacroRefl Matex Fmap Power PrevPower
            Thermo MicroF Burn Refl.XS SapUOX SapMOX Flux ;
SEQ_ASCII   _Saphyb_UOX :: FILE './_Saphyb_UOX' ;
SEQ_ASCII   _Saphyb_MOX :: FILE './_Saphyb_MOX' ;
SEQ_ASCII   _Refl.XS_as :: FILE './_Refl.XS_as' ;
MODULE      TRIVAT: RESINI: SIM: DELETE: GREP: UTL: EVO: ABORT:
            ROD: END: ;
PROCEDURE   GeoCo SetFuelMap InitSteady ;
PROCEDURE   assertS ;
*----
*  Local parameters
*----
LOGICAL True False := $True_L $False_L ;
REAL BUcycle PrevBUcycle ;
REAL CBinit := 2000.0 ;
INTEGER BUindex ;
*----
*  Calculation options
*----
INTEGER Splitx Splitz := 1 1 ;
STRING  Dir := "EDI2B" ;
*----
*  Recover the reflector Macrolib and Saphybs
*----
Refl.XS := _Refl.XS_as ;
SapUOX := _Saphyb_UOX ;
SapMOX := _Saphyb_MOX ;
*----
*  Set reflector cross sections (Lefebvre-Lebigot)
*----
MacroRefl := Refl.XS ;
*----
*  Define geometric properties
*----
REAL rPavX     := 17.0 ;  ! assemblage 17 x 17
INTEGER iPavX  := 17 ; ! assemblage 17 x 17
REAL LPitch    := 1.26 ;
REAL Lame      := 0.04 ;
REAL dx        := 21.5 ;
INTEGER MaxR   := iPavX iPavX * 33 * Splitx * Splitx * Splitz * ;
GeoRes Matex := GeoCo  ::
   <<Splitx>> <<Splitz>> <<MaxR>> <<dx>> ;
Track := TRIVAT: GeoRes ::
   MAXR <<MaxR>> DUAL 2 3 ;

*--
* Define the fuel map
*--
Fmap Matex := SetFuelMap Matex :: <<dx>> ;
*----
*  A simple cycle depletion
*----
STRING Cycle := "T1" ;
INTEGER CycleIndex := 1 ;
EVALUATE BUcycle BUindex := 0.0 1 ;
STRING   MainCycle ;

* Rod groups definition and rod map
Fmap := ROD: Fmap ::
  EDIT 0 PARA "BARR" 1.
  LINS 260 STEP 1.5875 NRFB 1
  RGRP 9  2
    R   20 2. 101.6 3.
    N1   0 2. 101.6 3.
    N2   0 2. 101.6 3. 
    SA   0 2. 101.6 3. 
    SB   0 2. 101.6 3.
    SC   0 2. 101.6 3.
    SD   0 2. 101.6 3.
    G1   0 0.
    G2   0 0.
  ENDRGRP
  RMAP 157
                           |   |   |
                   |  N2   |  R    |  N2   |
               |   |   |  SB   |  SB   |  SD   |
           |  N1   |  G2   |  G1   |  G2   |  N1   |
       |  SD   |  SC   |  SD   |   |   |  SC   |   |   |
      N2   |  G2   |  R    |  N1   |  R    |  G2   |  N2
   |   |  SB   |   |   |  SA   |  SA   |  SD   |  SB   |   |
   |  R    |  G1   |  N1   |  SA   |  N1   |  G1   |  R    |
   |   |  SB   |  SD   |  SA   |  SA   |   |   |  SB   |   |
      N2   |  G2   |  R    |  N1   |  R    |  G2   |  N2
       |   |   |  SC   |   |   |  SD   |  SC   |  SD   |
           |  N1   |  G2   |  G1   |  G2   |  N1   |
               |  SD   |  SB   |  SB   |   |   |
                   |  N2   |  R    |  N2   |
                           |   |   |
  ENDRMAP
;

Fmap := UTL: Fmap :: DUMP ;
  
EVALUATE MainCycle := "T" CycleIndex I_TO_S + ;
EVALUATE Cycle := MainCycle ;
Fmap := SIM: Fmap ::
 EDIT 1
 CYCLE <<Cycle>>
  QMAP
      H   G   F   E   D   C   B   A
   8 SPC SPC SPC SPC SPC SPC NEW SPC
   9 SPC SPC NEW SPC SPC SPC NEW NEW
  10 SPC NEW SPC SPC SPC SPC NEW  |
  11 SPC SPC SPC SPC SPC NEW NEW  |
  12 SPC SPC SPC SPC SPC NEW  |   |
  13 SPC SPC SPC NEW NEW  |   |   |
  14 NEW NEW NEW NEW  |   |   |   |
  15 SPC NEW  |   |   |   |   |   |
  SPEC
  E09 J11 L07 G05 G11 L09 J05 E07
  D10 K12 M06 F04 F12 M10 K04 D06 SET AVGB 12500. ! UOX
  E10 K11 L06 F05 F11 L10 K05 E06
  D09 J12 M07 G04 G12 M09 J04 D07 SET AVGB 12500. ! MOX
  C09 J13 N07 G03 G13 N09 J03 C07
  C10 K13 N06 F03 F13 N10 K03 C06 SET AVGB 25000.
  D11 L12 M05 E04 E12 M11 L04 D05 SET AVGB 26000.
  G08 H09 J08 H07
  F08 H10 K08 H06
  D08 H12 M08 H04
  E11 L11 L05 E05 SET AVGB 12500.
  F10 K10 K06 F06
  D12 M12 M04 D04
  E08 H11 L08 H05 SET AVGB 25000.
  C08 H13 N08 H03
  A08 H15 R08 H01
  G09 J09 J07 G07 SET AVGB 26000.
  H08 SET AVGB 12500.
 ENDCYCLE ;

Flux Thermo MicroF Burn Fmap Matex  := InitSteady
Fmap Matex SapUOX SapMOX MacroRefl Track ;

assertS Flux :: 'K-EFFECTIVE' 1 1.203020 ;

* We want to change the insertion of the R group
Fmap := ROD: Fmap ::
  RGRP 1
    R   19
  ENDRGRP
;

Fmap := UTL: Fmap :: DUMP ;

END: ;
QUIT "LIST" .
