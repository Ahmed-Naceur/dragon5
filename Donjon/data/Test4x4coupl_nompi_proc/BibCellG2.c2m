*DECK BibCellG2.c2m
*----------
*  Nom           : BibCellG2.c2m
*  Type          : procedure DRAGON
*  Usage         : Definir bibliotheque pour cellule G-2
*  Auteur        : G. Marleau
*  Date          : 2002/12/03
*  
*  Description de la procedure : 
*
*    Biblio     := BibCellG2 [Biblio  ConcEvol] ::
*      <<RefLib>>    <<BibOpt>>    
*      <<TempComb>>  <<EnriComb>>  <<DensComb>>
*      <<TempCalo>>  <<PurtCalo>>  <<DensCalo>>
*      <<TempMod>>   <<PurtMod>>   <<DensMod>> <<BoreMod>>
*      <<Impression>>
*      ;
*  Structures de donnees:
*    Biblio        : Bibliotheque de section efficace (L_LIBRARY).
*    ConcEvol      : Structure de donnee pour evolution (L_BURNUP).
*  Parametres de controle: 
*    RefLib        : Type de bibliotheque
*                    "Winfrith" -> Bibliotheque Winfrith WIMS-AECL
*                    "ENDFB5"   -> Bibliotheque ENDF/B-V WIMS-AECL
*                                  (defaut)
*                    "ENDFB6"   -> Bibliotheque ENDF/B-VI WIMS-AECL
*                    "IGE227W"  -> Bibliotheque Winfrith WIMS-AECL
*                                  isotopes de IGE227
*                    "IGE227E5" -> Bibliotheque ENDF/B-V WIMS-AECL
*                                  isotopes de IGE227
*    BibOpt        : Options de calcul
*                    BibOpt =  0 -> Initialiser la blbliotheque
*                                   aux conditions initiales sans
*                                   mise a jour en fonction du
*                                   temps d'evolution.
*                    BibOpt =  1 -> Initialiser la blbliotheque
*                                   aux conditions initiales et 
*                                   mettre a jour 
*                                   utilisant l'information dans 
*                                   ConcEvol.
*    TempComb      : Temperature combustible (C)
*    EnriComb      : Enrichissement combustible (%)
*    DensComb      : Densite combustible (g/cc)
*    TempCalo      : Temperature caloporteur (C)
*    PurtCalo      : % D2O caloporteur
*    DensCalo      : Densite caloporteur (g/cc) 
*                    A calculer si 0.0
*    TempMod       : Temperature moderateur (C)
*    PurtMod       : % D2O moderateur
*    DensMod       : Densite moderateur (g/cc)
*                    A calculer si 0.0
*    BoreMod       : Bore dans le moderateur (ppm)
*    Impression    : Niveau d'impression
*  Procedure requise:
*    BibCreaG2     : Creation le la bibliotheque de section efficace 
*                    pour le calcul de cellule
*  Modules DRAGON requis:
*    LIB:          : Gestion des bibliotheque microscopiques
*----------
* Definition des parametres de la procedure
* 
PARAMETER   Biblio  ConcEvol                            ::
  EDIT 1
    ::: LINKED_LIST  Biblio  ConcEvol        ;          ; 
STRING   RefLib                                         ; 
INTEGER  BibOpt                                         ;
REAL     TempComb EnriComb  DensComb
         TempCalo PurtCalo  DensCalo
         TempMod  PurtMod   DensMod  BoreMod            ;
INTEGER  Impression                                     ;
*----
*  Reccuperer information transferee a la procedure
*----
::   >>RefLib<<   >>BibOpt<<  
     >>TempComb<< >>EnriComb<<  >>DensComb<<
     >>TempCalo<< >>PurtCalo<<  >>DensCalo<< 
     >>TempMod<<  >>PurtMod<<   >>DensMod<<  >>BoreMod<<
     >>Impression<<                                     ;
*----
*  Procedures externes et Modules et structures de donnees
*----
PROCEDURE    BibCreaG2                                  ; 
MODULE       LIB: DELETE:                               ;
LINKED_LIST  TmpBibl                                    ;
*----
*  Imprimer donnees d'entree si requis
*----
IF Impression 10 >  THEN 
  ECHO "Bibliotheque                =" RefLib             ;
  ECHO "Option de calcul            =" BibOpt             ;
  ECHO "Temperature combustible     =" TempComb  "C"      ;
  ECHO "Enrichissement combustible  =" EnriComb  "%"      ;
  ECHO "Densite combustible         =" DensComb  "g/cc"   ;
  ECHO "Temperature caloporteur     =" TempCalo  "C"      ;
  ECHO "Purete caloporteur          =" PurtCalo  "%"      ;
  IF DensCalo 0.0 >  THEN
    ECHO "Densite caloporteur       =" DensCalo  "g/cc"   ;  
  ENDIF ;
  ECHO "Temperature moderateur      =" TempMod  "C"       ;
  ECHO "Purete moderateur           =" PurtMod  "%"       ;
  IF DensMod 0.0 > THEN
    ECHO "Densite moderateur        =" DensMod   "g/cc"   ;  
  ENDIF ;
  ECHO "Bore moderateur             =" BoreMod  "ppm"     ;
ENDIF ;
*----
*  Variables locales
*----
IF BibOpt  0 =  THEN 
  Biblio := BibCreaG2                                     ::
     <<RefLib>>   
     <<TempComb>>  <<EnriComb>>  <<DensComb>>
     <<TempCalo>>  <<PurtCalo>>  <<DensCalo>>
     <<TempMod>>   <<PurtMod>>   <<DensMod>> <<BoreMod>>
     <<Impression>>                                       ;
ELSE
  TmpBibl := BibCreaG2                                    ::
     <<RefLib>>   
     <<TempComb>>  <<EnriComb>>  <<DensComb>>
     <<TempCalo>>  <<PurtCalo>>  <<DensCalo>>
     <<TempMod>>   <<PurtMod>>   <<DensMod>> <<BoreMod>>
     <<Impression>>                                       ;
  TmpBibl := LIB: TmpBibl ConcEvol   ::  BURN 1           ;
  Biblio := TmpBibl                                       ;
ENDIF ;
*---- 
*  terminer execution
*----
QUIT "LIST"                                             .
