*DECK BibCreaG2.c2m
*----------
*  Nom           : BibCreaG2.c2m
*  Type          : procedure DRAGON
*  Usage         : Creer une bibliotheque pour cellule G-2
*                  a partir des parametres de cellules pour
*                  du combustible neuf
*  Auteur        : G. Marleau
*  Date          : 2002/12/03
*  
*  Description de la procedure : 
*    Biblio     := BibCreaG2 ::
*      <<RefLib>> 
*      <<TempComb>>  <<EnriComb>>  <<DensComb>>
*      <<TempCalo>>  <<PurtCalo>>  <<DensCalo>>
*      <<TempMod>>   <<PurtMod>>   <<DensMod>> <<BoreMod>>
*      <<Impression>>
*      ;
*  Structures de donnees:
*    Biblio        : Bibliotheque de section efficace (L_LIBRARY).
*  Parametres de controle: 
*    RefLib        : Type de bibliotheque
*                    "Winfrith" -> Bibliotheque Winfrith WIMS-AECL
*                    "ENDFB5"   -> Bibliotheque ENDF/B-V WIMS-AECL
*                                 (defaut)
*                    "ENDFB6"   -> Bibliotheque ENDF/B-VI WIMS-AECL
*                    "IGE227W"  -> Bibliotheque Winfrith WIMS-AECL
*                                  isotopes de IGE227
*                    "IGE227E5" -> Bibliotheque ENDF/B-V WIMS-AECL
*                                  isotopes de IGE227
*    TempComb      : Temperature combustible (C)
*    EnriComb      : Enrichissement combustible (%)
*    DensComb      : Densite combustible (g/cc)
*    TempCalo      : Temperature caloporteur (C)
*    PurtCalo      : % D2O caloporteur
*    DensCalo      : Densite caloporteur (g/cc) 
*                    A calculer si 0.0
*    TempMod       : Temperature moderateur (C)
*    PurtMod       : % D2O moderateur
*    DensMod       : Densite moderateur (g/cc)
*                    A calculer si 0.0
*    BoreMod       : Bore dans le moderateur (ppm)
*    Impression    : Niveau d'impression
*  Modules DRAGON requis:
*    INFO:         : Gestion des densites et concentration isotopique.
*    LIB:          : Gestion des bibliotheque microscopiques.
* 
*----------
* Definition des parametres de la procedure
* 
PARAMETER   Biblio                                      ::
  EDIT 1
    ::: LINKED_LIST  Biblio          ;                  ; 
STRING   RefLib                                         ;
REAL     TempComb EnriComb  DensComb
         TempCalo PurtCalo  DensCalo
         TempMod  PurtMod   DensMod  BoreMod            ;
INTEGER  Impression                                     ;
*----
*  Reccuperer information transferee a la procedure
*----
::   >>RefLib<<
     >>TempComb<< >>EnriComb<<  >>DensComb<<
     >>TempCalo<< >>PurtCalo<<  >>DensCalo<< 
     >>TempMod<<  >>PurtMod<<   >>DensMod<<  >>BoreMod<<
     >>Impression<<                                     ;
*----
*  Imprimer donnees d'entree si requis
*----
IF Impression 10 > THEN 
  ECHO "Bibliotheque                =" RefLib           ;
  ECHO "Temperature combustible     =" TempComb  "C"    ;
  ECHO "Enrichissement combustible  =" EnriComb  "%"    ;
  ECHO "Densite combustible         =" DensComb  "g/cc" ;
  ECHO "Temperature caloporteur     =" TempCalo  "C"    ;
  ECHO "Purete caloporteur          =" PurtCalo  "%"    ;
  IF DensCalo 0.0 >                                    THEN
      ECHO "Densite caloporteur       =" DensCalo  "g/cc" ;  
                                                    ENDIF ;
  ECHO "Temperature moderateur      =" TempMod  "C"     ;
  ECHO "Purete moderateur           =" PurtMod  "%"     ;
  IF DensMod 0.0 >                                    THEN
      ECHO "Densite moderateur        =" DensMod   "g/cc" ;  
                                                    ENDIF ;
  ECHO "Bore moderateur             =" BoreMod  "ppm"   ;
ENDIF ;
*---- 
*  Modules
*--
MODULE   INFO: LIB:                                     ;
*----
*  Local variables
*---- 
STRING   LibName                                        ;
STRING   IsoU5    IsoU8    IsoU6    IsoPu9   IsoO16 
         IsoH1    IsoD2    IsoB10   IsoB11   IsoBn
         IsoZr    IsoFe    IsoCr    IsoNi    IsoNb
         IsoC12   IsoHe                                 ;
*----
*  Nom de la bibliotheque et des isotopes
*----
IF     RefLib "Winfrith" =                           THEN
*----
*  Winfrith
*----              
  EVALUATE  LibName :=        "WIMSLIB"                 ;
  EVALUATE IsoU5    IsoU8    IsoU6    IsoPu9   IsoO16 
           IsoH1    IsoD2    IsoB10   IsoB11   IsoBn
           IsoZr    IsoFe    IsoCr    IsoNi    IsoNb
           IsoC12   IsoHe                               :=
          "U235"   "U238"   "U236"   "PU239"  "O16"
          "H1"     "D2"     "B10"    "   "    "B-NAT"
          "ZR91"   "FE56"   "CR52"   "NI58"   "NB93"  
          "C12"    "HE4"                                ;
ELSEIF RefLib "IGE227W" =                            THEN
*----
*  Winfrith
*  IGE227
*----              
  EVALUATE  LibName :=        "WIMSLIB"                 ;
  EVALUATE IsoU5    IsoU8    IsoU6    IsoPu9   IsoO16 
           IsoH1    IsoD2    IsoB10   IsoB11   IsoBn
           IsoZr    IsoFe    IsoCr    IsoNi    IsoNb
           IsoC12   IsoHe                              :=
          "U235"   "U238"   "U236"   "PU239"  "O16"
          "H1"     "D2"     "B10"    "   "    "B-NAT"
          "ZR91"   "FE56"   "CR52"   "NI58"   "NB93"  
          "C12"    "HE4"                                ;
ELSEIF RefLib "ENDFB6"   =                           THEN
*----
*  ENDF/B-VI
*----              
  EVALUATE LibName :=        "E6MLIB"                   ;
  EVALUATE IsoU5    IsoU8    IsoU6    IsoPu9   IsoO16 
           IsoH1    IsoD2    IsoB10   IsoB11   IsoBn
           IsoZr    IsoFe    IsoCr    IsoNi    IsoNb
           IsoC12   IsoHe                              :=
          "U235"   "U238"   "U236"   "PU239"  "O16"
          "H1H2O"  "DD2O"   "B10"    "B11"    "   "
          "     "  "FE56"   "CR52"   "NI58"   "NB93" 
          "C"      " "                                  ;
ELSEIF RefLib "IGE227E5"   =                         THEN
*----
*  ENDF/B-V
*  IGE227
*----              
  EVALUATE LibName :=        "E5WLIB"                   ;
  EVALUATE IsoU5    IsoU8    IsoU6    IsoPu9   IsoO16 
           IsoH1    IsoD2    IsoB10   IsoB11   IsoBn
           IsoZr    IsoFe    IsoCr    IsoNi    IsoNb
           IsoC12   IsoHe                               :=
          "U235"   "U238"   "U236"   "PU239"  "O16"
          "H-H2O"  "D-D2O"  "B10"    "B11"    "B10"
          "ZR-NAT" "FE"     "CR"     "NI"     "NB93" 
          "C"      "HE4"                                ;
ELSE
*----
*  ENDF/B-V
*----              
  EVALUATE LibName :=        "E5WLIB"                   ;
  EVALUATE IsoU5    IsoU8    IsoU6    IsoPu9   IsoO16 
           IsoH1    IsoD2    IsoB10   IsoB11   IsoBn
           IsoZr    IsoFe    IsoCr    IsoNi    IsoNb
           IsoC12   IsoHe                               :=
          "U235"   "U238"   "U236"   "PU239"  "O16"
          "H-H2O"  "D-D2O"  "B10"    "B11"    "   "
          "ZR-NAT" "FE"     "CR"     "NI"     "NB93" 
          "C"      "HE4"                                ;
                                                  ENDIF ;
*----
*  Parametres pour densite calo et moderateur
*  ainsi que concentration de bore
*----
REAL     LDensCalo  LDensMod                            ; 
REAL     BNZrNb    BNZr    BNMod                        :=
         0.00021   0.00031 BoreMod  1.0E-4 *            ;  
REAL     DZrNPt    DZrNCt                               :=
         97.5      99.71                                ;
REAL     B10ZrNb        B10Zr        
         B11ZrNb        B11Zr                       
         B10Mod         B11Mod                          :=
         0.199 BNZrNb * 0.199 BNZr *  
         0.801 BNZrNb * 0.801 BNZr *                  
         0.199 BNMod *  0.801 BNMod *                   ;  
REAL     DZr90Pt           DZr91Pt   
         DZr92Pt           DZr94Pt
         DZr96Pt                                        :=
         DZrNPt   51.45  * DZrNPt   11.22  *
         DZrNPt   17.15  * DZrNPt   17.38  *
         DZrNPt    2.80  *                              ;
REAL     DZr90Ct           DZr91Ct
         DZr92Ct           DZr94Ct 
         DZr96Ct                                        :=
         DZrNCt   51.45  * DZrNCt   11.22  *
         DZrNCt   17.15  * DZrNCt   17.38  *
         DZrNCt    2.80  *                              ;
REAL
  FwgtU8  FwgtU5  FwgtO16
  CwgtH1  CwgtD2  CwgtO16  Cdens
  MwgtH1  MwgtD2  MwgtO16  Mdens                        ; 
*----
*   Calcul  densite et WGT% pour caloporteur et moderateur
*   et WGT% combustible
*---- 
IF DensCalo 0.0 >                                      THEN
  INFO: ::
    TMP: <<TempCalo>> K 
    PUR: <<PurtCalo>> WGT% 
    LIB: WIMSAECL FIL: <<LibName>>
    ISO: 3        <<IsoH1>>   <<IsoD2>>   <<IsoO16>> 
    CALC WGT% D2O >>CwgtH1<<  >>CwgtD2<<  >>CwgtO16<<     ; 
  EVALUATE  LDensCalo := DensCalo                         ;
                                                       ELSE
  INFO: ::
    TMP: <<TempCalo>> K 
    PUR: <<PurtCalo>> WGT% 
    CALC DENS WATER >>LDensCalo<<
    LIB: WIMSAECL FIL: <<LibName>>
    ISO: 3        <<IsoH1>>   <<IsoD2>>   <<IsoO16>> 
    CALC WGT% D2O >>CwgtH1<<  >>CwgtD2<<  >>CwgtO16<<     ;
                                                    ENDIF ;
IF DensMod 0.0 >                                       THEN
  INFO: ::
    TMP: <<TempMod>> K 
    PUR: <<PurtMod>> WGT% 
    LIB: WIMSAECL FIL: <<LibName>>
    ISO: 3        <<IsoH1>>   <<IsoD2>>   <<IsoO16>> 
    CALC WGT% D2O >>MwgtH1<<  >>MwgtD2<<  >>MwgtO16<<     ; 
  EVALUATE  LDensMod := DensMod                           ;
                                                       ELSE
  INFO: ::
    TMP: <<TempMod>> K 
    PUR: <<PurtMod>> WGT% 
    CALC DENS WATER >>LDensMod<<
    LIB: WIMSAECL FIL: <<LibName>>
    ISO: 3        <<IsoH1>>   <<IsoD2>>   <<IsoO16>> 
    CALC WGT% D2O >>MwgtH1<<  >>MwgtD2<<  >>MwgtO16<<     ; 
                                                    ENDIF ;
INFO: ::   
  ENR: <<EnriComb>> WGT%
  LIB: WIMSAECL FIL: <<LibName>>
  ISO: 3        <<IsoU5>>   <<IsoU8>>   <<IsoO16>> 
  CALC WGT% UO2 >>FwgtU5<<  >>FwgtU8<<  >>FwgtO16<<     ;
*----
*  donnees d'evolution de <<LibName>> 
*  section efficace microscopiques de <<LibName>>
*----
*----
*  Nom de la bibliotheque et des isotopes
*----
IF     RefLib "Winfrith" =                           THEN
*----
*  Winfrith
*----              
  Biblio   := LIB:                                      ::
    EDIT <<Impression>>
    NMIX 10  CTRA WIMS
    DEPL LIB: WIMSAECL FIL: <<LibName>>
    MIXS LIB: WIMSAECL FIL: <<LibName>>
    MIX  1  <<TempCalo>>  <<LDensCalo>> 
      H1    = <<IsoH1>>  <<CwgtH1>>  
      D2    = <<IsoD2>>  <<CwgtD2>>
      O16   = <<IsoO16>> <<CwgtO16>>  
    MIX  2  <<TempCalo>>  6.57
      ZR    = <<IsoZr>>  <<DZrNPt>>
      NB    = <<IsoNb>>  2.5
      BN    = <<IsoBn>> <<BNZrNb>>   
    MIX  3  <<TempMod>>  0.0016987     
      O16 = <<IsoO16>>   72.89      
      C12   = <<IsoC12>>  27.11        
    MIX  4  <<TempMod>>  6.44      
      ZR    = <<IsoZr>>  <<DZrNCt>>
      FE    = <<IsoFe>>  0.16      
      CR    = <<IsoCr>>  0.11         
      NI    = <<IsoNi>>   0.06    
      BN    = <<IsoBn>> <<BNZr>>   
    MIX  5  <<TempMod>>  <<LDensMod>>  
      H1    = <<IsoH1>>  <<MwgtH1>>   
      D2    = <<IsoD2>>  <<MwgtD2>> 
      O16   = <<IsoO16>> <<MwgtO16>>  
      BN    = <<IsoBn>> <<BNMod>>    
    MIX  6  <<TempComb>>  <<DensComb>>    
      O16   = <<IsoO16>> <<FwgtO16>> 
      U235  = <<IsoU5>>  <<FwgtU5>>   1
      U236  = <<IsoU6>>   0.0         1
      U238  = <<IsoU8>>  <<FwgtU8>>   1
      PU239 = <<IsoPu9>>  0.0         1
      PU240 = PU240       0.0         1
      PU241 = PU241       0.0         1
    MIX  7 COMB 6 100.0 
    MIX  8 COMB 6 100.0 
    MIX  9 COMB 6 100.0 
    MIX 10  <<TempCalo>>  6.44       
      ZR    = <<IsoZr>>  <<DZrNCt>>
      FE    = <<IsoFe>>  0.16      
      CR    = <<IsoCr>>  0.11         
      NI    = <<IsoNi>>  0.06      
      BN    = <<IsoBn>> <<BNZr>>   ;                                
*----
ELSEIF RefLib "IGE227W" =                            THEN
*----
*  Winfrith
*  IGE227
*----              
  Biblio   := LIB:                                      ::
    EDIT <<Impression>>
    NMIX 10  CTRA WIMS
    DEPL LIB: WIMSAECL FIL: <<LibName>>
    MIXS LIB: WIMSAECL FIL: <<LibName>>
    MIX  1  <<TempCalo>>  <<LDensCalo>> 
      H1    = <<IsoH1>>  <<CwgtH1>>  
      D2    = <<IsoD2>>  <<CwgtD2>>
      O16   = <<IsoO16>> <<CwgtO16>>  
    MIX  2  <<TempCalo>>  6.57
      ZR    = <<IsoZr>>  <<DZrNPt>>
      NB    = <<IsoNb>>  2.5
      BN    = <<IsoBn>> <<BNZrNb>>   
    MIX  3  <<TempMod>>  0.0014     
      HE4   = <<IsoHe>>  100.0     
    MIX  4  <<TempMod>>  6.44      
      ZR    = <<IsoZr>>  <<DZrNCt>>
      FE    = <<IsoFe>>  0.16      
      CR    = <<IsoCr>>  0.11         
      NI    = <<IsoNi>>  0.06    
      BN    = <<IsoBn>> <<BNZr>>   
    MIX  5  <<TempMod>>  <<LDensMod>>  
      H1    = <<IsoH1>>  <<MwgtH1>>   
      D2    = <<IsoD2>>  <<MwgtD2>> 
      O16   = <<IsoO16>> <<MwgtO16>>  
      BN    = <<IsoBn>> <<BNMod>>    
    MIX  6  <<TempComb>>  <<DensComb>>    
      O16   = <<IsoO16>> <<FwgtO16>> 
      U235  = <<IsoU5>>  <<FwgtU5>>   1
      U236  = <<IsoU6>>   0.0         1
      U238  = <<IsoU8>>  <<FwgtU8>>   1
      PU239 = <<IsoPu9>>  0.0         1
      PU240 = PU240       0.0         1
      PU241 = PU241       0.0         1
    MIX  7 COMB 6 100.0 
    MIX  8 COMB 6 100.0 
    MIX  9 COMB 6 100.0 
    MIX 10  <<TempCalo>>  6.44       
      ZR    = <<IsoZr>>  <<DZrNCt>>
      FE    = <<IsoFe>>  0.16      
      CR    = <<IsoCr>>  0.11         
      NI    = <<IsoNi>>  0.06      
      BN   = <<IsoBn>> <<BNZr>>   ;                                
ELSEIF RefLib "ENDFB6"   =                           THEN
*----
*  ENDF/B-VI
*----              
  Biblio   := LIB:                                      ::
    EDIT <<Impression>>
    NMIX 10  CTRA WIMS
    DEPL LIB: WIMSAECL FIL: <<LibName>>
    MIXS LIB: WIMSAECL FIL: <<LibName>>
    MIX  1  <<TempCalo>>  <<LDensCalo>> 
      H1    = <<IsoH1>>  <<CwgtH1>>  
      D2    = <<IsoD2>>  <<CwgtD2>>
      O16   = <<IsoO16>> <<CwgtO16>>  
    MIX  2  <<TempCalo>>  6.57
      Zr90  = ZR90PT   <<DZr90Pt>> 
      Zr91  = ZR91PT   <<DZr91Pt>>
      Zr92  = ZR92PT   <<DZr92Pt>> 
      Zr94  = ZR94PT   <<DZr94Pt>>
      Zr96  = ZR96PT   <<DZr96Pt>>
      NB    = <<IsoNb>>  2.5
      B10   = <<IsoB10>>  <<B10ZrNb>>  
      B11   = <<IsoB11>>  <<B11ZrNb>>   
    MIX  3  <<TempMod>>  0.0016987     
      O16   = <<IsoO16>>   72.89      
      C12   = <<IsoC12>>  27.11        
    MIX  4  <<TempMod>>  6.44      
      Zr90  = ZR90CT   <<DZr90Ct>>  
      Zr91  = ZR91CT   <<DZr91Ct>>
      Zr92  = ZR92CT   <<DZr92Ct>>    
      Zr94  = ZR94CT   <<DZr94Ct>>
      Zr96  = ZR96CT   <<DZr96Ct>>
      FE    = <<IsoFe>>  0.16      
      CR    = <<IsoCr>>  0.11         
      NI    = <<IsoNi>>   0.06    
      B10   = <<IsoB10>> <<B10Zr>>  
      B11   = <<IsoB11>> <<B11Zr>>   
    MIX  5  <<TempMod>>  <<LDensMod>>  
      H1    = <<IsoH1>>  <<MwgtH1>>   
      D2    = <<IsoD2>>  <<MwgtD2>> 
      O16   = <<IsoO16>> <<MwgtO16>>  
      B10   = <<IsoB10>> <<B10Mod>>  
      B11   = <<IsoB11>> <<B11Mod>>                   
    MIX  6  <<TempComb>>  <<DensComb>>    
      O16   = <<IsoO16>> <<FwgtO16>> 
      U235  = <<IsoU5>>  <<FwgtU5>>   1
      U236  = <<IsoU6>>   0.0         1
      U238  = <<IsoU8>>  <<FwgtU8>>   1
      PU239 = <<IsoPu9>>  0.0         1
      PU240 = PU240       0.0         1
      PU241 = PU241       0.0         1
    MIX  7 COMB 6 100.0 
    MIX  8 COMB 6 100.0 
    MIX  9 COMB 6 100.0                                 
    MIX 10  <<TempCalo>>  6.44       
      Zr90  = ZR90CL   <<DZr90Ct>>  
      Zr91  = ZR91CL   <<DZr91Ct>>
      Zr92  = ZR92CL   <<DZr92Ct>>  
      Zr94  = ZR94CL   <<DZr94Ct>>
      Zr96  = ZR96CL   <<DZr96Ct>>
      FE    = <<IsoFe>>  0.16      
      CR    = <<IsoCr>>  0.11         
      NI    = <<IsoNi>>  0.06      
      B10   = <<IsoB10>> <<B10Zr>>  
      B11   = <<IsoB11>> <<B11Zr>>                      ;
ELSEIF RefLib "IGE227E5" =                            THEN
*----
*  ENDF/B-V
*  IGE227
*----              
  Biblio   := LIB:                                      ::
    EDIT <<Impression>>
    NMIX 10  CTRA WIMS
    DEPL LIB: WIMSAECL FIL: <<LibName>>
    MIXS LIB: WIMSAECL FIL: <<LibName>>
    MIX  1  <<TempCalo>>  <<LDensCalo>> 
      H1    = <<IsoH1>>  <<CwgtH1>>  
      D2    = <<IsoD2>>  <<CwgtD2>>
      O16   = <<IsoO16>> <<CwgtO16>>  
    MIX  2  <<TempCalo>>  6.57
      ZR    = <<IsoZr>>  <<DZrNPt>>
      NB    = <<IsoNb>>  2.5
      BN    = <<IsoBn>> <<BNZrNb>>   
    MIX  3  <<TempMod>>  0.0014     
      HE4   = <<IsoHe>>  100.0      
    MIX  4  <<TempMod>>  6.44      
      ZR    = <<IsoZr>>  <<DZrNCt>>
      FE    = <<IsoFe>>  0.16      
      CR    = <<IsoCr>>  0.11         
      NI    = <<IsoNi>>   0.06    
      BN    = <<IsoBn>> <<BNZr>>   
    MIX  5  <<TempMod>>  <<LDensMod>>  
      H1    = <<IsoH1>>  <<MwgtH1>>   
      D2    = <<IsoD2>>  <<MwgtD2>> 
      O16   = <<IsoO16>> <<MwgtO16>>  
      BN    = <<IsoBn>> <<BNMod>>    
    MIX  6  <<TempComb>>  <<DensComb>>    
      O16   = <<IsoO16>> <<FwgtO16>> 
      U235  = <<IsoU5>>  <<FwgtU5>>   1
      U236  = <<IsoU6>>   0.0         1
      U238  = <<IsoU8>>  <<FwgtU8>>   1
      PU239 = <<IsoPu9>>  0.0         1
      PU240 = PU240       0.0         1
      PU241 = PU241       0.0         1
    MIX  7 COMB 6 100.0 
    MIX  8 COMB 6 100.0 
    MIX  9 COMB 6 100.0                                
    MIX 10  <<TempCalo>>  6.44       
      ZR    = <<IsoZr>>  <<DZrNCt>>
      FE    = <<IsoFe>>  0.16      
      CR    = <<IsoCr>>  0.11         
      NI    = <<IsoNi>>  0.06      
      BN    = <<IsoBn>> <<BNZr>>    ;
ELSE
*----
*  ENDF/B-V
*----              
  Biblio   := LIB:                                      ::
    EDIT <<Impression>>
    NMIX 10  CTRA WIMS
    DEPL LIB: WIMSAECL FIL: <<LibName>>
    MIXS LIB: WIMSAECL FIL: <<LibName>>
    MIX  1  <<TempCalo>>  <<LDensCalo>> 
      H1    = <<IsoH1>>  <<CwgtH1>>  
      D2    = <<IsoD2>>  <<CwgtD2>>
      O16   = <<IsoO16>> <<CwgtO16>>  
    MIX  2  <<TempCalo>>  6.57
      ZR    = <<IsoZr>>  <<DZrNPt>>
      NB    = <<IsoNb>>  2.5
      B10   = <<IsoB10>>  <<B10ZrNb>>  
      B11   = <<IsoB11>>  <<B11ZrNb>>   
    MIX  3  <<TempMod>>  0.0016987     
      O16   = <<IsoO16>>   72.89      
      C12   = <<IsoC12>>  27.11        
    MIX  4  <<TempMod>>  6.44      
      ZR    = <<IsoZr>>  <<DZrNCt>>
      FE    = <<IsoFe>>  0.16      
      CR    = <<IsoCr>>  0.11         
      NI    = <<IsoNi>>   0.06    
      B10   = <<IsoB10>> <<B10Zr>>  
      B11   = <<IsoB11>> <<B11Zr>>   
    MIX  5  <<TempMod>>  <<LDensMod>>  
      H1    = <<IsoH1>>  <<MwgtH1>>   
      D2    = <<IsoD2>>  <<MwgtD2>> 
      O16   = <<IsoO16>> <<MwgtO16>>  
      B10   = <<IsoB10>> <<B10Mod>>  
      B11   = <<IsoB11>> <<B11Mod>>                   
    MIX  6  <<TempComb>>  <<DensComb>>    
      O16   = <<IsoO16>> <<FwgtO16>> 
      U235  = <<IsoU5>>  <<FwgtU5>>   1
      U236  = <<IsoU6>>   0.0         1
      U238  = <<IsoU8>>  <<FwgtU8>>   1
      PU239 = <<IsoPu9>>  0.0         1
      PU240 = PU240       0.0         1
      PU241 = PU241       0.0         1
    MIX  7 COMB 6 100.0 
    MIX  8 COMB 6 100.0 
    MIX  9 COMB 6 100.0                                
    MIX 10  <<TempCalo>>  6.44       
      ZR    = <<IsoZr>>  <<DZrNCt>>
      FE    = <<IsoFe>>  0.16      
      CR    = <<IsoCr>>  0.11         
      NI    = <<IsoNi>>  0.06      
      B10   = <<IsoB10>> <<B10Zr>>  
      B11   = <<IsoB11>> <<B11Zr>>    ;
                                                  ENDIF ;
*---- 
*  terminer execution
*----
QUIT "LIST"                                             .
