*DECK GeoCellG2.c2m
*----
*  Nom           : GeoCellG2.c2m
*  Type          : procedure DRAGON
*  Usage         : Definir les geometries de cellule Gentilly-2
*                  qui peuvent etre utilisees.
*  Auteur        : G. Marleau
*  Date          : 2002/12/03
*  
*  Description de la procedure : 
*
*  Tracking Intline := GeoCellG2 :: 
*    <<Modele>> <<Calo>> <<TypeCalc>> <<Impression>> ;
*  
*  Tracking      : structure de donnees tracking
*  Intline       : fichier de lignes d'integration
*  Modele        : Modele de cellule considere
*                  "NG2C"  -  > Nouvelle cellule G-2 avec
*                               frontiere Cartesienne
*                  "NG2A"    -> Nouvelle cellule G-2 avec
*                               frontiere annulaire
*                  "IGE227"  -> Geometrie du rapport
*                               IGE-227 avec  frontiere annulaire
*                               (defaut)
*                  "IGE227C"  -> Geometrie du rapport
*                               IGE-227 avec  frontiere Cartesienne
*                  "Simple"  -> Geometrie de test simplifiee
*  Calo          : Conditions du caloporteur
*                  "Present" -> cellule refrigeree (defaut)
*                  "Absent"  -> perte de caloporteur
*  TypeCalc      : Type de calcul
*                  "Flux"    -> Geometrie fine pour calcul de flux
*                              (defaut)
*                  "Auto"    -> Geometrie grossiere pour calcul 
*                               d'autoprotection
*                  "Homog"   -> Geometrie pour homogenisation
*  Impression    : Niveau d'impression
*                  0         -> Minimum 
*                  1         -> Standard 
*                 >1         -> Pour verification 
*----
*  Parametres de la procedure
*----
PARAMETER          Tracking  Intline                    ::
  EDIT 1
    ::: LINKED_LIST Tracking     ; 
    ::: SEQ_BINARY  Intline      ;                      ;
STRING    Modele  Calo TypeCalc                         ;              
INTEGER   Impression                                    ;
:: >>Modele<< >>Calo<< >>TypeCalc<< >>Impression<<      ;
*----------
*  Definir les options locales 
*----------     
MODULE       GEO: EXCELT:
             DELETE:                                    ;
LINKED_LIST  CANDU6                                     ;
INTEGER      CoolMix                                    ;
INTEGER      MaxRegion   := 32                          ;
INTEGER      NbAngles AngSymm                           ;
REAL         TrkDens                                    ;
*----
*  Definir le melange associe au caloporteur
*----
IF Calo "Present" = THEN
  EVALUATE   CoolMix := 1                               ;
ELSE 
  EVALUATE   CoolMix := 0                               ;
ENDIF                                                   ; 
*----
*  Choisir le modele
*----                                                   ;
IF  Modele "NG2C" = THEN 
*----
*  Nouvelle geometrie avec frontiere cartesienne
*----
  IF TypeCalc "Homog" = THEN
    EVALUATE   NbAngles AngSymm  TrkDens                :=
               31       1        10.0                   ;
*----
*  Geometrie pour homogenisation
*----
    CANDU6 := GEO: ::  CARCEL 5 
      EDIT <<Impression>>
      X- REFL MESHX -14.2875 14.2875 X+ REFL
      Y- REFL MESHY -14.2875 14.2875 Y+ REFL
      RADIUS  0.00000 0.7221626 2.160324 3.600681 
              5.168878 6.587482  
      MIX     3 4 5 6 2 1                               ;
  ELSEIF TypeCalc "Auto" = THEN
    EVALUATE   NbAngles AngSymm  TrkDens                :=
               31       4        20.0                   ;
*----
*  Geometrie grossiere pour autoprotection
*----
    CANDU6 := GEO: ::  CARCEL 5
      EDIT <<Impression>>
      X- REFL MESHX -14.2875 14.2875 X+ REFL
      Y- REFL MESHY -14.2875 14.2875 Y+ REFL
      RADIUS  0.00000 5.168878 5.60320 6.44780 
              6.587482 14.0
      MIX  <<CoolMix>> 2 3 4 5 5
      CLUSTER ROD1 ROD2 ROD3 ROD4
      ::: ROD1 := GEO: TUBE 2 MIX  6 10 
        NPIN  1 RPIN 0.0000 APIN 0.0000
        RADIUS 0.00000 0.6122 0.6540 ;
      ::: ROD2 := GEO: ROD1   MIX  7 10
        NPIN  6 RPIN 1.4885 APIN 0.0000 ;
      ::: ROD3 := GEO: ROD1   MIX  8 10
        NPIN 12 RPIN 2.8755 APIN 0.261799 ;
      ::: ROD4 := GEO: ROD1   MIX  9 10
        NPIN 18 RPIN 4.3305 APIN 0.0 ;                  ;
  ELSE
    EVALUATE   NbAngles AngSymm  TrkDens                :=
               31       4        20.0                   ;
*----
*  Defaut, geometrie fine pour flux
*----
    CANDU6 := GEO: ::  CARCEL 8 
      EDIT <<Impression>>
      X- REFL MESHX -14.2875 14.2875 X+ REFL
      Y- REFL MESHY -14.2875 14.2875 Y+ REFL
      RADIUS  0.00000 0.7221626 2.160324 3.600681 
              5.168878 5.60320 6.44780 6.587482 14.0 
      SPLITR   1  2  2  2  1  1  1  9
      MIX      <<CoolMix>>  <<CoolMix>>  <<CoolMix>>  <<CoolMix>>
               2  3  4  5  5
      CLUSTER ROD1 ROD2 ROD3 ROD4
      ::: ROD1 := GEO: TUBE 2 MIX  6 10
        NPIN  1 RPIN 0.0000 APIN 0.0000
        RADIUS 0.00000 0.6122 0.6540 SPLITR 2 1 ;
      ::: ROD2 := GEO: ROD1   MIX  7 10
        NPIN  6 RPIN 1.4885 APIN 0.0000 ;
      ::: ROD3 := GEO: ROD1   MIX  8 10
        NPIN 12 RPIN 2.8755 APIN 0.261799 ;
      ::: ROD4 := GEO: ROD1   MIX  9 10
        NPIN 18 RPIN 4.3305 APIN 0.0 ;                  ;
  ENDIF                                                 ;
ELSEIF  Modele "NG2A" = THEN
*----
*  Nouvelle geometrie avec frontiere annulaire
*----
  IF TypeCalc "Homog" = THEN
    EVALUATE   NbAngles AngSymm  TrkDens                :=
               1        1        10.0                   ;
*----
*  Geometrie pour homogenisation
*----
    CANDU6 := GEO: ::  TUBE 2 
      EDIT <<Impression>>
      R+ REFL 
      RADIUS  0.00000 6.5875 16.12171
      MIX  2 1
      CLUSTER FUEL
      ::: FUEL := GEO: TUBE 4 MIX  3 4 5 6 
        NPIN  1 RPIN 0.0000 APIN 0.0000
        RADIUS  0.00000 0.7221626 2.160324 3.600681 
                5.16887 ;                              ;
  ELSEIF TypeCalc "Auto" = THEN
    EVALUATE   NbAngles AngSymm  TrkDens                :=
               11       12       20.0                   ;
*----
*  Geometrie grossiere pour autoprotection
*----
    CANDU6 := GEO: ::  TUBE 5
      EDIT <<Impression>>
      R+ REFL 
      RADIUS  0.00000 5.168878 5.60320 6.44780 
              6.587482 16.12171
      MIX  <<CoolMix>> 2 3 4 5
      CLUSTER ROD1 ROD2 ROD3 ROD4
      ::: ROD1 := GEO: TUBE 2 MIX  6 10 
        NPIN  1 RPIN 0.0000 APIN 0.0000
        RADIUS 0.00000 0.6122 0.6540 ;
      ::: ROD2 := GEO: ROD1   MIX  7 10 
        NPIN  6 RPIN 1.4885 APIN 0.0000 ;
      ::: ROD3 := GEO: ROD1   MIX  8 10 
        NPIN 12 RPIN 2.8755 APIN 0.261799 ;
      ::: ROD4 := GEO: ROD1   MIX  9 10 
        NPIN 18 RPIN 4.3305 APIN 0.0 ;                  ;
  ELSE
    EVALUATE   NbAngles AngSymm  TrkDens                :=
               11       12       20.0                   ;
*----
*  Defaut, geometrie fine pour flux
*----
    CANDU6 := GEO: ::  TUBE 8 
      EDIT <<Impression>>
      R+ REFL 
      RADIUS  0.00000 0.7221626 2.160324 3.600681 
              5.168878 5.60320 6.44780 6.587482 16.12171 
      SPLITR   1  2  2  2  1  1  1 10
      MIX      <<CoolMix>>  <<CoolMix>>  <<CoolMix>>  <<CoolMix>>
               2  3  4  5
      CLUSTER ROD1 ROD2 ROD3 ROD4
      ::: ROD1 := GEO: TUBE 2 MIX  6 10
        NPIN  1 RPIN 0.0000 APIN 0.0000
        RADIUS 0.00000 0.6122 0.6540 SPLITR 2 1 ;
      ::: ROD2 := GEO: ROD1   MIX  7 10 
        NPIN  6 RPIN 1.4885 APIN 0.0000 ;
      ::: ROD3 := GEO: ROD1   MIX  8 10 
        NPIN 12 RPIN 2.8755 APIN 0.261799 ;
      ::: ROD4 := GEO: ROD1   MIX  9 10
        NPIN 18 RPIN 4.3305 APIN 0.0 ;                  ;
  ENDIF                                                 ;
ELSEIF Modele "IGE227C" = THEN
*----
*  Par defaut, geometrie IGE-227 avec frontiere Cartesienne
*----
  IF TypeCalc "Homog" = THEN
*----
*  Geometrie pour homogenisation
*----
    EVALUATE   NbAngles AngSymm  TrkDens                :=
               1        1        10.0                   ;
    CANDU6 := GEO: ::  CARCEL 2 
      EDIT <<Impression>>
      X- REFL MESHX -14.2875 14.2875 X+ REFL
      Y- REFL MESHY -14.2875 14.2875 Y+ REFL
       RADIUS  0.00000 5.1689 6.5875 
      MIX  3 2 1                                        ;
  ELSEIF TypeCalc "Auto" = THEN
*----
*  Geometrie grossiere pour autoprotection
*----
    EVALUATE   NbAngles AngSymm  TrkDens                :=
               31        4        10.0                   ;
    CANDU6 := GEO: ::  CARCEL 5
      EDIT <<Impression>>
      X- REFL MESHX -14.2875 14.2875 X+ REFL
      Y- REFL MESHY -14.2875 14.2875 Y+ REFL
      R+ REFL RADIUS  0.00000   5.1689   5.60320 6.44780 
                      6.5875   14.0
      MIX      <<CoolMix>>  2  3  4  5  5
      CLUSTER ROD1 ROD2 ROD3 ROD4
      ::: ROD1 := GEO: TUBE 2 MIX  6 10 
        NPIN  1 RPIN 0.0000 APIN 0.0000
        RADIUS 0.00000 0.6122 0.6540 ;
      ::: ROD2 := GEO: ROD1   MIX  7 10
        NPIN  6 RPIN 1.4885 APIN 0.0000 ;
      ::: ROD3 := GEO: ROD1   MIX  8 10
        NPIN 12 RPIN 2.8755 APIN 0.261799 ;
      ::: ROD4 := GEO: ROD1   MIX  9 10
        NPIN 18 RPIN 4.3305 APIN 0.0 ;                  ;
  ELSE
*----
*  Defaut, geometrie fine pour flux
*----
    EVALUATE   NbAngles AngSymm  TrkDens                :=
               31       4       20.0                    ;
    CANDU6 := GEO: ::  CARCEL 5 
      EDIT <<Impression>>
      X- REFL MESHX -14.2875 14.2875 X+ REFL
      Y- REFL MESHY -14.2875 14.2875 Y+ REFL
      R+ REFL RADIUS  0.00000   5.1689   5.60320 6.44780 
                      6.5875   14.0
      MIX      <<CoolMix>>  2  3  4  5  5
      SPLITR   6  1  1  1 9
      CLUSTER ROD1 ROD2 ROD3 ROD4
      ::: ROD1 := GEO: TUBE 2 MIX  6 10 
        NPIN  1 RPIN 0.0000 APIN 0.0000
        RADIUS 0.00000 0.6122 0.6540 SPLITR 2 1 ;
      ::: ROD2 := GEO: ROD1   MIX  7 10  
        NPIN  6 RPIN 1.4885 APIN 0.0000 ;
      ::: ROD3 := GEO: ROD1   MIX  8 10 
        NPIN 12 RPIN 2.8755 APIN 0.261799 ;
      ::: ROD4 := GEO: ROD1   MIX  9 10
        NPIN 18 RPIN 4.3305 APIN 0.0 ;                  ;
  ENDIF                                                 ;
ELSEIF Modele "Simple" = THEN
*----
*  Geometrie annulaire simple
*----
  IF TypeCalc "Homog" = THEN
*----
*  Geometrie pour homogenisation
*----
    EVALUATE   NbAngles AngSymm  TrkDens                :=
               1        1        10.0                   ;
    CANDU6 := GEO: ::  TUBE 1 
      EDIT <<Impression>>
      R+ REFL 
      R+ REFL RADIUS  0.00000  5.0
      MIX  2
      CLUSTER FUEL
      ::: FUEL := GEO: TUBE 1 MIX  2 
        NPIN  1 RPIN 0.0000 APIN 0.0000
        RADIUS 0.000  1.000  ;                        ;
  ELSEIF TypeCalc "Auto" = THEN
*----
*  Geometrie grossiere pour autoprotection
*----
    EVALUATE   NbAngles AngSymm  TrkDens                :=
               1        1        10.0                   ;
    CANDU6 := GEO: ::  TUBE 1 
      EDIT <<Impression>>
      R+ REFL 
      R+ REFL RADIUS  0.00000  5.0
      MIX  5
      CLUSTER FUEL
      ::: FUEL := GEO: TUBE 2 MIX  6 10
        NPIN  1 RPIN 0.0000 APIN 0.0000
        RADIUS 0.0000 0.8 1.000  ;                      ;
  ELSE
*----
*  Defaut, geometrie fine pour flux
*----
    EVALUATE   NbAngles AngSymm  TrkDens                :=
               1        1       20.0                   ;
    CANDU6 := GEO: ::  TUBE 1 
      EDIT <<Impression>>
      R+ REFL 
      R+ REFL RADIUS  0.00000  5.0
      MIX  5
      CLUSTER FUEL
      ::: FUEL := GEO: TUBE 2 MIX  6 10
        NPIN  1 RPIN 0.0000 APIN 0.0000
        RADIUS 0.0000 0.8 1.000  ;                      ;
  ENDIF                                                 ;
ELSE 
*----
*  Par defaut, geometrie IGE-227 avec frontiere annulaire
*----
  IF TypeCalc "Homog" = THEN
*----
*  Geometrie pour homogenisation
*----
    EVALUATE   NbAngles AngSymm  TrkDens                :=
               1        1        10.0                   ;
    CANDU6 := GEO: ::  TUBE 2 
      EDIT <<Impression>>
      R+ REFL 
      R+ REFL RADIUS  0.00000 6.5875 16.12171
      MIX  2 1
      CLUSTER FUEL
      ::: FUEL := GEO: TUBE 1 MIX  3 
        NPIN  1 RPIN 0.0000 APIN 0.0000
        RADIUS 0.0000  5.1689  ;                        ;
  ELSEIF TypeCalc "Auto" = THEN
*----
*  Geometrie grossiere pour autoprotection
*----
    EVALUATE   NbAngles AngSymm  TrkDens                :=
               7        12       10.0                   ;
    CANDU6 := GEO: ::  TUBE 5
      EDIT <<Impression>>
      R+ REFL RADIUS  0.00000   5.1689   5.60320 6.44780 
                      6.5875   16.12171
      MIX  <<CoolMix>> 2 3 4 5
      CLUSTER ROD1 ROD2 ROD3 ROD4
      ::: ROD1 := GEO: TUBE 2 MIX  6 10 
        NPIN  1 RPIN 0.0000 APIN 0.0000
        RADIUS 0.00000 0.6122 0.6540 ;
      ::: ROD2 := GEO: ROD1   MIX  7 10 
        NPIN  6 RPIN 1.4885 APIN 0.0000 ;
      ::: ROD3 := GEO: ROD1   MIX  8 10 
        NPIN 12 RPIN 2.8755 APIN 0.261799 ;
      ::: ROD4 := GEO: ROD1   MIX  9 10 
        NPIN 18 RPIN 4.3305 APIN 0.0 ;                  ;
  ELSE
*----
*  Defaut, geometrie fine pour flux
*----
    EVALUATE   NbAngles AngSymm  TrkDens                :=
               11       12       20.0                   ;
    CANDU6 := GEO: ::  TUBE 5 
      EDIT <<Impression>>
      R+ REFL 
      R+ REFL RADIUS  0.00000   5.1689   5.60320 6.44780 
                      6.5875   16.12171
      SPLITR   6  1  1  1 10
      MIX  <<CoolMix>> 2 3 4 5
      CLUSTER ROD1 ROD2 ROD3 ROD4
      ::: ROD1 := GEO: TUBE 2 MIX  6 10 
        NPIN  1 RPIN 0.0000 APIN 0.0000
        RADIUS 0.00000 0.6122 0.6540 SPLITR 2 1 ;
      ::: ROD2 := GEO: ROD1   MIX  7 10 
        NPIN  6 RPIN 1.4885 APIN 0.0000 ;
      ::: ROD3 := GEO: ROD1   MIX  8 10
        NPIN 12 RPIN 2.8755 APIN 0.261799 ;
      ::: ROD4 := GEO: ROD1   MIX  9 10
        NPIN 18 RPIN 4.3305 APIN 0.0 ;                  ;
  ENDIF                                                 ;
ENDIF                                                   ;
*----------
*  Analyse de la geometrie choisie
*----------
Tracking Intline := EXCELT: CANDU6                      ::
  EDIT <<Impression>>
  TITLE 'Gentilly-2 geometry'
  MAXR <<MaxRegion>> 
  TRAK TISO <<NbAngles>> <<TrkDens>> 
  SYMM <<AngSymm>>                                      ; 
*----
*  Eliminer les structures locales et
*  terminer
*----
CANDU6 := DELETE:  CANDU6                               ; 
QUIT "LIST"                                             .
