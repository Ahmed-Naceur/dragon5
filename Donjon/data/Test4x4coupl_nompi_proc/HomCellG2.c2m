*DECK HomCellG2.c2m
*----
*  Nom           : HomCellG2.c2m
*  Type          : procedure DRAGON
*  Usage         : Homogenisation des proprietes de cellule
*  Auteur        : G. Marleau
*  Date          : 2002/12/03
*  
*  Description de la procedure : 
*
*  MacroEdi  := HomCellG2 Flux Biblio GeoF IntLinF  ::
*    <<Modele>> <<Impression>>                    
*    ; 
*  MacroEdi      : structure contenant les macrolib
*                  apres homogenisation dans une 
*                  structure edition
*  Flux          : structure contenant les flux de cellule 
*                  a Timef
*  Biblio        : structure contenant la bibliotheque
*                  de section efficace originale
*  GeoF          : Structure contenant le tracking de la
*                  geometrie pour calcul de flux
*  IntLinF       : Fichier contenant contenant les lignes
*                  d'integration pour calcul de flux
*  Modele        : Modele de cellule considere
*                  "NG2C"  -  > Nouvelle cellule G-2 avec
*                               frontiere Cartesienne
*                  "NG2A"    -> Nouvelle cellule G-2 avec
*                               frontiere annulaire
*                  "IGE227"  -> Geometrie du rapport
*                               IGE-227 avec  frontiere annulaire
*                               (defaut)
*  Impression    : Niveau d'impression
*                  0         -> Minimum 
*                  1         -> Standard 
*                 >1         -> Pour verification 
*----
*  Parametres de la procedure
*----
PARAMETER         MacroEdi Flux Biblio GeoF  IntLinF    ::
  EDIT 1
    ::: LINKED_LIST MacroEdi Flux Biblio GeoF  ;
    ::: SEQ_BINARY  IntLinF                    ;        ;
STRING    Modele                                        ;
INTEGER   Impression                                    ;
:: >>Modele<< >>Impression<<                            ;
*----------
*  Definir les options locales 
*----------     
MODULE       GEO: EDI: EXCELT: 
             DELETE:                                    ; 
LINKED_LIST  GeoSPH                                     ;
*----
*  Choisir le modele
*----                                                   ;
IF  Modele "NG2C" = THEN 
*----
*  Homogenisation NG2C
*----
  GeoSPH := GEO: ::  CARCEL 5 
      EDIT <<Impression>>
      X- REFL MESHX -14.2875 14.2875 X+ REFL
      Y- REFL MESHY -14.2875 14.2875 Y+ REFL
      RADIUS  0.00000 0.7221626 2.160324 3.600681 
              5.168878 6.587482  
      MIX     3 4 5 6 2 1                               ;
  MacroEdi := EDI: Flux Biblio GeoF GeoSPH              ::
    EDIT <<Impression>>
    MERGE REGION 
      3 3 3 3 4 4 4 4 4 5 5 5 5 5 6 6 6 6 6  
      2 2 2 1 1 1 1 1 1 1 1 1 1                       
    SAVE ON HOMSPH
    SPH MGEO GeoSPH 
      ::: EXCELT: MAXR 6 TRAK TISO 31 10.0 ;            ;
  GeoSPH := DELETE: GeoSPH                              ;
  MacroEdi := EDI: MacroEdi  Flux Biblio GeoF           ::
    EDIT <<Impression>>
    MERGE REGION 
      3 3 3 3 4 4 4 4 4 5 5 5 5 5 6 6 6 6 6  
      2 2 2 1 1 1 1 1 1 1 1 1 1                      
    SAVE ON DirectM                                     ;
  MacroEdi := EDI: MacroEdi Flux Biblio GeoF            ::
    EDIT <<Impression>>
    MERGE MIX 
      3 2 2 2 1 3 3 3 3 3                             
    SAVE ON DirectS                                     ;
ELSEIF  Modele "NG2A" = THEN
*----
*  Homogenisation NG2A
*----
  GeoSPH := GEO: ::  TUBE 2 
    EDIT <<Impression>>
    R+ REFL 
    R+ REFL RADIUS  0.00000 6.5875 16.12171
    MIX  2 1
    CLUSTER FUEL
    ::: FUEL := GEO: TUBE 4 MIX  3 4 5 6 
      NPIN  1 RPIN 0.0000 APIN 0.0000
      RADIUS  0.00000 0.7221626 2.160324 3.600681 
              5.16887 ;                                 ;
  MacroEdi := EDI: Flux Biblio GeoF GeoSPH              ::
    EDIT <<Impression>>
    MERGE REGION 
      3 3 3 3 4 4 4 4 4 5 5 5 5 5 6 6 6 6 6  
      2 2 2 1 1 1 1 1 1 1 1 1 1                       
    SAVE ON HOMSPH
    SPH MGEO GeoSPH 
      ::: EXCELT: MAXR 6 TRAK TISO 31 10.0 ;            ;
  GeoSPH := DELETE: GeoSPH                              ;
  MacroEdi := EDI: MacroEdi  Flux Biblio GeoF           ::
    EDIT <<Impression>>
    MERGE REGION 
      3 3 3 3 4 4 4 4 4 5 5 5 5 5 6 6 6 6 6  
      2 2 2 1 1 1 1 1 1 1 1 1 1                      
    SAVE ON DirectM                                     ;
  MacroEdi := EDI: MacroEdi Flux Biblio GeoF            ::
    EDIT <<Impression>>
    MERGE MIX 
      3 2 2 2 1 3 3 3 3 3                             
    SAVE ON DirectS                                     ;
ELSE 
*----
*  Homogenisation IGE-227
*----
  MacroEdi := EDI: Flux Biblio GeoF                     ::
    EDIT <<Impression>>
    MERGE MIX 
      3 2 2 2 1 3 3 3 3 3                             
    SAVE ON DirectS                                     ;
ENDIF                                                   ;
QUIT "LIST"                                             .
