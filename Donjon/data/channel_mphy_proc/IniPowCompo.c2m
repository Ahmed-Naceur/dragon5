****************************************************************
*                                                              *
* Procedure :  IniPowCompo.c2m                                 *
* Purpose   :  Initialization component                        *
* Author    :  A. Hebert                                       *
*                                                              *
* CALL      :                                                  *
*  Fmap Matex Cpo CpRefl Track := IniPowCompo ;                *
*                                                              *
****************************************************************
PARAMETER  Fmap Matex Cpo CpRefl Track ::
  ::: LINKED_LIST Fmap Matex Cpo CpRefl Track ; ;

LINKED_LIST GeoCoeur ;
MODULE      TRIVAT: RESINI: UTL: END: ;

PROCEDURE   GetDim GeoCo SetFuelMap Axial_Refl ;
SEQ_ASCII   _ACompo :: FILE "./_ACompo" ;
*----
*  Local Parameters
*----
REAL    densB1 := 2000.0 ;
REAL    densB2 := 1000.0 ;
INTEGER Splitx Splitz := 4 4        ;
REAL    powi := 0.0 ;

REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 
     R_int_TG R_ext_TG R_int_TI R_ext_TI R_int_G R_ext_G 
     Cote CoteL
     RmodeN1 RmodeN2 
     R_ext_G2 R_ext_TGm R_ext_TIm
     N_U5_UOX N_U5_MOX
     N_Pu8 N_Pu9 N_Pu0 N_Pu1 N_Pu2 N_Am1
     densU_UOX densU_MOX densPu_MOX
     teneur1_Pu teneur2_Pu teneur3_Pu
     temp_comb temp_mode dens_mode
     temp_comb_0 temp_mode_0 dens_mode_0 ;

*----
*  Recover the fuel Multicompo
*----
Cpo := _ACompo ;
UTL: Cpo :: DIR ;
*--
* Set the reflector properties
*--
CpRefl := Axial_Refl ; 
UTL: CpRefl :: DIR ;

REAL ORF IRC ORC IRCGT ORCGT ORAG ORAN 
     IRCA ORCA ORDET ORDETC Lame LPitch Height ;

REAL pbore_0 := 500. ; ! natural boron concentration in ppm
REAL bVal := 8000. ; ! uniform burnup value

  GetDim ::
    >>Rcomb1<< >>Rcomb2<< >>Rcomb3<< >>Rcomb4<<
    >>R_int_TG<< >>R_ext_TG<< >>R_int_TI<< >>R_ext_TI<<
    >>R_int_G<< >>R_ext_G<< >>Cote<< >>Lame<< >>CoteL<<
    >>RmodeN1<< >>RmodeN2<< >>R_ext_G2<< >>R_ext_TGm<< >>R_ext_TIm<<
    >>N_U5_UOX<< >>N_U5_MOX<< >>N_Pu8<< >>N_Pu9<< >>N_Pu0<<
    >>N_Pu1<< >>N_Pu2<< >>N_Am1<< >>densU_UOX<< >>densU_MOX<<
    >>densPu_MOX<< >>teneur1_Pu<< >>teneur2_Pu<< >>teneur3_Pu<<
    >>temp_comb_0<< >>temp_mode_0<< >>dens_mode_0<< ;

EVALUATE temp_comb temp_mode dens_mode := 800.0 600.0 0.659 ;

EVALUATE
     ORF IRC ORC IRCGT ORCGT
     ORAG ORAN IRCA ORCA
     ORDET ORDETC
     Lame LPitch Height
     :=
     Rcomb4 R_int_TG R_ext_TG R_int_TI R_ext_TI
     R_int_TG R_ext_TG R_int_TI R_ext_TI
     R_int_TG R_ext_TG
     Lame Cote 10.0
     ;

REAL rPavX := 17.0 ; ! assemblage 17 x 17
INTEGER iPavX := 17 ; ! assemblage 17 x 17

INTEGER MaxR := iPavX iPavX * 33 * Splitx * Splitx * Splitz * ;

GeoCoeur Matex := GeoCo  ::
   <<Splitx>> <<Splitz>> <<MaxR>> ;
   
Track := TRIVAT: GeoCoeur ::
   EDIT 1 MAXR <<MaxR>> DUAL 3 3 ;

*--
* Fuel map definition
*--
Fmap Matex := SetFuelMap Matex ;

UTL: Fmap :: STEP UP GEOMAP DIR IMPR MESHX * IMPR MESHY * IMPR MESHZ * ;

Fmap := RESINI: Fmap :: EDIT 2

  BTYPE INST-BURN
  INST-BVAL SAME <<bVal>>
  SET-PARAM 'C-BORE' <<densB1>>
  SET-PARAM 'T-FUEL' <<temp_comb>>
  SET-PARAM 'T-COOL' <<temp_mode>>
  SET-PARAM 'D-COOL' <<dens_mode>>
  ;

END: ;
QUIT "LIST" .
