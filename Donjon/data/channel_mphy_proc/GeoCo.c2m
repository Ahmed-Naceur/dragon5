*DECK GeoCo.c2m
*----
*  Name          : GeoCo.c2m
*  Type          : DONJON procedure
*  Use           : Core Geometry
*  Procedure called as: 
*
*  Bench := GeoCo ::
*    <<Splitx>> <<Splitz>> <<MaxR>> ;
*
*  Parameters of the procedure
*  Bench     : Core Geometry
*  Input data:
*    Splitx  : cartesian splitting
*    Splitz  : axial splitting
*----
* Definition of the input and output data structures and 
* of the input and output parameters for this procedure
*----
PARAMETER       GeoRes Matex  ::
::: LINKED_LIST GeoRes Matex ; ;
*----
*  Local modules, procedures and structures
*----
MODULE      USPLIT: GEO: END: ;
LINKED_LIST GeoCoeur ;
*----
*  Get calculation options
*----
INTEGER Splitx Splitz MaxR ;
:: >>Splitx<< >>Splitz<< >>MaxR<< ;

REAL  x1 := 21.6 ;

REAL z1 dz := 20.0 23.0 ;

REAL  z2 z3 z4 z5 z6 z7 z8 z9 z10 z11 z12 z13 z14 
      z15 z16 z17 :=
      z1 dz +       z1  2. dz * + z1  3. dz * + z1  4. dz * +
      z1  5. dz * + z1  6. dz * + z1  7. dz * + z1  8. dz * +
      z1  9. dz * + z1 10. dz * + z1 11. dz * + z1 12. dz * +
      z1 13. dz * + z1 14. dz * + z1 15. dz * + z1 16. dz * + ;
      
REAL  z18 := z17 z1 + ;

GeoCoeur := GEO: :: CAR3D 1 1 18
  EDIT 3
  X+ REFL  X- REFL
  Y+ REFL  Y- REFL
  Z+ VOID  Z- VOID
  MIX  

PLANE 1 (* lower core *)
  2
PLANE 2
  1
PLANE 3 SAME 2
PLANE 4 SAME 2
PLANE 5 SAME 2
PLANE 6 SAME 2
PLANE 7 SAME 2
PLANE 8 SAME 2
PLANE 9 SAME 2
PLANE 10 SAME 2
PLANE 11 SAME 2
PLANE 12 SAME 2
PLANE 13 SAME 2
PLANE 14 SAME 2
PLANE 15 SAME 2
PLANE 16 SAME 2
PLANE 17 SAME 2
PLANE 18 (* higher core *)
  2

  MESHX 0. <<x1>>

  MESHY 0. <<x1>>

  MESHZ 0.0 <<z1>> <<z2>> <<z3>> <<z4>> <<z5>> <<z6>> <<z7>> 
        <<z8>>  <<z9>>  <<z10>> <<z11>> <<z12>> <<z13>> <<z14>>
        <<z15>> <<z16>> <<z17>> <<z18>>

  SPLITX <<Splitx>>
  SPLITY <<Splitx>>

  SPLITZ <<Splitz>> <<Splitz>> <<Splitz>> <<Splitz>> <<Splitz>>
         <<Splitz>> <<Splitz>> <<Splitz>> <<Splitz>> <<Splitz>> 
         <<Splitz>> <<Splitz>> <<Splitz>> <<Splitz>> <<Splitz>>
         <<Splitz>> <<Splitz>> <<Splitz>>
;

GeoRes Matex := USPLIT: GeoCoeur :: NGRP 2 MAXR <<MaxR>>
               NREFL 1  RMIX  2
               NFUEL 1  FMIX  1
;

END: ;

QUIT "LIST" .
