****************************************************************
*                                                              *
* Procedure :  InitSteady.c2m                                  *
* Purpose   :  initialize objects for Steady procedure         *
* Author    :  V. Salino                                       *
*                                                              *
* CALL      :                                                  *
*  Flux Thermo XeMicroF XeBurn Fmap Matex := InitSteady        *
*  Fmap Matex ReflXS Track ;                                   *
*                                                              *
****************************************************************
PARAMETER Flux Thermo XeMicroF Fmap Matex Track SapUOX ::
  ::: LINKED_LIST Flux Thermo XeMicroF Fmap Matex Track SapUOX ; ;
MODULE SCR: NCR: MACINI: RESINI: TRIVAA: FLUD: FLPOW: THM: EVO:
       SIM: GREP: UTL: DELETE: ABORT: END: MAC: ;
*--
* Local objects and variable
*--
LINKED_LIST MacroTot MacroRefl MacroF System PowerIter ;
REAL CBinterp := 0.0 ;
REAL TOTAL1_inf   TOTAL2_inf
     DIFF1_inf    DIFF2_inf ;
REAL TOTAL1_sup   TOTAL2_sup
     DIFF1_sup    DIFF2_sup ;
REAL TOTAL1_rad   TOTAL2_rad
     DIFF1_rad    DIFF2_rad ;
EVALUATE TOTAL1_sup TOTAL2_sup :=
         .158287E+01 .276758E-01 +
         .503068E+00 .282751E-01 + ;
EVALUATE DIFF1_sup DIFF2_sup :=
         1. 3. TOTAL1_sup * /
         1. 3. TOTAL2_sup * / ;
EVALUATE TOTAL1_inf TOTAL2_inf :=
         .141535E+01 .261305E-01 +
         .511240E+00 .198432E-01 + ;
EVALUATE DIFF1_inf DIFF2_inf :=
         1. 3. TOTAL1_inf * /
         1. 3. TOTAL2_inf * / ;
EVALUATE TOTAL1_rad TOTAL2_rad :=
         .82752 .270997E-01 +
         .35577 .110125E+00 + ;
EVALUATE DIFF1_rad DIFF2_rad :=
           1. 3. TOTAL1_rad * /
           1. 3. TOTAL2_rad * / ;
*--
* Thermalhydraulics parameters
*--
*100% PN
REAL Tinlet := 286.5 273.15 + ;
*0% PN
!REAL Tinlet := 286.0 273.15 + ;
REAL FlowRate := 21724.0 3.0 * 1.0 0.065 - * ;     ! m3/h, 6.5% bypass
*REAL dx       := 21.504E-2 ;                      ! m
REAL dx       := 21.613E-2 ;                       ! m
REAL R_Tub_OD := 1.245E-2 2.0 / ;                  ! m
REAL R_Tub_ID := R_Tub_OD 0.05E-2 - ;              ! m
REAL R_Pin_OD := 0.950E-2 2.0 / ;                  ! m
REAL R_Pin_ID := R_Pin_OD 0.057E-2 - ;             ! m
REAL R_Pel    := 0.8192E-2 2.0 / ;                 ! m
REAL Tot_tub  := R_Tub_OD 2.0 ** $Pi_R * 25.0  * ; ! m2
REAL Tot_pin  := R_Pin_OD 2.0 ** $Pi_R * 264.0 * ; ! m2
REAL sass     := dx 2.0 ** ;                       ! m2
REAL asssect  := sass Tot_tub - Tot_pin - ;        ! m2
REAL coresect := 157.0 asssect * ;                 ! m2
REAL    densB1  :=    0.         ;
*--
* Flat power distribution
*--
REAL Ptot := 1.0E-6 ; ! 1W
REAL BundPow := Ptot 157.0 / 31.0 / 1.0E+3 * (* MW to kW *) ;
Fmap := RESINI: Fmap ::
  BUNDLE-POW SAME <<BundPow>> ;
*--
* Thermalhydraulics computation
*--
      Thermo Fmap := THM: Fmap ::
        EDIT 3
        CRITFL 1.80E+6 (* W/m^2 *)
        ASSMB 264 25
        CWSECT <<coresect>> <<FlowRate>>
        INLET  1.57E+07 <<Tinlet>>
        RADIUS <<R_Pel>> <<R_Pin_ID>> <<R_Pin_OD>> <<R_Tub_OD>>
        POROS  0.05
        FPUISS 0.95
        SET-PARAM 'C-BORE' <<densB1>>
        CONV 50 50 50 0.1 1.0E-3
        ;
*--
* Saphyb Interpolation
* Historic effects are neglected for simplicity's sake
XeMicroF := SCR: SapUOX Fmap ::
    EDIT 0 
    MICRO LINEAR
    EQUI EQVS
    TABLE SapUOX 'BURN'
    MIX 4
       INST-BURN
       SET 'DMOD' MAP
       SET 'TCOM' MAP
       SET 'BARR' 1.
       MICRO ALL
           'B10'      *
           'B11'      *
           'XE135PF'  *
           'I135PF'   *
           'ND147PF'  *
           'PM147PF'  *
           'PM148PF'  *
           'PM148MPF' *
           'PM149PF'  *
           'SM149PF'  *
           '*MAC*RES' 1.
      ENDMIX
  CHAIN
  B10      STABLE
  B11      STABLE
  I135PF   NG    0.
  XE135PF  NG    0. FROM DECAY 1.0E+00 I135PF
  ND147PF  STABLE
  PM147PF  STABLE
  PM148PF  STABLE
  PM148MPF STABLE
  PM149PF  STABLE
  SM149PF  STABLE
  MACR     NFTOT 0.
  ENDCHAIN

  ;
MacroF := XeMicroF :: STEP UP "MACROLIB" ;
*---
*  Reflector definition
*---

  MacroRefl := MAC: ::
    EDIT 3 NGRO 2 NMIX 3 NIFI 0
    ANIS 1 CTRA NONE
    ENER 2.E7 0.625 1.E-3
    VOLUME 1. 1. 1.
    READ INPUT

*---
*  Upscattering is set to zero, as done in HEMERA, despite having an upscattering cross section available.
*---
    MIX 1 ! Radial Reflector
      FLUX-INTG  1. 1.
      TOTAL <<TOTAL1_rad>> <<TOTAL2_rad>>
      DIFF  <<DIFF1_rad>> <<DIFF2_rad>>
      SCAT 2 2 (*2->1*) 0.               (*1->1*) .82752
           2 2 (*2->2*) .35577 (*1->2*) .113898E-01

    MIX 2 ! Lower Reflector
      FLUX-INTG  1. 1.
      TOTAL <<TOTAL1_inf>> <<TOTAL2_inf>>
      DIFF  <<DIFF1_inf>> <<DIFF2_inf>>
      SCAT 2 2 (*2->1*) 0.               (*1->1*) .141535E+01
           2 2 (*2->2*) .511240E+00 (*1->2*) .187184E-01

    MIX 3 ! Higher Reflector
      FLUX-INTG  1. 1.
      TOTAL <<TOTAL1_sup>> <<TOTAL2_sup>>
      DIFF  <<DIFF1_sup>> <<DIFF2_sup>>
      SCAT 2 2 (*2->1*) 0.               (*1->1*) .158287E+01
           2 2 (*2->2*) .503068E+00 (*1->2*) .200239E-01

    NORM
  ;
*--
* Flux and power computation
*--
MacroTot Matex := MACINI: Matex MacroRefl MacroF ;
MacroF MacroRefl := DELETE: MacroF MacroRefl ;
System := TRIVAA: MacroTot Track :: EDIT 0 ;
MacroTot := DELETE: MacroTot ;
Flux := FLUD: System Track :: EDIT 0 ACCE 5 3 ;
System := DELETE: System ;
PowerIter Fmap := FLPOW: Fmap Flux Track Matex ::
  EDIT 0 PTOT <<Ptot>> PRINT DISTR POWER ;
*--
* Xenon saturation
*--
PowerIter := DELETE: PowerIter ;

END: ;
