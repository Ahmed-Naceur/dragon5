*DECK ComparePdist.c2m
*======================================================
* Name:   ComparePdist.c2m
* Type:   DONJON procedure
* Use:    Compare 2 Power Distributions using SIM:
* Author: A. Bruneau 10.2014
*         [CS-SI | IRSN/PSN-EXP/SNC/LNR]
* Modified by V. Salino 01.2015
*          Removed name requirement (unnecessary) and
*          simplified
*------------------------------------------------------
* Procedure called as:
*------------------------------------------------------
* Input Required:
*  > Fmap [LINKED_LIST, L_FLMAP]
*  > OldPower [LINKED_LIST, L_POWER]
*  > Power [LINKED_LIST, L_POWER]
* Output Provided:
*  < EpsOut [REAL]
*======================================================
PARAMETER   Fmap OldPower Power :: ::: LINKED_LIST
            Fmap OldPower Power ; ;
MODULE      SIM: DELETE: END: ;
LINKED_LIST LocalFmap ;
STRING      CyPower1 CyPower2 := "CyPower1" "CyPower2" ;
REAL        EpsOut ;

*------------------------------------------------------
* Retrieve fuel map
*------------------------------------------------------
LocalFmap := Fmap ;

*------------------------------------------------------
* Initialization of a virtual cycle
* Any BU distribution may be used
*------------------------------------------------------
LocalFmap := SIM: LocalFmap ::
 EDIT 1
 CYCLE <<CyPower1>>
  QMAP
      H   G   F   E   D   C   B   A
   8 NEW NEW NEW NEW NEW NEW NEW NEW
   9 NEW NEW NEW NEW NEW NEW NEW NEW
  10 NEW NEW NEW NEW NEW NEW NEW NEW
  11 NEW NEW NEW NEW NEW NEW NEW NEW
  12 NEW NEW NEW NEW NEW NEW NEW  |
  13 NEW NEW NEW NEW NEW NEW NEW  |
  14 NEW NEW NEW NEW NEW NEW  |   |
  15 NEW NEW NEW NEW  |   |   |   |
 ENDCYCLE ;

*------------------------------------------------------
* Create & compare cycles from Power objects
*------------------------------------------------------
LocalFmap := SIM: LocalFmap OldPower ::
      EDIT 1
      CYCLE <<CyPower1>>
      TIME 1.E-15 SECOND
      ENDCYCLE
      ;

LocalFmap := SIM: LocalFmap ::
      EDIT 1
      CYCLE <<CyPower2>> FROM <<CyPower1>>
      QMAP
        H   G   F   E   D   C   B   A
     8 H08 G08 F08 E08 D08 C08 B08 A08
     9 H09 G09 F09 E09 D09 C09 B09 A09
    10 H10 G10 F10 E10 D10 C10 B10 A10
    11 H11 G11 F11 E11 D11 C11 B11 A11
    12 H12 G12 F12 E12 D12 C12 B12  |
    13 H13 G13 F13 E13 D13 C13 B13  |
    14 H14 G14 F14 E14 D14 C14  |   |
    15 H15 G15 F15 E15  |   |   |   |
      ENDCYCLE
      ;

LocalFmap := SIM: LocalFmap Power ::
      EDIT 1
      CYCLE <<CyPower2>>
      TIME 1.E-15 SECOND
      ENDCYCLE
      ;

*------------------------------------------------------
* Compare dist-powr -> max epsilon in 'EpsOut'
*------------------------------------------------------
LocalFmap := SIM: LocalFmap Power ::
      EDIT 1
      COMPARE <<CyPower1>> <<CyPower2>>
      DIST-POWR >>EpsOut<<
      ;

*------------------------------------------------------
* Delete local LINKED_LIST & provide EpsOut
*------------------------------------------------------
LocalFmap := DELETE: LocalFmap ;
:: <<EpsOut>> ;

END: ;
QUIT "LIST" .
